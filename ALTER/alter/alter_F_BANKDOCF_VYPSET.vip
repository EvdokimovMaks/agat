//Настройка импорта банковской выписки.
//Добавление флажка "Записывать в примечание номер и дату документа из выписки"

#include ExtAttr.vih
#include GetKau.vih

#component "F_BANKDOCF"

alter interface vypset;

var
  _extAttr: iExtAttr;
  _getKau: GetKau;
  _cAttrExtFlags: comp;
  _cAttrNamIspDog: comp;

create view
as select
  if(IsValid(tnAvExtFlags), if(AvExtFlags.vString='', 0, Word(AvExtFlags.vString)), 0)
    (FieldName=IsSetNumber),
  if(IsValid(tnKatPodrIspDog), KatPodrIspDog.Name, '')
    (FieldName=IspDog)
from
  AttrVal AvExtFlags,
  AttrVal avIspDog,
  KatPodr KatPodrIspDog
where ((
  coBankDocF           == AvExtFlags.wTable and
  BankDocF.Nrec        == AvExtFlags.cRec and
  _cAttrExtFlags       == AvExtFlags.cAttrNam and
  coBankDocF           == avIspDog.wTable and
  BankDocF.Nrec        == avIspDog.cRec and
  _cAttrNamIspDog      == avIspDog.cAttrNam and
  avIspDog.vComp       == KatPodrIspDog.Nrec
));

Window wBankDocFExt 'Агат. Дополнительные настройки банковской выписки' EscClose, DoAccept;
  show(,,80, 4);
  screen scBankDocFExt;
    table BankDocF;
    fields
      IsSetNumber: noProtect, noFrame;
      IspDog ('Исполнитель договора'): Protect, PickButton;
<<

  [.] Записывать в назначение номер и дату выписки`
  [.] Устанавливать Исполнитель договора`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
  end; //screen scBankDocFExt;

  handleevent
    cmPick:
    {
      case curfield of
        #IspDog:
        {
          var c: comp = KatPodrIspDog.Nrec;
          if _getKau.GetCodeKau(cgiPick, cgKau_KatPodr, c) > 0
            _extAttr.coSetAttrID(coBankDocF, BankDocF.Nrec, _cAttrNamIspDog, c, ShowKau(cgKau_KatPodr, c));
          RescanPanel(tnBankDocF);
        }
      end;
    }
  end;

end; //Window wBankDocFExt

Window wBankDocF 'Настройка импорта банковской выписки' EscClose;

  Panel panBankDocF
    Table BankDocF;

    ExtendFormats scrBankDocF;
    Screen scrBankDocF ('Настройка импорта банковской выписки для ее обработки', hcBankdSVypisNastr, sci13478Esc);
      buttons
        cmShowBankDocFExt;
<<
















                                                                               <.   Агат. Доп. настройки   .>
>>

    end; //Screen scrBankDocF

  end; //Panel panBankDocF

  handleevent
    cmShowBankDocFExt: RunWindowModal(wBankDocFExt);
  end;

end; //Window wBankDocF

tableevent table BankDocF;
  cmExprFieldChanged:
  {
    case inherited::handleevent(cmExprFieldChanged) of
      heAbort: Abort;
      heStop: Stop;
    else
      case CurField of
        #IsSetNumber:
        {
          UpdateTable;
          _extAttr.sSetAttrID(coBankDocF, BankDocF.Nrec, _cAttrExtFlags, ExprFieldValue);
          RescanPanel(tnBankDocF);
        }
      end;
    end;
  }
end; //tableevent table BancDocF

handleevent

  cmInit:
  {
    case inherited::handleevent(cmInit) of
      heAbort: Abort;
      heStop: Stop;
    else
    {
      _cAttrExtFlags := _extAttr.CreateAttr(coBankDocF, 'Дополнительные флаги', cgAttrType_Str);
      _cAttrNamIspDog := _extAttr.CreateAttrComp(coBankDocF, 'Исполнитель договора', cgAttrType_Comp, cgKatKau_User, cgKau_KatPodr);
    }
    end;
  }

end;

end.
