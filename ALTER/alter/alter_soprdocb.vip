#component "L_SOPRDOC"

alter interface soprdocb;

table struct tNpp (SpsoprNrec: comp, Npp: string[20]) with index (i01 = SpsoprNrec);

var
  _curNpp: string;

create view as select * from tNpp;

procedure SaveNpp;
{
  delete all tNpp;
  pushpos(#spsopr);
  _loop spsopr
    insert tNpp set tNpp.SpsoprNrec := spsopr.nrec, tNpp.Npp := spsopr.npp;
  poppos(#spsopr);
}

procedure RestoreNpp;
{
  var curNppIsSet: boolean;
  curNppIsSet := false;

  pushpos(#spsopr);
  _loop spsopr
  {
    if getfirst tNpp where ((spsopr.nrec == tNpp.SpsoprNrec)) = tsOk
    {
      update current spsopr set spsopr.npp := tNpp.Npp;
    }
    else if (not curNppIsSet) and (_curNpp != '')
    {
      update current spsopr set spsopr.npp := _curNpp;
      curNppIsSet := true;
    }
  }
  poppos(#spsopr);
}

window editsopr;
tableevent table spsopr;
cmPick:
{
  if (curfield = #katmc.name or curfield = #katmc.barkod)
  {
    _curNpp := if((not isNew) and (isValidAll(#spsopr)), spsopr.Npp, '');

    SaveNpp;
    if inherited::handleevent(cmPick) = heOk
      RestoreNpp;
  }
  else
    inherited::handleevent(cmPick);
}
end;
end;
end.

