#include PlporHelper.vih
#include Query.vih

#component "F_BANKDOCF"

alter interface vypsee;

var
  _plporHelper: PlporHelper;
  _qFindPlpor: IQuery;
  _markedSum: double;

overload
  Function CreateNewPlPor(sum: double; sumVal: double; NewNRec: comp): boolean;
  Function GetNumber(_TiDkGal, _TiDk: word; _Descr: TDescr; _DatVip : date; _NoDok: string): string;
  Function PlPor_FindByDateVipSum: boolean;
end;

extendformats scSeePlPor;

create view
as select tmpTotal.Sum
from soprhoz plporSoprhoz, schfact plporSchfact, tmpPlPor tmpPlPorMarked,
     (select Sum(tmpPlPorTotal.SumPlat) (fieldname=Sum)
      from tmpPlPor tmpPlPorTotal
      where (FoundMarker(markProcess, TmpPlPor.nRec))
     ) tmpTotal
where ((
  plpor.tidk            == plporSoprhoz.tipdoc   and
  plpor.nrec            == plporSoprhoz.csoprdoc and
  plporSoprhoz.cschfact == plporSchfact.nrec and
  root                  == tmpPlPorMarked.Nrec and (FoundMarker(markProcess, tmpPlPorMarked.Nrec))
));

procedure RefreshMarkedSum;
{
  _markedSum := 0;
  _loop tmpPlPorMarked
    _markedSum += tmpPlPorMarked.SumPlat;
}

Panel panButtons;
Screen scSeePlPor '' ('', hcBankdSVypisWork, sci1EnEsc);
fields
  _markedSum: ['[|-]3666`666`666`666`666.88'], Skip;
buttons
  cmUpdateTotals, [singleline];
<<

                                                                                                                            Сумма по помеченным: .@@@@@@@@@@@@@@@ <.Обновить.>
>>

end;
end;

Function CreateNewPlPor(sum: double; sumVal: double; NewNRec: comp): boolean;
{

  if inherited::CreateNewPlPor(sum, sumVal, NewNRec)
  {
    var IsUpdateNeeded: boolean = false;

    if plpor.tidkgal = 1
    {
      update current plpor set plpor.nodok := '00000';
      plpor.nodok := _plporHelper.GetNewPlporNumber(plpor.tidkgal, year(plpor.datvip));
      IsUpdateNeeded := True;
    }

    oExtAttr.sSetAttrId(9015, plpor.nrec, ATTRNAM_NREC_PLPOR_NOMER_V_BANKE, TmpPlPor.nodok);

    //Получаем дополнительные настройки
    var s: string = piExtAttr.sGetAttr(coBankDocF, BankDocF.Nrec, 'Дополнительные флаги');
    var w: word = if(s='', 0, Word(s));
    //Если установлена настойка - записываем в примечание номер и дату документа из выписки
    if (w and 1) = 1
    {
      PlPor.Podotchet := 'б/о №' + TmpPlPor.NoDok + ' от ' + TmpPlPor.DatVip;
      IsUpdateNeeded := true;
    }
    //Исполнитель договора
    if (w and 2) = 2
    {
      var c: comp = piExtAttr.coGetAttr(coBankDocF, BankDocF.Nrec, 'Исполнитель договора');
      piExtAttr.coSetAttr(coPlPor, PlPor.Nrec, 'Исполнитель договора', c, ShowKau(cgKau_KatPodr, c));
    }

    if IsUpdateNeeded
      update current PlPor;

    _loop plporSoprhoz
    {
      if getfirst plporSchfact = tsOk
      {
        if delete current plporSchfact = tsOk
          update current plporSoprhoz set plporSoprhoz.cschfact := 0;
      }
    }

    result := true;

  }
  else
    result := false;

}


Function GetNumber(_TiDkGal, _TiDk: word; _Descr: TDescr; _DatVip : date; _NoDok: string): string;
{

  case _TiDkGal of
    2: result := _NoDok;
  else
    result := inherited::GetNumber(_TiDkGal, _TiDk, _Descr, _DatVip, _NoDok);
  end;

}


sql query queryFindPlpor =
  select top 1 plpor.nrec
  from plpor
  join attrval avNumber on avNumber.wTable = 9015 and avNumber.cRec = plpor.nrec and
               avNumber.cAttrNam = :cattrnam and avNumber.vString = :nodok
  where plpor.tidk = :tidk and plpor.datvip = :datvip and plpor.sumplat = :sumplat and
        plpor.cplat = :cplat and plpor.cpol = :cpol
  order by plpor.datvip desc
;

Function PlPor_FindByDateVipSum: boolean;
{

  if not inherited::PlPor_FindByDateVipSum
  {

    if _qFindPlpor = nullref
      _qFindPlpor := queryManager.createQuery(queryFindPlpor);

    var rs: IResultSet;
    rs := _qFindPlpor.setParam('cattrnam', ATTRNAM_NREC_PLPOR_NOMER_V_BANKE)
                     .setParam('nodok'   , TmpPlPor.NoDok)
                     .setParam('tidk'    , TmpPlPor.tidk)
                     .setParam('datvip'  , TmpPlPor.datvip)
                     .setParam('sumplat' , TmpPlPor.sumplat)
                     .setParam('cplat'   , TmpPlPor.cplat)
                     .setParam('cpol'    , TmpPlPor.cpol)
                     .getResultSet;

    if rs.getFirst = tsOk
    {
      TmpPlPor.cPlPor := comp(rs.row.valAt(1));
      result := update current TmpPlPor = tsOk;
    }
    else
      result := false;

  }
  else
    result := true;

}

handleevent

cmIdle:
{
  inherited::handleevent(cmIdle);
  RefreshMarkedSum;
  RereadRecord(#BankDocF);
}

cmSelectAll:
{
  inherited::handleevent(cmSelectAll);
  RefreshMarkedSum;
  RereadRecord(#BankDocF);
}

cmUnSelectAll:
{
  inherited::handleevent(cmUnSelectAll);
  RefreshMarkedSum;
  RereadRecord(#BankDocF);
}

cmUpdateTotals:
{
  RefreshMarkedSum;
  RereadRecord(#BankDocF);
}

end;

end.
