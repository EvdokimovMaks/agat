#include Query.vih

#component "F_SOPRHOZ"
alter interface SoprHoz

var
  _qZakVibDO: IQuery;

create view
as select *
from mnplan TrudPotr
bounds ByStatusTrudPotr = SoprHoz.cSoprDoc /== TrudPotr.nRec and (TrudPotr.cStatus = 00010000000000E2h);

sql query qZakVibDO =
  select distinct SubString(av.vString, 1, InStr(' ', av.vString + ' ', 1) - 1) as Zakaz
  from SpStep
  join AttrVal av on av.wTable = 1104 and av.cRec = SpStep.Nrec and av.cAttrNam = #comp(0001000000000A87h) //заказ выбытия
  where SpStep.cStepDoc = :cStepDoc
;

function extInfoDO: string;
{
  result := '';
  var c: comp = SoprHoz.cStepDoc;
  _qZakVibDO.setParam('cStepDoc', c);
  if _qZakVibDO.execute.errorCode = tsOk
    while _qZakVibDO.fetch.errorCode = tsOk
      result += _qZakVibDO.row.valAt(1) + ' ';
}

function extInfo: string;
{
  result := '';
  case SoprHoz.TiDkGal of
    41, 43: result := extInfoDO;
  end;
}

extendformats brSoprHozTblSum;
Browse brSoprHozTblSum ('Разноска хозяйственных документов по ТХО. <Alt+B> - Ограничения.', hcHOFSoprHozDoc, scInterfHO02);
  Show at (, , , 22);
  Table SoprHoz;
  fields
    extInfo 'Доп. информация': [30], protect;
end;

handleevent
cminit:
{
  if CurTiDkGal = 1204
  {
    if (not BoundActive(tbByStatusTrudPotr)) AddBounds(tbByStatusTrudPotr);
  }
  else
  {
    if BoundActive(tbByStatusTrudPotr) SubBounds(tbByStatusTrudPotr);
  }
  _qZakVibDO := queryManager.createQuery(qZakVibDO);
  if (inherited::handleevent(cminit) = heAbort) Abort;
}
end;
end.
