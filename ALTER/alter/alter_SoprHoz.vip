#include Query.vih

#component "F_SOPRHOZ"
alter interface SoprHoz

var
  _qZakVibDO: IQuery;
  _qExtInfoPKP: IQuery;

function getStatus(w: word): string;
{
  result := case(w;
                 0: '(Оформл.)',
                 1: '(Исп.)',
                 2: '(Приост.)',
                 3: '(Раст.)';
                 '(Ст:' + w + ')');
}

create view
as select
  if(isValid(tnAgTopDogovor), agTopDogovor.NoDoc + getStatus(agTopDogovor.Status), '') +
  if(isValid(tnAgAppDogovor), ' ДС: ' + agAppDogovor.NoDoc + getStatus(agAppDogovor.Status), '') (fieldName = DogovorFld)
from
  mnplan TrudPotr
 ,Dogovor agTopDogovor
 ,Dogovor agAppDogovor
where ((
      SoprHoz.cDogovor    == agTopDogovor.Nrec
  and SoprHoz.cAppDogovor == agAppDogovor.Nrec
))
bounds ByStatusTrudPotr = SoprHoz.cSoprDoc /== TrudPotr.nRec and (TrudPotr.cStatus = 00010000000000E2h);

sql query qZakVibDO =
  select distinct SubString(av.vString, 1, InStr(' ', av.vString + ' ', 1) - 1) as Zakaz
  from SpStep
  join AttrVal av on av.wTable = 1104 and av.cRec = SpStep.Nrec and av.cAttrNam = #comp(0001000000000A87h) //заказ выбытия
  where SpStep.cStepDoc = :cStepDoc
;

function extInfoDO: string;
{
  result := '';
  _qZakVibDO.setParam('cStepDoc', SoprHoz.cStepDoc);
  if _qZakVibDO.execute.errorCode = tsOk
    while _qZakVibDO.fetch.errorCode = tsOk
      result += _qZakVibDO.row.valAt(1) + ' ';
}

sql query qExtInfoPKP =
  select distinct SubString(KatStroy.Name, 1, InStr(' ', KatStroy.Name + ' ', 1) - 1)
  from SpDocs
  join SpecMtr on SpecMtr.coTable = 1723 and SpecMtr.cSpec = SpDocs.Nrec
  join KatStroy on KatStroy.Nrec = SpecMtr.cObj
  where SpDocs.TiDk = 403 and SpDocs.cDoc = :cCalPlan
;

function extInfoPKP: string;
{
  result := '';
  _qExtInfoPKP.setParam('cCalPlan', SoprHoz.cSoprDoc);
  if _qExtInfoPKP.execute.errorCode = tsOk
    while _qExtInfoPKP.fetch.errorCode = tsOk
      result += _qExtInfoPKP.row.valAt(1) + ' ';
}

function extInfo: string;
{
  result := '';
  case SoprHoz.TiDkGal of
    41, 43: result := extInfoDO;
    403: result := extInfoPKP;
  end;
}

extendformats brSoprHozTblSum;
Browse brSoprHozTblSum ('Разноска хозяйственных документов по ТХО. <Alt+B> - Ограничения.', hcHOFSoprHozDoc, scInterfHO02);
  Show at (, , , 22);
  Table SoprHoz;
  fields
    DogovorFld 'Договор': [20], protect;
    extInfo 'Доп. информация': [30], protect;
end;

handleevent
cminit:
{
  if CurTiDkGal = 1204
  {
    if (not BoundActive(tbByStatusTrudPotr)) AddBounds(tbByStatusTrudPotr);
  }
  else
  {
    if BoundActive(tbByStatusTrudPotr) SubBounds(tbByStatusTrudPotr);
  }
  _qZakVibDO := queryManager.createQuery(qZakVibDO);
  _qExtInfoPKP := queryManager.createQuery(qExtInfoPKP);
  if (inherited::handleevent(cminit) = heAbort) Abort;
}
end;
end.
