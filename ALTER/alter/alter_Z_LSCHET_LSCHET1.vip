#include ExtAttr.vih
#include ExtAttrPicker.vih

#component "Z_LSCHET"

//карточка лицевого счета сотрудника
alter interface lschet1;

extendformats bankscreen;

var
  _extAttr: iExtAttr;
  _extAttrPicker: ExtAttrPicker;

//добавляем во вьюху поля для отображения корпоративной карты сотрудника
create view
as select _extAttr.sGetAttrID(coLschet, curlschet.nrec, ATTRNAM_NREC_LSCHET_KORPORATIVNAYA_KARTA) (fieldname=CorpCardFld)
;

//вкладка "Банк": добавляем поле для выбора корпоративной карты сотрудника
screen bankscreen;
fields
  CorpCardFld: protect, pickbutton;
<<






`№ корпоративной карты` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end;

handleevent

  //добавляем обработчик выбора корпоративной карты
  cmPick:
  {
    case curfield of
      #CorpCardFld: { _extAttrPicker.PickExtAttr(coLschet, curlschet.nrec, ATTRNAM_NREC_LSCHET_KORPORATIVNAYA_KARTA); rereadrecord; }
    else
      inherited::handleevent(cmPick);
    end;
  }

  //добавляем обработчик очистки корпоративной карты
  cmDelOnProtect:
  {
    case curfield of
      #CorpCardFld: { _extAttr.DeleteValue(coLschet, curlschet.nrec, ATTRNAM_NREC_LSCHET_KORPORATIVNAYA_KARTA); rereadrecord; }
    else
      inherited::handleevent(cmDelOnProtect);
    end;
  }
end;

end.
