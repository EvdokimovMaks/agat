#component "F_GETAN"

const
  cmAGATExtCMD1 = 9001;
  cmAGATExtCMD2 = 9002;
end;

alter interface GTCGKAU_OBJBUILD;

table struct tfltr ( nrec: comp ) with index (i01 = nrec (unique));

create view
as select *
from tfltr, tfltr tfltr1
bounds byExtFilter = katstroy.nrec /== tfltr.nrec;


handleevent
cmAGATExtCMD1:
{
  var kaureffDepNrec: comp;
  if (runinterface(Z_KATPODR::GetAnyPodr, kaureffDepNrec, 0) <> cmDefault) exit;

  delete all tfltr1;

  insert tfltr (nrec)
  select kaureff.crec
  from kaureff
  where ((
    2               == kaureff.wkau and
    kaureffDepNrec  == kaureff.ckau and
    2101            == kaureff.cotable
  )) ignore_errors(tsDuplicateKey);

  insert tfltr (nrec)
  select katstroy.cstroy
  from tfltr f, katstroy
  where ((
    f.nrec  /== katstroy.nrec and (katstroy.cstroy <> 0)
  )) ignore_errors(tsDuplicateKey);

  if (not BoundActive(tbbyExtFilter)) AddBounds(tbbyExtFilter);
  rereadrecord;
}
cmAGATExtCMD2:
{
  if (BoundActive(tbbyExtFilter)) subBounds(tbbyExtFilter);
  rereadrecord;
}
cmHotKeys:
{
  var s: string;
  s := if(FocusedFormat = BRGETKAU, 'F_GETAN::HotKeys_AllGtKau_ForTree_Browse', 'F_GETAN::HotKeys_AllGtKau_ForTree_Tree');
  s += ';Separator;'+'F_GETAN::GTCGKAU_OBJBUILD_AGAT_MENU';
  puthotcommand(rundynmenu(s));
}
end;
end.

GTCGKAU_OBJBUILD_AGAT_MENU menu
{
- 'Фильтр по подразделению', cmAGATExtCMD1;
- 'Снять фильтр', cmAGATExtCMD2;
}
