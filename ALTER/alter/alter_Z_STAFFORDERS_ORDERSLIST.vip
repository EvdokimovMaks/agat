#include Query.vih

#component "Z_STAFFORDERS"

alter interface ORDERSLIST;
extendformats BRWDOC;

sql query qContdocRSByTitledoc =
select distinct attrval.vstring
from partdoc
join contdoc on contdoc.cpart = partdoc.nrec
join attrval on attrval.wtable = 25045 and
                attrval.crec = contdoc.nrec and
                attrval.cattrnam = #comp(0001000000000920h) and
                attrval.vcomp <> #comp(0)
where partdoc.cdoc = :TitledocNrec
;

sql query qContdocZakazByTitledoc =
select distinct substring(attrval.vstring,1,instr(' ', attrval.vstring+' ', 1)-1) as vstring
from partdoc
join contdoc on contdoc.cpart = partdoc.nrec
join attrval on attrval.wtable = 25045 and
                attrval.crec = contdoc.nrec and
                attrval.cattrnam = #comp(0001000000000921h) and
                attrval.vcomp <> #comp(0)
where partdoc.cdoc = :TitledocNrec
;

var
  _queryZakaz: IQuery;
  _queryRS: IQuery;



function getExtAttrVString(aQuery: IQuery; aTitledocNrec: comp): string;
{
  var s: string; s := '';
  aQuery.setParam('TitledocNrec', aTitledocNrec);
  aQuery.Execute;
  while (aQuery.fetch.errorCode = tsOk)
  {
    s := if(s='', aQuery.row.ValAt(1), s + '; '+aQuery.row.ValAt(1));
  }
  result := s;
}

function getExtAttrRSVString(aTitledocNrec: comp): string;    cacheable;
{
  result := getExtAttrVString(_queryRS, aTitledocNrec);
}

function getExtAttrZakazVString(aTitledocNrec: comp): string; cacheable;
{
  result := getExtAttrVString(_queryZakaz, aTitledocNrec);
}

browse brwdoc;
table tmptitledoc;
fields
  [ExtAttrRSVstring] getExtAttrRSVString(tmpTitledoc.nrec) 'Расчетный счет': [10], protect;
  [ExtAttrZakazVstring] getExtAttrZakazVString(tmpTitledoc.nrec) 'Заказ': [10], protect;
end;

handleevent
cmInit:
{
  inherited::handleevent(cmInit);
  _queryRS := queryManager.createQuery(qContdocRSByTitledoc);
  _queryZakaz := queryManager.createQuery(qContdocZakazByTitledoc);
}
cmDefault:
{
  inherited::handleevent(cmDefault);
  ClearFuncCache;
}
end;
end.
