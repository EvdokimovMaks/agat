#include Query.vih

#component "F_PLPOR"

//Обслуживающий объект для платежек, как пишут галактисты "облегченный" -_-'
alter interface PLPORSIMPLE;

sql query qLastPlporNumber =
  select top 1 nodok
  from plpor
  where tidkgal = :tidkgal and year(datetime(datvip,0)) = :year
  order by nodok desc
;

overload
  //Перекрываем ф-цию получения нового номера для платежек
  Function ObjPlporSimple.PlPor_GetNewNumber (_TiDkGal : Word; _TiDk : Word; _Descr : tDescr; _DatVip : Date) : tNoDok;
end;

//Перекрываем ф-цию получения нового номера для платежек
//Если тип платежки - собств.пл.пор - берем номер последней платежки в году _DatVip и прибавляем 1,
//игнорируя все настройки. Требование Светланы.
Function ObjPlporSimple.PlPor_GetNewNumber (_TiDkGal : Word; _TiDk : Word; _Descr : tDescr; _DatVip : Date) : tNoDok;
{
  var newNumber: tNoDok;
  newNumber := inherited::PlPor_GetNewNumber(_TiDkGal, _TiDk, _Descr, _DatVip);

  if _TiDkGal = 1
  {
    var _DatVipYear: word;
    _DatVipYear := year(_DatVip);

    var rs: IResultSet;
    rs := queryManager.createQuery(qLastPlporNumber)
                      .setParam('tidkgal', _TiDkGal)
                      .setParam('year', _DatVipYear)
                      .getResultSet;
    if rs.getFirst = tsOk
      newNumber := NextNumStr(rs.row.valat(1));
    else
      newNumber := '000001';
  }

  result := newNumber;

}

end.
