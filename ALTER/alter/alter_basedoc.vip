#include ContractId.vih
#include SpstepHelper.vih
#component "L_BASEDOC"

alter interface VSCHET;

  var
    cidHelper: AGAT::ContractIDHelper;

  create view
  as select cidHelper.GetContractID(GetContractIDKey_ByKatstroy, specmtr.cobj) (fieldname=ContractID)
  ;
end.

alter interface VSCHETB;

  overload
    function CopyDO(nRecDO : Comp; naltype : Word; NRecDog : Comp; WithOutRes : Boolean; ChargeParam : Word; NeedNomMes : Word): comp;
    Function DoBaseDocDelete(DelSoprDoc : boolean; bPack: boolean = FALSE): word;
    Procedure PickFromKat;
  end;

  extendformats scspstep;

  var
    cDogovor_old, cAppDogovor_old, cCalPlan_old: comp;
    mw_ea: iExtAttr;
    cidHelper: AGAT::ContractIDHelper;
    _spstepHelper: AGAT::SpstepHelper;

  create view vAttrVal
  var _AvNrec: comp;
  as select *
  from attrval
  where ((
    _AvNrec == attrval.nrec
  ));

  function isVisaSet(_visaValueNrec: comp): boolean;
  {
    result := _visaValueNrec = VIZA_SOGLASOVANO
           or _visaValueNrec = VIZA_NE_TREBUETSA
           or _visaValueNrec = VIZA_S_OBSHIH_SCHETOV;
  }

  function maxDateIn(d1: date; d2: date; d3: date = 0; d4: date = 0; d5: date = 0; d6: date = 0): date;
  {
    var d: date; d := 0;
    if (d1 > d) d := d1;
    if (d2 > d) d := d2;
    if (d3 > d) d := d3;
    if (d4 > d) d := d4;
    if (d5 > d) d := d5;
    if (d6 > d) d := d6;
    result := d;
  }

  function isStatusVklVReestr(_statusNrec: comp): boolean;
  {
    result := _statusNrec = STATUS_VKL_V_REESTR or _statusNrec = STATUS_OPLACHEN;
  }

  function GetDaysDeferredPay: longint;
  {
    var _cd: date;
    _cd := cur_date;
    var BuhViza, KazViza, DirViza, PeuViza: comp;
    BuhViza := mw_ea.coGetAttrID(WT_BASEDOC, basedoc.nrec, ATTRNAM_BUH);
    KazViza := mw_ea.coGetAttrID(WT_BASEDOC, basedoc.nrec, ATTRNAM_KAZN);
    DirViza := mw_ea.coGetAttrID(WT_BASEDOC, basedoc.nrec, ATTRNAM_DIR);
    PeuViza := mw_ea.coGetAttrID(WT_BASEDOC, basedoc.nrec, ATTRNAM_PEU);
    var BuhVizaDate, KazVizaDate, DirVizaDate, PeuVizaDate: date;
    vAttrVal._AvNrec := BuhViza;
    if vAttrVal.getfirst attrval = tsOk { BuhVizaDate := attrval.ATL_LASTDATE };
    vAttrVal._AvNrec := KazViza;
    if vAttrVal.getfirst attrval = tsOk { KazVizaDate := attrval.ATL_LASTDATE };
    vAttrVal._AvNrec := DirViza;
    if vAttrVal.getfirst attrval = tsOk { DirVizaDate := attrval.ATL_LASTDATE };
    vAttrVal._AvNrec := PeuViza;
    if vAttrVal.getfirst attrval = tsOk { PeuVizaDate := attrval.ATL_LASTDATE };
    result := if(
                  isVisaSet(BuhViza) and
                  isVisaSet(KazViza) and
                  isVisaSet(DirViza) and
                  isVisaSet(PeuViza) and
                  not isStatusVklVReestr(basedoc.cnote),
                  calcdaysbetweendates(maxDateIn(BuhVizaDate, KazVizaDate, DirVizaDate, PeuVizaDate), _cd, true),
                  0
                );
  }

  create view
  var
    rzdTax: double;
    rzdTax18: double;
    rzdTax10: double;
  as select cidHelper.GetContractID(GetContractIDKey_ByKatstroy, specmtr.cobj) (fieldname=ContractID),
            GetDaysDeferredPay() (fieldname=DaysDeferredPay)
  from attrval mwav_naznpl
  where ((1102 == mwav_naznpl.wtable and basedoc.nrec == mwav_naznpl.crec and ATTRNAM_NAZNPL == mwav_naznpl.cattrnam))
  ;

  create view mw_v
  as select *
  from attrval av, basedoc bd, dogovor, stepdoc sd, spstep sps, specmtr sm, katstroy ks, katsopr;

  Procedure PickFromKat;
  {
    inherited::PickFromKat;
    _spstepHelper.SetSpstepStZatrByBasedoc(basedoc.nrec);
  }

  Function PermitDelete: boolean;
  {
    var _s: string;
    var _c: comp;
    _c := basedoc.nrec;
    _s := '';
    if (mw_v.getfirst av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_SOGL == av.cattrnam)) = tsOk)
      _s := trim(mw_v.av.vstring);
    result := not(_s <> '' and basedoc.desgr <> sgettune('USER.DESCR'));
  }

  Function DoBaseDocDelete(DelSoprDoc : boolean; bPack: boolean = FALSE): word;
  {
    result := 0;
    if not PermitDelete
    {
      message('Запрещено удалять счета, созданные другим пользователем, переданные на согласование');
      result := 0;
    }
    else
    {
      result := inherited::DoBaseDocDelete(DelSoprDoc,bPack);
    }
  }

  function CopyDO(nRecDO : Comp; naltype : Word; NRecDog : Comp; WithOutRes : Boolean; ChargeParam : Word; NeedNomMes : Word): comp; {
    var _c: comp;
    _c := inherited::CopyDO(nRecDO,naltype,NRecDog,WithOutRes,ChargeParam,NeedNomMes);
    if _c <> 0 {
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_SOGL   == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_BUH    == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_DIR    == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_KAZN   == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_PEU    == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_REZERV == av.cattrnam));

      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_PRBUH  == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_PRKAZ  == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_PRDIR  == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_PRPEU  == av.cattrnam));

      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_BUHSCH == av.cattrnam));
      mw_v.delete all av where ((1102 == av.wtable and _c == av.crec and ATTRNAM_BDR    == av.cattrnam));

      mw_v.update bd where ((_c == bd.nrec)) set bd.dform := cur_date;
    }
    result := _c;
  }

//#region Расчет налогов по билетам РЖД

  procedure RZDRecalc;
  {
    set rzdTax18 := round( (spstep.summa-11*rzdTax)/(118/18-11) , 6);
    set rzdTax10 := rzdTax - rzdTax18;
  }

  procedure RZDRefresh;
  {
    RZDRecalc;
    rescanpanel(#spstep);
  }

  procedure RZDAddTax(grnalNrec: comp; percent: double; sum: double);
  {
    clearbuffer (#spdocnal);
    spdocnal.cspdoc    := spstep.nrec;
    spdocnal.cdoc      := basedoc.nrec;
    spdocnal.tipdoc    := 1101;
    spdocnal.cgrnal    := grnalNrec;
    spdocnal.cnalog    := 2;
    spdocnal.nalog     := percent;
    spdocnal.sumnal    := sum;
    spdocnal.cval      := 0;
    spdocnal.sumval    := sum;
    spdocnal.summa     := sum;
    spdocnal.corg      := basedoc.corg;
    spdocnal.valcurse  := 1;
    spdocnal.croscurse := 1;
    spdocnal.iscustom  := 6;
    insert current spdocnal;
  }

  procedure RZDSetTaxes;
  {
    var grnalNrec: comp;
    if rzdTax10 = 0 and rzdTax18 != 0
      grnalNrec := 4001A6F2CE900165h //НДС 18%
    else if rzdTax10 != 0 and rzdTax18 = 0
      grnalNrec := 4001A573A6E5F4D3h //НДС 10%
    else
      grnalNrec := 0001000000000006h; //НДС10,18

    spstep.nds       := rzdTax;
    spstep.cgrnal    := grnalNrec;
    spstep.manualtax := 1;
    update current spstep;

    delete all spdocnal where ((spstep.nrec == spdocnal.cspdoc));

    if rzdTax10 != 0 RZDAddTax(grnalNrec, 10, rzdTax10);
    if rzdTax18 != 0 RZDAddTax(grnalNrec, 18, rzdTax18);

  }

  window wGetRZDTaxSum 'Введите сумму налогов по билету', doAccept, escClose;
  show (,,45,8);
  screen scRZDTaxCalc;
  fields
    spstep.summa: ['[|-]3666`666`666`666.88\2p'], left, skip;
    rzdTax      : ['[|-]3666`666`666`666.88\2p'], noprotect;
    rzdTax10    : ['[|-]3666`666`666`666.88\2p'], left, skip;
    rzdTax18    : ['[|-]3666`666`666`666.88\2p'], left, skip;
  buttons
    cmRZDRecalc;
    cmRZDSetTaxes, default;
    cmCancel;
<<

  `Сумма по позиции`.@@@@@@@@@@@@@@@@@@@@
  `Сумма налогов`   .@@@@@@@@@@@@@@@@@@@@
  `Налог 10%:`      .@@@@@@@@@@@@@@@@@@@@
  `Налог 18%:`      .@@@@@@@@@@@@@@@@@@@@

<. Пересчет .> <.Установить .> <. Отмена .>
>>
  end;
  handleevent
    cmInit: { rzdTax := spstep.nds; RZDRefresh;  }
    cmCheckField: { RZDRefresh }
    cmRZDRecalc: { RZDRefresh }
    cmRZDSetTaxes: { RZDRefresh; putcommand(cmDefault); }
  end;
  end; //window wGetRZDTaxSum

  window wspstep;
    Panel p1;
    Table SpStep;

    screen scspstep;
    buttons
      cmCalcTaxesRZD;
<<







                                                                               <. Расчет налогов по билетам .>
>>
    end; //screen

    end; //panel

    handleevent
      cmCalcTaxesRZD:  {
        if (runwindowmodal(wGetRZDTaxSum) = cmDefault and rzdTax10 >=0 and rzdTax18 >= 0) RZDSetTaxes;
        rescanpanel(#spstep);
        rereadrecord(#spdocnal);
      }
    end;
  end;

//#endregion

  window wdopattr;
    tableevent table basedoc;
      cmPositionChanged: {
        inherited::handleevent(cmPositionChanged);
        cDogovor_old    := basedoc.cdogovor;
        cAppDogovor_old := basedoc.cappdogovor;
        cCalPlan_old    := basedoc.ccalplan;
      }
      cmInsert: {
        inherited::handleevent(cmInsert);
        cDogovor_old    := basedoc.cdogovor;
        cAppDogovor_old := basedoc.cappdogovor;
        cCalPlan_old    := basedoc.ccalplan;
      }
    end;

    handleevent
      cmpick: {
        case curfield of
          #dognodoc, #APPDOGNODOC, #calplan.nodoc: {
            if (basedoc.status = 2) and (basedoc.cdogovor <> cDogovor_old) or (basedoc.cappdogovor <> cAppDogovor_old) or (basedoc.ccalplan <> cCalPlan_old) {
              basedoc.cnote  := '40009D44C1790C6Bh';
              basedoc.status := 1;
              update current basedoc;
              stepdoc.status := 1;
              update current stepdoc;
              if (cDogovor_old > 0) {
                message('Счет переведен в статус "Оформляемый"');
              }
            }
            cDogovor_old    := basedoc.cdogovor;
            cAppDogovor_old := basedoc.cappdogovor;
            cCalPlan_old    := basedoc.ccalplan;
          }
        end;
      }
    end; //handleeven
  end; //window

  handleevent
    cmCheckField:
    {
      case curfield of
        #mwav_naznpl.vstring:
        {
          if isvalidall(#mwav_naznpl)
          {
            update current mwav_naznpl;
          }
          else
          {
            var s: string;
            s := mwav_naznpl.vstring;
            clearbuffer(#mwav_naznpl);
            mwav_naznpl.vstring := s;
            insert current mwav_naznpl;
          }
        }
      end;
    }
    cmpick: {
      case curfield of
        #mwav_naznpl.vstring:
        {
          if basedoc.nrec <> 0
          {
            /*
            var s: string;
            s := 'По счету №' + basedoc.nodoc + ' от ' + datetostr(basedoc.ddoc,'DD.MM.YYYY');
            s += ' ' + basedoc.name;
            s += ' дог/контр ' + DOGNODOC + ' от ' + datetostr(DOGDDOC, 'DD.MM.YYYY');
            if (basedoc.nds > 0)
              s += if(basedoc.vhodnal = 1, ', в т.ч. ', ', кроме того ') + 'НДС ' + doubletostr(basedoc.nds,'[|-]3666 666 666 666.88');
            */
            var s1,s2,s3, DDS, Prim, Dog, Schet, Isp, Etap, Akt: string;
            s1 := ''; s2 := ''; s3 := '';
            var Avans, Predopl: boolean;
            var _c: comp;
            DDS     := trim(if(getanykau(0, 10009, mw_ea.coGetAttrID(1102, basedoc.nrec, 000100000000008Fh)), givenanname(2),''));
            Isp     := trim(if(getanykau(0, 2    , mw_ea.coGetAttrID(1102, basedoc.nrec, 000100000000005Bh)), givenanname(5),''));
            Avans   := upcase(trim(if(getanykau(0, 10008, mw_ea.coGetAttrID(1102, basedoc.nrec, 000100000000008Dh)), givenanname(2),''))) = 'А';
            Predopl := BaseDoc.VidDoc = 111;
            Prim    := BaseDoc.Name;
            if (substr(Prim,1,1) <> locase(substr(Prim,1,1)) and substr(Prim,2,1) <> upcase(substr(Prim,2,1))) //если у примечания первая буква большая, а вторая маленькая
              Prim := locase(substr(Prim,1,1)) + substr(Prim,2,255);
            //_c := if(BaseDoc.cAppDogovor <> 0, BaseDoc.cAppDogovor, BaseDoc.cDogovor);
            _c := BaseDoc.cDogovor;
            if mw_v.getfirst Dogovor where ((_c == Dogovor.nRec)) = tsOk
              Dog   := 'договор '+mw_v.Dogovor.NoDoc+' от '+datetostr(mw_v.Dogovor.DDoc,'DD/MM/YY')
            else
              Dog   := '';
            Schet   := 'По счету №'+Basedoc.Nodoc+' от '+datetostr(Basedoc.DDoc,'DD/MM/YY');

            _c := BaseDoc.nRec;
            Etap  := '';
            if mw_v.getfirst sd where ((_c == sd.cbasedoc)) = tsOk
              mw_v._loop sps where ((sd.nrec == sps.cstepdoc))
              {
                if mw_v.getfirst sm where ((0001000000000001h == sm.csaldtune and 1104 == sm.cotable and sps.nrec == sm.cspec)) = tsOk
                  if mw_v.getfirst ks where ((sm.cobj == ks.nrec)) = tsOk
                  {
                    Etap := 'заказ '+substr(mw_v.ks.name,1,pos(' ',mw_v.ks.name+' '));
                    break;
                  }
              }
            Etap  := '';  //заказ не нужен, но код оставлю

            Akt     := '';
            _c := stepdoc.nrec;
            mw_v._loop katsopr where ((_c == katsopr.cstepdoc))
            {
              Akt   += if(Akt<>'',', ','') + if(mw_v.katsopr.vidsopr=101,'накладная', 'акт') + ' №' + mw_v.katsopr.nsopr + ' от ' + datetostr(mw_v.katsopr.dsopr,'DD/MM/YY');
            }
            case Isp of
            '44201':
            {
              case DDS of
                '204001','204002','204003','204004','204005','204006','204007','204008','204009','210301', '210303', '210800', '403002':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := if(Avans and Predopl, 'аванс', '');
                }
              end;
            }
            '44202':
            {
              case DDS of
                '204001':
                {
                  s1 := Schet + if(Dog <> '', ' ' + Dog, '');
                  s2 := Prim;
                  s3 := if(Avans, 'аванс', '');
                }
                '206006':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := if(Avans and Predopl, 'аванс', '');
                }
              end;
            }
            '100', '300', '480', '490', '900':
            {
              case DDS of
                '205000':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := Dog + if(Etap<>'', ' ' + Etap, '') + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
                }
              end;
            }
            '090':
            {
              case DDS of
                '205000':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := Dog + if(Etap<>'', ' ' + Etap, '') + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
                }
                '210407', '210701', '210702':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := if(Avans and Predopl, 'аванс', '');
                }
                '206002':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := Dog + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
                }
              end;
            }
            '777':
            {
              case DDS of
                '210305', '210306':
                {
                  s1 := Schet;
                  s2 := Prim;
                }
                '210404', '210407':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := Dog + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
                }
              end;
            }
            '77706':
            {
              case DDS of
                '209001', '209002', '209004':
                {
                  s1 := Schet;
                  s2 := Prim;
                }
              end;
            }
            '77703':
            {
              case DDS of
                '210602':
                {
                  s1 := Schet + if(Dog<>'', ' '+Dog, '');
                  s2 := Prim;
                  s3 := if(Avans, 'аванс', '') + if(Akt<>'', ' ' + Akt, '');
                }
              end;
            }
            '762':
            {
              case DDS of
                '207101', '207102', '207103', '208002':
                {
                  s1 := Schet;
                  s2 := Prim;
                  s3 := Dog;
                }
              end;
            }
            '640':
            {
              case DDS of
                '401201', '401202', '401400':
                {
                  s1 := Schet + if(Dog<>'', ' '+Dog, '');
                  s2 := Prim;
                  s3 := if(Avans, 'аванс', '');
                }
                '401101', '401102', '401301', '401302', '403001', '403003', '404000':
                {
                  s1 := Schet + if(Dog<>'', ' '+Dog, '');
                  s2 := Prim;
                  s3 := if(Avans, 'аванс', '') + if(Akt<>'', ' ' + Akt, '');
                }
              end;
            }
            end;

            if s1='' and s2='' and s3=''
            {
              s1 := Schet;
              s2 := Prim;
            }

            if not isvalidall(#mwav_naznpl)
            {
              clearbuffer(#mwav_naznpl);
              insert current mwav_naznpl;
            }

            if (basedoc.nds > 0)
              s3 += if(s3<>'',' ','') + 'НДС ' + doubletostr(round(basedoc.nds,2),'[|-]3666 666 666 666.88')
            else
              s3 += if(s3<>'',' ','') + 'НДС не облагается';
            mwav_naznpl.vstring := s1+if(s2<>'', ' '+s2,'')+if(s3<>'',' '+s3,'');
            update current mwav_naznpl;
            rescanpanel(#basedoc);
            rescanpanel(#mwav_naznpl);
          }
        }
      end;
    }
  end;
end.
