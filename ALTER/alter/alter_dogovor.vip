//Issue #000 Дополнительные поля по договору
//Issue #000 Кнопки Directum
//Issue #??? Установка статьи затрат в ЦУ к новой позиции договора из атрибута шапки договора
//Issue #327 Перенос статусов ПКП в момент создания отменяющего соглашения из предыдущего

#include ExtAttr.Vih
#include ExtClassEx.vih
#include SaldTune.vih
#include ContractID.vih
#include Dir_ExpImp_OnLine.Vih

#component "L_DOGOVOR"

alter interface dogovor;

overload
  Procedure CancelCurrentDogovor;
end;

ExtendFormats BRDOGOVORSELECTMAIN;

table struct ag_TmpNotes (
  cCalPlan: comp,
  Status: word,
  cNote: comp
) with index (
  i01 = cCalPlan
);

var
  cidHelper: AGAT::ContractIDHelper;
  ButSP : longint;
  iDir_ExpImp_OnLine: Dir_ExpImp_OnLine;
  #include ExtAttr.var
  #include SaldTune.var

#declare JoinDogovorAttrval(cattrnam, avAlias)
  coDogovor       == #avAlias.wtable and
  dogovor1.nrec   == #avAlias.crec and
  #cattrnam       == #avAlias.cattrnam
#end

create view
as select
  avVidDey.vString,
  avZakaz.vString,
  avPrinadl.vString,
  avRynok.vString,
  avPotreb.vString,
  avISK.vString,
  avSMP.vString,
  avStZ.vString
  //cidHelper.GetContractID(GetContractIDKey_ByDogovor, dogovor.nrec) (fieldname=ContractID)
from attrval avVidDey,
     attrval avZakaz,
     attrval avIGK,
     exclassval cvIGK,
     exclassseg csIGK,
     attrval avPrinadl,
     attrval avRynok,
     attrval avPotreb,
     attrval avISK,
     attrval avSMP,
     attrval avStZ,
     ag_TmpNotes
where ((
      #JoinDogovorAttrval(281474976710829, avVidDey)
  and #JoinDogovorAttrval(281474976710827, avZakaz)
  and 2101            == avIGK.wtable
  and avZakaz.vComp   == avIGK.crec
  and 281474976711884 == avIGK.cattrnam and (avZakaz.vComp <> 0)
  and #JoinDogovorAttrval(281474976710833, avPrinadl)
  and #JoinDogovorAttrval(281474976710832, avRynok)
  and #JoinDogovorAttrval(281474976710831, avPotreb)
  and #JoinDogovorAttrval(281474976712383, avISK)
  and #JoinDogovorAttrval(281474976712384, avSMP)
  and #JoinDogovorAttrval(281474976710835, avStZ)

  //Статья затрат капстроя по статье затрат 2013 из внешнего атрибута к договору
  and FPKATIMP_CODE_STZ2013_STZSTROY == FpKatImp.Code
  and FpKatImp.Nrec                  == FpImpRel.cMain
  and cgKau_StZ2013                  == FpImpRel.ImpKodAn
  and avStZ.vComp                    == FpImpRel.cImpMean
))
;

//Для статусов ПКП при формировании отменяющего соглашения
//см. CancelCurrentDogovor
create view vCancelD
  var
    _cDogovor: comp;
from
  Dogovor ChildDog
 ,TmpDogKOD
 ,CalPlan
 ,ag_TmpNotes
where ((
      vCancelD._cDogovor == ChildDog.cDogovor
  and TmpDogKOD.Nrec     == CalPlan.cDogovor
  and CalPlan.Nrec       == ag_TmpNotes.cCalPlan
));

//Формирует отменяющее соглашение к договору (#327)
Procedure CancelCurrentDogovor;
{
  var bChangeStatus: boolean = true; //Признак - менять ли статусы ПКП отменяющего соглашения на Оформляемый
  //анализируем настройку Агат.Управление договорами / Режим установки статусов ПКП при формировании отм.согл.
  case wGetTune('AGAT.DOGOVOR.OTMSOGL_PKP_STATUS') of
    0: bChangeStatus := false;
    1: bChangeStatus := true;
    2: bChangeStatus := Message('Изменить статусы ПКП отменяющего соглашения на "Оформляемый"', YesNo) = cmYes;
  end;

  if not bChangeStatus
  {
    //Запоминаем статусы ПКП договора и его подчиненных договоров
    mtClear(tnag_TmpNotes, mfNormal);
    var c: comp = Dogovor.Nrec;
    var i: longint = 0;
    var m: TPtr = InitMarker('', 8, 10, 10, false);
    InsertMarker(m, c);
    for (i := 0; GetMarker(m, i, c); i++)
    {
      insert ag_TmpNotes (cCalPlan, Status, cNote)
      select CalPlan.Nrec, CalPlan.Status, CalPlan.cNote
      from CalPlan
      where (( c == CalPlan.cDogovor ));

      vCancelD._cDogovor := c;
      vCancelD._loop ChildDog
        InsertMarker(m, vCancelD.ChildDog.Nrec);
    }
    DoneMarker(m, '');

    //запускаем стандартное формирование отменяющего соглашения
    inherited::CancelCurrentDogovor;

    //Восстанавливаем статусы ПКП договора и подчиненных договоров
    vCancelD._loop TmpDogKOD
      vCancelD._loop CalPlan
        if vCancelD.getfirst ag_TmpNotes = tsOk
          if vCancelD.ag_TmpNotes.Status != vCancelD.CalPlan.Status or
             vCancelD.ag_TmpNotes.cNote  != vCancelD.CalPlan.cNote
          {
            vCancelD.update current CalPlan set Status := vCancelD.ag_TmpNotes.Status,
                                                cNote  := vCancelD.ag_TmpNotes.cNote;
          }
  }
  else
    inherited::CancelCurrentDogovor;
}

procedure pickStZatr;
{
  var c: comp = oExtAttr.coGetAttrId(coDogovor, Dogovor.Nrec, ATTRNAM_NREC_DOGOVOR_STZ);
  if iGetKau.GetCodeKau(cgiPick, cgKau_StZ2013, c) > 0
    oExtAttr.coSetAttrId(coDogovor, Dogovor.NRec, ATTRNAM_NREC_DOGOVOR_STZ, c, ShowKau(cgKau_StZ2013, c));
  RescanPanel(tnDogovor);
}

//#region Визуальная часть

tree BRDOGOVORSELECTMAIN (,hcDogViewListDogovor,sci1478EnEscTreeI);
table dogovor1;
fields
  katvidd.name       'Вид'#13'договора'               : [10], protect;
  avVidDey.vString   'Вид'#13'деятельности'           : [10], protect;
  avZakaz.vString    'Номер'#13'заказа'               : [10], protect;
  avIGK.vString      'ИГК'                            : [10], protect, { font = { backColor = if(FIGK <> avIGK.vString, 12, 0); } };
  avPrinadl.vString  'Принадлежность'#13'продукции'   : [10], protect;
  avRynok.vString    'Рынок'                          : [10], protect;
  avPotreb.vString   'Тип конечного'#13'потребителя'  : [10], protect;
  avISK.vString      'ИСК'                            : [10], protect;
  avSMP.vString      'СМП'                            : [10], protect;
  avStZ.vString      'Статья расходов'                : [10], protect;
end;

//Окно редактирования договора
window WIDOGOVOREDITMAIN;
  HandleEvent
    cmInit:
    {
      if inherited :: handleEvent (cmInit)=heAbort then abort;
      cfsSetProp('butSP2','Title',
           if(piExtAttr.sGetAttr(coDogovor,Dogovor1.nrec,'Directum ID')  <>''
               ,'Карточка в DIR(отпр.)','Карточка в Директум') );
    }
    cmValue60:
    { iDir_ExpImp_OnLine.LaunchDIRECTUM_EDoc('',400,dogovor.nrec,'SUBTYPE=EDIT')

    }
    cmValue59:
    { iDir_ExpImp_OnLine.LaunchDIRECTUM_EDoc('',400,dogovor.nrec,'SUBTYPE=LINK')
    }
    cmValue58:
    { iDir_ExpImp_OnLine.LaunchDIRECTUM_EDoc('',400,dogovor.nrec,'SUBTYPE=KART')
    }
    cmAlt1: pickStZatr;
  end;
end;

//Окно редактирования ДС
Window wiDogovorAttachMain;
  HandleEvent
    cmInit:
    {
      if inherited :: handleEvent (cmInit)=heAbort then abort;
      cfsSetProp('butSP5','Title',
           if(piExtAttr.sGetAttr(coDogovor,Dogovor1.nrec,'Directum ID')  <>''
               ,'Карточка в DIR(отпр.)','Карточка в Директум') );
    }
    cmValue60:
    { iDir_ExpImp_OnLine.LaunchDIRECTUM_EDoc('',401,dogovor.nrec,'SUBTYPE=EDIT')

    }
    cmValue59:
    { iDir_ExpImp_OnLine.LaunchDIRECTUM_EDoc('',401,dogovor.nrec,'SUBTYPE=LINK')
    }
    cmValue58:
    { iDir_ExpImp_OnLine.LaunchDIRECTUM_EDoc('',401,dogovor.nrec,'SUBTYPE=KART')
    }
    cmAlt1: pickStZatr;
  end;
end;

//#endregion Визуальная часть


Panel paSpDog
  Table SpDocs;
  HandleEvent // Panel paSpDog;
    cmSetDefault:
    {
      if Inherited::HandleEvent(cmSetDefault) = heAbort
        Abort;
      else
        if getfirst avStZ = tsOk
        if avStZ.vComp != comp(0)
        if getfirst FpKatImp = tsOk
        if getfirst FpImpRel = tsOk
        {
          var vcSaldTune: comp = coGetTune('CELUCHFORMODUL.BUY');
          oMTRFun.InsSpecMTR_Kau(
            coSpDocs
          , SpDocs.Nrec
          , vcSaldTune
          , comp(0)
          , FpImpRel.cMean
          , oTune.GetDefKauRef(vcSaldTune, 2)
          , oTune.GetDefKauRef(vcSaldTune, 3)
          , oTune.GetDefKauRef(vcSaldTune, 4)
          , oTune.GetDefKauRef(vcSaldTune, 5)
          , oTune.GetDefKauRef(vcSaldTune, 6)
          , oTune.GetDefKauRef(vcSaldTune, 7)
          , oTune.GetDefKauRef(vcSaldTune, 8)
          , oTune.GetDefKauRef(vcSaldTune, 9)
          );
        }
    }
  end; //HandleEvent
end; //Panel paSpDog


//=============================
handleevent

  cmInit:
  {
    if inherited :: handleEvent (cmInit)=heAbort then abort;

    //Подавление предупреждений компилятора
    if false
    {
      if getfirst csigk = tsOk {}
      if getfirst cvigk = tsOk {}
    }

    SetColumnTitle(BRDOGOVORSELECTMAIN, #DOGOVOR1.NODOC_EXT, 'Шифр');

    #include cnf_koef.vpp
    // добавить кнопку для импорта
    cfsCreateObject ('button','SCDOGOVOREDITMAIN','butSP',FALSE,ButSP);
    cfsSetProp('butSP','Origin_X',279*wkoefx);
    cfsSetProp('butSP','Origin_Y',245*wkoef);
    cfsSetProp('butSP','Size_X',123*wkoefx);
    cfsSetProp('butSP','Size_Y',22*wkoef);
    cfsSetProp('butSP','Title','Скан-Копия');
    cfsSetProp('butSP','StatusCtx',sci13Esc);
    cfsSetProp('butSP','Command',cmValue60);

    // добавить кнопку для импорта
    cfsCreateObject ('button','SCDOGOVOREDITMAIN','butSP1',FALSE,ButSP);
    cfsSetProp('butSP1','Origin_X',279*wkoefx);
    cfsSetProp('butSP1','Origin_Y',270*wkoef);
    cfsSetProp('butSP1','Size_X',123*wkoefx);
    cfsSetProp('butSP1','Size_Y',22*wkoef);
    cfsSetProp('butSP1','Title','Связанные локументы');
    cfsSetProp('butSP1','StatusCtx',sci13Esc);
    cfsSetProp('butSP1','Command',cmValue59);

    // добавить кнопку для импорта
    cfsCreateObject ('button','SCDOGOVOREDITMAIN','butSP2',FALSE,ButSP);
    cfsSetProp('butSP2','Origin_X',279*wkoefx);
    cfsSetProp('butSP2','Origin_Y',295*wkoef);
    cfsSetProp('butSP2','Size_X',123*wkoefx);
    cfsSetProp('butSP2','Size_Y',22*wkoef);
    cfsSetProp('butSP2','Title','Карточка в Директум');
    cfsSetProp('butSP2','StatusCtx',sci13Esc);
    cfsSetProp('butSP2','Command',cmValue58);
    //===================

    cfsCreateObject ('button','SCDOGOVORATTACHMAIN','butSP3',FALSE,ButSP);
    cfsSetProp('butSP3','Origin_X',395*wkoefx);
    cfsSetProp('butSP3','Origin_Y',245*wkoef);
    cfsSetProp('butSP3','Size_X',123*wkoefx);
    cfsSetProp('butSP3','Size_Y',22*wkoef);
    cfsSetProp('butSP3','Title','Скан-Копия');
    cfsSetProp('butSP3','StatusCtx',sci13Esc);
    cfsSetProp('butSP3','Command',cmValue60);

    // добавить кнопку для импорта
    cfsCreateObject ('button','SCDOGOVORATTACHMAIN','butSP4',FALSE,ButSP);
    cfsSetProp('butSP4','Origin_X',395*wkoefx);
    cfsSetProp('butSP4','Origin_Y',270*wkoef);
    cfsSetProp('butSP4','Size_X',123*wkoefx);
    cfsSetProp('butSP4','Size_Y',22*wkoef);
    cfsSetProp('butSP4','Title','Связанные документы');
    cfsSetProp('butSP4','StatusCtx',sci13Esc);
    cfsSetProp('butSP4','Command',cmValue59);

    // добавить кнопку для импорта
    cfsCreateObject ('button','SCDOGOVORATTACHMAIN','butSP5',FALSE,ButSP);
    cfsSetProp('butSP5','Origin_X',395*wkoefx);
    cfsSetProp('butSP5','Origin_Y',295*wkoef);
    cfsSetProp('butSP5','Size_X',123*wkoefx);
    cfsSetProp('butSP5','Size_Y',22*wkoef);
    cfsSetProp('butSP5','Title','Карточка в Директум');
    cfsSetProp('butSP5','StatusCtx',sci13Esc);
    cfsSetProp('butSP5','Command',cmValue58);


  }

  cmAlt1: pickStZatr;

end; //handleevent


end.
