#component "Z_KATPODR"

alter interface SALARYDEPARTMENTSSELECTOR;

create view
var
  _sDepartmentsKod: string[20];
from AllMemo AllMemoMarkList, sDepartments sDepartmentsByKod
where ((
  coX$Users  == AllMemoMarkList.wTable and
  UserId     == AllMemoMarkList.cRec and
  word(-101) == AllMemoMarkList.Npp and
  _sDepartmentsKod == sDepartmentsByKod.Kod
));

file fMarkList;

window wMarkList 'Пометка списка подразделений' escClose, doAccept;
show(,,40,);
text AllMemoMarkList.Mem;
  show(,,,20);

screen sc1;
  show(,21,,) fixed_y;
  buttons
    cmDoOk, default;
    cmCancel;
<<
          <. Продолжить .> <. Отмена .>
>>
end;

handleevent
  cmDoOk: putcommand(cmDefault);
end;

end; //window wMarkList

windowevent SelectDepartmentsWindow;

cmValue9:
{

  if getfirst AllMemoMarkList != tsOk
  {
    ClearBuffer(tnAllMemoMarkList);
    AllMemoMarkList.wTable := coX$Users;
    AllMemoMarkList.cRec := UserId;
    AllMemoMarkList.Npp := word(-101);
    insert current AllMemoMarkList;
  }

  if runwindowmodal(wMarkList) = cmDefault
  {
    update current AllMemoMarkList;

    if getfirst AllMemoMarkList = tsOk
    if fMarkList.OpenMemo(AllMemoMarkList.Mem)
    {
      var undefinedPodr: string;
      undefinedPodr := '';

      while not fMarkList.Eof
      {
        fMarkList.ReadLn(_sDepartmentsKod);
        _sDepartmentsKod := trim(_sDepartmentsKod);
        if _sDepartmentsKod != ''
        {
          if getfirst sDepartmentsByKod = tsOk
            InsertMarker(Marker, sDepartmentsByKod.Nrec);
          else
            undefinedPodr += ' ' + _sDepartmentsKod;
        }
      }
      fMarkList.Close;

      if undefinedPodr != ''
        message('Не найдены подразделения ' + undefinedPodr);
    }

    rereadrecord(#sDepartments);

  }

}

cmHotkeys:
  PutHotCommand(RunDynMenu('Z_KATPODR::GetPodalnContext;SEPARATOR;Z_KATPODR::GetPodalnContext_Agat'));

end;  //windowevent

end.

GetPodalnContext_Agat menu
{
- 'Пометка списка подразделений по кодам', cmValue9,
  'Пометка списка подразделений по кодам', hcNoContext, '', , sci1Esc;
}
