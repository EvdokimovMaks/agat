#include ExtAttr.vih

#component "Z_PAYREP"

alter interface PICKOTPFLIST;

ExtendFormats BPICKOTP;

const
  attrnam_contdoc_zakaz = 0001000000000921h;
  attrnam_contdoc_rs    = 0001000000000920h;
end;

var
  _ea: IExtAttr;
  _getkau: GetKau;
  KatMRKR, SubGroupMRKR: longint; //markers

table struct tHiddenSrclist (
  refdoc: comp
) with index (
  i01 = refdoc(unique)
);

table struct tZakazRS (
  ZakazNrec: comp,
  RSNrec: comp
) with index (
  i01 = ZakazNrec + RSNrec (unique)
);

create view
var
  _filterContdocZakazNrec: comp;
  _filterContdocRSNrec: comp;
  _filterFlag: byte;
  _tabN: tTabN;
  _datePay: date;
as select
  _ea.sGetAttrId(coContDoc, contdoc.nrec, attrnam_contdoc_zakaz) (fieldname=ContDocZakaz),
  _ea.sGetAttrId(coContDoc, contdoc.nrec, attrnam_contdoc_rs)    (fieldname=ContDocRS)
from contdoc,
     tHiddenSrclist, srclist hsrclist, tZakazRS,
     AllMemo AllMemoMarkList,
     SrcList SrcListAddMark
where ((
  factotpusk.ccontdoc             == contdoc.nrec and
  tHiddenSrclist.refdoc           == hsrclist.refdoc and

  coX$Users                       == AllMemoMarkList.wTable and
  UserId                          == AllMemoMarkList.cRec and
  word(-102)                      == AllMemoMarkList.Npp and

  True                            == SrcListAddMark.Visible and
  _tabN                           == SrcListAddMark.TabN and
  ''                              == SrcListAddMark.StrTabN and
  _datePay                        == SrcListAddMark.DatePay

));

file fMarkList;

//#region Процедуры и функции для установки фильтров

function getFieldFilterAn(aNrec: comp; aKodgrKAU: word): string;
{
  if aNrec = comp(0)
    result := '<Не установлен>';
  else
    result := if ( getanykau(1,aKodgrKAU,aNrec), givenanname(1), 'Ошибка. Выберите значение снова');
}

function getFieldFilterMany(aMRKR: longint; aKodgrKAU: word): string; // для множественного выбора
{
  var aNrec: comp;
  GetMarker(aMRKR, 0, aNrec);
  case GetMarkerCount(aMRKR) of
    0: result := '<Не установлен>';
    1: result := if(getanykau(1, aKodgrKAU, aNrec), givenanname(1), 'Выбрано 1 значение');
    else result := 'Выбрано ' + GetMarkerCount(aMRKR) + ' значений';
  end;
}

function getFieldFilterContdocZakaz: string;
{
  result := getFieldFilterAn(_filterContdocZakazNrec, 40);
}

function getFieldFilterContdocRS: string;
{
  result := getFieldFilterAn(_filterContdocRSNrec, 10545);
}

function getFieldFilterSubGroup: string;
{
  result := getFieldFilterMany(SubGroupMRKR, 10057);
}

function getFieldFilterKategory: string;
{
  result := getFieldFilterMany(KatMRKR, 10036);
}

function getContdocExtAttrNrec(aAttrnamNrec: comp): comp;
{
  result := 0;
  if getfirst factotpusk = tsOk
    //if getfirst partdoc = tsOK
      if getfirst contdoc = tsOk
        result := _ea.coGetAttrId(coContDoc, contdoc.nrec, aAttrnamNrec);
}

procedure ClearFilter;
{
  _loop tHiddenSrclist
    if getfirst hsrclist = tsOk
      update current hsrclist set hsrclist.visible := true;
  delete all tHiddenSrclist;
}

procedure SetFilter;
{
  startnewvisual(vtIndicatorVisual, vfTimer+vfBreak, 'Установка фильтра', recordsintable(#srcList));
  var showSrcList: boolean;
  pushpos(#srcList);
  ClearFilter;

  var i: integer;
  var SubgroupNrec: comp;
  var KategoryNrec: comp;

  _loop srcList
  {
    SubgroupNrec := comp(0);
    KategoryNrec := comp(0);
    if getfirst lschet = tsOk
    {
      for(i := 1; i < 6; i++)
      {
        if lschet.tbld[i] = 10057 SubgroupNrec := lschet.ckaud[i];
        if lschet.tbld[i] = 10036 KategoryNrec := lschet.ckaud[i];
      }
    }

    showSrcList := true;
    if (_filterFlag and 1) = 1
      showSrcList := showSrcList and (getContdocExtAttrNrec(attrnam_contdoc_zakaz) = _filterContdocZakazNrec);
    if (_filterFlag and 2) = 2
      showSrcList := showSrcList and (getContdocExtAttrNrec(attrnam_contdoc_rs)    = _filterContdocRSNrec);
    if (_filterFlag and 4) = 4
      showSrcList := showSrcList and FoundMarker(SubGroupMRKR, SubgroupNrec); // subgroup
    if (_filterFlag and 8) = 8
      showSrcList := showSrcList and FoundMarker(KatMRKR, KategoryNrec); // kategory
    if not showSrcList
    {
      insert tHiddenSrclist set tHiddenSrclist.refdoc := srcList.refdoc;
      update current srcList set srcList.visible := false;
    }
    if not nextvisual break;
  }
  poppos(#srcList);
  stopvisual('',0);
}

//#endregion

window wSelectZakazRS 'Подбор заказа и счета' doaccept, escclose;
browse brSelectZakazRS;
table tZakazRS;
fields
  if(getanykau(1, 40, tZakazRS.ZakazNrec), givenanname(1), '<Не установлен>') 'Заказ': [20], protect;
  if(getanykau(1, 10545, tZakazRS.RSNrec), givenanname(1), '<Не установлен>') 'Счет': [20], protect;
end;
end;

window wSetFilter 'Установка/снятие фильтра';
  show(,,56,9);
  screen scFilter;
  fields
    _filterFlag: noprotect;
    [FieldFilterContdocZakaz] getFieldFilterContdocZakaz: protect, pickbutton;
    [FieldFilterContdocRS]    getFieldFilterContdocRS   : protect, pickbutton;
    [FieldFilterSubGroup]     getFieldFilterSubGroup    : protect, pickbutton;
    [FieldFilterKategory]     getFieldFilterKategory    : protect, pickbutton;
  buttons
    cmPodbor;
    cmSetFilter, default;
    cmUnsetFilter;
<<

  [.] Заказ`               .@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Расчетный счет`      .@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Подгруппа персонала` .@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Категория`           .@@@@@@@@@@@@@@@@@@@@@@@@@@
                          <.  Подбор заказа и счета  .>

 <. Установить фильтр .>  <.      Снять фильтр       .>
>>
  end;
  handleevent
    cmPodbor:
    {
      if recordsintable(#tZakazRS) = 0
      {
        startnewvisual(vtIndicatorVisual, vfTimer+vfBreak, 'Инициализация', recordsintable(#srcList));
        var znrec, rsnrec: comp;
        pushpos(#srcList);
        _loop srcList
        {
          znrec  := getContdocExtAttrNrec(attrnam_contdoc_zakaz);
          rsnrec := getContdocExtAttrNrec(attrnam_contdoc_rs);
          if getfirst tZakazRS where ((znrec == tZakazRS.ZakazNrec and rsnrec == tZakazRS.RSNrec)) != tsOk
          {
            clearbuffer(#tZakazRS);
            tZakazRS.ZakazNrec := znrec;
            tZakazRS.RSNrec := rsnrec;
            insert current tZakazRS;
          }
          if not nextvisual break;
        }
        poppos(#srcList);
        rereadrecord(#tZakazRS);
        stopvisual('',0);
      }

      if runwindowmodal(wSelectZakazRS) = cmDefault
      {
        _filterContdocZakazNrec := tZakazRS.ZakazNrec;
        _filterContdocRSNrec    := tZakazRS.RSNrec;
        _filterFlag             := _filterFlag or 1 or 2;
        rereadrecord;
      }
    }
    cmPick:
    {
      case curfield of
      #FieldFilterContdocZakaz:
      {
        var c: comp;
        c := _filterContdocZakazNrec;
        if _getkau.GetCodeKau(1,40,c) > 0
        {
          _filterContdocZakazNrec := c;
          _filterFlag := _filterFlag or 1;
        }
      }
      #FieldFilterContdocRS:
      {
        var c: comp;
        c := _filterContdocRSNrec;
        if _getkau.GetCodeKau(1,10545,c) > 0
        {
          _filterContdocRSNrec := c;
          _filterFlag := _filterFlag or 2;
        }
      }
      #FieldFilterSubGroup:
      {
        var c: comp;
        var _mrkname: string;
        if _getkau.GetCodeKau(2, 10057, c) > 0
        {
          _mrkname := _getkau.getmarkername(10057);
          SubGroupMRKR := initmarker(_mrkname,8,10,10,false);
          _filterFlag := _filterFlag or 4;
//          DoneMarker(SubGroupMRKR, _mrkname);
        }
      }
      #FieldFilterKategory:
      {
        var c: comp;
        var _mrkname: string;
        if _getkau.GetCodeKau(2, 10036, c) > 0
        {
          _mrkname := _getkau.getmarkername(10036);
          KatMRKR := initmarker(_mrkname,8,10,10,false);
          _filterFlag := _filterFlag or 8;
//          DoneMarker(KatMRKR, _mrkname);
        }
      }
      end;
      rereadrecord;
    }
    cmSetFilter:
    {
      SetFilter;
      rereadrecord(#srcList);
      closewindow(wSetFilter);
    }
    cmUnsetFilter:
    {
      ClearFilter;
      _filterFlag := 0;
      rereadrecord(#srcList);
      closewindow(wSetFilter);
    }
  end;
end;

window wMarkTabn 'Пометка по табельным номерам' escClose doAccept;
  show(,,40,);

  screen scDate;
    show(,,,3) fixed_y;
    fields
      _datePay: noprotect;
<<

`Дата выплаты`.@@@@@@@@@@
>>
  end;

  text AllMemoMarkList.Mem;
    show(,4,,20);

  screen scBtn;
    show(,21,,) fixed_y;
    buttons
      cmDoOk, default;
      cmCancel;
<<
          <. Продолжить .> <. Отмена .>
>>
  end;

  handleevent
    cmInit: if not ReadMyDsk(_datePay, 'PickOtpFList_datePay', false) _datePay := cur_date;
    cmDone: SaveMyDsk(_datePay, 'PickOtpFList_datePay');
    cmDoOk: putcommand(cmDefault);
    cmDefault: SaveMyDsk(_datePay, 'PickOtpFList_datePay');
  end;

end; //window wMarkTabn

window WPICKOTP;

  browse BPICKOTP (,,sci1EnIns);
  table srclist;
  fields
    ContDocZakaz 'Заказ': [10], protect;
    ContDocRS 'Счет': [10], protect;
  end;

  handleevent
    cmFilterSave:
    {
      runwindowmodal(wSetFilter);
    }
    cmHotkeys:
    {
      puthotcommand(runloadmenu(loadmenu('mnuPICKOTPFLIST')))
    }
    cmValue9:  //Пометка по табельным номерам
    {
      if getfirst AllMemoMarkList != tsOk
      {
        ClearBuffer(tnAllMemoMarkList);
        AllMemoMarkList.wTable := coX$Users;
        AllMemoMarkList.cRec := UserId;
        AllMemoMarkList.Npp := word(-102);
        insert current AllMemoMarkList;
      }

      if runwindowmodal(wMarkTabn) = cmDefault
      {
        update current AllMemoMarkList;

        if getfirst AllMemoMarkList = tsOk
        if fMarkList.OpenMemo(AllMemoMarkList.Mem)
        {
          var s, undefinedRecs: string = '';

          while not fMarkList.Eof
          {
            fMarkList.ReadLn(s);
            _tabN := trim(s);
            if _tabN > 0
            {
              _loop SrcListAddMark
                update current SrcListAddMark set SrcListAddMark.Marked := true;
              if getfirst SrcListAddMark != tsOk
                undefinedRecs += ' ' + _tabN;
            }
          }
          fMarkList.Close;

          if undefinedRecs != ''
            message('Не найдены табельные номера ' + undefinedRecs);
        }

        rereadrecord(#SrcList);

      }

    }
  end;

end; //window WPICKOTP

handleevent
  cmDelOnProtect:
  {
    case curfield of
    #FieldFilterContdocZakaz: { _filterContdocZakazNrec := 0; rereadrecord; }
    #FieldFilterContdocRS   : { _filterContdocRSNrec := 0; rereadrecord; }
    #FieldFilterSubGroup    : { ClearMarker(SubGroupMRKR); ReReadRecord; }
    #FieldFilterKategory    : { ClearMarker(KatMRKR); ReReadRecord; }
    else
      inherited::handleevent(cmDelOnProtect);
    end;
  }
end;

end.

mnuPICKOTPFLIST menu
{
  - 'Фильтр', cmFilterSave, 'Установка фильтра';
  - 'Пометка по табельным номерам', cmValue9, 'Пометка по табельным номерам';
}
