#include TriggerFunctions.vih
#include RSHelper.vih
#include ExtAttr.vih
#include DogovorHelper.vih
#include KatstroyHelper.vih

#declare AgatTriggerAttrVal
action
{
  result := true;
  var buf : record as table AttrVal;
  getTableBuffer(buf);
  case buf.wtable of     //атрибут ДО

    1102: {
      var tf: TriggerFunctions;
      case buf.cAttrNam of
        0001000000000089h: result := tf.UserInGroup(UserId, 00010000000000D4h) or (pr_CurUserAdmin);            //виза ДО бухгалтерии, группа s_бухгалтерия
        0001000000000087h: result := tf.UserInGroup(UserId, 000100000000008Dh) or (pr_CurUserAdmin);            //виза казначейства, группа s_казначейство
        000100000000008Ah: result := tf.UserInGroup(UserId, 00010000000000D5h) or (pr_CurUserAdmin);            //виза коммерческой дирекции, группа s_комдир
        0001000000000088h: result := tf.UserInGroup(UserId, 0001000000000100h) or (pr_CurUserAdmin);            //виза ПЭУ, группа s_ПЭУ
        0001000000000097h: result := (sGetTune('USER.DESGR') = 'БУХГ') or (pr_CurUserAdmin);                    //примечание бухгалтерии
        0001000000000098h: result := (sGetTune('USER.DESGR') = 'КАЗН') or (pr_CurUserAdmin);                    //примечание казначейства
        000100000000008Bh: result := (sGetTune('USER.DESGR') = 'КАЗН') or (pr_CurUserAdmin);                    //передано на согласование
        0001000000000099h: result := (tf.UserInGroup(UserId, 0001000000000100h)) or (pr_CurUserAdmin);          //примечание ПЭУ
        000100000000009Ah:                                                                                      //примечание ком.дир
        {
          var _Name: string;
          var tf: TriggerFunctions;
          _Name := tf.GetUserFullName;
          if not(substr(_Name,1,4)='РУК:' or sGetTune('USER.DESGR') = 'ОМТС' or sGetTune('USER.DESGR') = 'ОСЗ') result := false;
        }
      end;
    }

  end;
}
rollback
{
  message('Вам запрещено изменять значение этого атрибута');
}
#end

handler with replace Agat_BeforeInsertAttrVal on trigger AttrVal before insert
#AgatTriggerAttrVal

handler with replace Agat_BeforeUpdateAttrVal on trigger AttrVal before update
#AgatTriggerAttrVal

handler with replace Agat_BeforeDeleteAttrVal on trigger AttrVal before delete
#AgatTriggerAttrVal

#declare AgatTriggerAttrValAfter(isDelete)
action
{
  result := true;
  var buf : record as table AttrVal;
  getTableBuffer(buf);
  case buf.wtable of

    coSpstep: {

      //Установка р/с списания по заказу выбытия
      if buf.cAttrNam = 0001000000000A87h
      {
        var ea: IExtAttr;
        var rsHelper: RSHelper;
        var rsNrec: comp;
        rsNrec := rsHelper.GetRSNrecByKatstroyNrec(buf.vComp);
        if rsNrec != 0
          ea.coSetAttrID(1104, buf.crec, 0001000000000A86h, rsNrec, if(getanykau(1, 10545, rsNrec), givenanname(1), ''));
      }

    } //coSpstep

    coDogovor: {

      //Заказ к договору
      if buf.cAttrNam = 00010000000000ABh
      {

        var ea: IExtAttr;
        var dh: DogovorHelper;

        var sIGKNew: string;
        sIGKNew := sTxoGetField('spkau', 'name', ea.coGetAttrID(coKatstroy, buf.vcomp, 00010000000004CCh));

        if #isDelete
          dh.setIGK(buf.crec, '');
        else
          dh.setIGK(buf.crec, sIGKNew);

      }

    } //coDogovor

    coKatstroy: {

      //ИГК к заказу
      if buf.cAttrNam = 00010000000004CCh
      {

        var ea: IExtAttr;
        var dh: DogovorHelper;
        var kh: KatstroyHelper;

        startnewvisual(vtRotatevisual, vfTimer, 'Установка ИГК у связанных с заказом договоров', 0);

        var sIGKNew: string;
        sIGKNew := sTxoGetField('spkau', 'name', buf.vcomp);

        var m: longint;
        m := initmarker('', sizeof(comp), 10, 100, false);
        kh.FillDogovorsMarker(buf.crec, m);

        var i: longint;
        var dogovorNrec: comp = 0;
        for(i := 0; getmarker(m, i, dogovorNrec); i++)
        {

          if #isDelete
            dh.setIGK(dogovorNrec, '');
          else
            dh.setIGK(dogovorNrec, sIGKNew);

        }

        donemarker(m, '');

        stopvisual('', 0);

      }

    } //coKatstroy

  end;
}
#end


handler with replace Agat_AfterInsertAttrVal on trigger AttrVal after insert
#AgatTriggerAttrValAfter(false)

handler with replace Agat_AfterUpdateAttrVal on trigger AttrVal after update
#AgatTriggerAttrValAfter(false)

#define Agat_AfterDeleteAttrVal
handler with replace Agat_AfterDeleteAttrVal on trigger AttrVal after delete
#AgatTriggerAttrValAfter(true)
