#include BasedocHelper.vih
#component "AGAT"

handler with replace Agat_BeforeUpdateBasedoc on trigger Basedoc before update
action
{
  result := true;

  //буфер записи ДО новый, который будет записан
  var buf: record as table Basedoc;
  GetTableBuffer(buf);

  //хелпер для работы с ДО
  var helper: BasedocHelper;

  //буфер записи ДО старый, до изменений
  var oldbuf: type$basedoc;
  oldbuf := helper.GetBuffer(buf.nrec);


  //Если статус ДО "В реестре"
  if buf.cnote = KATNOTES_NREC_BASEDOC_REESTR
  {

    //Получаем визы ДО
    var vizas: IBasedocVizas;
    vizas := helper.GetBasedocVizas(buf.nrec);

    //Если хотя бы одна из виз не согласована - отменяем транзакцию
    if (vizas.BUH.ValueNrec != VIZA_SOGLASOVANO) or
       (vizas.KAZ.ValueNrec != VIZA_SOGLASOVANO) or
       (vizas.DIR.ValueNrec != VIZA_SOGLASOVANO)
    {
      result := false;
    }

  }


  //Если статус Исполняемый и изменен Договор/ДС/ПКП - меняем статус на Оформляемый
  if oldbuf.status = 2 and
     (oldbuf.cdogovor != buf.cdogovor or
      oldbuf.cappdogovor != buf.cappdogovor or
      oldbuf.ccalplan != buf.ccalplan)
  {

    buf.status := 1;
    buf.cnote := KATNOTES_NREC_BASEDOC_OFORML;
    result := SetTableBuffer(buf) = tsOk;
    message('Счет переведен в статус "Оформляемый"');

  }

}
rollback
{
  message(chr(3) + 'Проверьте атрибуты "Виза бухгалтерии",' + chr(13) +
  '"Виза коммерческой дирекции" и "Виза казначейства"');
}
