#component "AGAT"

#include SvodVedZPSdel.vih

interface MakeProdTarg 'Формирование производственных заданий по окладникам';
show(,,62,5);

const
  MARK_NAME = 'MNPLmarkers';
end;

var
  SV: AGAT::SvodVedZPSdel;
  selnarady: string;
  M, L: longint;
  i: word;
  NarNrec, InpNrec: comp;

create view vMNPL
as select *
from mnplan
where ((
  NarNrec == mnplan.nrec
));

parameters InpNrec; // ссылка на текущий план

procedure SetStrNar;
{
  case GetMarkerCount(M) of
    0: selnarady := 'Укажите сдельный наряд';
    1: {
         GetMarker(M, 0, NarNrec);
         if vMNPL.getfirst mnplan = tsOk { };
         selnarady := vMNPL.mnplan.name;
       }
    else selnarady := 'Выбрано ' + GetMarkerCount(M) + ' сдельных наряда';
  end;
}

screen sc1;
fields
  selnarady: protect, pickbutton;

buttons
  cmMakeRep;
  cmCancel;

<<

  Сдельные наряды .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

  <. Сформировать .>                             <. Отмена .>
>>
end;

handleevent
cminit:
{
  M := InitMarker(MARK_NAME, sizeof(comp), 100, 10);
  ClearMarker(M);
  if InpNrec <> 0 then
    NarNrec := InpNrec
  else
    NarNrec := 0;
  InsertMarker(M, NarNrec); // добавляем ссылку на текущий выбранный план в маркер
  SetStrNar;
}
cmpick:
{
  if curfield = #selnarady then
  {
    L := 0;
    DoneMarker(M, MARK_NAME);
    if RunInterface(Naryady, L) = cmDefault
      M := L
    else
      M := InitMarker(MARK_NAME, sizeof(comp), 100, 10);
    SetStrNar;
  }
}
cmMakeRep:
{
  if GetMarkerCount(M) = 0 then
  {
    message('Не выбраны сдельные наряды');
    exit;
  }
  SV.ClearPT;
  for (i:=0; i<GetMarkerCount(M); i++)
  {
    GetMarker(M, i, NarNrec);
    SV.addPws(NarNrec, GetMarkerCount(M), 2);
  }
  SV.printreport(2);
}
cmCancel:
{
  exit;
}
cmDelOnProtect:
{
  ClearMarker(M);
  selnarady := 'Укажите сдельный наряд';
  ReReadRecord;
}
end;

end.



// Интерфейс для печати производственных заданий
VipInterface UserReport_PRDCTTRGT implements IUserReport Licensed(free);
Interface UserReport_PRDCTTRGT;
  create view;
  procedure Run;
  {
    runinterfacenomodal(AGAT::MakeProdTarg);
  }

  function GetReportName: String;
  {
    GetReportName := 'Производственные задания рабочим окладникам';
  }

  function GetGroupName (Level : Word) : String;
  {
    Level := Level;
    GetGroupName := '';
  }

  function GetPriority : Integer;
  {
    GetPriority := 0;
  }

  function VisibleInModule(Ind : Byte) : String;
  {
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'MANUFPLAN';
    end;
  }
end.
