#component "AGAT"

interface PlanOMTOCreateBasedoc;
const
  WLIST_SPMNPLAN  = 20000;
  VID_PLSNAB      = 25;
  KODGRKAU_PLSNAB = 29;
  KATNOTES_BASEDOC = 40009D44C1790C6Bh;
end;
var
  MnplanNrec: comp;
create view
as select *
from mnplan mp, spmnplan spm, pick, spmnpl spl, valspmnp vsp, indent,
     basedoc, basedoc bd, ttndoc, stepdoc sd, soprhoz sh, spstep sps, specmtr sm, objacct oa, spobjacc spoa
where ((
  MnplanNrec      == mp.nrec and
  mp.nrec         == spm.cmnplan and
  WLIST_SPMNPLAN  == pick.wlist and
  spm.nrec        == pick.crec and
  spm.nrec        == spl.cspmnplan and
  VID_PLSNAB      == spl.wkolan and
  KODGRKAU_PLSNAB == spl.wkodgr4 and
  mp.nrec         == spl.canval4 and
  spl.nrec        == vsp.cspmnpl and
  spm.canval3     == indent.nrec
));

parameters MnplanNrec;

procedure CreateBasedoc;
{
  var headerCreated: boolean;
  var basedocName: string;
  var spstepNpp: longint;
  headerCreated := false;
  basedocName := 'по заявке ';
  spstepNpp := 0;
  _loop spm
  {
    if (getfirst pick != tsOk) continue;
    if (getfirst basedoc where ((spm.canval5 == basedoc.nrec)) = tsOk) continue;
    if (getfirst spl != tsOk) continue;
    if (getfirst vsp != tsOk) continue;

    if not headerCreated
    {
      clearbuffer(#bd);
      bd.descr := sgettune('USER.DESCR');
      bd.desgr := sgettune('USER.DESGR');
      bd.vhodnal := 1;
      bd.status := 1;
      bd.cnote := KATNOTES_BASEDOC;
      bd.viddoc := 101;
      bd.dform := cur_date;
      bd.ddoc := bd.dform;
      bd.nodoc := datetostr(bd.dform,'YYYYMMDD');
      bd.yeardoc := year(bd.dform);
      bd.koldn := 3;
      bd.dend := add_day(bd.dform,3);
      bd.cmybank := cogettune('MYBANK');
      bd.cgrpol := cogettune('MYORG');
      bd.direct := 2;
      bd.tipmoney := 1;
      bd.prior := 99;
      bd.cotvpodr := 0001000000000003h;
      insert current bd;

      clearbuffer(#ttndoc);
      ttndoc.wtable := 1102;
      ttndoc.cdoc := bd.nrec;
      insert current ttndoc;

      clearbuffer(#sd);
      sd.cbasedoc := bd.nrec;
      sd.nstep := 1;
      sd.dstart := bd.dform;
      sd.dend := bd.dend;
      sd.status := 1;
      insert current sd;

      clearbuffer(#sh);
      sh.cstepdoc := sd.nrec;
      sh.datob := bd.ddoc;
      sh.csoprdoc := bd.nrec;
      sh.tipdoc := 41;
      sh.tidkgal := 41;
      sh.nodoc := bd.nodoc;
      sh.descr := bd.descr;
      sh.desgr := bd.desgr;
      sh.direct := bd.direct;
      insert current sh;

      clearbuffer(#oa);
      oa.typeown := 34;
      oa.cowner := bd.nrec;
      oa.kindrec := 1;
      oa.typeobj := 29;
      oa.cobject := mp.nrec;
      oa.vidsopr := 101;
      insert current oa;

      headerCreated := true;
    }

    if (getfirst indent = tsOk)
      if pos(indent.noind+';',basedocName) = 0
        basedocName += indent.noind+'; ';

    clearbuffer(#sps);
    spstepNpp++;
    sps.npp := spstepNpp;
    sps.prmc := 1;
    sps.cmcusl := spm.cizd;
    sps.cotped := spm.cotped;
    sps.kol := vsp.vprice;
    sps.ddoc := bd.ddoc;
    sps.cstepdoc := sd.nrec;
    sps.kolskl := sps.kol;
    insert current sps;

    clearbuffer(#sm);
    sm.cotable := 1104;
    sm.cspec := sps.nrec;
    sm.csaldtune := 0001000000000001h;
    sm.cobj := spm.canval1;
    sm.ckau[4] := spm.canval3;
    insert current sm;

    clearbuffer(#spoa);
    spoa.cobjacct := oa.nrec;
    spoa.kindrec := 1;
    spoa.typenorm := 1;
    spoa.typepos := 31;
    spoa.cpos := spl.nrec;
    spoa.typeed := 1;
    spoa.cotped := spm.cotped;
    spoa.kolcpos := vsp.vprice;
    spoa.vidsopr := 101;
    insert current spoa;

    var spoaNrec: comp;
    spoaNrec := spoa.nrec;

    clearbuffer(#spoa);
    spoa.cobjacct := oa.nrec;
    spoa.kindrec := 1;
    spoa.typepos := 31;
    spoa.cpos := spl.nrec;
    spoa.cotped := sps.cotped;
    spoa.kolcpos := sps.kol;
    spoa.startdate := bd.ddoc;
    spoa.typeobj := 35;
    spoa.cobject := sps.nrec;
    spoa.typehier := 1;
    spoa.cspobjacc := spoaNrec;
    spoa.vidsopr := 101;
    insert current spoa;

    update current spm set spm.canval5 := bd.nrec, spm.wkodgr5 = 6, spm.crolean5 := 0001000000000011h; //ДО на закупку
  }

  if (headerCreated)
  {
    update current bd set bd.name := basedocName;
  }
}

handleevent
cminit:
{
  if getfirst mp != tsOk exit;
  if message('Сформировать ДО по помеченным позициям?', YesNo) = cmYes
    CreateBasedoc;
}
end;
end.
