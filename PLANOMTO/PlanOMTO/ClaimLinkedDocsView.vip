#include ClaimLinkedDocs.vih
#include Dogovor.vih

#component "AGAT"

#include ClaimLinkedDocsView.vih
interface ClaimLinkedDocsView;

var
  _cld: IClaimLinkedDocs;

create view
as select *
from _cld, katdoc, katorg, dogovor
where ((
  _cld.tidkgal == katdoc.tidkgal and
  _cld.corg == katorg.nrec and
  _cld.nrec == dogovor.nrec
));

browse brMain (,,Sci14Esc);
table _cld;
fields
  katdoc.name  'Тип'            : [30], protect;
  _cld.number  'Номер'          : [10], protect;
  _cld.datedoc 'Дата документа' : [10], protect;
  _cld.sum     'Сумма документа': [10,2], protect;
  _cld.sumpos  'Сумма позиции'  : [10,2], protect;
  _cld.dateOpl 'Дата оплаты'    : [10], protect;
  _cld.sumOpl  'Сумма оплаты'   : [10,2], protect;
  katorg.name  'Организация'    : [20], protect;
end;

tableevent table _cld;
  cmDefault: if curtable = #_cld OpenCurrent;
  cmEdit: if curtable = #_cld OpenCurrent;
end;

constructor Create(aClaimLinkedDocs: IClaimLinkedDocs);
{
  _cld := aClaimLinkedDocs;
  result := _cld != nullref;
}

procedure Load(claimNrec: comp);
{
  _cld.Load(claimNrec);
}

procedure Refresh;
{
  rereadrecord(#_cld);
}

procedure OpenCurrent;
{
  case _cld.tidkgal of
    400,401: //договор, соглашение
      if getfirst dogovor = tsOk
        runinterfacenomodal(L_DOGOVOR::DOGOVOR, 0, 0, 0, 0, dogovor.cdogovor, dogovor.nrec, IFilterDog(NullRef));
    41: //ДО на закупку
      runinterfacenomodal(L_BASEDOC::VSCHETB, _cld.nrec);
    43: //ДО предоплату закупок
      runinterfacenomodal(L_BASEDOC::VSCHETPB, _cld.nrec);
    101: //накладная на прием МЦ
      runinterfacenomodal(L_SOPRDOC::SOPRDOCB, 101, _cld.nrec);
    630: //акт на пересортицу
      runinterfacenomodal(L_SKLAD::PERESORT, 630, _cld.nrec);
    501: //накладные на отпуск в производство
      runinterfacenomodal(M_UP::IMNFINTRF, 501, _cld.nrec);
    1000: //заявки
      runinterfacenomodal(M_MNPLAN::BOOKINGS, 1, _cld.nrec, 0);
  else
    message('Открытие документов "'+katdoc.name+'" не поддерживается');
  end;
}

property ClaimLinkedDocs: IClaimLinkedDocs absolute _cld;

end.
