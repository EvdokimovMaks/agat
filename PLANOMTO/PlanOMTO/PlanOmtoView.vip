#include PlanOmto.vih
#include marker.vih

#component "AGAT"

interface PlanOmtoView;

var
  _po: IPlanOmto;
  _marker: IMarker(Marker) new;

create view
as select ('№' + indent.noind + ' от ' + indent.dind + ', строка ' + claim.wpos) (fieldname=ClaimName)
from _po, claim, indent, katnotes, katorg, katstroy, persons,
     katmc device, groupmc, katmc mc, katotped oe
where ((
  _po.ClaimNrec      == claim.nrec and
  _po.IndentNrec     == indent.nrec and
  _po.ClaimStateNrec == katnotes.nrec and
  _po.DeliverOrgNrec == katorg.nrec and
  _po.KatstroyNrec   == katstroy.nrec and
  _po.PersonNrec     == persons.nrec and
  _po.DeviceNrec     == device.nrec and
  _po.GroupmcNrec    == groupmc.nrec and
  _po.KatmcNrec      == mc.nrec and
  _po.OtpedNrec      == oe.nrec
));

browse brPlanOMTO (,,sci14InsPMEnEsc);
table _po;
recMarker = _marker { _po.ClaimNrec };
fields
  groupmc.name           ' '#13'Группа'                 : [10], protect;
  mc.name                ' '#13'Матценность'            : [30], protect, pickbutton;
  _po.Tu                 ' '#13'ТУ'                     : [10], protect;
  _po.Description        ' '#13'Примечания'             : [10], noprotect;
  oe.name                ' '#13'Ед.изм.'                : [5 ], protect;
  _po.ClaimQty           'По'#13'заявке'                : [8 ], protect;
  _po.SaldoQty           'Свободный'#13'остаток'        : [8 ], protect;
  _po.RegradeQty         'Кол-во'#13'перенесено'        : [8 ], protect;
  0                      'Кол-во'#13'к закупке'         : [8 ], noprotect;
  _po.BasedocQty         'Кол-во'#13'заказано'          : [8 ], protect;
  _po.BasedocSum         'Сумма'#13'заказано'           : [8 ], protect;
  _po.PaidSum            'Сумма'#13'оплачено'           : [8 ], protect;
  _po.DeliveredQty       'Кол-во'#13'закуплено'         : [8 ], protect;
  _po.IssuedQty          'Кол-во'#13'выдано'            : [8 ], protect;
  0                      'Дефицит'#13'по закупке'       : [8 ], protect;
  0                      'Дефицит'#13'по заявке'        : [8 ], protect;
  katnotes.name          ' '#13'Статус'                 : [10], protect, pickbutton;
  //_po.PriorName    ' '#13'Приоритет'              : [10], protect;
  _po.IndentDate         'Заявка'#13'от'                : [10], protect;
  _po.IndentDeliverDate  'Требуемая дата'#13'поставки'  : [10], protect;
  _po.ClaimAcceptDate    'Дата'#13'принятия в работу'   : [10], protect;
  _po.DeliverDatePlan    'Дата поставки'#13'расчетная'  : [10], protect; //, {font={backColor=if(_poDeliveryOverdate, COLOR_WARN, 0);}};
  _po.DeliverDateChanged 'Дата поставки'#13'измененная' : [10], noprotect;
  _po.DeliverDateFact    'Дата поставки'#13'фактическая': [10], protect; //, {font={backColor=if(_poDeliveryOverdate, COLOR_WARN, 0);}};
  _po.BasedocDate        'Дата'#13'счета'               : [10], protect; //, {font={backColor=if(_poUnpaidBasedoc, COLOR_WARN, 0);}};
  _po.PaidDate           'Дата оплаты'#13'счета'        : [10], protect; //, {font={backColor=if(_poUnpaidBasedoc, COLOR_WARN, 0);}};
  //_po.BasedocName  'Счет'#13'номер'               : [10], protect, pickbutton;
  katorg.name            ' '#13'Поставщик'              : [10], protect;
  katstroy.name          ' '#13'Заказ'                  : [10], protect, pickbutton;
  ClaimName              ' '#13'Заявка'                 : [10], protect, pickbutton;
  persons.fio            'Ответственный'#13'сотрудник'  : [10], protect;
end;

procedure SetCurrentAccept(value: boolean);
{
  _po.SetAccept(value);
  _po.Save;
}

#declare IterateMarked(Callback, QuestionText)
  var iterationMarker: longint;
  iterationMarker := initmarker('', 8, 10, 100, false);
  _marker.ExportTo(iterationMarker);
  if getmarkercount(iterationMarker) > 0
  {
    if message(#QuestionText, YesNo) = cmYes
    {
      pushpos(#_po);
      _loop _po where foundmarker(iterationMarker, _po.ClaimNrec)
      {
        #Callback;
      }
      poppos(#_po);
    }
  }
  else
    #Callback;
  donemarker(iterationMarker, '');
#end

procedure Refresh;
{
  rereadrecord(#_po);
}

constructor Create(aPlanOmto: IPlanOmto);
{
  _po := aPlanOmto;
  _marker.Caption   := ' Отмечено [%d] ';
  result := _po != nullref;
}

property PlanOmto: IPlanOmto absolute _po;


handleevent
cmHotkeys:
{
  PutHotCommand(RunMenu('mnuAgatPlanOmtoView'));
}
cmCalcUslParam:  //Alt+W  Принять к исполнению помеченные
{
  #iterateMarked(SetCurrentAccept(true),'Установить статус "Принята к исполнению" у помеченных позиций?')
  Refresh;
}
cmOpenFile:  //Alt+R  Отменить прием к исполнению помеченных
{
  #iterateMarked(SetCurrentAccept(false),'Установить статус "Оформляемый" у помеченных позиций?')
  Refresh;
}
end;

end.


mnuAgatPlanOmtoView Menu
{
- 'Принять к исполнению помеченные', cmCalcUslParam,
  'Установить статус "Принята к исполнению" у помеченных позиций', , 'Alt+W', kbAltW, sci1Esc;
- 'Отменить прием к исполнению помеченных', cmOpenFile,
  'Установить статус "Оформляемый" у помеченных позиций', , 'Alt+R', kbAltR, sci1Esc;
}
