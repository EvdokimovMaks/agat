#include IPlanOmto.obj
#include Getkau.vih

#component "AGAT"

#include PlanOmtoActionWithParamsUI.vih
interface PlanOmtoActionLinkBasedoc;

var
  _getkau: GetKau;
  _po: IPlanOmto;
  _basedocNrec: comp;
  _overwrite: boolean;
  _linkAll: boolean;

create view
as select if(getanykau(1, 6, _basedocNrec), givenanname(1), '') (fieldname=basedocField)
from basedoc bd, stepdoc sd, spstep sps, specmtr sm, indent
where ((
  _basedocNrec      == bd.nrec and
  _basedocNrec      == sd.cbasedoc and
  sd.nrec           == sps.cstepdoc and
  1                 == sps.prmc and
  0001000000000001h == sm.csaldtune and
  1104              == sm.cotable and
  sps.nrec          == sm.cspec
));

create view v
as select * from _po;

window winConfig 'Настройка привязки ДО' doaccept escclose;
  show(,,40,8);

  screen sc1
  fields
    basedocField: protect, pickbutton;
    _overwrite: noprotect, noframe;
    _linkAll: noprotect, noframe;
  buttons
    cmDoOk, default;
    cmCancel;
<<

`ДО`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

 [:] Заменять привязку`
 [:] Не сопоставлять МЦ, привязывать все`

          <. Продолжить .> <. Отмена .>
>>
  end;

  handleevent
  cmPick:
  {
    case curfield of
      #basedocField:
      {
        var c: comp;
        c := _basedocNrec;
        if _getkau.GetCodeKau(1, 6, c) > 0
        {
          _basedocNrec := c;
          rereadrecord;
        }
      }
    end;
  }
  cmDoOk: putcommand(cmDefault);
  cmDone:
  {
    savemydsk(_basedocNrec, 'PlanOmtoActionLinkBasedoc_basedocNrec');
    savemydsk(_overwrite, 'PlanOmtoActionLinkBasedoc_overwrite');
  }
  end;
end;


procedure IPlanOmtoActionWithParamsUI.InitParams;
{
  if not readmydsk(_basedocNrec, 'PlanOmtoActionLinkBasedoc_basedocNrec', false) _basedocNrec := 0;
  if not readmydsk(_overwrite, 'PlanOmtoActionLinkBasedoc_overwrite', false) _overwrite := false;
}

function IPlanOmtoActionWithParamsUI.ShowParamsUi: word;
{
  result := runwindowmodal(winConfig);
}

function IPlanOmtoActionWithParamsUI.DoAction(aPlanOmto: IPlanOmto; params: objref = nullref): word;
{
  if params = nullref {}

  var basedocOrgNrec: comp = 0;
  if getfirst bd = tsOk
    basedocOrgNrec := bd.corg;

  _po := aPlanOmto;
  _loop sd
    _loop sps
    {
      if getfirst sm != tsOk
      {
        clearbuffer(#sm);
        sm.csaldtune := 0001000000000001h;
        sm.cotable   := 1104;
        sm.cspec     := sps.nrec;
        result := insert current sm;
        if result != tsOk exit;
      }

      var spsMcNrec: comp;
      spsMcNrec := sps.cmcusl;

      if (v.getfirst _po where (v._po.KatmcNrec = spsMcNrec or _linkAll) ) = tsOk
      {
        if v._po.ClaimNrec != sm.ckau[4] or _overwrite
        {
          sm.cobj := v._po.KatstroyNrec;
          sm.ckau[1] := _po.CostitemNrec;
          if getfirst indent where ((_po.IndentNrec == indent.nrec)) = tsOk
            sm.ckau[2] := indent.corder;
          sm.ckau[4] := v._po.ClaimNrec;
          result := update current sm;
          if result != tsOk exit;
        }
        if basedocOrgNrec != 0
        {
          v._po.DeliverOrgNrec := basedocOrgNrec;
          v.update current _po;
          _po.SaveData;
        }
      }
    }

  result := PlanOmtoActionSuccess;
}

end.
