#include ClaimOverheadsTotal.vih
#include System.vih

#component "AGAT"

interface ClaimOverheadsTotal;


var
  _isDirty    : boolean;
  _mnplanNrec : comp;
  _periodNrec : comp;
  _katpodrNrec: comp;
  _slojNrec   : comp;
  _number     : string[20];
  _name       : string;
  _startDate  : date;
  _endDate    : date;
  _sp         : IClaimOverheadsTotalSp;

create view vLoad
var
  _loadMnplanNrec: comp;
as select *
from mnplan, spmnplan
where ((
  _loadMnplanNrec == mnplan.nrec and
  mnplan.nrec     == spmnplan.cmnplan and
  0               == spmnplan.cspmnplan
));

create view vSave
var
  _saveMnplanNrec: comp;
as select *
from mnplan
where (( _saveMnplanNrec == mnplan.nrec ));

procedure CopyMnplanBufferToProperties(buf: type$mnplan);
{
  _mnplanNrec  := buf.nrec;
  _periodNrec  := buf.canval1;
  _katpodrNrec := buf.canval2;
  _number      := buf.number;
  _name        := buf.name;
  _slojNrec    := buf.cSloj;
  _startDate   := buf.startDate;
  _endDate     := buf.endDate;
}

procedure CopyPropertiesToMnplanBuffer(buf: type$mnplan);
{
  buf.nrec      := _mnplanNrec;
  buf.canval1   := _periodNrec;
  buf.canval2   := _katpodrNrec;
  buf.number    := _number;
  buf.name      := _name;
  buf.cSloj     := _slojNrec;
  buf.startDate := _startDate;
  buf.endDate   := _endDate;
}

procedure IClaimOverheadsTotal.SetDirty;   { _IsDirty := true;  }

procedure UnsetDirty; { _IsDirty := false; }

function IClaimOverheadsTotal.Load(aMnplanNrec: comp): boolean;
{
  vLoad._loadMnplanNrec := aMnplanNrec;
  if vLoad.getfirst fastfirstrow mnplan = tsOk
  {
    CopyMnplanBufferToProperties(type$mnplan(vLoad.mnplan.buffer));
    UnsetDirty;
    result := true;
    _sp.Load(aMnplanNrec);
  }
  else
    result := false;
}

function IClaimOverheadsTotal.Save: boolean;
{
  if _isDirty
  {
    vSave._saveMnplanNrec := _mnplanNrec;
    if vSave.getfirst fastfirstrow mnplan = tsOk
    {
      CopyPropertiesToMnplanBuffer(vSave.mnplan.buffer);
      vSave.update current mnplan;
      result := _sp.Save;
      UnsetDirty;
    }
    else
      result := false;
  }
  else
    result := true;
}

function IsDirty: boolean;
{
  result := _isDirty;
}

constructor Init;
{
  _sp := IClaimOverheadsTotalSp(new(ClaimOverheadsTotalSp));
  _sp.Claim := IClaimOverheadsTotal(self);

  result := true;
}

#declare WriteProperty(PropertyName)
{ if(#PropertyName != result) { #PropertyName := result; SetDirty; } }
#end

property IClaimOverheadsTotal.MnplanNrec : comp              read _mnplanNrec;
property IClaimOverheadsTotal.PeriodNrec : comp              read _periodNrec   write #WriteProperty(_periodNrec);
property IClaimOverheadsTotal.KatpodrNrec: comp              read _katpodrNrec  write #WriteProperty(_katpodrNrec);
property IClaimOverheadsTotal.Number     : string[20]        read _number       write #WriteProperty(_number);
property IClaimOverheadsTotal.Name       : string            read _name         write #WriteProperty(_name);
property IClaimOverheadsTotal.SlojNrec   : comp              read _slojNrec     write #WriteProperty(_slojNrec);
property IClaimOverheadsTotal.StartDate  : date              read _startDate    write #WriteProperty(_startDate);
property IClaimOverheadsTotal.EndDate    : date              read _endDate      write #WriteProperty(_endDate);
property IClaimOverheadsTotal.Sp         : IClaimOverheadsTotalSp read _sp;
end.
