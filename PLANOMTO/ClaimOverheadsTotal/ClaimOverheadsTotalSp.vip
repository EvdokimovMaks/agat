#include ClaimOverheadsTotal.vih
#include Query.vih
#include IModify.vih

#component "AGAT"

interface ClaimOverheadsTotalSp;

sql Query qLoadSp =
select mnplan.canval2           as KatpodrNrec,
       spm.nrec                 as Nrec,
       spm.cspmnplan            as ParentNrec,
       spm.nrec                 as SpmnplanNrec,
       spm.condrec              as ConditionNrec,
       spm.number               as Number,
       spm.canval1              as CostitemNrec,
       spm.canval4              as CONrec,
       spm.typeizd              as ItemType,
       spm.cizd                 as ItemNrec,
       case when spm.cotped = #comp(0)
            then coalesce(oe.nrec,#comp(0))
            else spm.cotped end as OtpedNrec,
       coalesce(vsp.kol, 0)     as Qty,
       coalesce(vsp.price, 0)   as Price,
       coalesce(vsp.vprice, 0)  as Sum
from mnplan
join spmnplan spm on spm.cmnplan = mnplan.nrec
left join katotped oe on oe.prmc = spm.typeizd and oe.cmcusl = spm.cizd and oe.akt = 1 and spm.cizd <> #comp(0)
left join spmnpl spl on spl.cspmnplan = spm.nrec and spl.wkolan = 25
left join valspmnp vsp on vsp.cspmnpl = spl.nrec and spl.nrec is not null
where mnplan.nrec = :MnplanNrec
;

sql query qLoadSpMonth =
select spm.nrec as SpmnplanNrec,
       month(datetime(per.dbeg,0)) as MonthIndex,
       vsp.kol as Qty,
       vsp.price as Price,
       vsp.vPrice as Sum
from mnplan
left join fpperiod mainper on mainper.nrec = mnplan.canval1
join spmnplan spm on spm.cmnplan = mnplan.nrec
join spmnpl spl on spl.cspmnplan = spm.nrec and spl.wkolan = 1
join fpperiod per on spl.canval4 = per.nrec and per.dbeg between coalesce(mainper.dbeg,0) and coalesce(mainper.dend,0)
join valspmnp vsp on vsp.cspmnpl = spl.nrec
where mnplan.nrec = :MnplanNrec
;

sql query qGetPeriodNrec =
select per.nrec
from fpperiod per1
join fpperiod per on per.cmain = per1.cmain
                 and year(datetime(per.dbeg,0))=year(datetime(per1.dbeg,0))
                 and month(datetime(per.dbeg,0)) = :MonthIndex
join fptper tper on tper.nrec = per.ctper and tper.edizm = 2
where per1.nrec = :ClaimPeriodNrec
;

var
  _claim: IClaimOverheadsTotal;
  _qGetPeriodNrec: IQuery;

file f;

create view
as select *
from tClaimOverheadsSp tSp, tClaimOverheadsSp tSpChildren, tClaimOverheadsSp
where ((
  tSp.nrec == tSpChildren.parentNrec
));

create view vSp
var
  _spNrec: comp
as select *
from tClaimOverheadsSp tSp, tClaimOverheadsSp tSpChildren
where ((
  _spNrec          == tSp.nrec and
  _spNrec          == tSpChildren.parentNrec
));

create view vDelete
as select *
from tClaimOverheadsSp tSp;

create view vCheck
var
  _parentSpNrec: comp;
  _number: string[20];
  _costitemNrec: comp;
as select *
from tClaimOverheadsSp tSp
where ((
  _parentSpNrec    == tSp.parentNrec and
  (vCheck.tSp.costitemNrec = _costitemNrec and
   vCheck.tSp.number       = _number and
   vCheck.tSp.ItemNrec     = 0)
));

create view vSave
var
  _parentNrec: comp;
as select *
from  tClaimOverheadsSp tSpParent,
      tClaimOverheadsSp tSp,
      spmnplan spm,
      spmnpl,
      valspmnp,
      mnanal,
      allmemo
where ((
  _parentNrec       == tSpParent.nrec and
  _parentNrec       == tSp.parentNrec and
  tSp.spmnplanNrec  == spm.nrec and
  _claim.SlojNrec   == mnanal.csloj and
  0                 == mnanal.cblock and
  0                 == mnanal.fldan1 and
  _claim.MnplanNrec == mnanal.fldan2
))
;

create view vRemovedSpmnplan
var
  _mnplanNrec: comp;
as select *
from spmnplan, tClaimOverheadsSp tSp, spmnpl, valspmnp
where ((
  _mnplanNrec   == spmnplan.cmnplan and
  spmnplan.nrec == spmnpl.cspmnplan and
  spmnpl.nrec   == valspmnp.cspmnpl and
  spmnplan.nrec == tSp.SpmnplanNrec
));

create view v
as select *
from tClaimOverheadsSp tSp, allmemo
;

create view vAddPodr
var _curtSPNrec: comp;
as select *
from tClaimOverheadsSp tSp, tClaimOverheadsSp tSpChildren
where ((
  _curtSPNrec == tSp.nrec and
  tSp.nrec    == tSpChildren.parentNrec
));


//#region Вспомогательные функции

function  isValidMonthIndex(monthIndex: byte): boolean;
{
  result := monthIndex > 0 and monthIndex <= 12;
}

procedure RecalcCurrent;
{
  var i: byte;
  tSp.qty := 0;
  tSp.sum := 0;
  for(i := 1; i <= 12; i++)
  {
    tSp.monthSum[i] := tSp.monthQty[i] * tSp.price;
    tSp.qty += tSp.monthQty[i];
    tSp.sum += tSp.monthSum[i];
  }

  _claim.SetDirty;
}

procedure RecalcSpAgregate(aSpNrec: comp);
{
  vSp._spNrec := aSpNrec;

  if vSp.getfirst fastfirstrow tSp = tsOk
  {

    vSp.tSp.sum    := 0;
    vSp.tSp.price  := 0;
    vSp.tSp.qty    := 0;

    var i: byte;
    for(i := 1; i <= 12; i++)
    {
      vSp.tSp.monthSum[i] := 0;
      vSp.tSp.monthQty[i] := 0;
    }

    vSp._loop tSpChildren
    {
      for(i := 1; i <= 12; i++)
      {
        vSp.tSp.monthSum[i] += vSp.tSpChildren.monthSum[i];
        vSp.tSp.monthQty[i] += vSp.tSpChildren.monthQty[i];
      }
      vSp.tSp.qty         += vSp.tSpChildren.qty;
      vSp.tSp.sum         += vSp.tSpChildren.sum;
    }

    vSp.update current  tSp;

    if vSp.tSp.parentNrec != 0
      RecalcSpAgregate(vSp.tSp.parentNrec);
  }

  _claim.SetDirty;
}

procedure DeleteSpChildren(aSpNrec: comp);
{
  var l: longint;
  l := initmarker('', 8, 10, 20, false);

  insertmarker(l, aSpNrec);

  var i: longint;
  var c: comp;
  for(i := 0; getmarker(l, i, c); i++)
  {
    vSp._spNrec := c;
    vSp._loop tSpChildren
      insertmarker(l, vSp.tSpChildren.nrec);
  }

  AtDeleteMarker(l, 0);

  vDelete.delete all tSp where foundmarker(l, vDelete.tSp.nrec);

  donemarker(l, '');

  _claim.SetDirty;
}

function CheckSpCostitem(aParentSpNrec    : comp;
                         aCostitemNrec    : comp;
                         aNumber          : string;
                         aAllowAddChildren: boolean = true;
                         aItemType        : word = 1;
                         aCONrec          : comp = 0001000000000077h): comp;
{
  vCheck._parentSpNrec := aParentSpNrec;
  vCheck._costitemNrec := aCostitemNrec;
  vCheck._number       := aNumber;

  if vCheck.getfirst tSp != tsOk
  {
    vCheck.clearbuffer(#vCheck.tSp);
    vCheck.tSp.parentNrec   := aParentSpNrec;
    vCheck.tSp.costitemNrec := aCostitemNrec;
    vCheck.tSp.number       := aNumber;
    vCheck.insert current tSp;
    _claim.SetDirty;
  }

  vCheck.tSp.isCostitem    := true;
  vCheck.tSp.allowAddChild := aAllowAddChildren;
  vCheck.tSp.itemType      := aItemType;
  vCheck.tSp.coNrec        := aCONrec;
  vCheck.tSp.otpedNrec     := 0;
  vCheck.update current tSp;

  result := vCheck.tSp.nrec;
}

procedure SetPrice(value: double);
{
  tSp.price := value;
  RecalcCurrent;
}

type TSpmnplan = record as table spmnplan;
type TSpmnpl = record as table spmnpl;

function GetPeriodNrec(claimPeriodNrec: comp; monthIndex: byte): comp; cacheable;
{
  result := 0;

  if _qGetPeriodNrec = nullref
    _qGetPeriodNrec := queryManager.createQuery(qGetPeriodNrec);

  var rs: IResultSet;
  rs := _qGetPeriodNrec.setParam('ClaimPeriodNrec', claimPeriodNrec)
                       .setParam('MonthIndex', monthIndex)
                       .getResultSet;
  if rs != nullref
    if rs.getFirst = tsOk
      result := rs.row.Val('nrec');
}

procedure SaveValspmnp(spmnplBuf: TSpmnpl; aQty: double; aPrice: double; aSum: double);
{
  if vSave.getfirst fastfirstrow valspmnp where ((spmnplBuf.nrec == valspmnp.cspmnpl
                                              and 1              == valspmnp.num)) != tsOk
  {
    vSave.clearbuffer(#vSave.valspmnp);
    vSave.valspmnp.cspmnpl := spmnplBuf.nrec;
    vSave.valspmnp.num     := 1;
    vSave.insert current valspmnp;
  }
  vSave.valspmnp.cspmnplan := spmnplBuf.cspmnplan;
  vSave.valspmnp.cmnanal   := spmnplBuf.cmnanal;
  vSave.valspmnp.cOtpEd    := spmnplBuf.cotped;
  vSave.valspmnp.EdTime    := spmnplBuf.edtime;
  vSave.valspmnp.kol       := aQty;
  vSave.valspmnp.price     := aPrice;
  vSave.valspmnp.vprice    := aSum;
  vSave.update current valspmnp;
}

procedure DeleteSpmnpl( _spmnplanNrec: comp;
                        _wkolan      : word;
                        _wkodgr4     : word;
                        _canval4     : comp);
{
  if vSave.getfirst fastfirstrow spmnpl where (( _spmnplanNrec == spmnpl.cspmnplan
                                             and _wkolan       == spmnpl.wkolan
                                             and _wkodgr4      == spmnpl.wkodgr4
                                             and _canval4      == spmnpl.canval4 )) = tsOk
  {
    vSave.delete all valspmnp where ((vSave.spmnpl.nrec == valspmnp.cspmnpl));
    vSave.delete current spmnpl;
  }

}

function SaveSpmnpl(bufSpmnplan: TSpmnplan;
                    _wkolan    : word;
                    _wkodgr4   : word;
                    _crolean4  : comp;
                    _canval4   : comp): TSpmnpl;
{
  if vSave.getfirst fastfirstrow mnanal != tsOk
  {
    vSave.clearbuffer(#vSave.mnanal);
    vSave.mnanal.csloj  := _claim.SlojNrec;
    vSave.mnanal.cblock := 0;
    vSave.mnanal.fldan1 := 0;
    vSave.mnanal.fldan2 := _claim.MnplanNrec;
    vSave.insert current mnanal;
  }

  if vSave.getfirst fastfirstrow spmnpl where (( bufSpmnplan.nrec == spmnpl.cspmnplan
                                             and _wkolan          == spmnpl.wkolan
                                             and _wkodgr4         == spmnpl.wkodgr4
                                             and _canval4         == spmnpl.canval4 )) != tsOk
  {
    vSave.clearbuffer(#vSave.spmnpl);
    vSave.spmnpl.cspmnplan := bufSpmnplan.nrec;
    vSave.spmnpl.wkolan    := _wkolan;
    vSave.spmnpl.wkodgr4   := _wkodgr4;
    vSave.spmnpl.crolean4  := _crolean4;
    vSave.spmnpl.canval4   := _canval4;
    vSave.insert current spmnpl;
  }

  vSave.spmnpl.cspmnplan := bufSpmnplan.nrec;
  vSave.spmnpl.cRoleAn1  := bufSpmnplan.cRoleAn1;
  vSave.spmnpl.wKodGr1   := bufSpmnplan.wKodGr1;
  vSave.spmnpl.cAnVal1   := bufSpmnplan.cAnVal1;
  vSave.spmnpl.cRoleAn2  := bufSpmnplan.cRoleAn2;
  vSave.spmnpl.wKodGr2   := bufSpmnplan.wKodGr2;
  vSave.spmnpl.cAnVal2   := bufSpmnplan.cAnVal2;
  vSave.spmnpl.cRoleAn3  := bufSpmnplan.cRoleAn3;
  vSave.spmnpl.wKodGr3   := bufSpmnplan.wKodGr3;
  vSave.spmnpl.cAnVal3   := bufSpmnplan.cAnVal3;
  vSave.spmnpl.wKodGr8   := bufSpmnplan.wKodGr4;
  vSave.spmnpl.cAnVal8   := bufSpmnplan.cAnVal4;
  vSave.spmnpl.typeizd   := bufSpmnplan.TypeIzd;
  vSave.spmnpl.cRoleIzd  := bufSpmnplan.cRoleIzd;
  vSave.spmnpl.cIzd      := bufSpmnplan.cIzd;
  vSave.spmnpl.cOtpEd    := bufSpmnplan.cOtpEd;
  vSave.spmnpl.NumSort   := bufSpmnplan.NumSort;
  vSave.spmnpl.Number    := bufSpmnplan.Number;
  vSave.spmnpl.cSloj     := bufSpmnplan.cSloj;
  vSave.spmnpl.cMnPlan   := bufSpmnplan.cmnplan;
  vSave.spmnpl.cVal1     := bufSpmnplan.cVal1;
  vSave.spmnpl.cMnAnal   := if(vSave.getfirst mnanal = tsOk, vSave.mnanal.nrec, 0);
  vSave.update current spmnpl;
  result := TSpmnpl(vSave.spmnpl.buffer);
}

function SaveSpmnplan: TSpmnplan;
{

  if vSave.tSp.spmnplanNrec = 0
  {
    vSave.clearbuffer(#vSave.spm);
    vSave.spm.cmnplan       := _claim.MnplanNrec;
    vSave.spm.startdate     := _claim.StartDate;
    vSave.spm.enddate       := _claim.EndDate;
    vSave.spm.croleizd      := 40018CA016897966h;
    vSave.spm.crolean1      := 0001000000000020h;      //Статья бюджета
    vSave.spm.wkodgr1       := 20;                     //Статья бюджета
    vSave.spm.crolean2      := 000100000000003Eh;      //Контрагент
    vSave.spm.wkodgr2       := 1;                      //Организация
    vSave.spm.canval2       := 0;
    vSave.spm.crolean3      := 40013599DF00FAADh;      //Договор
    vSave.spm.wkodgr3       := 14;                     //Договоры
    vSave.spm.canval3       := 0;
    vSave.spm.crolean4      := 0001000000000031h;      //Центр ответсвенности
    vSave.spm.wkodgr4       := 22;                     //Центры ответственности
    vSave.spm.csloj         := _claim.SlojNrec;
    vSave.insert current spm;
    vSave.update current tSp set tSp.SpmnplanNrec := vSave.spm.nrec;
  }

  if vSave.getfirst fastfirstrow spm = tsOk
  {
    vSave.spm.number        := vSave.tSp.number;
    vSave.spm.cizd          := vSave.tSp.itemNrec;
    vSave.spm.typeizd       := vSave.tSp.itemType;
    vSave.spm.cotped        := vSave.tSp.otpedNrec;
    vSave.spm.canval1       := vSave.tSp.costitemNrec;
    vSave.spm.canval4       := vSave.tSp.coNrec;
    vSave.spm.cspmnplan     := if(vSave.getfirst tSpParent = tsOk, vSave.tSpParent.spmnplanNrec, 0);
    //vSave.spm.cstatus       := if(vSave.tSp.isCostitem, 0, 00010000000000E1h);
    vSave.spm.condrec       := if(vSave.tSp.isCostitem, 1, 0);
    vSave.spm.cval1         := 0001000000000231h;
    vSave.update current spm;

    if vSave.tSp.Description = ''
    {
      if vSave.getfirst allmemo where ((11038          == allmemo.wtable and
                                        vSave.spm.nrec == allmemo.cRec  )) = tsOk
      {
        vSave.delete current allmemo;
      }
    }
    else
    {
      if vSave.getfirst allmemo where ((11038          == allmemo.wtable and
                                        vSave.spm.nrec == allmemo.cRec  )) != tsOk
      {
        vSave.clearbuffer(#vSave.allmemo);
        vSave.allmemo.wtable := 11038;
        vSave.allmemo.cRec   := vSave.spm.nrec;
        vSave.insert current allmemo;
      }
      ClearMemoField(vSave.allmemo.mem);
      f.OpenMemo(vSave.allmemo.mem);
      f.Writeln(vSave.tSp.Description);
      f.Close;
      vSave.update current allmemo;
    }
  }

  result := TSpmnplan(vSave.spm.buffer);
}

procedure SaveChildren(aParentNrec: comp);
{
  var l: longint;
  l := initmarker('',8,10,10,false);
  vSave._parentNrec := aParentNrec;
  vSave._loop tSp
  {

    var spmnplanBuf: TSpmnplan;
    spmnplanBuf := SaveSpmnplan;

    //все значения нулевые - удаляем ячейку
    if vSave.tSp.qty=0 and vSave.tSp.price=0 and vSave.tSp.sum=0
      DeleteSpmnpl(spmnplanBuf.nrec, 25, 29, _claim.MnplanNrec);
    else
    {
      var spmnplBuf: TSpmnpl;
      spmnplBuf := SaveSpmnpl(spmnplanBuf, 25, 29, 400174D9C8CF511Eh, _claim.MnplanNrec);
      SaveValspmnp(spmnplBuf, vSave.tSp.qty, vSave.tSp.price, vSave.tSp.sum);
    }

    var monthIndex: byte;
    for (monthIndex := 1; monthIndex <= 12; monthIndex++)
    {
      var monthPeriodNrec: comp;
      monthPeriodNrec := getPeriodNrec(_claim.PeriodNrec, monthIndex);

      if vSave.tSp.monthQty[monthIndex] = 0
        DeleteSpmnpl(spmnplanBuf.nrec, 1, 21, monthPeriodNrec);
      else
      {
        var spmnplBuf: TSpmnpl;
        spmnplBuf := SaveSpmnpl(spmnplanBuf, 1, 21, 40015C4CA3652682h, monthPeriodNrec);
        SaveValspmnp(spmnplBuf, vSave.tSp.monthQty[monthIndex], vSave.tSp.price, vSave.tSp.monthSum[monthIndex]);
      }
    }

    insertmarker(l, vSave.tSp.Nrec);
  }

  var i: longint;
  var c: comp;
  for(i := 0; getmarker(l, i, c); i++)
  {
    SaveChildren(c);
  }

  donemarker(l,'');
}

procedure DeleteRemovedSpmnplan;
{
  var l: longint;
  l := initmarker('',8,10,10,false);
  vRemovedSpmnplan._mnplanNrec := _claim.MnplanNrec;
  vRemovedSpmnplan._loop spmnplan
  {
    if vRemovedSpmnplan.getfirst tSp != tsOk
    {
      insertmarker(l, vRemovedSpmnplan.spmnplan.nrec);
      vRemovedSpmnplan._loop spmnpl
        vRemovedSpmnplan.delete all valspmnp;
      vRemovedSpmnplan.delete all spmnpl;
    }
  }
  vRemovedSpmnplan.delete all spmnplan where foundmarker(l, vRemovedSpmnplan.spmnplan.nrec);

  donemarker(l,'');
}
//#endregion

//#region IModify

Function IModify.doInsert : Word;
{
  RecalcCurrent;
  result := insert current tSp;
  RecalcSpAgregate(tSp.parentNrec);
  _claim.SetDirty;
}

Function IModify.doUpdate : Word;
{
  if tSp.IsCostitem
  {
    result := update current tSp;
    RecalcSpAgregate(tSp.Nrec);
  }
  else
  {
    RecalcCurrent;
    result := update current tSp;
    RecalcSpAgregate(tSp.parentNrec);
  }
  _claim.SetDirty;
}

Function IModify.doDelete : Word;
{
  var aParentSpNrec: comp;
  aParentSpNrec := tSp.parentNrec;

  DeleteSpChildren(tSp.nrec);

  result := delete current tSp;

  RecalcSpAgregate(aParentSpNrec);

  _claim.SetDirty;
}

implementation IModify.doFlush  abstract;

//#endregion

//#region IClaimOverheadsTotalSp

function  IClaimOverheadsTotalSp.Load(aMnplanNrec: comp): boolean;
{
//  delete all tClaimOverheadsSp;
  CheckCostitems;
/*
  sqlCopyInto(
    queryManager.createQuery(qLoadSp).setParam('MnplanNrec',aMnplanNrec).getResultSet,
    #tClaimOverheadsSp,
    true
  );  */
  var rsSp: IResultSet;
  rsSp := queryManager.createQuery(qLoadSp).setParam('MnplanNrec',aMnplanNrec).getResultSet;
  if rsSp != nullref
    if rsSp.getfirst = tsOk do
    {
      var CurItemNrec: comp;
      CurItemNrec := rsSp.row.val('ITEMNREC');
      if CurItemNrec != comp(0)
      {
        ClearBuffer(#tClaimOverheadsSp);
        tClaimOverheadsSp.spmnplanNrec   := rsSp.row.val('SpmnplanNrec');
        tClaimOverheadsSp.conditionNrec  := rsSp.row.val('ConditionNrec');
        tClaimOverheadsSp.number         := rsSp.row.val('Number');
        tClaimOverheadsSp.costitemNrec   := rsSp.row.val('CostitemNrec');
        tClaimOverheadsSp.itemType       := rsSp.row.val('ItemType');
        tClaimOverheadsSp.itemNrec       := CurItemNrec;
        tClaimOverheadsSp.KatpodrNrec    := rsSp.row.val('KatpodrNrec');
        tClaimOverheadsSp.otpedNrec      := rsSp.row.val('OtpedNrec');
        tClaimOverheadsSp.coNrec         := rsSp.row.val('CONrec');
        tClaimOverheadsSp.Kind           := 1;
        tClaimOverheadsSp.isCostitem     := false;
        tClaimOverheadsSp.allowAddChild  := true;
        tClaimOverheadsSp.qty            := rsSp.row.val('Qty');
        tClaimOverheadsSp.price          := rsSp.row.val('Price');
        tClaimOverheadsSp.sum            := rsSp.row.val('Sum');

        if getfirst tSp where ((tClaimOverheadsSp.costitemNrec == tSp.CostitemNrec and
                                0                              == tSp.ItemNrec)) = tsOk
        {
          tClaimOverheadsSp.parentNrec := tSp.Nrec;
        }

        insert current tClaimOverheadsSp;
      }
    } while rsSp.getnext = tsOk;

  var rs: IResultSet;
  rs := queryManager.createQuery(qLoadSpMonth).setParam('MnplanNrec', aMnplanNrec).getResultSet;

  if rs != nullref
    if rs.getfirst = tsOk do
    {
      vSp._spNrec := rs.row.Val('SpmnplanNrec');
      if vSp.getfirst tSp = tsOk
      {
        var monthIndex: byte;
        monthIndex := rs.row.Val('MonthIndex');

        if isValidMonthIndex(monthIndex)
        {
          vSp.tSp.monthQty[monthIndex] := rs.row.Val('Qty');
          vSp.tSp.monthSum[monthIndex] := rs.row.Val('Sum');

          vSp.update current tSp;
        }
      }
    } while rs.getNext = tsOk;

  v._loop tSp
  {
    if v.tSp.IsCostitem
      v.tSp.Kind := 0                //статья
    else if (v.tSp.ItemType = 1) or (v.tSp.ItemType = 2)
    {
      v.tSp.Kind := 1                //мц
      v.tSp.allowAddChild := true;

      vAddPodr._curtSPNrec := v.tSp.Nrec;   //каждой мц добавляем подразделение
      if vAddPodr.getfirst tSp = tsOk
      {
        ClearBuffer(#vAddPodr.tSpChildren);
        vAddPodr.tSpChildren.ParentNrec    := vAddPodr.tSp.Nrec;
        vAddPodr.tSpChildren.AllowAddChild := false;
        vAddPodr.tSpChildren.ItemType      := 3;
        vAddPodr.tSpChildren.Kind          := 2;
        vAddPodr.tSpChildren.KatpodrNrec   := vAddPodr.tSp.KatpodrNrec;
        vAddPodr.tSpChildren.CostitemNrec  := vAddPodr.tSp.CostitemNrec;
        vAddPodr.insert current tSpChildren;
      }
    }
    else
      v.tSp.Kind := 2;             //подразделение
    v.update current tSp;

    if v.getfirst allmemo where ((11038              == allmemo.wtable and
                                  v.tSp.SpmnplanNrec == allmemo.cRec )) = tsOk
    {
      var s: string = '';
      f.OpenMemo(v.allmemo.mem);
      while not f.eof
      {
        f.Readln(s);
        v.tSp.Description := v.tSp.Description + if(v.tSp.Description != '', ' ', '') + s;
      }
      f.Close;
      v.update current tSp;
    }
  }

  result := true;
  message(RecordsInTable(#tClaimOverheadsSp));
}

procedure IClaimOverheadsTotalSp.CheckCostitems;
{
  var spNrec1, spNrec2, spNrec3: comp;
  spNrec1 := CheckSpCostitem(0      , 0001000000000471h, '00001', false); //Расходы на материально - техническое обеспечение
  spNrec2 := CheckSpCostitem(spNrec1, 00010000000005E2h, '00002', false); //Некапитализируемые затраты (стоимость единицы < 100 000 тыс. руб.)
  spNrec3 := CheckSpCostitem(spNrec2, 000100000000031Ch, '00003', false); //Мебель, бытовая техника и пр.
             CheckSpCostitem(spNrec3, 00010000000009CCh, '00004', true);  //Мебель
             CheckSpCostitem(spNrec3, 00010000000009CDh, '00005', true);  //Бытовая техника
             CheckSpCostitem(spNrec2, 000100000000047Ch, '00006', true);  //Канцелярские принадлежности
             CheckSpCostitem(spNrec2, 000100000000047Dh, '00007', true);  //Материалы для нужд производства
             CheckSpCostitem(spNrec2, 000100000000047Eh, '00008', true);  //Инструменты, оборудование, приборы и пр.
             CheckSpCostitem(spNrec2, 00010000000009A4h, '00009', true);  //Материалы для копировально-множественной техники типографии
             CheckSpCostitem(spNrec2, 000100000000047Fh, '00010', true);  //Прочие материально-производственные запасы
}

function  IClaimOverheadsTotalSp.Save: boolean;
{
  ClearFuncCache;
  SaveChildren(0);
  DeleteRemovedSpmnplan;
  result := true;
}

function IClaimOverheadsTotalSp.IsLeaf: boolean;
{
  result := getfirst fastfirstrow tSpChildren != tsOk;
}

function  IClaimOverheadsTotalSp.MonthQty(monthIndex: byte): double;
{
  result := if(isValidMonthIndex(monthIndex), tSp.monthQty[monthIndex], 0);
}

function  IClaimOverheadsTotalSp.MonthSum(monthIndex: byte): double;
{
  result := if(isValidMonthIndex(monthIndex), tSp.monthSum[monthIndex], 0);
}

procedure IClaimOverheadsTotalSp.SetMonthQty(monthIndex: byte; value: double);
{
  if isValidMonthIndex(monthIndex)
    if value != tSp.monthQty[monthIndex]
    {
      set tSp.monthQty[monthIndex] := value;
      RecalcCurrent;
    }
}

function  IClaimOverheadsTotalSp.GetBuffer(aSpNrec: comp): TtClaimOverheadsSp;
{
  var buf: TtClaimOverheadsSp;
  ClearAdvRecord(buf);
  vSp._spNrec := aSpNrec;
  if vSp.getfirst tSp = tsOk
    buf := TtClaimOverheadsSp(vSp.tSp.buffer);
  result := buf;
}

function IClaimOverheadsTotalSp.InsertNewWithBuffer(buf: TtClaimOverheadsSp): TtClaimOverheadsSp;
{
  v.clearbuffer(#v.tSp);
  v.tSp.buffer := buf;
  v.insert current tSp;
  result := TtClaimOverheadsSp(v.tSp.buffer);
  _claim.SetDirty;
}

property IClaimOverheadsTotalSp.Nrec             : comp                 absolute tSp.nrec;
property IClaimOverheadsTotalSp.ParentNrec       : comp                 absolute tSp.parentNrec;
property IClaimOverheadsTotalSp.SpmnplanNrec     : comp                 absolute tSp.spmnplanNrec;
property IClaimOverheadsTotalSp.ConditionNrec    : comp                 absolute tSp.conditionNrec;
property IClaimOverheadsTotalSp.Number           : string[20]           absolute tSp.number;
property IClaimOverheadsTotalSp.CostitemNrec     : comp                 absolute tSp.costitemNrec;
property IClaimOverheadsTotalSp.ItemType         : word                 absolute tSp.itemType;
property IClaimOverheadsTotalSp.ItemNrec         : comp                 absolute tSp.itemNrec;
property IClaimOverheadsTotalSp.KatpodrNrec      : comp                 absolute tSp.KatpodrNrec;
property IClaimOverheadsTotalSp.OtpedNrec        : comp                 absolute tSp.otpedNrec;
property IClaimOverheadsTotalSp.CONrec           : comp                 absolute tSp.coNrec;
property IClaimOverheadsTotalSp.Description      : string               absolute tSp.Description;
property IClaimOverheadsTotalSp.Price            : double               read     tSp.price write SetPrice(result);
property IClaimOverheadsTotalSp.Qty              : double               absolute tSp.qty;
property IClaimOverheadsTotalSp.Sum              : double               absolute tSp.sum;
property IClaimOverheadsTotalSp.Kind             : byte                 absolute tSp.Kind;
property IClaimOverheadsTotalSp.IsCostitem       : boolean              absolute tSp.isCostitem;
property IClaimOverheadsTotalSp.AllowAddChildren : boolean              absolute tSp.allowAddChild;
property IClaimOverheadsTotalSp.Claim            : IClaimOverheadsTotal absolute _claim;

index IClaimOverheadsTotalSp.iByNrec auto;
index IClaimOverheadsTotalSp.iByParentNrec auto;

//#endregion

end.
