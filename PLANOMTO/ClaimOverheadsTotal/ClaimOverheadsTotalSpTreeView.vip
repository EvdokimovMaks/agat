#include ClaimOverheadsTotal.vih
#include GetKau.vih
#include Query.vih
#include Marker.vih

#component "AGAT"

interface ClaimOverheadsTotalSpTreeView;

sql query QueryGetIncomePrices =
select sko.dord, sps.price
from sporder spo
join sklorder sko on sko.nrec = spo.csklorder
join katsopr ks on ks.nrec = sko.csopr and ks.vidsopr in (101, 111)
join spsopr sps on sps.nrec = spo.cspsopr
where spo.sp = 0 and spo.vidorder = 0 and spo.cmc = :KatmcNrec
order by sko.dord
;

var
  _sp: IClaimOverheadsTotalSp (ClaimOverheadsTotalSp) new;
  _getkau: GetKau;
  _qGetIncomePrices: IQuery;
  _marker: longint;

table struct tCostitems (
  SpNrec: comp,
  CostitemNrec: comp,
  CostitemName: string
) with index (
  i01 = SpNrec
);

function GetCostItemName(aKind: word): string; forward;

create view
var
  _parentNrec: comp;
as select
//  if(_sp.IsCostitem, fpstbud.Name, if(_sp.ItemType = 2, 'Усл', 'МЦ') )
  GetCostItemName(_sp.kind) (fieldname=CostitemName),
  if(_sp.ItemType = 2, katusl.name, katmc.name) (fieldname=ItemName),
  katotped.abbr (fieldname=OtpedName),
  _sp.MonthQty(1 ) (fieldname=MonthQty1 ),
  _sp.MonthQty(2 ) (fieldname=MonthQty2 ),
  _sp.MonthQty(3 ) (fieldname=MonthQty3 ),
  _sp.MonthQty(4 ) (fieldname=MonthQty4 ),
  _sp.MonthQty(5 ) (fieldname=MonthQty5 ),
  _sp.MonthQty(6 ) (fieldname=MonthQty6 ),
  _sp.MonthQty(7 ) (fieldname=MonthQty7 ),
  _sp.MonthQty(8 ) (fieldname=MonthQty8 ),
  _sp.MonthQty(9 ) (fieldname=MonthQty9 ),
  _sp.MonthQty(10) (fieldname=MonthQty10),
  _sp.MonthQty(11) (fieldname=MonthQty11),
  _sp.MonthQty(12) (fieldname=MonthQty12),
  _sp.MonthSum(1 ) (fieldname=MonthSum1 ),
  _sp.MonthSum(2 ) (fieldname=MonthSum2 ),
  _sp.MonthSum(3 ) (fieldname=MonthSum3 ),
  _sp.MonthSum(4 ) (fieldname=MonthSum4 ),
  _sp.MonthSum(5 ) (fieldname=MonthSum5 ),
  _sp.MonthSum(6 ) (fieldname=MonthSum6 ),
  _sp.MonthSum(7 ) (fieldname=MonthSum7 ),
  _sp.MonthSum(8 ) (fieldname=MonthSum8 ),
  _sp.MonthSum(9 ) (fieldname=MonthSum9 ),
  _sp.MonthSum(10) (fieldname=MonthSum10),
  _sp.MonthSum(11) (fieldname=MonthSum11),
  _sp.MonthSum(12) (fieldname=MonthSum12),
  foundmarker(_marker, _sp.nrec) (fieldname=IsMarked)
from _sp, fpstbud, katusl, katmc, katotped, pickpric, prices, tCostitems
where ((
  _sp.costitemNrec == fpstbud.nrec and
  _sp.ItemNrec     == katusl.nrec and
  _sp.ItemNrec     == katmc.nrec and
  _sp.OtpedNrec    == katotped.nrec
))
bounds byParent = _parentNrec == _sp.ParentNrec
;

function GetCostItemName(aKind: word): string;
{
  case aKind of
    0: { //article
      if _sp.IsCostitem
        if getfirst fpstbud where ((_sp.costitemNrec == fpstbud.nrec)) = tsOk
        { result := fpstbud.Name; } }
    1: { //mc
      if _sp.ItemType = 2
        result := 'Усл'
      else
        result := 'МЦ'; }
    2: { //podr
      result := if(getanykau(1, 2, _sp.KatpodrNrec), givenanname(1), ''); }
  end;
}

#define QtyFieldFormat [8, '\4p[|-]36666666666666666~9999']

#define SumFieldFormat [12, '\2p[|-]3666`666`666`666`666~88']

tree 1 tr1 ( ,,sci1478EscExInsTree);
excelColumns;
table _sp;
fixedLeft(IsMarkedField, CostitemName, ItemName, OtpedName, _sp.Description, _sp.Qty, _sp.Price, _sp.Sum);
fields
  {font={bold=_sp.IsCostItem;color=if(IsMarked, ColorMark, 0);}};
  [IsMarkedField] if(IsMarked,'V','') ''  : [1], skip, noautosize;
  CostitemName     'Наименование'         : [30] , protect;
  ItemName         'Матценность/Услуга'   : [30] , protect, pickbutton;
  OtpedName        'Ед.изм'               : [8]  , protect, noautosize;
  _sp.Description  'Содержание'           : [10] , noprotect;

//  _sp.Nrec         'Nrec'                 : [10] , protect;
//  _sp.Number       'Number'               : [10] , protect;
//  _sp.ParentNrec   'ParentNrec'           : [10] , protect;
//  _sp.SpmnplanNrec 'SpmnplanNrec'         : [10] , protect;
//  _sp.costitemNrec 'CostitemNrec'         : [10] , protect;
//  _sp.OtpedNrec    'OtpedNrec'            : [10], protect;

  _sp.Qty          'Всего кол-во'         : #QtyFieldFormat  , protect;
  _sp.Price        'Цена'                 : #SumFieldFormat  , noprotect;
  _sp.Sum          'Всего сумма'          : #SumFieldFormat  , protect;
  MonthQty1        'Январь  '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum1        'Январь  '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty2        'Февраль '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum2        'Февраль '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty3        'Март    '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum3        'Март    '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty4        'Апрель  '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum4        'Апрель  '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty5        'Май     '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum5        'Май     '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty6        'Июнь    '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum6        'Июнь    '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty7        'Июль    '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum7        'Июль    '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty8        'Август  '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum8        'Август  '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty9        'Сентябрь'#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum9        'Сентябрь'#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty10       'Октябрь '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum10       'Октябрь '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty11       'Ноябрь  '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum11       'Ноябрь  '#13'Сумма'   : #SumFieldFormat  , protect;
  MonthQty12       'Декабрь '#13'Кол-во'  : #QtyFieldFormat  , noprotect;
  //MonthSum12       'Декабрь '#13'Сумма'   : #SumFieldFormat  , protect;
end;

constructor Init;
{
  _marker := InitMarker('',8, 10, 20, false);
  result := _marker != 0;
}

destructor Done;
{
  UnsetTreeBounds;
  donemarker(_marker,'');
}

procedure OpenAllNodes;
{
  TreePushPos(tr1);
  TreeGetFirstEx(tr1);
  do {
    TreeOpenNode(tr1);
  } while TreeGetNextEx(tr1);
  TreePopPos(tr1);
}

procedure CloseAllNodes;
{
  if TreeGetFirstEx(tr1)
    TreeCloseAllOpenedNode(tr1);
  rereadrecord(#_sp);
}

function  JumpToFirst: boolean;
{
  result := TreeGetFirstEx(tr1);
  rereadrecord(#_sp);
}

procedure UnsetTreeBounds;
{
  if BoundActive(tbByParent)
    SubBounds(tbByParent);
}

procedure SetTreeBounds;
{
  AddBounds(tbByParent);
}

procedure SetSp(aSp: IClaimOverheadsTotalSp);
{
  UnsetTreeBounds;
  _sp := aSp;
  SetTreeBounds;
  TreeGetFirstEx(tr1);
  rereadrecord(#_sp);
}

function AllowEditSp: boolean;
{
  result := not _sp.IsCostitem;
}

function GetLastIncomePrice(aKatmcNrec: comp): double;
{
  result := 0;

  if _qGetIncomePrices = nullref
    _qGetIncomePrices := queryManager.createQuery(QueryGetIncomePrices);

  var rs: IResultSet;
  rs := _qGetIncomePrices.setParam('KatmcNrec', aKatmcNrec).getResultSet;

  if rs != nullref
    if rs.getLast = tsOk
      result := rs.row.Val('price');
}

function  AddSpToChildLevel: comp;
{
  result := _sp.nrec;

  if (_sp.AllowAddChildren)
  {
    UpdateTable;

    var buf: TtClaimOverheadsSp;
    ClearAdvRecord(buf);

    buf.ParentNrec    := _sp.Nrec;
    buf.IsCostitem    := false;
    buf.AllowAddChild := false;
    buf.ItemType      := _sp.ItemType;
    buf.CostitemNrec  := _sp.CostitemNrec;

    result := _sp.InsertNewWithBuffer(buf).Nrec;
  }

}

function  AddSpToCurrentLevel: comp;
{
  result := _sp.Nrec;

  if _sp.ParentNrec = 0 exit;

  var parentBuf: TtClaimOverheadsSp;
  parentBuf := _sp.GetBuffer(_sp.ParentNrec);

  if parentBuf.allowAddChild
  {
    UpdateTable;

    var buf: TtClaimOverheadsSp;
    ClearAdvRecord(buf);

    buf.ParentNrec    := parentBuf.nrec;
    buf.IsCostitem    := false;
    buf.AllowAddChild := false;
    buf.ItemType      := parentBuf.itemType;
    buf.CostitemNrec  := parentBuf.costitemNrec;
    result := _sp.InsertNewWithBuffer(buf).Nrec;
  }
}

function DeleteCurrentSp: boolean;
{
  result := false;

  if AllowEditSp
    if isValid(#_sp)
      result := (delete current _sp) = tsOk;
}

function  PickItemFromCatalog: boolean;
{
  result := false;
  var c: comp; c := _sp.ItemNrec;
  if _getkau.GetCodeKau(1, word(if(_sp.ItemType=2, 5, 4)), c) > 0
  {
    set _sp.ItemNrec := c;

    var p: double;
    p := GetLastIncomePrice(c);
    if p > 0
      set _sp.Price := p;

    update current _sp;

    result := true;
  }
}

function GetPriceNrecByCostitemNrec(aCostitemNrec: comp): comp;
{
  case aCostitemNrec of
    00010000000009CCh: result := 000100000000000Eh; //Мебель
    00010000000009CDh: result := 000100000000001Ah; //Бытовая техника
    000100000000047Ch: result := 000100000000009Fh; //Канцелярские принадлежности
    000100000000047Dh: result := 00010000000000A1h; //Материалы для нужд производства
    000100000000047Eh: result := 00010000000000A4h; //Инструменты, оборудование, приборы и пр.
    00010000000009A4h: result := 00010000000000A0h; //Материалы для копировально-множественной техники типографии
    000100000000047Fh: result := 00010000000000A5h; //Прочие материально-производственные запасы
  else
    result := 0;
  end;
}

function PickItemFromPrice: boolean;
{
  result := false;
  if runinterface(L_PRICES::GetPrice, GetPriceNrecByCostitemNrec(_sp.CostitemNrec), 0, _sp.ItemType, false) = cmDefault
  {
    if getfirst pickpric = tsOk
    {
      _sp.ItemType := pickpric.pickprmc;
      _sp.ItemNrec := pickpric.pickthing;
      _sp.OtpedNrec := pickpric.picked;
      if getfirst prices where ((pickpric.pickrec == prices.nrec)) = tsOk
        _sp.Price := prices.price;
      update current _sp;

      while getnext pickpric = tsOk
      {
        treejumptorecord(tr1, AddSpToCurrentLevel);
        _sp.ItemType := pickpric.pickprmc;
        _sp.ItemNrec := pickpric.pickthing;
        _sp.OtpedNrec := pickpric.picked;
        if getfirst prices where ((pickpric.pickrec == prices.nrec)) = tsOk
          _sp.Price := prices.price;
        update current _sp;
      }

      result := true;
    }
  }
}

procedure SetMonthQty(monthIndex: byte; value: double);
{
  _sp.SetMonthQty(monthIndex, value);
  update current _sp;
  rereadrecord(#_sp);
}

tableevent table _sp
  cmTreeTop      : _parentNrec := 0;
  cmTreeUp       : _parentNrec := _sp.ParentNrec;
  cmTreeDown     : _parentNrec := _sp.Nrec;
  cmTreeNeedOwner: treejumptorecord(tr1, _sp.ParentNrec);
  cmTreeNodeType : if not _sp.IsCostitem and TreeIsTerminal(tr1) TreeSetNodeType(tr1, ntfText);

  cmUpdateRecord :
  {
    if AllowEditSp
      update current _sp;
    else
    {
      message('Редактирование текущей строки запрещено', error);
      abort;
    }
  }

  cmCheckField   :
  {
    if AllowEditSp
    {
      case curfield of
        #_sp.price, #_sp.Description: { update current _sp; }
      end;
    }
    else
    {
      message('Редактирование текущей строки запрещено', error);
      setmodified(false);
    }
    rereadrecord(#_sp);
  }

  cmExprFieldChanged:
  {
    if AllowEditSp
    {
      case curfield of
        #MonthQty1 : SetMonthQty(1 , ExprFieldValue);
        #MonthQty2 : SetMonthQty(2 , ExprFieldValue);
        #MonthQty3 : SetMonthQty(3 , ExprFieldValue);
        #MonthQty4 : SetMonthQty(4 , ExprFieldValue);
        #MonthQty5 : SetMonthQty(5 , ExprFieldValue);
        #MonthQty6 : SetMonthQty(6 , ExprFieldValue);
        #MonthQty7 : SetMonthQty(7 , ExprFieldValue);
        #MonthQty8 : SetMonthQty(8 , ExprFieldValue);
        #MonthQty9 : SetMonthQty(9 , ExprFieldValue);
        #MonthQty10: SetMonthQty(10, ExprFieldValue);
        #MonthQty11: SetMonthQty(11, ExprFieldValue);
        #MonthQty12: SetMonthQty(12, ExprFieldValue);
      end;
    }
    else
    {
      message('Редактирование текущей строки запрещено', error);
      rereadrecord(#_sp);
    }
  }

  cmDoMarkUnMark:
  {
    InvertMarker(_marker, _sp.nrec);
    RedrawCurrentAndGo(tr1, true);
  }

  cmSelAll:
  {
    TreePushPos(tr1);
    if TreeGetFirstEx(tr1) do
    {
      InsertMarker(_marker, _sp.nrec);
    } while TreeGetNextEx(tr1);
    TreePopPos(tr1);
    rereadrecord(#_sp);
  }

  cmUnSelAll:
  {
    ClearMarker(_marker);
    rereadrecord(#_sp);
  }

end;

window wSelectCostitem 'Выбор статьи', escclose, doaccept;
  show at (,,50, 10);
  browse brCostitems;
  table tCostitems;
  fields
    tCostitems.CostitemName 'Наименование': [30], protect;
  end;
  handleevent
    cmInit:
    {
      delete all tCostitems;
      pushpos(#_sp);
      resetbounds(#_sp);
      _loop _sp
      {
        if _sp.IsCostitem and _sp.AllowAddChildren
        {
          clearbuffer(#tCostitems);
          tCostitems.SpNrec := _sp.nrec;
          tCostitems.CostitemNrec := _sp.costitemNrec;
          tCostitems.CostitemName := costitemName;
          insert current tCostitems;
        }
      }
      setbounds(#_sp);
      poppos(#_sp);
      rereadrecord(#tCostitems);
    }
  end;
end;

handleevent

  cmPick:
  {
    case curfield of
      #CostitemName, #ItemName:
      {
        if AllowEditSp
          if PickItemFromPrice
            rereadrecord(#_sp);
      }
    end;
  }

  cmRestoreDoc:
  {
    case curfield of
      #CostitemName, #ItemName:
      {
        if AllowEditSp
          if PickItemFromCatalog
            rereadrecord(#_sp);
      }
    end;
  }

  cmInsLevel:
  {
    treejumptorecord(tr1, AddSpToChildLevel);
    rereadrecord(#_sp);
    stop;
  }

  cmAddNewRec:
  {
    treejumptorecord(tr1, AddSpToCurrentLevel);
    rereadrecord(#_sp);
    stop;
  }

  cmDelete:
  {
    if DeleteCurrentSp
    {
      rereadrecord(#_sp);
      if not isValid(#_sp)
      {
        redrawcurrentandgo(tr1, false);
        rereadrecord(#_sp);
      }
    }
    stop;
  }

  cmHotkeys:
  {
    PutHotCommand(RunLoadMenu(LoadMenu('ClaimOverheadsTotalSpTreeViewMenu')));
  }

  cmValue9:
  {
    if runwindowmodal(wSelectCostitem) = cmDefault
    {
      var lParents: longint;
      lParents := initmarker('', 8, 10, 20, false);

      pushpos(#_sp);
      resetbounds(#_sp);

      var c: comp = 0;
      _loop _sp
      {
        if not _sp.IsCostitem and foundmarker(_marker, _sp.nrec)
        {
          insertmarker(lParents, _sp.ParentNrec);
          c := _sp.nrec;
          _sp.parentNrec   := tCostitems.SpNrec;
          _sp.costitemNrec := tCostitems.CostitemNrec;
          update current _sp;
        }
      }

      _loop _sp
      {
        if foundmarker(lParents, _sp.nrec)
          update current _sp;
      }
      donemarker(lParents,'');

      setbounds(#_sp);
      poppos(#_sp);

      TreeJumpToRecord(tr1, c);
      rereadrecord(#_sp);
    }
  }

end;

end.


ClaimOverheadsTotalSpTreeViewMenu Menu
{
  - 'Изменить статью для помеченных записей', cmValue9, , , , , sci1Esc;
}
