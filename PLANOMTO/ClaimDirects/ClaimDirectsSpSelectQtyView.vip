#include ClaimDirects.vih

#component "AGAT"

interface ClaimDirectsSpSelectQtyView;

var
  _sp: IClaimDirectsSp(ClaimDirectsSp) new;
  _sq: ISelectQty(SelectQty) new;

create view
as select
if(_sp.IsCostItem,
     fpstbud.Name,
     if(_sp.ItemType = 2, 'Усл', 'МЦ') ) (fieldname = CostitemName),
  if(_sp.ItemType = 2, katusl.name, katmc.name) (fieldname = ItemName),
  katotped.abbr    (fieldname = OtpedName),
  katorg.Name      (fieldname = KatorgName),
  device.Name      (fieldname = DeviceName),
  priceStruct.Name (fieldname=PriceStructName)
from _sq,
     _sp,
     fpstbud,
     katusl,
     katmc,
     katotped,
     katorg,
     katmc device,
     addsumtune priceStruct
where ((
  _sq.crec            == _sp.nrec and
  _sp.costitemNrec    == fpstbud.nrec and
  _sp.ItemNrec        == katusl.nrec and
  _sp.ItemNrec        == katmc.nrec and
  _sp.OtpedNrec       == katotped.nrec and
  _sp.KatorgNrec      == katorg.nrec and
  _sp.DeviceNrec      == device.nrec and
  _sp.PriceStructNrec == priceStruct.nrec
));

constructor ClaimDirectsSpSelectQtyView(aCDSp: IClaimDirectsSp; aQty: ISelectQty);
{
  _sp := aCDSp;
  _sq := aQty;
  result := (_sp != nullref) and ( _sq != nullref);
}

#define QtyFieldFormat [8, '\4p[|-]36666666666666666~9999']

#define SumFieldFormat [12, '\2p[|-]3666`666`666`666`666~88']

window wSelQty, doaccept, escclose;
browse brSelQty;
show (,,,20);
table _sq;
fields
  CostitemName    'Тип'                       : [5] , protect;
  ItemName        'Матценность / Услуга'      : [30] , protect;
  OtpedName       'Ед.изм'                    : [8]  , protect, noautosize;
  _sp.Description 'Содержание'                : [10] , protect;
  fpstbud.Name    'Статья'                    : [20] , protect;
  PriceStructName 'Статья'#13'структуры цены' : [10] , protect;
  KatorgName      'Поставщик'                 : [10] , protect;
  DeviceName      'Прибор'                    : [10] , protect;
  _sp.Price       'Цена'                      : #SumFieldFormat, protect;
  _sp.PercentNds  '% НДС'                     : [5],  protect;
  _sp.qty         'Количество'                : #QtyFieldFormat, protect;
  _sp.OrderedQty  'В заявках'                 : #QtyFieldFormat, protect;
  _sq.SelectedQty 'Заказать'                  : [10], noprotect;
end; //br
screen scSelQty;
show (,21);
buttons
  cmCreateOrder, default;
<<
    <. Сформировать заявку .>
>>
end; //sc

handleevent
cmCreateOrder:
{
  update current _sq;
  CloseWindowEx(wSelQty, cmDefault);
}
end; //hndlvn

tableevent
cmUpdateRecord:
{
  update current _sq;
}
end; //tblvn
end; //win

function ShowUI: word;
{
  result := RunWindowModal(wSelQty);
}

end.
