#include ClaimDirects.vih
#include GetKau.vih

#component "AGAT"

interface SetClaimDirectsSpAttributesDialog;

var
  _getkau: GetKau;

create view
var
  _flag            : byte;
  _priceStructNrec : comp;
  _deviceNrec      : comp;
  _katorgNrec      : comp;
as select   katorg.Name      (fieldname=KatorgName),
            device.Name      (fieldname=DeviceName),
            priceStruct.Name (fieldname=PriceStructName)
from katorg,
     katmc device,
     addsumtune priceStruct
where ((
  _katorgNrec      == katorg.nrec and
  _deviceNrec      == device.nrec and
  _priceStructNrec == priceStruct.nrec
))
;

procedure SetFlagBit(bitNumber: byte; value: boolean);
{
  if value
    _flag := _flag or byte(2**bitNumber);
  else
    _flag := _flag xor byte(2**bitNumber);
}

function IsFlagBitSet(bitNumber: byte): boolean;
{
  result := (_flag and byte(2**bitNumber)) > 0;
}

function PickAn(wkodgrkau: word; var cRec: comp): boolean;
{
  result := false;
  var c: comp;
  if _getkau.GetCodeKau(1, wkodgrkau, c) > 0
  {
    cRec := c;
    result := true;
  }
}

function PickPriceStruct: boolean; { result := PickAn(73, _priceStructNrec); }

function PickDevice: boolean;      { result := PickAn(4, _deviceNrec); }

function PickKatorg: boolean;      { result := PickAn(1, _katorgNrec); }

procedure ClearPriceStruct; { _priceStructNrec := 0; }

procedure ClearDevice;      { _deviceNrec := 0; }

procedure ClearKatorg;      { _katorgNrec := 0; }

window wUI 'Укажите атрибуты для заполнения', doaccept escclose;
show(,,60,7);
screen sc1;
fields
  _flag            : noprotect;
  PriceStructName  : protect, pickbutton;
  DeviceName       : protect, pickbutton;
  KatorgName       : protect, pickbutton;
buttons
  cmDoOk, default;
  cmCancel;
<<

 [.] Статья структуры цены` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 [.] Прибор/Модуль/...    ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 [.] Поставщик            ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

                             <. Продолжить .> <. Отмена .>
>>
end;

handleevent
cmDoOk:
{
  if _flag > 0
    putCommand(cmDefault);
  else
    putCommand(cmCancel);
}
cmPick:
{
  case curfield of
    #PriceStructName: if PickPriceStruct { SetFlagBit(0, true); rereadrecord(#priceStruct) };
    #DeviceName     : if PickDevice { SetFlagBit(1, true); rereadrecord(#device) };
    #KatorgName     : if PickKatorg { SetFlagBit(2, true); rereadrecord(#katorg) };
  end;
  rereadrecord;
}
end;
end;

handleevent
cmDelOnProtect:
{
  case curfield of
    #PriceStructName: { ClearPriceStruct; rereadrecord(#priceStruct); }
    #DeviceName: { ClearDevice; rereadrecord(#device); }
    #KatorgName: { ClearKatorg; rereadrecord(#katorg); }
  end;
  rereadrecord;
}
end;

function ShowUI: word;
{
  result := runwindowmodal(wUI);
}

property IsSetPriceStruct: boolean read IsFlagBitSet(0) write SetFlagBit(0, result);
property IsSetDevice     : boolean read IsFlagBitSet(1) write SetFlagBit(1, result);
property IsSetKatorg     : boolean read IsFlagBitSet(2) write SetFlagBit(2, result);
property PriceStructNrec : comp    absolute _priceStructNrec ;
property DeviceNrec      : comp    absolute _deviceNrec;
property KatorgNrec      : comp    absolute _katorgNrec;
end.
