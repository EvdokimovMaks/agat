#include Cleverence.vih

#component "Cleverence"

interface DocumentReader;

var
  _reader: IReader;

function Load(fileName: string): IDocument;
{
  result := nullref;

  if _reader = nullref exit;

  if not _reader.Open(fileName) exit;

  var d: IDocument;
  if not loadvipref(d, 'CLEVERENCE::Document') exit;

  var i: word;

  //читаем названия полей заголовка документа
  var hdrFieldNames: array[0..0] of string;
  if not _reader.ReadNextRow exit;
  for (i:=1; i<=_reader.ColumnCount; ++i)
    hdrFieldNames[i] := locase(_reader.GetValue(i));

  //читаем значения полей заголовка документа, присваиваем свойствам документа значения
  if not _reader.ReadNextRow exit;
  for (i:=1; i<=_reader.ColumnCount; ++i)
  {
    case hdrFieldNames[i] of
      'year'    : d.Year     := _reader.GetValue(i);
      'month'   : d.Month    := _reader.GetValue(i);
      'podrnrec': d.PodrNrec := _reader.GetValue(i);
      'molnrec' : d.MOLNrec  := _reader.GetValue(i);
      'имя'     : d.Name     := _reader.GetValue(i);
    end;
  }

  //читаем названия полей спецификации документа
  var spFieldNames: array[0..0] of string;
  if not _reader.ReadNextRow exit;
  for (i:=1; i<=_reader.ColumnCount; ++i)
    spFieldNames[i] := locase(_reader.GetValue(i));

  var item: IDocumentItem;
  //создаем строки документа
  while _reader.ReadNextRow
  {
    if not loadvipref(item, 'CLEVERENCE::DocumentItem') exit;
    for (i:=1; i<=_reader.ColumnCount; ++i)
    {
      case spFieldNames[i] of
        'code'     : item.Barcode   := _reader.GetValue(i);
        'productid': item.ProductId := _reader.GetValue(i);
        'план'     : item.PlanQty   := _reader.GetValue(i);
        'факт'     : item.FactQty   := _reader.GetValue(i);
        'ячейка'   : item.Place     := _reader.GetValue(i);
      end;
    }
    d.AddItem(item);
  }

  result := d;
}

function IReaderSet(aReader: IReader): boolean;
{
  _reader := aReader;
  result := true;
}

end.
