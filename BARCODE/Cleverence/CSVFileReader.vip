#include Cleverence.vih

#component "Cleverence"

interface CSVFileReader;

var _separator: char;
var _a: array[1..100] of string;
var _colCnt: word;
file f;

constructor Init;
{
  _separator := ';';
  result := true;
}

function Open(fileName: string): boolean;
{
  result := f.OpenFile(fileName, stOpenRead);
}

function removeQuotes(s: string): string;
{
  if substr(s,1,1)='"' and substr(s,length(s),1)='"'
    result := substr(s, 2, length(s)-2);
}

function ReadNextRow: boolean;
{
  if f.eof
  {
    result := false;
    exit;
  }
  var ch: char;
  var s: string;
  s := '';
  _colcnt := 0;
  while not f.eof
  {
    f.Read(ch);
    if ch = chr(13) or ch = _separator
    {
      if s != ''
        _a[++_colcnt] := removeQuotes(Ansi2Oem(s));
      s := '';
      if (ch = chr(13))
      {
        f.Read(ch); //читаем #10 после #13
        break;
      }
    }
    else
    {
      s += ch;
    }
  }
  if s != ''
    _a[++_colcnt] := removeQuotes(Ansi2Oem(s));
  result := true;
}

function GetValue(colIndex: word): string;
{
  if colIndex > _colcnt or colIndex = 0
    result := ''
  else
    result := _a[colIndex];
}

procedure Close;
{
  f.Close;
}

property Separator: char absolute _separator;
property ColumnCount: word read _colCnt;

end.
