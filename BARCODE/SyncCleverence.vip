#include Cleverence.Warehouse.vih
#include Logger.vih
#include Extattr.vih
#include Query.vih

#component "AGAT"

interface SyncCleverence;

#include SyncCleverenceQueries.inc

var
  _connector: CLEVERENCE::oStorageConnector;
  _logger: UTILS::Logger new;
  _loggerSyncCat: UTILS::Logger new;

embedded scLog interface _logger.wLog;
show(,18,,) fixed_y;
end;

screen scMain;
show (,,,17);
buttons
  cmDoSync, default;
<<
 <. Синхронизация .>
>>
end;

function Connect: boolean;
{
  var connString: string;
  connString := sGetTune('AGAT.BARCODE.CONNECTIONSTRING');
  _logger.LogInfo(0, 'Подключение к серверу "'+connString+'"');
  if _connector.InitializeServerConnection(connString)
  {
    _logger.LogSuccess(1, 'Подключение успешно');
    result := true;
  }
  else
  {
    _logger.LogError(1, 'Ошибка подключения');
    result := false;
  }
}

function NewProductCollection(var pc: CLEVERENCE::oCollection): boolean;
{
  pc := _connector.CreateProductCollection;
  if pc = nullref
  {
    _logger.LogError(1,'Cleverence. Ошибка создания коллекции Products');
    result := false;
  }
  else
    result := true;
}

function SetProductPacking(aProduct: CLEVERENCE::oProduct): boolean;
{
  result := false;
  var packing: CLEVERENCE::oPacking;
  packing := _connector.CreatePacking;
  if (packing = nullref)
  {
    _logger.LogError(1,'Cleverence. Ошибка создания Packing');
    exit;
  }
  packing.Id := 1;
  packing.name := 'шт';
  packing.barcode := aProduct.Barcode;
  aProduct.packings.Add(packing);
  aProduct.BasePackingId := packing.Id;
  FreeVipInterface(packing);
  result := true;
}

function SetProductField(aProduct: CLEVERENCE::oProduct; aFieldName: string; aFieldValue: string): boolean;
{
  result := false;
  var fieldValue: CLEVERENCE::oFieldValue;
  fieldValue := _connector.CreateFieldValue;
  if (fieldValue = nullref)
  {
    _logger.LogError(1,'Cleverence. Ошибка создания fieldValue');
    exit;
  }
  fieldValue.FieldName := aFieldName;
  fieldValue.Value := aFieldValue;
  aProduct.Fields.Add(fieldValue);
  FreeVipInterface(fieldValue);
  result := true;
}

function NewProduct(var aProduct: CLEVERENCE::oProduct; aId: string; aBarcode: string; aName: string): boolean;
{
  result := false;
  aProduct := _connector.CreateProduct;
  if aProduct = nullref
  {
    _logger.LogError(1, 'Cleverence. Ошибка создания Product');
    exit;
  }
  aProduct.Id := aId;
  aProduct.Name := aName;
  aProduct.Barcode := aBarcode;
  aProduct.UnitConvertionRate := 1;
  result := true;
}

function SyncCatalogs: boolean;
{
  result := false;

  _logger.LogInfo(0, 'Экспорт справочников ОС и МБП');
  _logger.LogInfo(1, 'Выбор элементов справочников, измененных с даты последней успешной синхронизации');

  var q: IQuery;
  q := queryManager.CreateQuery(qOSMBP);
  q.setParam('date', dGetTune('AGAT.BARCODE.LASTCATSYNCDATE'));
  q.setParam('time', tGetTune('AGAT.BARCODE.LASTCATSYNCTIME'));
  q.setParam('anMBPBarcode', ATTRNAME_MBP_BARCODE);
  q.setParam('anMBPPlace', ATTRNAME_MBP_PLACE);

  var rs: IResultSet;
  rs := q.getResultSet;

  if (rs = nullref)
  {
    _logger.LogError(1, 'Ошибка выполнения запроса. Код ошибки:' + q.errorCode);
    exit;
  }

  var products: CLEVERENCE::oCollection;
  var product: CLEVERENCE::oProduct;

  var i, cnt: longint;
  var isUserBreak: boolean;

  if not NewProductCollection(products) exit;

  i := 0; cnt := 0;
  isUserBreak := false;

  StartNewVisual(vtIndicatorVisual, vfTimer+vfBreak+vfThread, '', rs.count);

  if (rs.getFirst = tsOk) do
  {
    if not NewProduct(product, rs.row.Val('kind')+string(rs.row.Val('nrec'),0,0), rs.row.Val('barcode'), rs.row.Val('name')) exit;

    if not SetProductPacking(product) exit;

    if not SetProductField(product, 'МОЛ', rs.row.Val('mol')) exit;
    if not SetProductField(product, 'Подразделение', rs.row.Val('podr')) exit;
    if not SetProductField(product, 'ДатаВводаВЭксплуатацию', rs.row.Val('datek')) exit;
    if not SetProductField(product, 'АдресМестонахождения', rs.row.Val('place')) exit;
    if not SetProductField(product, 'ИнвентарныйНомер', rs.row.Val('innum')) exit;

    products.Add(product);

    FreeVipInterface(product);

    i++;
    cnt++;

    if (i = 1000) then
    {
      _logger.LogInfo(1, 'Экспортировано позиций '+cnt+' из '+rs.count);
      _connector.SetProducts(products);
      FreeVipInterface(products);
      i := 0;
      if not NewProductCollection(products) exit;
    }

    if not nextvisual
    {
      isUserBreak := true;
      break;
    }

  } while (rs.getNext = tsOk);

  StopVisual('',0);

  if isUserBreak
  {
    _logger.LogWarning(1, 'Экспорт справочников прерван пользователем');
  }
  else
  {
    _logger.LogSuccess(1, 'Экспортировано позиций '+cnt+' из '+rs.count);
    dSetTune('AGAT.BARCODE.LASTCATSYNCDATE', cur_date);
    tSetTune('AGAT.BARCODE.LASTCATSYNCTIME', cur_time);
    _connector.SetProducts(products);
  }

  FreeVipInterface(products);
  result := true;
}

handleevent

cmDoSync:
{
  if not connect
    message('Ошибка подключения к серверу', error)
  else if not SyncCatalogs
    message('Ошибка синхронизации справочников', error)
}

cmInit:
{
  if not (getvipref(_connector, 'CLEVERENCE::iStorageConnector'))
  {
    message('Ошибка инициализации коннектора', error);
    abort;
    exit;
  }
}

end;

end.
