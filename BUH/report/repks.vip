#include Query.vih

#component "AGAT"

interface repks 'Проверочный отчет по заказам', escclose, doaccept;
show (,,33, 5);

sql query qs1 =
select '20', t.ckatstroy, sum(t.summa) as summa
from (select kauos[1] as ckatstroy, sum(sumob) as summa
      from oborot where scheto='820' and datob between 1 and :date
      group by kauos[1]
      union all
      select kauks[1], sum(sumob*(-1))
      from oborot where schetk='820' and datob between 1 and :date
      group by kauks[1]) t
group by t.ckatstroy

union all

select '45', t.ckatstroy, sum(t.summa) as summa
from (select kauos[3] as ckatstroy, sum(sumob) as summa
      from oborot where scheto='845' and datob between 1 and :date
      group by kauos[3]
      union all
      select kauks[3], sum(sumob*(-1))
      from oborot where schetk='845' and datob between 1 and :date
      group by kauks[3]) t
group by t.ckatstroy

union all

select '43', t.ckatstroy, sum(t.summa) as summa
from (select kauos[1] as ckatstroy, sum(sumob) as summa
      from oborot where scheto='843' and datob between 1 and :date
      group by kauos[1]
      union all
      select kauks[1], sum(sumob*(-1))
      from oborot where schetk='843' and datob between 1 and :date
      group by kauks[1]) t
group by t.ckatstroy

union all

select '103.02', t.ckatstroy, sum(t.summa) as summa
from (select kauos[1] as ckatstroy, sum(sumob) as summa
      from oborot where scheto='8103' and subossch='02' and datob between 1 and :date
      group by kauos[1]
      union all
      select kauks[1], sum(sumob*(-1))
      from oborot where schetk='8103' and subschk='02' and datob between 1 and :date
      group by kauks[1]) t
group by t.ckatstroy

union all

select '104.01', t.ckatstroy, sum(t.summa) as summa
from (select kauos[1] as ckatstroy, sum(sumob) as summa
      from oborot where scheto='8104' and subossch='01' and datob between 1 and :date
      group by kauos[1]
      union all
      select kauks[1], sum(sumob*(-1))
      from oborot where schetk='8104' and subschk='01' and datob between 1 and :date
      group by kauks[1]) t
group by t.ckatstroy

union all

select '77.01', t.ckatstroy, sum(t.summa) as summa
from (select kauos[1] as ckatstroy, sum(sumob*(-1)) as summa
      from oborot where scheto='877' and subossch='01' and datob between 1 and :date
      group by kauos[1]
      union all
      select kauks[1], sum(sumob)
      from oborot where schetk='877' and subschk='01' and datob between 1 and :date
      group by kauks[1]) t
group by t.ckatstroy;

table struct tt (
  schet: string[10],
  ckatstroy: comp,
  summa: double
) with index (
  i01 = schet + ckatstroy
);

table struct tks (
  ckatstroy: comp,
  name: string,
  sum20: double,
  sum45: double,
  sum43: double,
  sum103: double,
  sum104: double,
  sum77: double
) with index (
  i01 = ckatstroy(unique)
);

var
  drep: date;


create view as select * from tt, tks, katstroy;

screen sc1;
  fields
    drep: noprotect;
  buttons
    cmDoOk, default;
    cmCancel;
<<

 `Дата формирования` .@@@@@@@@@@

   <. Продолжить .> <. Отмена .>
>>
end;

window winfo;
browse br1;
table tks;
fields
  tks.name 'Заказ'   : [30]  , protect;
  tks.sum20  '20'    : [10,2], protect;
  tks.sum45  '45'    : [10,2], protect;
  tks.sum43  '43'    : [10,2], protect;
  tks.sum103 '103.02': [10,2], protect;
  tks.sum104 '104.01': [10,2], protect;
  tks.sum77  '77.01' : [10,2], protect;
end;
end;

handleevent
cmdook:
{
  delete all tt;
  delete all tks;
  var q: iquery;
  q := queryManager.CreateQuery(qs1);
  q.setParam('date', drep);
  var rs: iresultset;
  rs := q.getresultset;
  if rs = nullref
  {
    message('Ошибка формирования запроса');
    exit;
  }
  sqlCopyInto(rs, #tt, false);
  _loop tt
  {
    if (abs(tt.summa) < 0.01 or tt.ckatstroy = 0) continue;
    if getfirst tks where ((tt.ckatstroy == tks.ckatstroy)) != tsOk
    {
      clearbuffer(#tks);
      tks.ckatstroy := tt.ckatstroy;
      if getfirst katstroy where ((tt.ckatstroy == katstroy.nrec)) = tsOk
        tks.name := katstroy.name;
      insert current tks;
    }
    case tt.schet of
      '20'      : tks.sum20  := tt.summa;
      '45'      : tks.sum45  := tt.summa;
      '43'      : tks.sum43  := tt.summa;
      '103.02'  : tks.sum103 := tt.summa;
      '104.01'  : tks.sum104 := tt.summa;
      '77.01'   : tks.sum77  := tt.summa;
    end;
    update current tks;
  }
  delete all tks where (tks.sum20=0 and tks.sum45=0 and tks.sum43=0 and tks.sum103=0 and tks.sum104=0 and tks.sum77=0);
  runwindow(winfo);
}
cminit:
{
  drep := cur_date;
}
end;

end.

VipInterface UserReport_repks Implements IUserReport licensed(free);
Interface UserReport_repks;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::repks);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Проверочный отчет по заказам';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    level := 0;
    GetGroupName := '';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'B_FINANS';
    end;
  end;
end.

