#include ContractId.vih
#include BasedocReeReport.vih
#include extattr.vih
#include Query.vih
#include XLREPORT.vih

#component "AGAT"

interface BasedocReeReport_Hier;

table struct trs (
  spkRSNrec: comp,
  name: string
) with index (
  i01 = spkRSNrec
);

table struct tbd (
  nrec       : comp,
  viddoc     : word,
  ddoc       : date,
  nodoc      : string,
  bdds       : string,
  dform      : date,
  spkRSNrec  : comp,
  summa      : double,
  total      : double,
  sumopl     : double,
  dopl       : date,
  val        : string,
  avans      : string,
  status     : string,
  ispoln     : string,
  dds        : string,
  buhsch     : string,
  dsogl      : date,
  sogl       : string,
  org        : string,
  bank       : string,
  bankrs     : string,
  cfo        : string,
  zakaz      : string,
  dognum     : string,
  dogdate    : date,
  procav     : string,
  nazn       : string,
  vizaPEUNrec: comp,
  vizaKAZNrec: comp,
  vizaKOMNrec: comp,
  vizaBUHNrec: comp,
  vizaPEUName: string,
  vizaKAZName: string,
  vizaKOMName: string,
  vizaBUHName: string,
  vizaPEUDate: date,
  vizaKAZDate: date,
  vizaKOMDate: date,
  vizaBUHDate: date,
  vizaPEUUser: string,
  vizaKAZUser: string,
  vizaKOMUser: string,
  vizaBUHUser: string,
  vizaPEUPrim: string,
  vizaKAZPrim: string,
  vizaKOMPrim: string,
  vizaBUHPrim: string
) with index (
  i01 = nrec,
  i02 = spkRSNrec + ddoc + nodoc
);

//#region Queries

sql query qSpstepData =
select coalescen(katbank.nrec, spkRS.nrec, #comp(0)) as RSNrec,
       coalescen(katbank.name, spkRS.name, '')       as BankName,
       coalescen(katbank.schet1, spkRS.name, '')     as RSName,
       coalesce(spkIGK.nrec,#comp(0))                as IGKNrec,
       coalesce(spkIGK.name,'')                      as IGKName,
       coalesce(spkRejim.nrec,#comp(0))              as RejimNrec,
       coalesce(spkRejim.name,'')                    as RejimName,
       coalesce(ksVib.nrec,#comp(0))                 as ZakazVibNrec,
       substring(coalesce(ksVib.name,''),1,instr(' ',coalesce(ksVib.name,'')+' ',1)-1) as ZakazVibName,
       coalesce(ks.nrec,#comp(0))                    as ZakazNrec,
       substring(coalesce(ks.name,''),1,instr(' ',coalesce(ks.name,'')+' ',1)-1) as ZakazName,
       coalesce(cfo.nrec,#comp(0))                   as CFONrec,
       coalesce(cfo.kod,'')                          as CFOKod
  from basedoc bd
  join stepdoc sd on sd.cbasedoc = bd.nrec
  join spstep sps on sps.cstepdoc = sd.nrec
  left join attrval avVizaPEU on avVizaPEU.wtable = 1102 and avVizaPEU.cRec = bd.nrec and avVizaPEU.cattrnam = #comp(0001000000000088h)
  left join attrval avRS on avRS.wtable = 1104 and avRS.cRec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)
  left join attrval avZakVib on avZakVib.wtable = 1104 and avZakVib.cRec = sps.nrec and avZakVib.cattrnam = #comp(0001000000000A87h)
  left join attrval avRejim on avRejim.wtable = 1104 and avRejim.cRec = sps.nrec and avRejim.cattrnam = #comp(000100000000079Eh)
  left join specmtr spm on spm.csaldtune = #comp(0001000000000001h) and spm.cotable = 1104 and spm.cspec = sps.nrec
  left join (select spkRS.nrec as spkRSNrec, max(coalesce(spkIGK.nrec,#comp(0))) as spkIGKNrec, max(cb.cpodr) as katbankNrec
             from spcash
             join spkau spkRS on spkRS.nrec = case when spcash.tblos[1] = 10545 then spcash.kauos[1]
                                                   when spcash.tblos[2] = 10545 then spcash.kauos[2]
                                                   when spcash.tblos[3] = 10545 then spcash.kauos[3]
                                                   when spcash.tblos[4] = 10545 then spcash.kauos[4]
                                                   when spcash.tblos[5] = 10545 then spcash.kauos[5]
                                                   when spcash.tblos[6] = 10545 then spcash.kauos[6]
                                                   else #comp(0)
                                              end
             join cashbank cb on cb.nrec = spcash.ccashbank
             left join spkau spkIGK on spkIGK.nrec = case when spcash.tblos[1] = 10527 then spcash.kauos[1]
                                                          when spcash.tblos[2] = 10527 then spcash.kauos[2]
                                                          when spcash.tblos[3] = 10527 then spcash.kauos[3]
                                                          when spcash.tblos[4] = 10527 then spcash.kauos[4]
                                                          when spcash.tblos[5] = 10527 then spcash.kauos[5]
                                                          when spcash.tblos[6] = 10527 then spcash.kauos[6]
                                                          else #comp(0)
                                                     end
             where spcash.cplanssch=#comp(000100000000000Eh)
             group by spkRS.nrec
            ) tCashbank on tCashbank.spkRSNrec = avRS.vcomp
  left join spkau spkIGK on spkIGK.nrec = coalesce(tCashbank.spkIGKNrec,#comp(0))
  left join katbank on katbank.nrec = coalesce(tCashbank.katbankNrec,#comp(0))
  left join spkau spkRejim on spkRejim.nrec = coalesce(avRejim.vComp,#comp(0))
  left join spkau spkRS on spkRS.nrec = coalesce(avRS.vComp,#comp(0))
  left join katstroy ksVib on ksVib.nrec = coalesce(avZakVib.vComp,#comp(0))
  left join katstroy ks on ks.nrec = coalesce(spm.cobj,#comp(0))
  left join katpodr cfo on cfo.nrec = coalesce(spm.ckau[2],#comp(0))
  where bd.nrec = :basedocNrec and ( coalesce(avRS.vcomp,#comp(0)) = :rsNrec or coalesce(avVizaPEU.vcomp,#comp(0)) = #comp(0001000000001423h) )
;

sql query qSelectOpl =
  select distinct plpor.datob
  from basefin bf
  join plpor on plpor.nrec = bf.cplpor and plpor.datob <> 0
  where (bf.cbasedoc = :basedocNrec or bf.cpredopldo = :basedocNrec ) and bf.tidkgal in (1,2)
;

sql query querymain =
select bd.nrec,
       bd.viddoc as viddoc,
       bd.ddoc as ddoc,
       bd.nodoc as nodoc,
       coalesce(spkBdds.Name,'') as bdds,
       bd.dform as dform,
       tbd.spkRSNrec as spkRSNrec,
       tbd.summa as summa,
       tbd.total as total,
       case when avVizaPEU.cattrnam = #comp(0001000000000088h) then coalesce(bfall.summa,0) else coalesce(bfrs.summa,0) end as sumopl,
       case when avVizaPEU.cattrnam = #comp(0001000000000088h) then coalesce(bfall.dopl,0) else coalesce(bfrs.dopl,0) end as dopl,
       coalesce(klval.simvolv,'') as val,
       coalesce(spkAvans.code,'') as avans,
       coalesce(katnotes.name,'') as status,
       coalesce(podrIspoln.kod,'') as ispoln,
       coalesce(spkDds.code,'') as dds,
       coalesce(substring(buhschet.schet,2,10),'') as buhsch,
       coalesce(avSogl.atl_lastdate,0) as dsogl,
       coalesce(avSogl.vstring,'') as sogl,
       coalesce(org.name, '') as org,
       coalesce(bank.name, '') as bank,
       coalesce(bank.schet1, '') as bankrs,
       coalesce(getdocfo(bd.nrec),'-') as cfo,
       coalesce(getdozakaz(bd.nrec),'-') as zakaz,
       coalesce(dog.nodoc,'') as dognum,
       coalesce(dog.ddoc,0) as dogdate,
       bd.procopl as procav,
       bd.name as nazn,
       coalesce(spkPEU.nrec, #comp(0)) as vizaPEUNrec,
       coalesce(spkKAZ.nrec, #comp(0)) as vizaKAZNrec,
       coalesce(spkKOM.nrec, #comp(0)) as vizaKOMNrec,
       coalesce(spkBUH.nrec, #comp(0)) as vizaBUHNrec,
       coalesce(spkPEU.name, '') as vizaPEUName,
       coalesce(spkKAZ.name, '') as vizaKAZName,
       coalesce(spkKOM.name, '') as vizaKOMName,
       coalesce(spkBUH.name, '') as vizaBUHName,
       coalesce(avVizaPEU.atl_lastdate, 0) as vizaPEUDate,
       coalesce(avVizaKAZ.atl_lastdate, 0) as vizaKAZDate,
       coalesce(avVizaKOM.atl_lastdate, 0) as vizaKOMDate,
       coalesce(avVizaBUH.atl_lastdate, 0) as vizaBUHDate,
       coalesce(uPEU.xu$loginname, '') as vizaPEUUser,
       coalesce(uKAZ.xu$loginname, '') as vizaKAZUser,
       coalesce(uKOM.xu$loginname, '') as vizaKOMUser,
       coalesce(uBUH.xu$loginname, '') as vizaBUHUser,
       coalesce(avPrimPEU.vString, '') as vizaPEUPrim,
       coalesce(avPrimKAZ.vString, '') as vizaKAZPrim,
       coalesce(avPrimKOM.vString, '') as vizaKOMPrim,
       coalesce(avPrimBUH.vString, '') as vizaBUHPrim
from (select bd.nrec,
             case when coalesce(avVizaPEU.vcomp,#comp(0)) = #comp(0001000000001423h)
                  then #comp(0)
                  else case when coalesce(spkRS.nrec,#comp(0)) = #comp(0)
                            then #comp(-1)
                            else coalesce(spkRS.nrec,#comp(0))
                       end
             end as spkRSNrec,
             sum(sps.summa + case when bd.vhodnal=2 then sps.nds else 0 end) as summa,
             sum(case when bd.summa <> 0 then sps.summa / bd.summa * bd.total else 0 end) as total
      from bdreeagat
      join basedoc bd on bd.nrec = bdreeagat.crec
      join stepdoc sd on sd.cbasedoc = bd.nrec
      join spstep sps on sps.cstepdoc = sd.nrec
      left join attrval avRS on avRS.wtable = 1104 and avRS.cRec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)
      left join spkau spkRS on spkRS.nrec = coalesce(avRS.vcomp,#comp(0))
      left join attrval avVizaPEU on avVizaPEU.wtable = 1102 and avVizaPEU.cRec = bd.nrec and avVizaPEU.cattrnam = #comp(0001000000000088h)
      group by bd.nrec,
               case when coalesce(avVizaPEU.vcomp,#comp(0)) = #comp(0001000000001423h)
                    then #comp(0)
                    else case when coalesce(spkRS.nrec,#comp(0)) = #comp(0)
                              then #comp(-1)
                              else coalesce(spkRS.nrec,#comp(0))
                         end
               end
               //case when coalesce(avVizaPEU.vcomp,#comp(0)) = #comp(0001000000001423h) then #comp(0) else coalesce(spkRS.nrec,#comp(0)) end
     ) tbd

join basedoc bd on bd.nrec = tbd.nrec

left join attrval avSogl on avSogl.wtable = 1102 and avSogl.cRec = bd.nrec and avSogl.cattrnam = #comp(000100000000008Bh)
left join attrval avVizaPEU on avVizaPEU.wtable = 1102 and avVizaPEU.cRec = bd.nrec and avVizaPEU.cattrnam = #comp(0001000000000088h)
left join attrval avVizaKAZ on avVizaKAZ.wtable = 1102 and avVizaKAZ.cRec = bd.nrec and avVizaKAZ.cattrnam = #comp(0001000000000087h)
left join attrval avVizaKOM on avVizaKOM.wtable = 1102 and avVizaKOM.cRec = bd.nrec and avVizaKOM.cattrnam = #comp(000100000000008Ah)
left join attrval avVizaBUH on avVizaBUH.wtable = 1102 and avVizaBUH.cRec = bd.nrec and avVizaBUH.cattrnam = #comp(0001000000000089h)
left join spkau   spkPEU    on spkPEU.nrec = coalesce(avVizaPEU.vcomp,#comp(0))
left join spkau   spkKAZ    on spkKAZ.nrec = coalesce(avVizaKAZ.vcomp,#comp(0))
left join spkau   spkKOM    on spkKOM.nrec = coalesce(avVizaKOM.vcomp,#comp(0))
left join spkau   spkBUH    on spkBUH.nrec = coalesce(avVizaBUH.vcomp,#comp(0))
left join x$users uPEU      on uPEU.atl_nrec = coalesce(avVizaPEU.atl_lastuser,#comp(0))
left join x$users uKAZ      on uKAZ.atl_nrec = coalesce(avVizaKAZ.atl_lastuser,#comp(0))
left join x$users uKOM      on uKOM.atl_nrec = coalesce(avVizaKOM.atl_lastuser,#comp(0))
left join x$users uBUH      on uBUH.atl_nrec = coalesce(avVizaBUH.atl_lastuser,#comp(0))
left join attrval avPrimPEU on avPrimPEU.wtable = 1102 and avPrimPEU.cRec = bd.nrec and avPrimPEU.cattrnam = #comp(0001000000000099h)
left join attrval avPrimKAZ on avPrimKAZ.wtable = 1102 and avPrimKAZ.cRec = bd.nrec and avPrimKAZ.cattrnam = #comp(0001000000000098h)
left join attrval avPrimKOM on avPrimKOM.wtable = 1102 and avPrimKOM.cRec = bd.nrec and avPrimKOM.cattrnam = #comp(000100000000009Ah)
left join attrval avPrimBUH on avPrimBUH.wtable = 1102 and avPrimBUH.cRec = bd.nrec and avPrimBUH.cattrnam = #comp(0001000000000097h)

left join attrval avAvans on avAvans.wtable=1102 and avAvans.crec=bd.nrec and avAvans.cattrnam=#comp(000100000000008Dh)
left join spkau spkAvans on spkAvans.nrec = coalesce(avAvans.vComp, #comp(0))

left join attrval avBdds on avBdds.wtable=1102 and avBdds.crec=bd.nrec and avBdds.cattrnam=#comp(000100000000008Eh)
left join spkau spkBdds on spkBdds.nrec = coalesce(avBdds.vComp, #comp(0))

left join klval on klval.nrec = bd.cval

left join katnotes on katnotes.nrec = bd.cnote

left join attrval avIspoln on avIspoln.wtable=1102 and avIspoln.crec=bd.nrec and avIspoln.cattrnam=#comp(000100000000005Bh)
left join katpodr podrIspoln on podrIspoln.nrec=coalesce(avIspoln.vComp,#comp(0))

left join attrval avDds on avDds.wtable=1102 and avDds.crec=bd.nrec and avDds.cattrnam=#comp(000100000000008Fh)
left join spkau spkDds on spkDds.nrec = coalesce(avDds.vComp, #comp(0))

left join attrval avSch on avSch.wtable=1102 and avSch.crec=bd.nrec and avSch.cattrnam=#comp(0001000000000094h)
left join buhschet on buhschet.nrec = coalesce(avSch.vComp, #comp(0))

left join katorg org on org.nrec = bd.corg

left join katbank bank on bank.nrec = bd.cbank

left join dogovor dog on dog.nrec = bd.cdogovor

left join (select cbasedoc, coalesce(spkau.nrec, #comp(0)) as spkRSNrec, max(basefin.ddoc) as dopl, sum(summa) as summa
           from basefin
           join usersdoc ud on ud.tipgal=basefin.tidkgal and ud.tipusers=basefin.tipdoc
           join cashbank cb on cb.nrec = ud.ccashbank
           join spcash on spcash.ccashbank = cb.nrec and spcash.cplanssch=#comp(000100000000000Eh)
           join spkau on spkau.nrec = case when spcash.tblos[1] = 10545 then spcash.kauos[1]
                                           when spcash.tblos[2] = 10545 then spcash.kauos[2]
                                           when spcash.tblos[3] = 10545 then spcash.kauos[3]
                                           when spcash.tblos[4] = 10545 then spcash.kauos[4]
                                           when spcash.tblos[5] = 10545 then spcash.kauos[5]
                                           when spcash.tblos[6] = 10545 then spcash.kauos[6]
                                           else #comp(0)
                                      end
           where tidkgal = 1 and cbasedoc<>#comp(0)
           group by cbasedoc, coalesce(spkau.nrec, #comp(0))
          ) bfrs on bfrs.cbasedoc = tbd.nrec and bfrs.spkRSNrec = tbd.spkRSNrec

left join (select cbasedoc, max(basefin.ddoc) as dopl, sum(summa) as summa
           from basefin
           where tidkgal = 1 and cbasedoc<>#comp(0)
           group by cbasedoc
          ) bfall on bfall.cbasedoc = bd.nrec
;

//#endregion

var
  _qOpl: IQuery;
  _qSpstepData: IQuery;
  _contractIDHelper: ContractIDHelper;
  pXL: XLSRepBuilder;
  ea: iextattr;
  sXLSFileName, sXLTFileName: string;

create view
as select *
from tbd, trs, spkau,
     trs brrs, tbd brbd, basedoc brbasedoc, katorg brorg, katbank brbank, dogovor brdog,
     katnotes brnote, statlog brstatlog(STATLOG02), klval brval, stepdoc brsd, basefin brbf
where ((
  brrs.spkRSNrec     == brbd.spkRSNrec    and
  brbd.nrec          == brbasedoc.nrec    and
  brbasedoc.corg     == brorg.nrec        and
  brbasedoc.cbank    == brbank.nrec       and
  brbasedoc.cdogovor == brdog.nrec        and
  brbasedoc.cnote    == brnote.nrec       and
  40                 == brstatlog.doctype and
  brbasedoc.nrec     == brstatlog.cdoc    and
  brbasedoc.cval     == brval.nrec        and
  brbd.nrec          == brsd.cbasedoc     and
  brbd.nrec          == brbf.cbasedoc
));

create view vExtAttrInfo
var
  _wtable: word;
  _crec: comp;
  _cattrnam: comp;
as select *
from attrval, x$users
where ((
  _wtable   == attrval.wtable   and
  _crec     == attrval.crec     and
  _cattrnam == attrval.cattrnam and
  attrval.atl_lastuser == x$users.atl_nrec
));

function isVizaSObshihSchetov(vizaNrec: comp): boolean;
{
  result := vizaNrec = VIZA_S_OBSHIH_SCHETOV;
}

procedure CreateTmpTableForMarker(lMarker: longint; tableName: string);
{
  sqlCreateTmpTable('table '+tableName+'(crec: comp);', ctmNormal);
  sqlExecDirect('delete '+tableName);
  var q: IQuery;
  q := queryManager.createQuery('insert '+tableName+' (crec) values (:crec)');

  var i: longint = 0;
  var c: comp;
  for(i := 0; getmarker(lMarker, i, c); i++)
    q.SetParam('crec', c).execute;
}

procedure PrepareData(lBasedocMarker: longint);
{
  CreateTmpTableForMarker(lBasedocMarker, 'bdreeagat');
  delete all tbd;
  sqlCopyInto(queryManager.createQuery(querymain).getResultSet, #tbd, true);
  delete all trs;
  _loop tbd
  {
    var currentRSNrec: comp;
    //if isVizaSObshihSchetov(tbd.vizaPEUNrec)
    //  currentRSNrec := 0;
    //else
    //  currentRSNrec := if(tbd.spkRSNrec = 0, comp(-1), tbd.spkRSNrec);
    currentRSNrec := tbd.spkRSNrec;

    if getfirst trs where ((currentRSNrec == trs.spkRSNrec)) != tsOk
    {
      clearbuffer(#trs);
      trs.spkRSNrec := currentRSNrec;
      trs.name := case(trs.spkRSNrec;
                       comp(-1) : 'Счет не установлен',
                       comp(0)  : 'Общий счет';
                       if(getfirst spkau where ((currentRSNrec == spkau.nrec)) = tsOk, spkau.name, 'Счет Nrec='+string(currentRSNrec,0,0)));
      insert current trs;
    }
  }
}

function IBasedocReeReport.Title: string;
{
  result := 'Реестр ДО с группировкой по счетам';
}

function vizafld(vizaName: string; dSogl: date; sogl: string): string; cacheable;
{
  if vizaName != ''
    result := vizaName
  else if dSogl > 0 and sogl != ''
    result := calcdaysbetweendates(dSogl, cur_date, true)+ ' дн.'
  else
    result := '';
}

function GetExtAttrSetInfo(aWTable: word; aCRec: comp; aCAttrnam: comp): string;
{
  vExtAttrInfo._wtable := aWTable;
  vExtAttrInfo._crec := aCRec;
  vExtAttrInfo._cattrnam := aCAttrnam;
  if vExtAttrInfo.getfirst attrval = tsOk
  {
    result := datetostr(vExtAttrInfo.attrval.atl_lastdate,'DD.MM.YYYY')
              + ' ' + timetostr(vExtAttrInfo.attrval.atl_lasttime, 'HH:MM')
              + ' ' + if(vExtAttrInfo.getfirst x$users = tsOk,
                         vExtAttrInfo.x$users.xu$loginname,
                         'неизвестный пользователь '+string(vExtAttrInfo.attrval.atl_lastuser,0,0));
  }
  else
    result := '';
}

function GetBDReeExtAttrInfo(aTitle: string; aCAttrnam: comp): string;
{
  result := aTitle+': '+GetExtAttrSetInfo(WT_BASEDOC, brbd.nrec, aCAttrnam);
}

procedure AddSpstepInfo(spstepDataResultSet: IResultSet;
                        xlsFieldName       : string;
                        queryFieldNrec     : string;
                        queryFieldName     : string;
                        delimiter          : string = ';');
{
  var isFirst: boolean = true;
  var l: longint;
  l := initmarker('', 8, 10, 10, false);
  if spstepDataResultSet.getFirst = tsOk do
  {
    var c: comp;
    c := spstepDataResultSet.row.val(queryFieldNrec);
    if not foundmarker(l, c) and c != 0
    {
      insertmarker(l, c);

      if isFirst
        isFirst := false;
      else
        pXL.AddTblStringFldValue(xlsFieldName, delimiter+' ');

      pXL.AddTblStringFldValue(xlsFieldName, spstepDataResultSet.row.val(queryFieldName));
    }
  } while spstepDataResultSet.getNext = tsOk;
  donemarker(l, '');
}

function IBasedocReeReport.PrintReport(lBasedocMarker: longint; params: IParams = nullref): boolean;
{
  _qOpl := queryManager.createQuery(qSelectOpl);

  _qSpstepData := queryManager.createQuery(qSpstepData);

  PrepareData(lBasedocMarker);

  var szakaz, scfo, _dds: string;

  sXLTFileName := TranslatePath('%StartPath%xls\agat_basedoc_ree_hier.xlt');
  sXLSFileName := pXL.CreateReport(sXLTFileName, True)
  //sXLSFileName := pXL.CreateXLT(sXLTFileName, True);

  pXL.CreateTbls(sXLSFileName);
  pXL.CreateTbl('Main');
  pXL.CreateTblFld('rowtype');
  pXL.CreateTblFld('viddoc');
  pXL.CreateTblFld('ddoc'  );
  pXL.CreateTblFld('nodoc' );
  pXL.CreateTblFld('bdds'  );
  pXL.CreateTblFld('dform' );
  pXL.CreateTblFld('summa' );
  pXL.CreateTblFld('total' );
  pXL.CreateTblFld('sumopl');
  pXL.CreateTblFld('sumdolg');
  pXL.CreateTblFld('val'   );
  pXL.CreateTblFld('statusopl');
  pXL.CreateTblFld('dopl'  );
  pXL.CreateTblFld('avans' );
  pXL.CreateTblFld('status');
  pXL.CreateTblFld('ispoln');
  pXL.CreateTblFld('dds'   );
  pXL.CreateTblFld('bdr'   );
  pXL.CreateTblFld('buhsch');
  pXL.CreateTblFld('dsogl' );
  pXL.CreateTblFld('sogl'  );
  pXL.CreateTblFld('vbuh'  );
  pXL.CreateTblFld('vkaz'  );
  pXL.CreateTblFld('vpeu'  );
  pXL.CreateTblFld('vdir'  );
  pXL.CreateTblFld('pbuh'  );
  pXL.CreateTblFld('pkaz'  );
  pXL.CreateTblFld('ppeu'  );
  pXL.CreateTblFld('pdir'  );
  pXL.CreateTblFld('vrez'  );
  pXL.CreateTblFld('soglinfo');
  pXL.CreateTblFld('org'   );
  pXL.CreateTblFld('bank'  );
  pXL.CreateTblFld('bankrs');
  pXL.CreateTblFld('init'  );
  pXL.CreateTblFld('ContractIdVib');
  pXL.CreateTblFld('BankRSVib');
  pXL.CreateTblFld('BankVib');
  pXL.CreateTblFld('ZakazVib');
  pXL.CreateTblFld('Rejim');
  pXL.CreateTblFld('cfo'   );
  pXL.CreateTblFld('zakaz' );
  pXL.CreateTblFld('contractId');
  pXL.CreateTblFld('viddoc');
  pXL.CreateTblFld('dognum' );
  pXL.CreateTblFld('dogdate');
  pXL.CreateTblFld('procav');
  pXL.CreateTblFld('nazn');
  pXL.CreateTblFld('npp');
  pXL.CreateTblFld('statusName');
  pXL.CreateTblFld('statusComment');

  var _npp: longint = 0;

  _loop brrs {
    pXL.ClearTblBuffer;
    pXL.SetTblStringFldValue('rowtype'  , 'СЧЕТ');
    pXL.SetTblStringFldValue('npp'      , brrs.name);
    pXL.InsTblRow;

    _loop brbd  {
      _npp += 1;
      //выводим строки
      if getfirst brbasedoc = tsOk {};
      if getfirst brorg     = tsOk {};
      if getfirst brbank    = tsOk {};
      if getfirst brnote    = tsOk {};
      pXL.ClearTblBuffer;
      pXL.SetTblStringFldValue('npp'      , _npp);
      pXL.SetTblStringFldValue('rowtype'  , 'ДО');
      pXL.SetTblStringFldValue('ddoc'     , datetostr(brbd.ddoc,'DD.MM.YYYY'));
      pXL.SetTblStringFldValue('nodoc'    , brbd.nodoc                       );
      pXL.SetTblStringFldValue('viddoc'   , if(brbd.viddoc=111,'ДО на предоплату закупок','ДО на закупку'));
      pXL.SetTblStringFldValue('nazn'     , brbd.nazn                        );
      pXL.SetTblStringFldValue('bdds'     , brbd.bdds                        );
      pXL.SetTblStringFldValue('dform'    , datetostr(brbd.dform,'DD.MM.YYYY'));
      pXL.SetTblNumberFldValue('summa'    , brbd.summa                       );
      pXL.SetTblNumberFldValue('total'    , brbd.total                       );
      pXL.SetTblNumberFldValue('sumopl'   , brbd.sumopl                      );
      pXL.SetTblNumberFldValue('sumdolg'  , round(brbd.total-brbd.sumopl,2)  );
      pXL.SetTblStringFldValue('statusopl', if(brbd.sumopl<=0,'Не оплачен', if(brbd.sumopl>=brbd.total,'Полностью оплачен', 'Частично оплачен')));

      _qOpl.setParam('basedocNrec', brbd.nrec);
      if _qOpl.execute.errorcode = tsOk
      {
        if _qOpl.fetch.errorCode = tsOk
        {
          pXL.AddTblStringFldValue('dopl', datetostr(_qOpl.row.Val('datob'),'DD.MM.YYYY'));
          while _qOpl.fetch.errorCode = tsOk
            pXL.AddTblStringFldValue('dopl', '; '+datetostr(_qOpl.row.Val('datob'),'DD.MM.YYYY'));
        }
      }

      pXL.SetTblStringFldValue('avans'    , brbd.avans                       );
      pXL.SetTblStringFldValue('status'   , brbd.status                      );
      pXL.SetTblStringFldValue('ispoln'   , brbd.ispoln                      );
      if pos(' ',brbd.dds) > 0 {
        _dds := replace(substr(brbd.dds,1,pos(' ',brbd.dds)), '.', '');
      } else {
        _dds := brbd.dds;
      }
      pXL.SetTblStringFldValue('dds'      , _dds                             );
      pXL.SetTblStringFldValue('buhsch'   , brbd.buhsch                      );
      pXL.SetTblStringFldValue('dsogl'    , datetostr(brbd.dsogl,'DD.MM.YYYY'));
      pXL.SetTblStringFldValue('sogl'     , if(brbd.sogl='',
                                               calcdaysbetweendates(brbd.dform, cur_date, true)+' дн.',
                                               brbd.sogl));
      pXL.SetTblStringFldValue('vbuh'     , vizafld(brbd.vizaBUHName, brbd.dsogl, brbd.sogl));
      pXL.SetTblStringFldValue('vkaz'     , vizafld(brbd.vizaKAZName, brbd.dsogl, brbd.sogl));
      pXL.SetTblStringFldValue('vpeu'     , vizafld(brbd.vizaPEUName, brbd.dsogl, brbd.sogl));
      pXL.SetTblStringFldValue('vdir'     , vizafld(brbd.vizaKOMName, brbd.dsogl, brbd.sogl));

      pXL.SetTblStringFldValue('pbuh'     , brbd.vizaBUHPrim);
      pXL.SetTblStringFldValue('pkaz'     , brbd.vizaKAZPrim);
      pXL.SetTblStringFldValue('ppeu'     , brbd.vizaPEUPrim);
      pXL.SetTblStringFldValue('pdir'     , brbd.vizaKOMPrim);

      pXL.AddTblStringFldValue('soglinfo' ,         GetBDReeExtAttrInfo('Передано на согл.    ', ATTRNAM_SOGL));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('Казначейство         ', ATTRNAM_KAZN));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('Бухгалтерия          ', ATTRNAM_BUH));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('ПЭУ                  ', ATTRNAM_PEU));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('Коммерческая дирекция', ATTRNAM_DIR));

      pXL.SetTblStringFldValue('org'      , brorg.name                       );
      pXL.SetTblStringFldValue('bank'     , brbank.name                      );
      pXL.SetTblStringFldValue('bankrs'   , brbank.schet1                    );

      var rsSpstepData: IResultSet;
      _qSpstepData.setParam('basedocNrec' , brbd.nrec);
      _qSpstepData.setParam('rsNrec'      , brrs.spkRSNrec);
      rsSpstepData := _qSpstepData.getResultSet;

      AddSpstepInfo(rsSpstepData, 'ContractIdVib'   , 'IGKNrec'     , 'IGKName');
      AddSpstepInfo(rsSpstepData, 'BankRSVib'       , 'RSNrec'      , 'RSName');
      AddSpstepInfo(rsSpstepData, 'BankVib'         , 'RSNrec'      , 'BankName');
      AddSpstepInfo(rsSpstepData, 'ZakazVib'        , 'ZakazVibNrec', 'ZakazVibName');
      AddSpstepInfo(rsSpstepData, 'Rejim'           , 'RejimNrec'   , 'RejimName');
      /*
      pXL.SetTblStringFldValue('zakaz'    , brbd.zakaz                       );
      pXL.SetTblStringFldValue('cfo'      , brbd.cfo                         );
      */
      AddSpstepInfo(rsSpstepData, 'zakaz'        , 'ZakazNrec', 'ZakazName');
      AddSpstepInfo(rsSpstepData, 'cfo'          , 'CFONrec'  , 'CFOKod');
      if (getfirst brsd = tsOk)
        pXL.SetTblStringFldValue('contractId', _contractIDHelper.GetContractId(GetContractIDKey_ByStepdoc, brsd.nrec));

      if getfirst brdog     = tsOk {
        pXL.SetTblStringFldValue('dognum' , brdog.nodoc                    );
        pXL.SetTblStringFldValue('dogdate', datetostr(brdog.ddoc,'DD.MM.YYYY'));
      }
      if getfirst brval     = tsOk {
        pXL.SetTblStringFldValue('val'    , brval.dollar);
      } else {
        pXL.SetTblStringFldValue('val'    , '');
      }
      pXL.SetTblStringFldValue('procav'   , if(brbasedoc.procopl>0,
                                               brbasedoc.procopl,
                                               if(brbd.total>0 and brbd.sumopl>0,
                                                  round(brbd.sumopl/brbd.total*100,2),
                                                  ''
                                               )
                                            ) );
      if getfirst brnote = tsOk
        pXL.SetTblStringFldValue('statusName', brnote.name);
      else
        pXL.SetTblStringFldValue('statusName', '');

      if getlast brstatlog = tsOk
        pXL.SetTblStringFldValue('statusComment', brstatlog.comment);
      else
        pXL.SetTblStringFldValue('statusComment', '');

      pXL.InsTblRow;
    } //loop brbd
  }//loop brrs
  pXL.PublishTbl('Main');
  pXL.LoadReport(sXLSFileName);
  pXL.DisConnectExcel;
  result := true;
}

end.
