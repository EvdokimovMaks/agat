#include XLREPORT.vih

#component "AGAT"

interface ThreeMillionReport;

var
  pXL: XLSRepBuilder;
  sXLSFileName: string = '';
  sXLTFileName: string = '';
  dBeg, dEnd  : date;
  igkMarker   : longint;
  isCreateXLT : boolean = false;

//#region Queries

sql query qGetPlporsIGK =
select pl.nodok    as PlporNumber,
       pl.sumplat  as Summa,
       pl.datvip   as PlporDate,
       pl.NAMEPL1  as Naznach,
       spkIGK.name as IGKName,
       spkIGK.nrec as IGKNrec
from spcash spc
join cashbank cb on cb.nrec = spc.ccashbank
join usersdoc ud on ud.tipgal = 1 and ud.razdel = cb.razdel and ud.ccashbank = cb.nrec
join plpor pl on pl.tidk = ud.tipusers and pl.datvip between :dbeg and :dend
join attrval av on av.wtable = 9015 and av.crec = pl.nrec and av.cattrnam = #comp(000100000000077Ah) and av.vcomp = #comp(000100000000139Eh)
join spkau spkRS on spkRS.nrec = (case when spc.tblos[1] = 10545 then spc.kauos[1]
                                       when spc.tblos[2] = 10545 then spc.kauos[2]
                                       when spc.tblos[3] = 10545 then spc.kauos[3]
                                       when spc.tblos[4] = 10545 then spc.kauos[4]
                                       when spc.tblos[5] = 10545 then spc.kauos[5]
                                       when spc.tblos[6] = 10545 then spc.kauos[6] end)
join spkau spkIGK on spkIGK.nrec = (case when spc.tblos[1] = 10527 then spc.kauos[1]
                                         when spc.tblos[2] = 10527 then spc.kauos[2]
                                         when spc.tblos[3] = 10527 then spc.kauos[3]
                                         when spc.tblos[4] = 10527 then spc.kauos[4]
                                         when spc.tblos[5] = 10527 then spc.kauos[5]
                                         when spc.tblos[6] = 10527 then spc.kauos[6] end)
;

sql query qGetBDWithoutPlpors =
select bd.nodoc    as DocNumber,
       bd.ddoc     as DocDate,
       naz.vstring as Naznach,
       sps.summa   as Summa,
       spkIGK.nrec as IGKNrec,
       spkIGK.name as IGKName
from basedoc bd
join attrval vk on vk.wtable = 1102 and vk.crec = bd.nrec and vk.cattrnam = #comp(0001000000000087h) and
                   vk.vcomp in (#comp(00010000000001CEh), #comp(00010000000013CCh), #comp(0001000000001423h))
join attrval vb on vb.wtable = 1102 and vb.crec = bd.nrec and vb.cattrnam = #comp(0001000000000089h) and
                   vb.vcomp in (#comp(00010000000001CEh), #comp(00010000000013CCh), #comp(0001000000001423h))
join attrval vp on vp.wtable = 1102 and vp.crec = bd.nrec and vp.cattrnam = #comp(0001000000000088h) and
                   vp.vcomp in (#comp(00010000000001CEh), #comp(00010000000013CCh), #comp(0001000000001423h))
join attrval vd on vd.wtable = 1102 and vd.crec = bd.nrec and vd.cattrnam = #comp(000100000000008Ah) and
                   vd.vcomp in (#comp(00010000000001CEh), #comp(00010000000013CCh), #comp(0001000000001423h))
left join attrval naz on naz.wtable = 1102 and naz.crec = bd.nrec and naz.cattrnam = #comp(000100000000010Dh)
join stepdoc sd on sd.cbasedoc = bd.nrec
join spstep sps on sps.cstepdoc = sd.nrec
join attrval avRejim on avRejim.wtable = 1104 and avRejim.crec = sps.nrec and
                        avRejim.cattrnam = #comp(000100000000079Eh) and avRejim.vComp = #comp(000100000000139Eh)
join attrval avRS on avRS.wtable = 1104 and avRS.crec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)
join spkau spkRS on spkRS.nrec = avRS.vcomp
left join (select bf.cbasedoc, spkau.nrec as RSNrec, count(*) as cnt
           from basefin bf
           join usersdoc ud on ud.tipusers = bf.tipdoc
           join cashbank cb on cb.nrec = ud.ccashbank
           join spcash spc on spc.ccashbank = cb.nrec and spc.cplanssch = #comp(000100000000000Eh)
           join spkau on spkau.nrec = (case when spc.tblos[1] = 10545 then spc.kauos[1]
                                            when spc.tblos[2] = 10545 then spc.kauos[2]
                                            when spc.tblos[3] = 10545 then spc.kauos[3]
                                            when spc.tblos[4] = 10545 then spc.kauos[4]
                                            when spc.tblos[5] = 10545 then spc.kauos[5]
                                            when spc.tblos[6] = 10545 then spc.kauos[6] end)
           where bf.cbasedoc <> #comp(0)
           group by bf.cbasedoc, spkau.nrec
          ) bf on bf.cbasedoc = bd.nrec and bf.RSNrec = spkRS.nrec
join spcash spc on spc.cplanssch = #comp(000100000000000Eh) and (case when spc.tblos[1] = 10545 then spc.kauos[1]
                                                                      when spc.tblos[2] = 10545 then spc.kauos[2]
                                                                      when spc.tblos[3] = 10545 then spc.kauos[3]
                                                                      when spc.tblos[4] = 10545 then spc.kauos[4]
                                                                      when spc.tblos[5] = 10545 then spc.kauos[5]
                                                                      when spc.tblos[6] = 10545 then spc.kauos[6] end) = spkRS.nrec
left join spkau spkIGK on spkIGK.nrec = (case when spc.tblos[1] = 10527 then spc.kauos[1]
                                              when spc.tblos[2] = 10527 then spc.kauos[2]
                                              when spc.tblos[3] = 10527 then spc.kauos[3]
                                              when spc.tblos[4] = 10527 then spc.kauos[4]
                                              when spc.tblos[5] = 10527 then spc.kauos[5]
                                              when spc.tblos[6] = 10527 then spc.kauos[6] end)
where bd.viddoc in (101,111) and coalesce(bf.cnt,0) = 0
;

//#endregion

table struct tRS (
  IGKName : string,
  IGKNrec : comp
) with index (
  i01 = IGKNrec
);

table struct tPlporsIGK (
  PlporNumber : string,
  PlporDate   : date,
  Naznach     : string,
  Summa       : double,
  IGKName     : string,
  IGKNrec     : comp
) with index (
  i01 = IGKNrec
);

table struct tBDWithoutPlpors (
  DocNumber : string,
  DocDate   : date,
  Naznach   : string,
  Summa     : double,
  IGKName   : string,
  IGKNrec   : comp
) with index (
  i01 = IGKNrec
);

create view
as select *
from tRS, tPlporsIGK, tBDWithoutPlpors;

parameters dBeg, dEnd, igkMarker, sXLTFileName, isCreateXLT

procedure MakeRSTable; //content tRS by 2 tables (tPlporsIGK and tBDWithoutPlpors)
{
  var q: IQuery;
  var rs: IResultSet;

  var isFilteredByIGK: boolean = false;

  if igkMarker != 0
    if getmarkercount(igkMarker) > 0
      isFilteredByIGK := true;

  if isFilteredByIGK
  {
    //создаем временную таблицу для маркера по ИГК
    sqlCreateTmpTable('table tigkfilter (crec: comp);', ctmNormal);
    //заполняем ее значениями из маркера
    sqlTruncateTmpTable('tigkfilter');
    q := querymanager.createquery('insert tigkfilter (crec) values (:crec)');

    var i: longint = 0;
    var c: comp = 0;
    for (i := 0; getmarker(igkMarker, i, c); i++)
      q.setparam('crec', c).execute;
  }

  //выполняем 1ый запрос и rs в таблицу

  var s1: longint = 0;
  sqladdstr(s1, qGetPlporsIGK);
  if isFilteredByIGK
    sqladdstr(s1,'where spkigk.nrec in (select crec from tigkfilter)')

  q := querymanager.createquery(s1);

  q.setParam('dBeg', dBeg);
  q.setParam('dEnd', dEnd);
  rs := q.getresultset;
  if (rs.getfirst = tsOk) do
  {
    ClearBuffer(#tPlporsIGK);
    tPlporsIGK.PlporNumber := rs.row.val('PlporNumber');
    tPlporsIGK.PlporDate   := rs.row.val('PlporDate');
    tPlporsIGK.Naznach     := rs.row.val('Naznach');
    tPlporsIGK.Summa       := rs.row.val('Summa');
    tPlporsIGK.IGKName     := rs.row.val('IGKName');
    tPlporsIGK.IGKNrec     := rs.row.val('IGKNrec');
    insert current tPlporsIGK;
  } while (rs.getnext = tsOk);

  //потом второй запрос

  var s2: longint = 0;
  sqlAddStr(s2, qGetBDWithoutPlpors);
  if isFilteredByIGK
    sqlAddStr(s2, 'and coalesce(spkigk.nrec,#comp(0)) in (select crec from tigkfilter)');

  q := querymanager.createquery(s2);

  rs := q.getresultset;
  if (rs.getfirst = tsOk) do
  {
    ClearBuffer(#tBDWithoutPlpors);
    tBDWithoutPlpors.DocNumber := rs.row.val('DocNumber');
    tBDWithoutPlpors.DocDate   := rs.row.val('DocDate');
    tBDWithoutPlpors.Naznach   := rs.row.val('Naznach');
    tBDWithoutPlpors.Summa     := rs.row.val('Summa');
    tBDWithoutPlpors.IGKName   := rs.row.val('IGKName');
    tBDWithoutPlpors.IGKNrec   := rs.row.val('IGKNrec');
    insert current tBDWithoutPlpors;
  } while (rs.getnext = tsOk);

  _loop tPlporsIGK
  {
    if getfirst tRS where ((tPlporsIGK.IGKNrec == tRS.IGKNrec)) != tsOk
    {
      ClearBuffer(#tRS);
      tRS.IGKNrec := tPlporsIGK.IGKNrec;
      tRS.IGKName := tPlporsIGK.IGKName;
      insert current tRS;
    }
  }

  _loop tBDWithoutPlpors
  {
    if getfirst tRS where ((tBDWithoutPlpors.IGKNrec == tRS.IGKNrec)) != tsOk
    {
      ClearBuffer(#tRS);
      tRS.IGKNrec := tBDWithoutPlpors.IGKNrec;
      tRS.IGKName := tBDWithoutPlpors.IGKName;
      insert current tRS;
    }
  }

  if isFilteredByIGK
    sqlDropTmpTable('tigkfilter');
}

procedure PrintRazdel(aStr: string); //kind of documents
{
  pXL.ClearTblBuffer;
  pXL.SetTblStringFldValue('IGKName', aStr);
  pXL.SetTblNumberFldValue('Status',  2);
  pXL.InsTblRow;
}

procedure PrintReport;
{
  if (not isCreateXLT) Set sXLSFileName := pXL.CreateReport(sXLTFileName, True)
                  else Set sXLSFileName := pXL.CreateXLT(sXLTFileName, True);
  pXL.CreateTbls(sXLSFileName);
  pXL.CreateTbl('Main');
  pXL.CreateTblFld('IGKName');
  pXL.CreateTblFld('DocNumber');
  pXL.CreateTblFld('DocDate');
  pXL.CreateTblFld('Summa');
  pXL.CreateTblFld('Naznach');
  pXL.CreateTblFld('Status');

  if (not isCreateXLT)
  {
    var PreSumPl, PreSumBD, TotalSum: double = 0;
    TotalSum := 0;
    StartNewVisual(vtRotateVisual, vfThread + vfBreak + vfTimer, 'Формирование отчёта', 0);
    _loop tRS
    {
      PreSumPl := 0;
      PreSumBD := 0;
      pXL.ClearTblBuffer;
      pXL.SetTblStringFldValue('IGKName', ''''+tRS.IGKName);
      pXL.SetTblNumberFldValue('Status',  1);
      pXL.InsTblRow;
      PrintRazdel('1. Платежки в период с ' + dBeg + ' по ' + dEnd);
      _loop tPlporsIGK where ((tRS.IGKNrec == tPlporsIGK.IGKNrec))
      {
        pXL.ClearTblBuffer;
        pXL.SetTblStringFldValue('IGKName',   ''''+tPlporsIGK.IGKName);
        pXL.SetTblStringFldValue('DocNumber', tPlporsIGK.PlporNumber);
        pXL.SetTblDateFldValue  ('DocDate',   tPlporsIGK.PlporDate);
        pXL.SetTblStringFldValue('Naznach',   tPlporsIGK.Naznach);
        pXL.SetTblNumberFldValue('Summa',     tPlporsIGK.Summa);
        pXL.SetTblNumberFldValue('Status',    0);
        PreSumPl := PreSumPl + tPlporsIGK.Summa;
        pXL.InsTblRow;
      }
      pXL.ClearTblBuffer;
      pXL.SetTblStringFldValue('IGKName', 'Итого по платежам');
      pXL.SetTblNumberFldValue('Summa',   PreSumPl);
      pXL.SetTblNumberFldValue('Status',  3);
      pXL.InsTblRow;
      PrintRazdel('2. ДО без платежек входящие в период с ' + dBeg + ' по ' + dEnd);
      _loop tBDWithoutPlpors where ((tRS.IGKNrec == tBDWithoutPlpors.IGKNrec and (tBDWithoutPlpors.DocDate <= dEnd)))
      {
        pXL.ClearTblBuffer;
        pXL.SetTblStringFldValue('IGKName',   ''''+tBDWithoutPlpors.IGKName);
        pXL.SetTblStringFldValue('DocNumber', tBDWithoutPlpors.DocNumber);
        pXL.SetTblDateFldValue  ('DocDate',   tBDWithoutPlpors.DocDate);
        pXL.SetTblStringFldValue('Naznach',   tBDWithoutPlpors.Naznach);
        pXL.SetTblNumberFldValue('Summa',     tBDWithoutPlpors.Summa);
        pXL.SetTblNumberFldValue('Status',    0);
        PreSumBD := PreSumBD + tBDWithoutPlpors.Summa;
        pXL.InsTblRow;
      }
      pXL.ClearTblBuffer;
      pXL.SetTblStringFldValue('IGKName', 'Итого по ДО');
      pXL.SetTblNumberFldValue('Summa',   PreSumBD);
      pXL.SetTblNumberFldValue('Status',  3);
      pXL.InsTblRow;
      PrintRazdel('3. ДО без платежек в будущем периоде');
      _loop tBDWithoutPlpors where ((tRS.IGKNrec == tBDWithoutPlpors.IGKNrec and (tBDWithoutPlpors.DocDate > dEnd))) //ознакомительный раздел
      {
        pXL.ClearTblBuffer;
        pXL.SetTblStringFldValue('IGKName',   ''''+tBDWithoutPlpors.IGKName);
        pXL.SetTblStringFldValue('DocNumber', tBDWithoutPlpors.DocNumber);
        pXL.SetTblDateFldValue  ('DocDate',   tBDWithoutPlpors.DocDate);
        pXL.SetTblStringFldValue('Naznach',   tBDWithoutPlpors.Naznach);
        pXL.SetTblNumberFldValue('Summa',     tBDWithoutPlpors.Summa);
        pXL.SetTblNumberFldValue('Status',    0);
        pXL.InsTblRow;
      }
      pXL.ClearTblBuffer;
      pXL.SetTblStringFldValue('IGKName', 'Итого по ИГК ' + tRS.IGKName);
      pXL.SetTblNumberFldValue('Summa',   PreSumPl + PreSumBD);
      pXL.SetTblNumberFldValue('Status',  4);
      pXL.InsTblRow;
      TotalSum := TotalSum + PreSumPl + PreSumBD;
      if (not NextVisual) break;
    }
    pXL.ClearTblBuffer;
    pXL.SetTblStringFldValue('IGKName', 'ИТОГО');
    pXL.SetTblNumberFldValue('Summa',   TotalSum);
    pXL.SetTblNumberFldValue('Status',  4);
    pXL.InsTblRow;
  }
  stopvisual('',0);
  pXL.PublishTbl('Main');
  pXL.CreateVar(sXLSFileName);
  pXL.SetDateVar('dBeg', dBeg);
  pXL.SetDateVar('dEnd', dEnd);
  pXL.PublishVar;
  pXL.LoadReport(sXLSFileName);
  pXL.DisConnectExcel;
}

handleevent
cminit:
{
  if dEnd = 0
    dEnd := cur_date;

  if dBeg = 0
    dBeg := sub_day(dEnd,30);

  if sXLTFileName = ''
    sXLTFileName := TranslatePath('%StartPath%xls\ReportForThreeMln.xltm'); //rename to .xltm when content report

  MakeRSTable;
  PrintReport; //first of all use true for create XLT, then false - to make report

}
end;
end.

interface ReportForThreeMlnUI 'Отчёт по ДО с режимом использования до 3 млн (ФЗ-275)', escclose, doaccept;
show (,,40,7);

var
  dBeg, dEnd: date;
  mnth: byte;
  yeah: word;
  sXLTFileName: string;
  igkMarker: longint;
  _getkau: getkau;

function GetIGKMarkerView: string;
{
  var cnt: longint;
  cnt := getmarkercount(igkMarker);
  case cnt of
    0: result := 'Все ИГК';
    1: {
      var c: comp = 0;
      if getmarker(igkMarker, 0, c)
        result := if(getanykau(1, 10527, c), givenanname(1), '');
      else
        result := 'Выбрано элементов: ' + cnt;
    }
    else
      result := 'Выбрано элементов: ' + cnt;
  end;
}

create view
(IGKMarkerField)
as select GetIGKMarkerView
;

screen scMain;
fields
  mnth: [list 1 'Январь', 2 'Февраль', 3 'Март', 4 'Апрель', 5 'Май', 6 'Июль',
              7 'Июнь', 8 'Август', 9 'Сентябрь', 10 'Октябрь', 11 'Ноябрь', 12 'Декабрь'];
  yeah: noprotect, SpinButton[1, 2000, 2100];
  IGKMarkerField: protect, pickbutton;
buttons
  cmMakeMagic, default;
<<

`Месяц`.@@@@@@@@@@@@@ `год`.@@@@@@@@@@

`ИГК`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

            <.     Сформировать     .>
>>
end;


handleevent
cmPick:
{
  case curfield of
    #IGKMarkerField:
    {
      if _getkau.GetCodeKau(2, 10527, 0) > 0
      {
        var l: longint;
        l := initmarker(_getkau.GetMarkerName(10527), 8, 10, 10, false);
        CopyMarker(igkMarker, l);
        donemarker(l,'');
        rereadrecord;
      }
    }
  end;
}
cmDelonprotect:
{
  case curfield of
    #IGKMarkerField:
    {
      clearmarker(igkMarker);
      rereadrecord;
    }
  end;
}
cmMakeMagic:
{
  if mnth = 0 or yeah = 0
  {
    message('Не заполнен период формирования отчета. Заполните пожалуйста период', error);
    exit;
  }
  dBeg := date(1, mnth, yeah);
  dEnd := date(Last_Day(dBeg), mnth, yeah);
  runinterface(AGAT::ThreeMillionReport, dBeg, dEnd, igkMarker, sXLTFileName, false);
}
cminit:
{
  if not readmydsk(mnth, 'Month_Rep3Mln', false) mnth := 1;
  if not readmydsk(yeah, 'Year_Rep3Mln', false) yeah := Year(Cur_Date);

  igkMarker := initmarker('igkMarker_Rep3Mln', 8, 10, 10, false);
  sXLTFileName := TranslatePath('%StartPath%xls\ReportForThreeMln.xltm'); //rename to .xltm when content report
  rereadrecord;
}
cmDone:
{
  savemydsk(mnth, 'Month_Rep3Mln');
  savemydsk(yeah, 'Year_Rep3Mln');
  donemarker(igkMarker, 'igkMarker_Rep3Mln');
}
end;
end.

VipInterface UserReport_ThreeMillionReport Implements IUserReport licensed(free);
Interface UserReport_ThreeMillionReport;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::ReportForThreeMlnUI);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Агат. Отчёт по ДО с режимом использования до 3 млн (ФЗ-275)';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    if (Level = 1)
      GetGroupName := ''
    else
      GetGroupName := '';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'BUY';
    end;
  end;
end.

#component "L_BASEDOC"
.LinkForm 'AGAT_BD_3MLNREP' Prototype is 'PRBDOC'
.Group 'Закупка'
.Group 'Предоплата закупок'
.var
  dBeg, dEnd: date;
  igkMarker: longint;
.endvar
.create view vSpec
as select *
from stepdoc sd, spstep sps, attrval rs, spcash spc
where ((
  comp(BaseDocNrec) == sd.cbasedoc   and
  sd.nrec           == sps.cstepdoc  and
  1104              /== rs.wtable     and
  sps.nrec          /== rs.crec       and
  0001000000000A86h /== rs.cattrnam   and
  000100000000000Eh == spc.cplanssch and
  (spc.tblos[2]     =  10545         and
   spc.kauos[2]     =  rs.vcomp      and
   spc.kauos[1]     <> 0)
));
.NameInList 'Агат. Отчёт по ДО с режимом использования до 3 млн (ФЗ-275)'
.f 'NUL'
.{
.{ CheckEnter ISBASEFIN
.}
.{ CheckEnter ISEXCLASS
.}
.{ CheckEnter ISGROUP
.}
.{
.}
.begin
  var d: date;
  d := StrToDate(ddoc, 'DD/MM/YYYY');
  dBeg := date(1, Month(d), Year(d));
  dEnd := date(Last_Day(dBeg), Month(dBeg), Year(dBeg));
  igkMarker := initmarker('', 8, 10, 20, false);
  vSpec._loop ViewTable
  {
    InsertMarker(igkMarker, vSpec.spc.kauos[1]);
  }
  runinterfacenomodal(AGAT::ThreeMillionReport, dBeg, dEnd, igkMarker, TranslatePath('%StartPath%xls\ReportForThreeMln.xltm'), false);
end.
.if DELIMITER
.else
.end
.}
.endform
