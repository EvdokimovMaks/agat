#include PlporsByBaseDoc.vih
#include extattr.vih
#include MyKatSchet.vih

#component "AGAT"

interface iPlatezh;

var
  ea: iextattr;
  ks: iPlanSchet;

sql query qPlatezh =
  select ko.kodplatnds    as KPP,
         bd.nrec          as BaseDocNrec,
         bd.viddoc        as BaseDocViddoc,
         bd.name          as BaseDocName,
         bd.nodoc         as Nodoc,
         bd.ddoc          as DateDoc,
         bd.descr         as Descr,
         bd.desgr         as Desgr,
         bd.CMYBANK       as CMYBANK,
         bd.cgrotpr       as cgrotpr,
         bd.cgrpol        as cgrpol,
         bd.CTRANSP       as CTRANSP,
         bd.CBANK         as CBank,
         bd.CORG          as Cpol,
         bd.CDOGOVOR      as CDogovor,
         bd.CAPPDOGOVOR   as CAPPDOGOVOR,
         coalesce(avOsnDO.vcomp,#comp(0)) as OsnDONrec,
         coalesce(osndoStepdoc.nrec,#comp(0)) as OsnStepdocNrec,
         coalesce(cp.directplat, 0)  as CalplanDirectplat,
         coalesce(cp.nrec, #comp(0)) as CalplanNrec,
         naz.vstring      as Naznach,
         sd.nrec          as StepDocNrec,
         viz.vcomp        as VizaPEU,
         spst.Summa * case when bd.viddoc=111 then bd.procopl/100 else 1 end as Summa,
         spst.NDS * case when bd.viddoc=111 then bd.procopl/100 else 1 end as NDS,
         spst.RSFromSpec  as RSFromSpec,
         spst.UserMode    as UserMode,
         spst.UserModeStr as UserModeStr,
         spst.BankSpec    as BankSpec,
         spst.UserDocType as UserDocType,
         spst.Summa18     as Summa18,
         spst.CNal18      as CNal18,
         spst.Stavka18    as Stavka18,
         spst.Summa10     as Summa10,
         spst.CNal10      as CNal10,
         spst.Stavka10    as Stavka10
  from basedoc bd
  left join calplan cp on cp.nrec = bd.ccalplan
  left join katorg ko on ko.nrec = bd.corg
  left join attrval viz on viz.wtable = 1102 and viz.crec = bd.nrec and viz.cattrnam = #comp(281474976710792)
  left join attrval naz on naz.wtable = 1102 and naz.crec = bd.nrec and naz.cattrnam = #comp(000100000000010Dh)
  left join attrval avOsnDO on avOsnDO.wtable = 1102 and avOsnDO.crec = bd.nrec and avOsnDO.cattrnam = #comp(281474976710841)
  left join stepdoc osndoStepdoc on osndoStepdoc.cbasedoc = coalesce(avOsnDO.vcomp,#comp(0)) and coalesce(avOsnDO.vcomp,#comp(0)) <> #comp(0)
  left join stepdoc sd  on sd.cbasedoc = bd.nrec
  left join (select sum(sps.summa)    as Summa,
                    sum(sps.nds)      as NDS,
                    sps.cstepdoc      as CStepDoc,
                    av.vcomp          as RSFromSpec, // расчетный счет позиции спецификации ДО
                    max(um.vcomp)     as UserMode,
                    max(um.vstring)   as UserModeStr,
                    cb.cpodr          as BankSpec,
                    ud.tipusers       as UserDocType, // TIDK для платёжки
                    sum(nal18.SumNal) as Summa18,
                    max(nal18.CNalog) as CNal18,
                    max(nal18.Stavka) as Stavka18,
                    sum(nal10.SumNal) as Summa10,
                    max(nal10.CNalog) as CNal10,
                    max(nal10.Stavka) as Stavka10
             from spstep sps
             left join attrval av  on av.wtable = 1104 and av.crec = sps.nrec and av.cattrnam = #comp(0001000000000A86h)
             left join attrval um  on um.wtable = 1104 and um.crec = sps.nrec and um.cattrnam = #comp(000100000000079Eh)
             left join (select spkau.nrec as cRS, max(spc.ccashbank) as ccashbank
                        from spcash spc
                        join spkau on (case when spc.tblos[1] = 10545 then spc.kauos[1]
                                            when spc.tblos[2] = 10545 then spc.kauos[2]
                                            when spc.tblos[3] = 10545 then spc.kauos[3]
                                            when spc.tblos[4] = 10545 then spc.kauos[4]
                                            when spc.tblos[5] = 10545 then spc.kauos[5]
                                            when spc.tblos[6] = 10545 then spc.kauos[6] end) = spkau.nrec
                        where spc.cplanssch = #comp(000100000000000Eh)
                        group by spkau.nrec
                       ) spc on spc.cRS = coalesce(av.vcomp,#comp(0)) and coalesce(av.vcomp,#comp(0)) <> #comp(0)
             left join cashbank cb on cb.nrec = spc.ccashbank
             left join usersdoc ud on ud.tipgal = 1 and ud.razdel = cb.razdel and ud.ccashbank = cb.nrec

             left join (select n.cspdoc,
                               n.summa  as SumNal,
                               n.cnalog as CNalog,
                               n.nalog  as Stavka
                        from spdocnal n
                        where n.tipdoc = 1101 and n.nalog = 18
                       ) nal18 on nal18.cspdoc = sps.nrec
             left join (select n.cspdoc,
                               n.summa  as SumNal,
                               n.cnalog as CNalog,
                               n.nalog  as Stavka
                        from spdocnal n
                        where n.tipdoc = 1101 and n.nalog = 10
                       ) nal10 on nal10.cspdoc = sps.nrec
             group by sps.cstepdoc, av.vcomp, cb.cpodr, ud.tipusers
            ) spst on spst.cstepdoc = sd.nrec
  where bd.nrec = :basedocNrec
;

sql query qGetBySpCash =
  select cb.name     as BankName,
         cb.cpodr    as BankNrec,
         ud.tipusers as UserType
  from spcash spc
  join cashbank cb on cb.nrec = spc.ccashbank
  left join usersdoc ud on ud.ccashbank = cb.nrec and ud.razdel = cb.razdel and ud.tipgal = 1
  where spc.nrec = :SpCashNrec
;

create view
as select *
from plpor, soprhoz, basefin, nalogfin;

procedure CreatePlpors(aBaseDocNrec: comp; var aCash: comp; var aBankNrec: comp; var aTIDK: word; aLogFileName: string);
{
  var q, q1: IQuery;
  var rs, rs1: IResultSet;
  var VSogl, RSSpec: comp;
  var TIDK: word;
  var aViddoc: word;
  q := queryManager.createQuery(qPlatezh);
  q.setParam('basedocNrec', aBaseDocNrec);
  rs := q.getResultSet;
  if (rs.getFirst = tsOk) do
  {
    aViddoc := rs.row.val('BasedocViddoc');
    VSogl := rs.row.val('VizaPEU');
    RSSpec := rs.row.val('RSFromSpec');
    TIDK := rs.row.val('UserDocType');
    if RSSpec = 0 and VSogl != VIZA_S_OBSHIH_SCHETOV
    {
      LogStrToFile(aLogFileName, '|                                                                                                 |        Платежное поручение не сформировано,      |');
      LogStrToFile(aLogFileName, '| ДО ' + if(Length(rs.row.val('Naznach'))>=93, substr(rs.row.val('Naznach'),1,93), Pad(rs.row.val('Naznach'), 93)) + '|       т.к. не заполнен атрибут спецификации      |');
      LogStrToFile(aLogFileName, '|                                                                                                 |                "Р/сч списания ДС"                |');
      LogStrToFile(aLogFileName, '|-------------------------------------------------------------------------------------------------|--------------------------------------------------|');
      continue; //если в спецификации не заполнен атрибут "Р/счет списания ДС", то переходим к след итерации
    }
    if TIDK = 0 and VSogl != VIZA_S_OBSHIH_SCHETOV
    {
      LogStrToFile(aLogFileName, '|                                                                                                 |        Платежное поручение не сформировано,      |');
      LogStrToFile(aLogFileName, '| ДО ' + if(Length(rs.row.val('Naznach'))>=93, substr(rs.row.val('Naznach'),1,93), Pad(rs.row.val('Naznach'), 93)) + '|  т.к. нет р/счета предприятия выбранного банка   |');
      LogStrToFile(aLogFileName, '|                                                                                                 |                                                  |');
      LogStrToFile(aLogFileName, '|-------------------------------------------------------------------------------------------------|--------------------------------------------------|');
      continue; // если р/счёт не привязан к банку, платежку не формируем!
    }
    if VSogl = VIZA_S_OBSHIH_SCHETOV and aCash = 0
    {
      message(chr(3) + 'Выберите пожалуйста общий р/счёт для формирования платежек по ДО со значением Визы ПЭУ "С общих р/счет"' + chr(13) +
              'Платежки с данным р/счетом будут сформированы для всех ДО с такой визой');
      //if RunInterface('F_CASHBANK::SELUSDOC', 0, 0, 0, comp(0), aCash) = cmDefault
      if ks.SingleSelector(aCash)
      {
        q1 := queryManager.createQuery(qGetBySpCash);
        q1.setParam('SpCashNrec', aCash);
        rs1 := q1.getResultSet;
        if rs1 = nullref
        {
          message('Не найден общий р/сч', error);
          exit;
        }
        aBankNrec := rs1.row.val('BankNrec');
        aTIDK := rs1.row.val('UserType');
      }
      else
      {
        message('Процедура формирования платежных поручений прервана', error);
        exit;
      }
    }

    if VSogl = VIZA_S_OBSHIH_SCHETOV and aTIDK = 0
    {
      LogStrToFile(aLogFileName, '|                                                                                                 |        Платежное поручение не сформировано,      |');
      LogStrToFile(aLogFileName, '| ДО ' + if(Length(rs.row.val('Naznach'))>=93, substr(rs.row.val('Naznach'),1,93), Pad(rs.row.val('Naznach'), 93)) + '|  т.к. нет р/счета предприятия выбранного банка   |');
      LogStrToFile(aLogFileName, '|                                                                                                 |                                                  |');
      LogStrToFile(aLogFileName, '|-------------------------------------------------------------------------------------------------|--------------------------------------------------|');
      continue; // если выбранный общий счёт не привязан к банку, платежку не формируем!
    }

    ClearBuffer(#plpor);
    plpor.tidkgal   := 1;
    plpor.nodok     := rs.row.val('Nodoc');
    plpor.descr     := rs.row.val('Descr');
    plpor.desgr     := rs.row.val('Desgr');
    plpor.datvip    := rs.row.val('DateDoc');
    plpor.datots    := plpor.datvip;
    plpor.cplat     := CoGetTune('myorg');
    if VSogl = VIZA_S_OBSHIH_SCHETOV
    {
      plpor.cbankplat := aBankNrec;
      plpor.tidk      := aTIDK;
    }
    else
    {
      plpor.cbankplat := rs.row.val('BankSpec');
      plpor.tidk      := rs.row.val('UserDocType');
    }
    plpor.cpol      := rs.row.val('Cpol');
    plpor.cbankpol  := rs.row.val('CBank');
    plpor.cgruzotp  := plpor.cpol;
    plpor.cgruzpol  := plpor.cplat;
    plpor.sumplat   := rs.row.val('Summa'); //Сумма платежа по док-ту
    plpor.cstepdoc  := rs.row.val('StepDocNrec'); //Ссылка на ЭТАП StepDoc
    plpor.cstepdoc  := rs.row.val('OsnStepdocNrec');
    plpor.cwaymove  := rs.row.val('CTRANSP'); //Ссылка на способ транспортировки
    plpor.namepl1   := rs.row.val('Naznach');
    plpor.namepl4   := 'Сумма НДС ' + rs.row.val('NDS') + ' руб.';
    plpor.summa4    := rs.row.val('NDS');
    plpor.modedoc   := 2048
    insert current plpor;

    ea.coSetAttrID(WT_PLPOR, plpor.nrec, ATTRNAM_USEMODE_PLPOR, rs.row.val('UserMode'), rs.row.val('UserModeStr'));

    LogStrToFile(aLogFileName, '|                                                                                                 |         Платёжное поручение сформировано         |');
    LogStrToFile(aLogFileName, '| ДО ' + if(Length(rs.row.val('Naznach'))>=93, substr(rs.row.val('Naznach'),1,93), Pad(rs.row.val('Naznach'), 93)) + '|                № ' + Pad(plpor.nodok, 32) + '|');
    LogStrToFile(aLogFileName, '|                                                                                                 |               от ' + Pad(DateToStr(plpor.datvip, 'DD.MM.YYYY'), 32) + '|');
    LogStrToFile(aLogFileName, '+-------------------------------------------------------------------------------------------------+--------------------------------------------------+');

    ClearBuffer(#soprhoz);
    soprhoz.cdogovor := rs.row.val('CDogovor');
    if (aViddoc = 111)
      soprhoz.cpredopldo := rs.row.val('BaseDocNrec');
    soprhoz.cstepdoc := plpor.cstepdoc;
    soprhoz.csoprdoc := plpor.nrec;
    soprhoz.tipdoc   := plpor.tidk;
    soprhoz.tidkgal  := 1;
    soprhoz.nodoc    := plpor.nodok;
    soprhoz.descr    := plpor.descr;
    soprhoz.desgr    := plpor.desgr;
    soprhoz.namesho  := rs.row.val('BaseDocName');
    soprhoz.summa    := plpor.sumplat;
    soprhoz.direct   := 2;
    soprhoz.vhsumhoz := '+';
    soprhoz.modedoc  := plpor.modedoc;
    soprhoz.corg     := plpor.cpol;
    if rs.row.val('CalplanDirectPlat') <> 0
      soprhoz.cfinplan := rs.row.val('CalplanNrec')
    else
      soprhoz.ccalplan := rs.row.val('CalplanNrec');
    insert current soprhoz;

    ClearBuffer(#basefin);
    basefin.cdogovor := soprhoz.cdogovor;
    if (aViddoc = 111)
    {
      basefin.cpredopldo := rs.row.val('BaseDocNrec');
      basefin.cbasedoc   := rs.row.val('OsnDONrec');
    }
    else
    {
      basefin.cbasedoc   := rs.row.val('BaseDocNrec');
    }
    basefin.cstepdoc := plpor.cstepdoc;
    basefin.tipdoc   := plpor.tidk;
    basefin.tidkgal  := 1;
    basefin.cplpor   := plpor.nrec;
    basefin.nodoc    := plpor.nodok;
    basefin.descr    := plpor.descr;
    basefin.desgr    := plpor.desgr;
    basefin.namepl   := rs.row.val('BaseDocName');
    basefin.summa    := plpor.sumplat;
    basefin.direct   := 2;
    basefin.corg     := plpor.cpol;
    basefin.ccalplan := soprhoz.ccalplan;
    basefin.csoprhoz := soprhoz.nrec;
    insert current basefin;

    if rs.row.val('Summa18') != 0
    {
      ClearBuffer(#nalogfin);
      nalogfin.csoprhoz := soprhoz.nrec;
      nalogfin.cnalog   := rs.row.val('CNal18');
      nalogfin.keynalog := rs.row.val('Stavka18');
      nalogfin.stavka   := rs.row.val('Stavka18');
      nalogfin.summa    := rs.row.val('Summa18');
      nalogfin.tidkgal  := 1;
      nalogfin.csoprdoc := plpor.nrec;
      insert current nalogfin;
    }
    if rs.row.val('Summa10') != 0
    {
      ClearBuffer(#nalogfin);
      nalogfin.csoprhoz := soprhoz.nrec;
      nalogfin.cnalog   := rs.row.val('CNal10');
      nalogfin.keynalog := rs.row.val('Stavka10');
      nalogfin.stavka   := rs.row.val('Stavka10');
      nalogfin.summa    := rs.row.val('Summa10');
      nalogfin.tidkgal  := 1;
      nalogfin.csoprdoc := plpor.nrec;
      insert current nalogfin;
    }
  } while (rs.getnext = tsOk);
}
end.
