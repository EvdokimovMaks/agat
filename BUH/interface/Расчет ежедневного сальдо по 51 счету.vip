#component "AGAT"
interface calc_saldday_51 'Расчет ежедневного сальдо по субсчету 51 счета';
  show at (,,29,7);
  var
    d1,d2: date;
    subs: string;

  create view
  as select *
  from planssch ps;

  screen scmain;
    fields
      d1: noprotect;
      d2: noprotect;
      subs: protect, pickbutton;
    buttons
      cmDoOk;
<<
 Пересчитать сальдо за период
 с .@@@@@@@@@ по .@@@@@@@@@

 Субсчет .@@@@@

 <.  Выполнить пересчет  .>
>>
  end;

  handleevent
    cmDoOk: {
      if (d1 > d2) exit;
      var _cplansch: comp;
      _cplansch := cogettune('FIN.USER.CPLANSSCH');
      var symb: string;
      symb := if(getfirst ps where ((_cplansch == ps.nrec)) = tsOk, ps.code, '');
      var d: date;
      d := d1;
      StartNewVisual(vtRotateVisual, vfThread + vfTimer, '', 0);
      while d <= d2 {
        setvisualheader(d);
        calc_newsaldo(0, _cplansch, coSaldDay, symb+'51', subs, 0,0,0,0,0,0,0, sub_day(d,1), d);
        d := add_day(d,1);
      }
      stopvisual('',0);
    }
    cmpick: {
      case curfield of
        #subs: {
          runinterface(F_BUHSCHET::GETBUHSCHET, 0, cogettune('FIN.USER.CPLANSSCH'), '51', subs);
          rereadrecord;
        }
      end;
    }
    cminit: {
      if (not readmydsk(d1, 'calc_saldday_51_d1', false)) d1 := cur_date;
      if (not readmydsk(d1, 'calc_saldday_51_d2', false)) d2 := cur_date;
      if (not readmydsk(subs, 'calc_saldday_51_subs', false)) subs := '';
    }
    cmdone: {
      savemydsk(d1, 'calc_saldday_51_d1');
      savemydsk(d1, 'calc_saldday_51_d2');
      savemydsk(subs, 'calc_saldday_51_subs');
    }
  end;
end.
