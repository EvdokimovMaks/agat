#include IdentAPI.vih
#include AG_PatFlt.vih
#include AG_SettlementsPatParam.vih
#include AG_PatMemo.vih
#include AG_PatFltConvToQuery.vih
#include Settlements.vih

#component "F_PAT"

VipInterface IdApi_AG_Settlements Implements ObjIdentApiPlugin Licensed(free);
interface IdApi_AG_Settlements;

var
  _params: AG_IPatParams(AG_PatParams) new;
  _paramGen: AG_SettlementsPatParamGen new;
  _patFlt: AG_IPatFlt;
  _mKatStroy: TPtr;
  _settlements: Settlements new;
  _settl: ISettlements;
  _wGroup: word;

create view
as select _settl.Nrec
from _settl
condition byGroup = _settl.wGroup = _wGroup
;

window wParams 'Параметры';
  embedded scParams interface _paramGen;
  end;
end;

//генерация строки параметров [Param1:"Value1"][Param2:"Value2"]
function ParamGen : string;
{
  result := _paramGen.ParamGen;
}

//Инициализировать окно парметров ключа
function InitPluginWnd( TiDkGal, SysOper : word; Key : string ) : boolean;
{
  result := _paramGen.Init(TiDkGal, SysOper, Key);
}

//разбор массива параметров [Param1:"Value1"][Param2:"Value2"]
//(параметры уже предварительно разобраны калькулятором и переложены в массив )
function ParamParser(hTxo: longint; TiDkGal, SysOper: word ): boolean;
{
  result := false;

  var paramFactory: AG_IPatParamFactory;
  if not GetVipRef(paramFactory, 'AGAT::AG_PatParamFactory') exit;

  var paramParser: AG_IPatParamParser = AG_IPatParamParser(
    new(AG_PatParamParser, Create(paramFactory))
  );

  var ctx: TAG_PatParamContext;
  ctx.hTxo    := hTxo;
  ctx.TiDkGal := TiDkGal;
  ctx.SysOper := SysOper;

  result := paramParser.parse(_params, ctx);
}

//инициализация расчета -  срабатывает один раз при первом обращении к плагину
//во время разноске одного документа, закрытии одного счета, формирования одного FR отчета и т.п.
procedure InitCalculation (hTxo : longint; buf : TTxoApiInfoDoc);
{
  #__UNUSED__(hTxo, buf)
  _mKatStroy := InitMarker('', 8, 10, 10, true);
  _settlements.mKatStroy := _mKatStroy;
  _settl := ISettlements(_settlements);
}

//конкретный расчет конкретных параметров
procedure MakeCalculation (hTxo : longint; buf : TTxoApiInfoDoc);
{
  ClearMarker(_mKatStroy);
  _patFlt := AG_IPatFlt(new(AG_PatFlt, Create(hTxo)));
  if _params.getP('ЗАКАЗ') = NullRef exit;
  if not _patFlt.findByName(_params.getP('ЗАКАЗ').Value) exit;
  if _patFlt.items.first do
  {
    InsertMarker(_mKatStroy, comp(_patFlt.items.Value))
  }
  while _patFlt.items.next;

  var sResult: string = if(_params.getP('РЕЗ') = NullRef, 'ОПЛАТА', _params.getP('РЕЗ').Value);

  _wGroup := if(_params.getP('ГРУППА') = NullRef or sResult = 'ОТГРУЗКА', 0,
                word(_params.getP('ГРУППА').Value));
  if _wGroup = 0
  {
    if ConditionActive(tcByGroup)
      PopCondition(tcByGroup);
  }
  else
  {
    if not ConditionActive(tcByGroup)
      PushCondition(tcByGroup);
  }

  _settlements.mKatStroy := _mKatStroy;
  _settlements.dDate := buf.dEndFp;
  _settlements.refresh;

  var rslt: double = 0;
  _loop _settl
  {
    rslt += case(sResult;
                 'ОПЛАТА': _settl.Payment,
                 'ОТГРУЗКА': _settl.Shipment,
                 'АВАНС': _settl.Advance,
                 'ПОСТОПЛАТА': _settl.PostPay,
                 'ПОСТОПЛБЕЗНДС': _settl.PostPay - _settl.PostPayNDS,
                 'ЗАЧЕТАВАНСА': _settl.AdvSetOff,
                 'ЗАЧАВБЕЗНДС': _settl.AdvSetOff - _settl.AdvSetOffNDS,
                 'ОТГРНДС': _settl.ShipNDS,
                 'ПОСТОПЛНДС': _settl.PostPayNDS,
                 'ЗАЧАВНДС': _settl.AdvSetOffNDS;
                 0);
  }

  TxoHeadSetResultDouble(hTxo, rslt);
}


//завершение расчета -  срабатывает один раз после завершения всех расчетов
//при разноске одного документа, закрытии одного счета, формирования одного FR отчета и т.п.
procedure DoneCalculation (hTxo : longint; buf : TTxoApiInfoDoc);
{
  #__UNUSED__(hTxo, buf)
  DoneMarker(_mKatStroy, '');
}

end.

VipInterface IdApiReg_AG_Settlements Implements ObjIdentApiPluginRegistrator Licensed(Free);

interface IdApiReg_AG_Settlements;
procedure DoRegisterIdents;
{
  AddIdent('AG_ВЗАИМОРАСЧЕТЫ', 'Взаиморасчеты с поставщиками', 'IdApi_AG_Settlements', 'wParams');
  AddIdent('AG_ВЗР', 'То же что и AG_ВЗАИМОРАСЧЕТЫ (но короткое название) ', 'IdApi_AG_Settlements', 'wParams');
}
end.
