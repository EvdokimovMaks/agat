#include Query.vih
#include AG_PatFlt.vih
#include AG_PatFltConvToQuery.vih

#component "AGAT"

interface AG_PatFltConvToQuery;

var
  _flt: AG_IPatFlt;
  _bTmpTablesPrepared: boolean;
  _tblNameNrecFltr: string;
  _tblNameMaskFltr: string;

function getNewSqlTmpTableName(sPrefix: string): string;
{
  result := sPrefix + _DateTimeToStr(_CurDateTime, 'NNSSSSS');
}

procedure mySqlAddStr(var sqlStr: longint; s: string);
{
  sqlAddStr(sqlStr, s);
  LogStrToFile('log.txt', s);
}

function prepareSqlTmpTable: boolean;
{
  if not _bTmpTablesPrepared
  {
    var s: longint = 0;
    _tblNameNrecFltr := getNewSqlTmpTableName('NrecFltr');
    SqlAddStr(s, 'table ' + _tblNameNrecFltr + ' (');
    SqlAddStr(s, '  FltIdx: word');
    SqlAddStr(s, ' ,cRec: comp');
    SqlAddStr(s, ') with index (');
    SqlAddStr(s, '  i01 = FltIdx');
    SqlAddStr(s, ' ,i02 = cRec');
    SqlAddStr(s, ');');
    result := sqlCreateTmpTable(s, ctmNormal) = tsOk;
    _bTmpTablesPrepared := result;
    if not result exit;

    sqlFreeStr(s);
    _tblNameMaskFltr := getNewSqlTmpTableName('MaskFltr');
    SqlAddStr(s, 'table ' + _tblNameMaskFltr + '(');
    SqlAddStr(s, '  FltIdx: word');
    SqlAddStr(s, ' ,Mode: word');
    SqlAddStr(s, ' ,Mask: string');
    SqlAddStr(s, ') with index (');
    SqlAddStr(s, '  i01 = FltIdx + Mode');
    SqlAddStr(s, ');');
    result &= sqlCreateTmpTable(s, ctmNormal) = tsOk;
    _bTmpTablesPrepared := result;
  }
  else
    result := true;
}

function prepareDataForNrecFilter: boolean;
{
  result := sqlExecDirect('delete ' + _tblNameNrecFltr + ' where FltIdx = ' +
                          _flt.Idx) = tsOk;

  var fltIdx: word = _flt.Idx;
  var cRec: comp = 0;

  var stmt: longint = sqlAllocStmt;
  sqlPrepare(stmt, 'insert into ' + _tblNameNrecFltr + ' (FltIdx, cRec) values (:fltIdx, :cRec)');
  if _flt.items.first do
  {
    cRec := _flt.items.value;
    sqlBindParamByName(stmt, 'fltIdx', fltIdx);
    sqlBindParamByName(stmt, 'cRec', cRec);
    result &= sqlExecute(stmt) = tsOk;
  }
  while _flt.items.next;
  sqlFreeStmt(stmt);
}

function fltMaskToSqlMask(mask: string): string;
{
  result := replace(mask, '#', '%');
}

function prepareDataForMaskFilter: boolean;
{
  result := sqlExecDirect('delete ' + _tblNameMaskFltr + ' where FltIdx = ' +
                          _flt.Idx) = tsOk;
  var fltIdx: longint = _flt.Idx;
  var mode: word = 0;
  var mask: string = '';
  var stmt: longint = sqlAllocStmt;
  sqlPrepare(stmt, 'insert ' + _tblNameMaskFltr + ' (FltIdx, Mode, Mask) values (?, ?, ?)');
  sqlBindParam(stmt, 1, fltIdx);
  sqlBindParam(stmt, 2, mode);
  sqlBindParam(stmt, 3, mask);
  if _flt.items.first do
  {
    mode := _flt.items.mode;
    mask := fltMaskToSqlMask(_flt.items.value);
    result &= sqlExecute(stmt) = tsOk;
  }
  while _flt.items.next;
  sqlFreeStmt(stmt);
}

function prepareData: boolean;
{
  result := case(_flt.Mode;
                 lc_PATFltMode_ByNRec: prepareDataForNrecFilter,
                 lc_PATFltMode_ByMask: prepareDataForMaskFilter;
                 false);
}

function toSqlStrNrecFltr(sFld: string; var lSqlStr: longint): boolean;
{
  mySqlAddStr(lSqlStr, '(' + sFld + if(_flt.isExclude, ' not in ', ' in '));
  mySqlAddStr(lSqlStr, '(select cRec from ' + _tblNameNrecFltr);
  mySqlAddStr(lSqlStr, ' where ' + _tblNameNrecFltr + '.FltIdx=' + _flt.Idx + '))');
  result := true;
}

function toSqlStrMaskFltr(sFld: string; var lSqlStr: longint): boolean;
{
  mySqlAddStr(lSqlStr, '(');
  mySqlAddStr(lSqlStr, '  (select count(*) from ' + _tblNameMaskFltr);
  mySqlAddStr(lSqlStr, '   where ' + _tblNameMaskFltr + '.FltIdx=' + _flt.Idx);
  mySqlAddStr(lSqlStr, '   and ' + _tblNameMaskFltr + '.Mode=' + lc_SpPATFltMode_ByIncludeMask);
  mySqlAddStr(lSqlStr, '   and ' + sFld + ' like ' + _tblNameMaskFltr + '.mask');
  mySqlAddStr(lSqlStr, '  ) > 0 ');
  mySqlAddStr(lSqlStr, '  and');
  mySqlAddStr(lSqlStr, '  (select count(*) from ' + _tblNameMaskFltr);
  mySqlAddStr(lSqlStr, '   where ' + _tblNameMaskFltr + '.FltIdx=' + _flt.Idx);
  mySqlAddStr(lSqlStr, '   and ' + _tblNameMaskFltr + '.Mode=' + lc_SpPATFltMode_ByIncludeMask);
  mySqlAddStr(lSqlStr, '   and ' + sFld + ' like ' + _tblNameMaskFltr + '.mask');
  mySqlAddStr(lSqlStr, '  ) > 0');
  mySqlAddStr(lSqlStr, ')');
  result := true;
}

function toSqlStr(sFltName: string; sFld: string; var lSqlStr: longint): boolean;
{
  result := false;
  if not _flt.findByName(sFltName) exit;
  if not prepareSqlTmpTable exit;
  if not prepareData exit;
  result := case(_flt.Mode;
                 lc_PATFltMode_ByNRec: toSqlStrNrecFltr(sFld, lSqlStr),
                 lc_PATFltMode_ByMask: toSqlStrMaskFltr(sFld, lSqlStr);
                 false);
}

constructor Init;
{
  result := false;
}

constructor Create(flt: AG_IPatFlt);
{
  _flt := flt;
  result := _flt != NullRef;
}

destructor Done;
{
  if _bTmpTablesPrepared
  {
    sqlDropTmpTable(_tblNameNrecFltr);
    sqlDropTmpTable(_tblNameMaskFltr);
  }
}

end.
