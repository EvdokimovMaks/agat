#include GlobalGoods.vih
#include Params.vih

#component "M_MNPLAN"

VipInterface Algoritm_5110 Implements oAlgoritm_Sys;
Interface Algoritm_5110 '' (,hcNoContext,) EscClose, Cyan;

var
  _globalGoodsCollectionFiller: IGlobalGoodsCollectionFiller;
  _goodsMatch                 : IGoodsMatch;
  _paramsFactory              : ParamsFactory;

create view
var
  _formplNrec: comp;
as select *
from formpl
where ((
  _formplNrec == formpl.nrec
))
;

Function RunInter(wflag: word; var pt: TRecAcc): boolean;
{
  wflag := wflag;
  result := false;

  var goodsCollection: IGlobalGoodsCollection;
  goodsCollection := nullref;
  if loadvipref(goodsCollection, 'AGAT::GlobalGoodsCollection') and _globalGoodsCollectionFiller != nullref
  {
    _globalGoodsCollectionFiller.FillCollection(goodsCollection, _paramsFactory.CreateParams.SetParam('nrec', pt.cpNrec));
    _goodsMatch.SearchMatches(goodsCollection);
    _goodsMatch.ShowUI;
  }
}

function InitInter(var pt: TRecAcc): boolean;
{
  _formplNrec := pt.cFormpl;

  _globalGoodsCollectionFiller := nullref;
  if getfirst formpl = tsOk
  {
    case formpl.resword[1] of
      coBasedoc: getvipref(_globalGoodsCollectionFiller, 'AGAT::GlobalGoodsColFillerBasedoc');
    else
      getvipref(_globalGoodsCollectionFiller, 'AGAT::GlobalGoodsColFillerKatsopr');
    end;
  }
  if _globalGoodsCollectionFiller = nullref
    getvipref(_globalGoodsCollectionFiller, 'AGAT::GlobalGoodsColFillerKatsopr');

  result := getvipref(_goodsMatch, 'AGAT::GoodsMatch') and (_globalGoodsCollectionFiller != nullref);
}

Function DoneInter(var pt: TRecAcc): boolean;
{
  pt.Position := pt.Position;
  DoneInter := true;
}

function GetNum: word;
{
  GetNum := 5110;
}

function GetName: string; {
  GetName := 'АГАТ. Установка соответствий МЦ Global';
}

function GetMemo (st:oSteckCommonString): boolean; {
  GetMemo := true;
  st.push(string(''));
}

function GetNameInter: string; {
  GetNameInter := 'Algoritm_5110' ;
}

function GetNameInterPr: string; {
  GetNameInterPr := 'AlgoritmSetup_5110' ;
}

end.
