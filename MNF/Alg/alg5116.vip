#include ClaimDirects.vih
#include System.vih

#component "M_MNPLAN"

VipInterface Algoritm_5116 Implements oAlgoritm_Sys;
Interface Algoritm_5116 '' (,hcNoContext,) EscClose, Cyan;

var
  _sync: ClaimDirectsSync;

create view
var
  _formplNrec: comp;
as select *
from formpl
where ((
  _formplNrec == formpl.nrec
))
;

Function RunInter(wflag: word; var pt: TRecAcc): boolean;
{
  wflag := wflag;
  result := false;

  var prot: IAtlProtocol(AtlProtocol) new;

  var claimDirects: IClaimDirects(ClaimDirects) new;

  prot.LogStartVisual('Заполнение по входящим заявкам', 4);

  prot.LogNextVisual('Загрузка годовой заявки');
  prot.LogMessage(APE_INFO, 'Загрузка годовой заявки');
  if not claimDirects.Load(pt.cMnPlan)
    prot.LogLine(APE_ERROR, 'Ошибка загрузки годовой заявки');
  else
  {
    prot.LogCommit;

    prot.LogNextVisual('Формирование новых позиций');

    prot.LogMessage(APE_INFO, 'Формирование новых позиций');
    _sync.SyncClaimWithInputIndents(claimDirects, prot);
    prot.LogCommit;

    prot.LogNextVisual('Сохранение изменений');
    prot.LogMessage(APE_INFO, 'Сохранение изменений');
    if claimDirects.Save
    {
      prot.LogCommit;

      prot.LogNextVisual('Формирование исходящих связей годовой заявки с текущими');
      prot.LogMessage(APE_INFO, 'Формирование исходящих связей');
      _sync.SyncOutputLinksWithInputLinks(claimDirects, prot);
      prot.LogCommit;
    }
    else
    {
      prot.LogLine(APE_ERROR, 'Ошибка сохранения годовой заявки');
      prot.LogCommit;
    }

  }

  prot.LogStopVisual;
  prot.Visible := true;

  result := true;
}

function InitInter(var pt: TRecAcc): boolean;
{
  _formplNrec := pt.cFormpl;
  result := _sync != nullref;
}

Function DoneInter(var pt: TRecAcc): boolean;
{
  pt.Position := pt.Position;
  DoneInter := true;
}

function GetNum: word;
{
  GetNum := 5116;
}

function GetName: string; {
  GetName := 'АГАТ. Заполнение годовой заявки по прямым расходам по входящим текущим заявкам';
}

function GetMemo (st:oSteckCommonString): boolean; {
  GetMemo := true;
  st.push(string(''));
}

function GetNameInter: string; {
  GetNameInter := 'Algoritm_5116' ;
}

function GetNameInterPr: string; {
  GetNameInterPr := '' ;
}

end.
