!===============================================================================================================
! Надо сделать настройки. Строки помечены  - В настр
!===============================================================================================================
#include AGAT_GOZ_TMP.tbl
#component "AGAT"
!===============================================================================================================
Interface AGAT_GOZ_Pr2 'Справка о заключенных контрактах на поставку продукции с головными исполнителями ГОЗ' (,,)   EscClose, cyan;
Show (,,45,5);
!===============================================================================================================
create view
var 
 _dBeg
,_dEnd            
                  
,dd1,dd2         
                  : date;

 _Item_KP 
,_Item_PrP
,_curnRec_DogSogl
,_curVidDog       
,_cSoprDoc
                  : comp;
 _myPath
,_mySchet
,_myKorSchet
                  : string;

 _Line
,_Line2
,_Line3
,_Line4
,_Line5
,_curTiDk_DogSogl
,_npp             
                  : word;

 _Kol
                  : double;


as select 
  if (SpDocs.PrMC = 1, KatMC.Name, KatUsl.Name)
            (FieldName = _SpecName),

*
from
 Dogovor
,ExClassVal 
,ExClassSeg

,ExClassVal ExClassVal_Dog
,ExClassSeg ExClassSeg_Dog


,AttrNam AttrNam_KP
,AttrVal AttrVal_KP
,AttrNam AttrNam_PrP
,AttrVal AttrVal_PrP
,KatOrg
,_Dog
,_Dog _DogTmp
,_Dog _DogDel
,Dogovor  Sogl
,SpDocs
,SpecMTR
,KatStroy
,_Dog _DogRep (_Dog_02)
,_Zakaz
,_Zakaz _ZakazDel
,SpGrSch
,KatVidD
,GroupSch
,GroupSch PSC


,Oborot Oborot62_D2013 (accelerate)
,Oborot Oborot62_D2005 (accelerate)

,Oborot Oborot62_K2013 (accelerate)
,BuhSchet BuhSchet_K2013 (accelerate)

,Oborot Oborot62_K2005 (accelerate)
,BuhSchet BuhSchet_K2005 (accelerate)


,_Avans
,_Avans _AvansDel

,_FOpl
,_FOpl _FOplDel

,PLANSSCH

,CalPlan
,SpDocs SpDocsCalPlan
,SpecMTR SpecMTRCalPlan

,AttrNam AttrNamPSC
,AttrVal AttrValPSC

,AttrNam AttrNamDPrik1
,AttrVal AttrValDPrik1
,AttrNam AttrNamDPrik2
,AttrVal AttrValDPrik2

,_Nakl
,_Nakl _NaklDel

,_Akt
,_Akt _AktDel

,AttrNam AttrNamIstFin
,AttrVal AttrValIstFin

,PlPor
,KatMC
,KatUsl

,SpecMTR SpecMTR_GF
,KatSopr Katsopr_GF
,SpSopr SpSopr_GF

!============================================================================================$${
,KatStroy KatStroy_Zakaz
,SpecMTR SpecMTR_Zakaz
,SpStep  SpStep_Zakaz
,StepDoc StepDoc_Zakaz
,BaseDoc BaseDoc_Zakaz
,_Zakaz _Zakaz_Zakaz
!============================================================================================$$}

where ((
    comp(0)                     ==  Dogovor.cDogovor

and Dogovor.nRec                ==  ExClassVal.cRec
and word(123)                   ==  ExClassVal.ClassCode
and ExClassVal.cClassSeg        ==  ExClassSeg.nRec

!============================================================
and word(1707)                  ==  AttrNam_KP.wTable
and 'Конечный потребитель'      ==  AttrNam_KP.Name
and AttrNam_KP.wTable           ==  AttrVal_KP.wTable
and Dogovor.nRec                ==  AttrVal_KP.cRec
and AttrNam_KP.nRec             ==  AttrVal_KP.cAttrNam
!============================================================
and word(1707)                  ==  AttrNam_PrP.wTable
and 'Принадлежность продукции'  ==  AttrNam_PrP.Name
and AttrNam_PrP.wTable          ==  AttrVal_PrP.wTable
and Dogovor.nRec                ==  AttrVal_PrP.cRec
and AttrNam_PrP.nRec            ==  AttrVal_PrP.cAttrNam
!============================================================
and Dogovor.cOrg                ==  KatOrg.nRec
and Dogovor.nRec                ==  _Dog.nRec

and _DogTmp.nRec                ==  Sogl.cDogovor
and word(1)                     ==  Sogl.Status

and _DogTmp.cVidDog             ==  KatVidD.nRec
and _curTiDk_DogSogl            ==  SpDocs.TiDk
and _curnRec_DogSogl            ==  SpDocs.cDoc

and SpDocs.cMcUsl               ==  KatMc.nRec
and SpDocs.cMcUsl               ==  KatUsl.nRec

and word(1723)                  ==  SpecMTR.CoTable
and SpDocs.nRec                 ==  SpecMTR.cSpec
and SpecMTR.cObj                ==  KatStroy.nRec

and SpDocs.cMoveCell            ==  PSC.nRec
!============================================================
and _DogRep.nRec                ==  _Zakaz.cDogovor
and word(1)                     ==  SpGrSch.wList
and _DogRep.nRec                ==  SpGrSch.cBaseDoc
and SpGrSch.cGroupSch           ==  GroupSch.nRec

and '862'                       ==  Oborot62_K2013.SCHETK
and (
     '8100'                     <>  Oborot62_K2013.SCHETO and
     '862'                      <>  Oborot62_K2013.SCHETO
    )
and _Zakaz.cKatStroy            ==  Oborot62_K2013.KAUKS[3]
and Oborot62_K2013.SCHETK       ==  BuhSchet_K2013.SCHET
and Oborot62_K2013.SUBSCHK      ==  BuhSchet_K2013.SUBSCH

and '762'                       ==  Oborot62_K2005.SCHETK
and (
     '7100'                     <>  Oborot62_K2005.SCHETO and
     '762'                      <>  Oborot62_K2005.SCHETO
    )
and _Zakaz.cKatStroy            ==  Oborot62_K2005.KAUKS[2]
and Oborot62_K2005.SCHETK       ==  BuhSchet_K2005.SCHET
and Oborot62_K2005.SUBSCHK      ==  BuhSchet_K2005.SUBSCH



!============================================================
and '862'                       ==  Oborot62_D2013.SCHETO
and (
     '890'                       =  Oborot62_D2013.SCHETK
    )
and _Zakaz.cKatStroy            ==  Oborot62_D2013.KAUOS[3]

and '762'                       ==  Oborot62_D2005.SCHETO
and (
     '790'                       =  Oborot62_D2005.SCHETK
    )
and _Zakaz.cKatStroy            ==  Oborot62_D2005.KAUOS[2]


and _cSoprDoc                   ==  PlPor.nRec
and _Zakaz.cKatStroy            ==  _Avans.cKatStroy
and _Zakaz.cKatStroy            ==  _FOpl.cKatStroy

!============================================================
and word(1723)                  ==  SpecMTRCalPlan.CoTable
and (
      SpecMTR.cObj               =  SpecMTRCalPlan.cObj
    )
and SpecMTRCalPlan.cSpec        ==  SpDocsCalPlan.nRec
and (
      word(403)                  =  SpDocsCalPlan.TiDk
    )
and SpDocsCalPlan.cDoc          ==  CalPlan.nRec
and (
      SpDocs.cDoc                =  CalPlan.cDogovor //and
//      4                         <>  CalPlan.Status
    )

and word(1125)                  ==  AttrNamPSC.wTable
and 'Номер протокола согл. цены'==  AttrNamPSC.Name
and word(1125)                  ==  AttrValPSC.wTable
and PSC.nRec                    ==  AttrValPSC.cRec
and AttrNamPSC.nRec             ==  AttrValPSC.cAttrNam

and word(1702)                  ==  AttrNamDPrik1.wTable
and 'С (дата начала по приказу)'==  AttrNamDPrik1.Name
and word(1702)                  ==  AttrValDPrik1.wTable
and CalPlan.nRec                ==  AttrValDPrik1.cRec
and AttrNamDPrik1.nRec          ==  AttrValDPrik1.cAttrNam

and word(1702)                  ==  AttrNamDPrik2.wTable
and 'По (дата окончания по приказу)'==  AttrNamDPrik2.Name
and word(1702)                  ==  AttrValDPrik2.wTable
and CalPlan.nRec                ==  AttrValDPrik2.cRec
and AttrNamDPrik2.nRec          ==  AttrValDPrik2.cAttrNam
!============================================================
and _Zakaz.cKatStroy            ==  _Nakl.cKatStroy
and _Zakaz.cKatStroy            ==  _Akt.cKatStroy

and _DogRep.nRec                ==  ExClassVal_Dog.cRec
and word(179)                   ==  ExClassVal_Dog.ClassCode
and ExClassVal_Dog.cClassSeg    ==  ExClassSeg_Dog.nRec

and word(1707)                  ==  AttrNamIstFin.wTable
and 'Источник финансирования'   ==  AttrNamIstFin.Name
and word(1707)                  ==  AttrValIstFin.wTable
and _DogRep.nRec                ==  AttrValIstFin.cRec
and AttrNamIstFin.nRec          ==  AttrValIstFin.cAttrNam


))
 condition by_Date  = 
     (
       (
           _dBeg    <= Dogovor.dBeg
       and _dEnd    >= Dogovor.dBeg
       ) or
       (
           _dBeg    <= Dogovor.dEnd
       and _dEnd    >= Dogovor.dEnd
       ) or
       (
           _dBeg    >= Dogovor.dBeg
       and _dBeg    <= Dogovor.dEnd
       ) or
       (
           _dEnd    >= Dogovor.dBeg
       and _dEnd    <= Dogovor.dEnd
       )
     )
 condition by_KP    = 
     _Item_KP       =  AttrVal_KP.vComp

 condition by_PrP   = 
     _Item_PrP      =  AttrVal_PrP.vComp
 condition by_Stat  = 
     word(1)        =  Dogovor.Status
;

Procedure _PopAllCondition;
{
  PopCondition(tcby_Date);
  PopCondition(tcby_KP);
  PopCondition(tcby_PrP);
  PopCondition(tcby_Stat);
}

Procedure _InsDog;
{
  if (GetFirst _Dog <> tsOk) and (comp(281474976710703) = Dogovor.cVidDog)
     {
       ClearBuffer(#_Dog);
       _Dog.nRec      := Dogovor.nRec;
       _Dog.NoDoc     := Dogovor.NoDoc;
       _Dog.NoDoc_Ext := Dogovor.NoDoc_Ext;
       _Dog.dDoc      := Dogovor.dDoc;
       _Dog.dBeg      := Dogovor.dBeg;
       _Dog.OrgName   := KatOrg.Name;
       _Dog.Summa     := Dogovor.Summa;
       _Dog.TiDk      := Dogovor.TiDk;
       _Dog.cVidDog   := Dogovor.cVidDog;
       _Dog.VidCen    := ExClassSeg.Name;
       insert current _Dog;
     }
}

Procedure _CreateTmp;
{
  if GetFirst _DogTmp = tsOk do
     {
       if GetFirst Sogl = tsOk
          {
            _curTiDk_DogSogl := Sogl.TiDk;
            _curnRec_DogSogl := Sogl.nRec;
!============================================================================================$${
            _DogTmp.cMainDog := Sogl.cDogovor;
            update current _DogTmp;
!============================================================================================$$}
          } else 
          {
            _curTiDk_DogSogl := _DogTmp.TiDk;
            _curnRec_DogSogl := _DogTmp.nRec;
!============================================================================================$${
            _DogTmp.cMainDog := _DogTmp.nRec;
            update current _DogTmp;
!============================================================================================$$}
          }
       if GetFirst SpDocs = tsOk do
          {
            if GetFirst KatMc= tsOk {};
            if GetFirst KatUsl= tsOk {};

            if (GetFirst SpecMTR = tsOk) and (GetFirst KatStroy = tsOk)
               {
               dd1 :=  date(0,0,0);
               dd2 :=  date(0,0,0);
               if GetFirst SpecMTRCalPlan = tsOk do
                  {
                    if GetFirst SpDocsCalPlan = tsOk do
                       {
                         if GetFirst CalPlan  = tsOk
                            {
                              if (GetFirst AttrNamDPrik1 = tsOk and GetFirst AttrValDPrik1 = tsOk) {};
                              dd1 := if (CalPlan.dFrom <> date(0,0,0),CalPlan.dFrom,AttrValDPrik1.vDate);
                              if (GetFirst AttrNamDPrik2 = tsOk and GetFirst AttrValDPrik2 = tsOk) {};
                              dd2 := if (CalPlan.dFrom <> date(0,0,0),CalPlan.dTo,AttrValDPrik2.vDate);
                            }
                       } while (GetNext SpDocsCalPlan = tsOk and NextVisual);
                  } while (GetNext SpecMTRCalPlan = tsOk and NextVisual);

               if (    _dBeg    <= dd1
                   and _dEnd    >= dd2
                  ) or
                  (
                       _dBeg    <= dd1
                   and _dEnd    >= dd2
                  ) or
                  (
                       _dBeg    >= dd1
                   and _dBeg    <= dd2
                  ) or
                  (
                       _dEnd    >= dd1
                   and _dEnd    <= dd2
                  )
               {  // if _dBeg _dEnd
                  if GetFirst _Zakaz where ((KatStroy.nRec == _Zakaz.cKatStroy))  <> tsOk
                     {
                       if GetFirst KatVidD = tsOk {};

                       ClearBuffer(#_Zakaz);
                       _Zakaz.McUSLName := _SpecName;
                       _Zakaz.dFactPost := dd2;
                       _Zakaz.cDogovor  := _DogTmp.nRec;
                       _Zakaz.cKatStroy := KatStroy.nRec;
                       _Zakaz.ZakNo     := KatStroy.Name;
                       _Zakaz.KatVidD   := KatVidD.Name;
                       _Zakaz.Kol       := SpDocs.Kol;
                       _Zakaz.Price     := SpDocs.Price;
                       _Zakaz.Sum       := SpDocs.Summa;
                       if (GetFirst PSC = tsOk and GetFirst AttrNamPSC = tsOk and GetFirst AttrValPSC = tsOk)
                       _Zakaz.PSC   := AttrValPSC.vString+' от '+DateToStr(PSC.dFinIsh, 'DD.MM.YYYY');
                       insert current _Zakaz; 
                     }
!====================  Аванс/Факт. плат. =======================================================================

!===============================================================================================================
               } // if _dBeg _dEnd
               } // if (GetFirst SpecMTR = tsOk) and (GetFirst KatStroy = tsOk)
          } while (GetNext SpDocs = tsOk and NextVisual);
     } while (GetNext _DogTmp = tsOk and NextVisual);

!============================================================================================$${
!==========  Второй проход по заказам (только для авансов) ==================================
  if GetFirst KatStroy_Zakaz = tsOk do
     {
       if GetFirst SpecMTR_Zakaz where ((     comp(281474976710657)  ==  SpecMTR_Zakaz.cSaldTune
                                          and word(1723)             ==  SpecMTR_Zakaz.CoTable
                                          and KatStroy_Zakaz.nRec    ==  SpecMTR_Zakaz.cObj     )) <> tsOk 
          {

            if GetFirst SpecMTR_Zakaz where ((     comp(281474976710657)  ==  SpecMTR_Zakaz.cSaldTune
                                               and word(1104)             ==  SpecMTR_Zakaz.CoTable
                                               and KatStroy_Zakaz.nRec    ==  SpecMTR_Zakaz.cObj     )) = tsOk 
               {
                 if GetFirst SpStep_Zakaz where ((SpecMTR_Zakaz.cSpec == SpStep_Zakaz.nRec)) = tsOk
                    {
                      if GetFirst StepDoc_Zakaz where ((SpStep_Zakaz.cStepDoc == StepDoc_Zakaz.nRec)) = tsOk
                         {
                           if GetFirst BaseDoc_Zakaz where ((StepDoc_Zakaz.cBaseDoc == BaseDoc_Zakaz.nRec)) = tsOk
                              {
                                if GetFirst _DogTmp where ((BaseDoc_Zakaz.cDogovor  == _DogTmp.cMainDog)) = tsOk
                                   {
                                      if GetFirst _Zakaz_Zakaz where ((KatStroy_Zakaz.nRec == _Zakaz_Zakaz.cKatStroy)) <> tsOk
                                         {
                                           ClearBuffer(#_Zakaz_Zakaz);
                                           _Zakaz_Zakaz.cDogovor  := _DogTmp.nRec;
                                           _Zakaz_Zakaz.cKatStroy := KatStroy_Zakaz.nRec;
                                           _Zakaz_Zakaz.ZakNo     := KatStroy_Zakaz.Name;
                                           insert current _Zakaz_Zakaz; 
                                         }
                                   }
                              }
                         }
                    }
               }
          }
     } while (GetNext KatStroy_Zakaz = tsOk and NextVisual);
!============================================================================================$$}

!===============================================================================================================
}
Procedure _CreateExcelRep;
{
  _myPath := TranslatePath('%startpath%'+'Xls\AGAT_GOZ_Pr2.xlt');
//  _myPath := TranslatePath('D:\vip\RepDogGOZ\AGAT_GOZ_Pr2.xlt');
                            
  if FileExist(_myPath)
     {
       var xlREs : boolean;
       xlRes := xlCreateExcelWithTemplate(_myPath,true) 
       if (not xlRes)  { Message('Не открылся Excel'); Exit; }
       xlRes := xlIsExcelValid;
       if (not xlRes)  { Message('Не инициализирован Excel'); Exit; }
       xlRes := xlGetActiveWorkBookName(_myPath);
       if (not xlRes)
          { Message('Не активна книга Excel '+_myPath); Exit; }
!==============================================================================
       xlSetCellStringValue('Справка о заключенных ОАО "Концерн "Моринсис-Агат" Контрактах (договорах) на поставку продукции с головными исполнителями (исполнителями) ГОЗ в '+Year(_dBeg)+'-'+Year(_dEnd)+' г.г.',1,1,1,1);
       if XLCREATEMATRIX(10000, 22) {};

       _Line := 1; _npp := 1;
       if GetFirst _DogRep = tsOk do
          {
            if GetFirst _Zakaz = tsOk
               {
                 xlStWriteToMatrix (_Line, 1, _npp);
                 xlStWriteToMatrix (_Line, 2, _DogRep.OrgName);      // Заказчик
                 xlStWriteToMatrix (_Line, 3, '-');
                 xlStWriteToMatrix (_Line, 4, _DogRep.NoDoc+' от '+DateToStr(_DogRep.dBeg, 'DD.MM.YYYY')); //Договор № и дата
                 if GetFirst ExClassSeg_Dog = tsOk xlStWriteToMatrix (_Line, 5, ExClassSeg_Dog.Name) else xlStWriteToMatrix (_Line, 5, ' '); 

                 xlStWriteToMatrix (_Line, 8, _DogRep.Summa/1000);  // Сумма
                 if (GetFirst AttrNamIstFin = tsOk) and (GetFirst AttrValIstFin = tsOk)
                    {
	              xlStWriteToMatrix (_Line, 9, AttrValIstFin.VSTRING);  // Сумма
                    }

                 _Line2 := _Line;
                 _Kol := 0;
                 if GetFirst _Zakaz = tsOk do
                    { 
                      xlStWriteToMatrix (_Line2, 6, _DogRep.NoDoc_Ext+' '+_Zakaz.KatVidD+' '+_Zakaz.McUSLName);  //Наименование продукции
                      _Kol := _Kol + _Zakaz.Kol;
                      xlStWriteToMatrix (_Line2, 7, _Zakaz.Kol);   // Кол-во 
!============================== Плат. ====================================
                      if GetFirst Oborot62_K2013 = tsOk do
                         {
                           _cSoprDoc := Oborot62_K2013.CSOPRDOC;
                           if (GetFirst BuhSchet_K2013 = tsOk) 
                              {
                                if inStr('Аванс',BuhSchet_K2013.Name) <> 0
                                   {
                                     ClearBuffer(#_Avans);
                                     _Avans.cKatStroy :=  _Zakaz.cKatStroy;
                                     _Avans.Sum       :=  Oborot62_K2013.SumOb;
                                     _Avans.dDoc      :=  Oborot62_K2013.DatOb;
                                     if GetFirst PlPor = tsOk _Avans.NoDoc := PlPor.NODOK;
                                     insert current _Avans;
                                   }
                                ClearBuffer(#_FOpl);
                                _FOpl.cKatStroy :=  _Zakaz.cKatStroy;
                                _FOpl.Sum       :=  Oborot62_K2013.SumOb;
                                _FOpl.dDoc      :=  Oborot62_K2013.DatOb;
                                if GetFirst PlPor = tsOk _FOpl.NoDoc := PlPor.NODOK;
                                insert current _FOpl;
                              }
                         } while (GetNext Oborot62_K2013 = tsOk and NextVisual);
                 
                      if GetFirst Oborot62_K2005 = tsOk do
                         {
                           _cSoprDoc := Oborot62_K2005.CSOPRDOC;
                           if (GetFirst BuhSchet_K2005 = tsOk) 
                              {
                                if inStr('Аванс',BuhSchet_K2005.Name) <> 0
                                   {
                                     ClearBuffer(#_Avans);
                                     _Avans.cKatStroy :=  _Zakaz.cKatStroy;
                                     _Avans.Sum       :=  Oborot62_K2005.SumOb;
                                     _Avans.dDoc      :=  Oborot62_K2005.DatOb;
                                     if GetFirst PlPor = tsOk _Avans.NoDoc := PlPor.NODOK;
                                     insert current _Avans;
                                   }
                                ClearBuffer(#_FOpl);
                                _FOpl.cKatStroy :=  _Zakaz.cKatStroy;
                                _FOpl.Sum       :=  Oborot62_K2005.SumOb;
                                _FOpl.dDoc      :=  Oborot62_K2005.DatOb;
                                if GetFirst PlPor = tsOk _FOpl.NoDoc := PlPor.NODOK;
                                insert current _FOpl;
                              }
                         } while (GetNext Oborot62_K2005 = tsOk and NextVisual);

!============================================================================================
!======================================== отгрузки ==========================================
!============================================================================================

                      if GetFirst Oborot62_D2013 = tsOk do
                         {

                           ClearBuffer(#_Akt);
                           _Akt.cKatStroy :=  _Zakaz.cKatStroy;
                           _Akt.Sum       :=  Oborot62_D2013.SumOb;
                           _Akt.dDoc      :=  Oborot62_D2013.DatOb;

                           _cSoprDoc := Oborot62_D2013.CSOPRDOC;
                           if GetFirst KatSopr_GF where ((_cSoprDoc == KatSopr_GF.nRec)) = tsOk do
                              { 
                                if GetFirst SpSopr_GF where ((KatSopr_GF.nRec == SpSopr_GF.cSopr)) = tsOk do
                                   {
                                     if GetFirst SpecMTR_GF where
                                        ((
                                               comp(281474976710657)  ==  SpecMTR_GF.cSaldTune
                                           and word(1110)             ==  SpecMTR_GF.CoTable
                                           and SpSopr_GF.nRec         ==  SpecMTR_GF.cSpec
                                           and _Zakaz.cKatStroy       ==  SpecMTR_GF.cObj
                                        )) = tsOk 
                                        {
                                          _Akt.Kol := _Akt.Kol + SpSopr_GF.KOLFACT;
                                        }
                                   } while (GetNext SpSopr_GF where ((KatSopr_GF.nRec == SpSopr_GF.cSopr)) = tsOk and NextVisual);
                              } while (GetNext KatSopr_GF where ((_cSoprDoc == KatSopr_GF.nRec)) = tsOk and NextVisual)
                           insert current _Akt;

                         } while (GetNext Oborot62_D2013 = tsOk and NextVisual);
                 
                      if GetFirst Oborot62_D2005 = tsOk do
                         {
                           ClearBuffer(#_Akt);
                           _Akt.cKatStroy :=  _Zakaz.cKatStroy;
                           _Akt.Sum       :=  Oborot62_D2005.SumOb;
                           _Akt.dDoc      :=  Oborot62_D2005.DatOb;

                           _cSoprDoc := Oborot62_D2005.CSOPRDOC;
                           if GetFirst KatSopr_GF where ((_cSoprDoc == KatSopr_GF.nRec)) = tsOk do
                              { 
                                if GetFirst SpSopr_GF where ((KatSopr_GF.nRec == SpSopr_GF.cSopr)) = tsOk do
                                   {
                                     if GetFirst SpecMTR_GF where
                                        ((
                                               comp(281474976710657)  ==  SpecMTR_GF.cSaldTune
                                           and word(1110)             ==  SpecMTR_GF.CoTable
                                           and SpSopr_GF.nRec         ==  SpecMTR_GF.cSpec
                                           and _Zakaz.cKatStroy       ==  SpecMTR_GF.cObj
                                        )) = tsOk 
                                        {
                                          _Akt.Kol := _Akt.Kol + SpSopr_GF.KOLFACT;
                                        }
                                   } while (GetNext SpSopr_GF where ((KatSopr_GF.nRec == SpSopr_GF.cSopr)) = tsOk and NextVisual);
                              } while (GetNext KatSopr_GF where ((_cSoprDoc == KatSopr_GF.nRec)) = tsOk and NextVisual)

                           insert current _Akt;
                         } while (GetNext Oborot62_D2005 = tsOk and NextVisual);
!=====================================================================================
                      _Line3 := _Line2;
                      _Line4 := _Line2;
                      _Line5 := _Line2;

                      if GetFirst _FOpl = tsOk do
                         {
                           xlStWriteToMatrix (_Line4, 10, _FOpl.NoDoc);
                           xlStWriteToMatrix (_Line4, 11, DateToStr(_FOpl.dDoc, 'DD.MM.YYYY'));
                           xlStWriteToMatrix (_Line4, 12, _FOpl.Sum/1000);
                           xlStWriteToMatrix (_Line4, 21, _Zakaz.cDogovor);
                           xlStWriteToMatrix (_Line4, 22, _Zakaz.ZakNo); // Заказ
                           inc(_Line4);
                         } while (GetNext _FOpl = tsOk and NextVisual);

                      if GetFirst _Avans = tsOk do
                         {
                           xlStWriteToMatrix (_Line3, 13, _Avans.NoDoc);
                           xlStWriteToMatrix (_Line3, 14, DateToStr(_Avans.dDoc, 'DD.MM.YYYY'));
                           xlStWriteToMatrix (_Line3, 15, _Avans.Sum/1000);
                           xlStWriteToMatrix (_Line3, 21, _Zakaz.cDogovor);
                           xlStWriteToMatrix (_Line3, 22, _Zakaz.ZakNo); // Заказ
                           inc(_Line3);
                         } while (GetNext _Avans = tsOk and NextVisual);

                      xlStWriteToMatrix (_Line2, 16, _Zakaz.dFactPost);

                      if GetFirst _Akt = tsOk do
                         {
                           xlAddStrToMatrix (_Line5, 17, DateToStr(_Akt.dDoc, 'DD.MM.YYYY'));
                           xlStWriteToMatrix (_Line5, 18, if (_Akt.Kol = 0,' ',_Akt.Kol));

                           xlStWriteToMatrix (_Line5, 21, _Zakaz.cDogovor);
                           xlStWriteToMatrix (_Line5, 22, _Zakaz.ZakNo); // Заказ
                           inc(_Line5);
                         } while (GetNext _Akt = tsOk and NextVisual);

                 
                      if _Line3 < _Line5 then _Line3 := _Line5;
                      if (_Line3 - _Line2 <> 0) or (_Line4 - _Line2 <> 0)
                         {
                           if _Line3 >= _Line4
                             {
                              _Line2:= _Line3-1;
                             } else
                             {
                              _Line2:= _Line4-1;
                             }
                         }
                      xlStWriteToMatrix (_Line2, 21, _Zakaz.cDogovor);
                      inc(_Line2);
                      _Line := _Line2-1;
                    } while (GetNext _Zakaz = tsOk and NextVisual);

                 inc(_Line); inc(_npp);
               } //if GetFirst _Zakaz = tsOk

          } while (GetNext _DogRep = tsOk and NextVisual);
!==============================================================================
       xlWriteMatrixToExcel(6,1);
       if xlFreeMatrix{};
       if xlRes:=xlRunMacro('Format') {};
       xlKillExcel;
!==============================================================================
     } else Message ('Шаблон "AGAT_GOZ_Pr2.xlt" не найден!')//if FileExist(myPath)
}

Panel pnl_RepDogGOZ;
 Table Dogovor
  Screen scr1 ('...',,sci13Esc);
  Show at (,,,);
  Fields
   _dBeg         : noProtect;
   _dEnd         : noProtect;
  Buttons
  cmValue1,,,'Сформировать';
  cmValue2,,,'Выход';
<< 

`период с`.@@@@@@@@@@  `по`.@@@@@@@@@@

     <. Сформировать .>    <. Выход .>
>>
 end;
end; // Panel
HandleEvent
cmInit:{
  _PopAllCondition;
  _Item_KP  := comp(281474976711951);                                                 //В настр
  _Item_PrP := comp(281474976711945);  // Принадлежность продукции - Спецтехника      //В настр
  var datechanged : boolean;
  if ((not readmydsk(_dBeg,'AGAT_GOZ_Pr2_dBeg',datechanged)) or (datechanged)) _dBeg := Cur_date;
  if ((not readmydsk(_dEnd,'AGAT_GOZ_Pr2_dEnd',datechanged)) or (datechanged)) _dEnd := Cur_date;
} // cmInit

cmDone:{
  SaveMyDsk(_dBeg,'AGAT_GOZ_Pr2_dBeg');
  SaveMyDsk(_dEnd,'AGAT_GOZ_Pr2_dEnd');
}//cmDone

cmValue1:{
  StartNewVisual(vtRotateVisual,vfTimer + vfBreak + vfConfirm,''#3'Обработка договоров...'#13#13#3,1);
  
  Delete all _DogDel;
  Delete all _ZakazDel;
  Delete all _AvansDel;
  Delete all _FOplDel;
  Delete all _AktDel;
  Delete all _NaklDel;

 
  PushCondition(tcby_KP);
  PushCondition(tcby_PrP);
!======================================================================
! Собираем договоры по датам  
!======================================================================
  PushCondition(tcby_Date);
  if GetFirst Dogovor = tsOk do
     { 
       _InsDog;
     } while(GetNext Dogovor = tsOk and NextVisual);
!======================================================================
! Собираем договоры по статусу "исполняемый"
!======================================================================
  PopCondition(tcby_Date);
  PushCondition(tcby_Stat);
  if GetFirst Dogovor = tsOk do
     { 
       _InsDog;
     } while(GetNext Dogovor = tsOk and NextVisual);

  _PopAllCondition


  _CreateTmp;
  _CreateExcelRep;

  StopVisual('',0);
  CloseInterface(cmDefault);
}
end; // HandleEvent
end. // Interface

VipInterface UserReport_AGAT_GOZ_Pr2 Implements IUserReport licensed(free);
Interface UserReport_AGAT_GOZ_Pr2;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::AGAT_GOZ_Pr2);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Справка (приложение 2)';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    GetGroupName := 'Отчеты ГОЗ';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'DOGOVOR';
    end;
  end;
end.

