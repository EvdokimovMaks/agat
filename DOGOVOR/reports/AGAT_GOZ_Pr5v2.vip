!===============================================================================================================
! Надо сделать настройки. Строки помечены  - В настр
!===============================================================================================================
#include AGAT_GOZ_TMP.tbl
#component "AGAT"
!===============================================================================================================
Interface AGAT_GOZ_Pr5v2 'Справка о заключенных Контрактах (договорах) с головными исполнителями(исполнителями) ГОЗ в' (,,)   EscClose, cyan;
Show (,,45,5);
!===============================================================================================================
create view
var 
 _dBeg
,_dEnd            
                  
,dd1,dd2         
                  : date;

 _Item_KP 
,_Item_PrP
,_curnRec_DogSogl
,_curVidDog       
,_cSoprDoc
                  : comp;
 _myPath
,_mySchet
,_myKorSchet
                  : string;

 _Line
,_Line2
,_Line3
,_Line4
,_Line5


,_curTiDk_DogSogl
,_npp             
                  : word;

as select *
from
 Dogovor
,ExClassVal 
,ExClassSeg

,AttrNam AttrNam_KP
,AttrVal AttrVal_KP
,AttrNam AttrNam_PrP
,AttrVal AttrVal_PrP
,KatOrg
,_Dog
,_Dog _DogTmp
,_Dog _DogDel
,Dogovor  Sogl
,SpDocs
,SpecMTR
,KatStroy
,_Dog _DogRep (_Dog_02)
,_Zakaz
,_Zakaz _ZakazDel
,SpGrSch
,KatVidD
,GroupSch
,GroupSch PSC

,Oborot Oborot90_02 (accelerate)
,_FaktStoim
,_FaktStoim _FaktStoimDel

,Oborot Oborot90_09 (accelerate)
,_FaktPrib
,_FaktPrib _FaktPribDel

,Oborot Oborot62_D2013 (accelerate)
,Oborot Oborot62_D2005 (accelerate)

,Oborot Oborot62_K2013 (accelerate)
,BuhSchet BuhSchet_K2013 (accelerate)

,Oborot Oborot62_K2005 (accelerate)
,BuhSchet BuhSchet_K2005 (accelerate)


,_Avans
,_Avans _AvansDel
,_FOpl
,_FOpl _FOplDel

,PLANSSCH

,SpKau
,KatStroy KatStroy2001
,_tmpUserKAU

,CalPlan
,SpDocs SpDocsCalPlan
,SpecMTR SpecMTRCalPlan

,AttrNam AttrNamPSC
,AttrVal AttrValPSC

,AttrNam AttrNamDPrik1
,AttrVal AttrValDPrik1
,AttrNam AttrNamDPrik2
,AttrVal AttrValDPrik2

,_Akt
,_Akt _AktDel

,Oborot KR_Oborot90_09 (accelerate)
,PlPor
!============================================================================================$${
,KatStroy KatStroy_Zakaz
,SpecMTR SpecMTR_Zakaz
,SpStep  SpStep_Zakaz
,StepDoc StepDoc_Zakaz
,BaseDoc BaseDoc_Zakaz
,_Zakaz _Zakaz_Zakaz
!============================================================================================$$}

where ((
    comp(0)                     ==  Dogovor.cDogovor

and Dogovor.nRec                ==  ExClassVal.cRec
and word(123)                   ==  ExClassVal.ClassCode
and ExClassVal.cClassSeg        ==  ExClassSeg.nRec
!============================================================
and word(1707)                  ==  AttrNam_KP.wTable
and 'Конечный потребитель'      ==  AttrNam_KP.Name
and AttrNam_KP.wTable           ==  AttrVal_KP.wTable
and Dogovor.nRec                ==  AttrVal_KP.cRec
and AttrNam_KP.nRec             ==  AttrVal_KP.cAttrNam
!============================================================
and word(1707)                  ==  AttrNam_PrP.wTable
and 'Принадлежность продукции'  ==  AttrNam_PrP.Name
and AttrNam_PrP.wTable          ==  AttrVal_PrP.wTable
and Dogovor.nRec                ==  AttrVal_PrP.cRec
and AttrNam_PrP.nRec            ==  AttrVal_PrP.cAttrNam
!============================================================
and Dogovor.cOrg                ==  KatOrg.nRec
and Dogovor.nRec                ==  _Dog.nRec

and _DogTmp.nRec                ==  Sogl.cDogovor
and word(1)                     ==  Sogl.Status

and _DogTmp.cVidDog             ==  KatVidD.nRec
and _curTiDk_DogSogl            ==  SpDocs.TiDk
and _curnRec_DogSogl            ==  SpDocs.cDoc

and word(1723)                  ==  SpecMTR.CoTable
and SpDocs.nRec                 ==  SpecMTR.cSpec
and SpecMTR.cObj                ==  KatStroy.nRec

and SpDocs.cMoveCell            ==  PSC.nRec
!============================================================
and _DogRep.nRec                ==  _Zakaz.cDogovor
and word(1)                     ==  SpGrSch.wList
and _DogRep.nRec                ==  SpGrSch.cBaseDoc
and SpGrSch.cGroupSch           ==  GroupSch.nRec

and _mySchet                    ==  Oborot90_02.SCHETO
and '02'                        ==  Oborot90_02.SUBOSSCH
and _dBeg                       <<= Oborot90_02.DatOb
and _dEnd                       >>= Oborot90_02.DatOb
!============================================================
and _Zakaz.cKatStroy            ==  _FaktStoim.cKatStroy
!============================================================
and _mySchet                    ==  Oborot90_09.SCHETO
and '09'                        ==  Oborot90_09.SUBOSSCH
and _dBeg                       <<= Oborot90_09.DatOb
and _dEnd                       >>= Oborot90_09.DatOb
!============================================================
and _mySchet                    ==  KR_Oborot90_09.SCHETK
and '09'                        ==  KR_Oborot90_09.SUBSCHK
and _dBeg                       <<= KR_Oborot90_09.DatOb
and _dEnd                       >>= KR_Oborot90_09.DatOb
!============================================================
and _Zakaz.cKatStroy            ==  _FaktPrib.cKatStroy
!============================================================
and '862'                       ==  Oborot62_K2013.SCHETK
and (
     '8100'                     <>  Oborot62_K2013.SCHETO and
     '862'                      <>  Oborot62_K2013.SCHETO
    )
and _Zakaz.cKatStroy            ==  Oborot62_K2013.KAUKS[3]
and Oborot62_K2013.SCHETK       ==  BuhSchet_K2013.SCHET
and Oborot62_K2013.SUBSCHK      ==  BuhSchet_K2013.SUBSCH

and '762'                       ==  Oborot62_K2005.SCHETK
and (
     '7100'                     <>  Oborot62_K2005.SCHETO and
     '762'                      <>  Oborot62_K2005.SCHETO
    )
and _Zakaz.cKatStroy            ==  Oborot62_K2005.KAUKS[2]
and Oborot62_K2005.SCHETK       ==  BuhSchet_K2005.SCHET
and Oborot62_K2005.SUBSCHK      ==  BuhSchet_K2005.SUBSCH

!============================================================
and '862'                       ==  Oborot62_D2013.SCHETO
and (
     '890'                       =  Oborot62_D2013.SCHETK
    )
and _Zakaz.cKatStroy            ==  Oborot62_D2013.KAUOS[3]

and '762'                       ==  Oborot62_D2005.SCHETO
and (
     '790'                       =  Oborot62_D2005.SCHETK
    )
and _Zakaz.cKatStroy            ==  Oborot62_D2005.KAUOS[2]
!============================================================
and _cSoprDoc                   ==  PlPor.nRec
and _Zakaz.cKatStroy            ==  _Avans.cKatStroy
and _Zakaz.cKatStroy            ==  _FOpl.cKatStroy
!============================================================
and word(1723)                  ==  SpecMTRCalPlan.CoTable
and (
      SpecMTR.cObj               =  SpecMTRCalPlan.cObj
    )
and SpecMTRCalPlan.cSpec        ==  SpDocsCalPlan.nRec
and (
      word(403)                  =  SpDocsCalPlan.TiDk
    )
and SpDocsCalPlan.cDoc          ==  CalPlan.nRec
and (
      SpDocs.cDoc                =  CalPlan.cDogovor //and
//      4                         <>  CalPlan.Status
    )
and word(1125)                  ==  AttrNamPSC.wTable
and 'Номер протокола согл. цены'==  AttrNamPSC.Name
and word(1125)                  ==  AttrValPSC.wTable
and PSC.nRec                    ==  AttrValPSC.cRec
and AttrNamPSC.nRec             ==  AttrValPSC.cAttrNam

and word(1702)                  ==  AttrNamDPrik1.wTable
and 'С (дата начала по приказу)'==  AttrNamDPrik1.Name
and word(1702)                  ==  AttrValDPrik1.wTable
and CalPlan.nRec                ==  AttrValDPrik1.cRec
and AttrNamDPrik1.nRec          ==  AttrValDPrik1.cAttrNam

and word(1702)                  ==  AttrNamDPrik2.wTable
and 'По (дата окончания по приказу)'==  AttrNamDPrik2.Name
and word(1702)                  ==  AttrValDPrik2.wTable
and CalPlan.nRec                ==  AttrValDPrik2.cRec
and AttrNamDPrik2.nRec          ==  AttrValDPrik2.cAttrNam

!============================================================
!============================================================
and _Zakaz.cKatStroy            ==  _Nakl.cKatStroy
and _Zakaz.cKatStroy            ==  _Akt.cKatStroy


))
 condition by_Date  = 
     (
       (
           _dBeg    <= Dogovor.dBeg
       and _dEnd    >= Dogovor.dBeg
       ) or
       (
           _dBeg    <= Dogovor.dEnd
       and _dEnd    >= Dogovor.dEnd
       ) or
       (
           _dBeg    >= Dogovor.dBeg
       and _dBeg    <= Dogovor.dEnd
       ) or
       (
           _dEnd    >= Dogovor.dBeg
       and _dEnd    <= Dogovor.dEnd
       )
     )
 condition by_KP    = 
     _Item_KP       =  AttrVal_KP.vComp

 condition by_PrP   = 
     _Item_PrP      =  AttrVal_PrP.vComp
 condition by_Stat  = 
     word(1)        =  Dogovor.Status
;

Procedure _PopAllCondition;
{
  PopCondition(tcby_Date);
  PopCondition(tcby_KP);
  PopCondition(tcby_PrP);
  PopCondition(tcby_Stat);
}

Procedure _InsDog;
{
  if GetFirst _Dog <> tsOk
     {
       ClearBuffer(#_Dog);
       _Dog.nRec      := Dogovor.nRec;
       _Dog.NoDoc     := Dogovor.NoDoc;
       _Dog.NoDoc_Ext := Dogovor.NoDoc_Ext;
       _Dog.dDoc      := Dogovor.dDoc;
       _Dog.dBeg      := Dogovor.dBeg;
       _Dog.OrgName   := KatOrg.Name;
       _Dog.Summa     := Dogovor.Summa;
       _Dog.TiDk      := Dogovor.TiDk;
       _Dog.cVidDog   := Dogovor.cVidDog;
       _Dog.VidCen    := ExClassSeg.Name;
       insert current _Dog;
     }
}

Procedure _CreateTmp;
{
  if GetFirst _DogTmp = tsOk do
     {
       if GetFirst Sogl = tsOk
          {
            _curTiDk_DogSogl := Sogl.TiDk;
            _curnRec_DogSogl := Sogl.nRec;
!============================================================================================$${
            _DogTmp.cMainDog := Sogl.cDogovor;
            update current _DogTmp;
!============================================================================================$$}
          } else 
          {
            _curTiDk_DogSogl := _DogTmp.TiDk;
            _curnRec_DogSogl := _DogTmp.nRec;
!============================================================================================$${
            _DogTmp.cMainDog := _DogTmp.nRec;
            update current _DogTmp;
!============================================================================================$$}
          }
       if GetFirst SpDocs = tsOk do
          {
            if (GetFirst SpecMTR = tsOk) and (GetFirst KatStroy = tsOk)
               {
               dd1 :=  date(0,0,0);
               dd2 :=  date(0,0,0);
               if GetFirst SpecMTRCalPlan = tsOk do
                  {
                    if GetFirst SpDocsCalPlan = tsOk do
                       {
                         if GetFirst CalPlan  = tsOk
                            {
                              if (GetFirst AttrNamDPrik1 = tsOk and GetFirst AttrValDPrik1 = tsOk) {};
                              dd1 := if (CalPlan.dFrom <> date(0,0,0),CalPlan.dFrom,AttrValDPrik1.vDate);
                              if (GetFirst AttrNamDPrik2 = tsOk and GetFirst AttrValDPrik2 = tsOk) {};
                              dd2 := if (CalPlan.dFrom <> date(0,0,0),CalPlan.dTo,AttrValDPrik2.vDate);
                            }
                       } while (GetNext SpDocsCalPlan = tsOk and NextVisual);
                  } while (GetNext SpecMTRCalPlan = tsOk and NextVisual);

               if (    _dBeg    <= dd1
                   and _dEnd    >= dd2
                  ) or
                  (
                       _dBeg    <= dd1
                   and _dEnd    >= dd2
                  ) or
                  (
                       _dBeg    >= dd1
                   and _dBeg    <= dd2
                  ) or
                  (
                       _dEnd    >= dd1
                   and _dEnd    <= dd2
                  )
               {  // if _dBeg _dEnd
                  if GetFirst _Zakaz where ((KatStroy.nRec == _Zakaz.cKatStroy))  <> tsOk
                     {
                       ClearBuffer(#_Zakaz);
                       _Zakaz.cDogovor  := _DogTmp.nRec;
                       _Zakaz.cKatStroy := KatStroy.nRec;
                       _Zakaz.ZakNo     := KatStroy.Name;
                       if GetFirst KatVidD = tsOk {};
                       _Zakaz.KatVidD   := KatVidD.Name;

                       if InStr('Поставка',(_DogRep.NoDoc_Ext+' '+_Zakaz.KatVidD)) = 0
                          {
                            _Zakaz.Kol := 1;
                          }
                          else 
                          {
//                            if SpDocs.cOtpEd = comp(281480467411676) //В настр
//                              {
                                _Zakaz.Kol := SpDocs.Kol;
//                              } else double(1);
                          }
                       _Zakaz.Price     := SpDocs.Price;
                       _Zakaz.Sum       := SpDocs.Summa;
                       if (GetFirst PSC = tsOk and GetFirst AttrNamPSC = tsOk and GetFirst AttrValPSC = tsOk)
                       _Zakaz.PSC   := AttrValPSC.vString+' от '+DateToStr(PSC.dFinIsh, 'DD.MM.YYYY');
                       insert current _Zakaz; 
                     }
!===============================================================================================================
               } // if _dBeg _dEnd
               } // if (GetFirst SpecMTR = tsOk) and (GetFirst KatStroy = tsOk)
          } while (GetNext SpDocs = tsOk and NextVisual);
     } while (GetNext _DogTmp = tsOk and NextVisual);

!============================================================================================$${
!==========  Второй проход по заказам (только для авансов) ==================================
  if GetFirst KatStroy_Zakaz = tsOk do
     {
       if GetFirst SpecMTR_Zakaz where ((     comp(281474976710657)  ==  SpecMTR_Zakaz.cSaldTune
                                          and word(1723)             ==  SpecMTR_Zakaz.CoTable
                                          and KatStroy_Zakaz.nRec    ==  SpecMTR_Zakaz.cObj     )) <> tsOk 
          {

            if GetFirst SpecMTR_Zakaz where ((     comp(281474976710657)  ==  SpecMTR_Zakaz.cSaldTune
                                               and word(1104)             ==  SpecMTR_Zakaz.CoTable
                                               and KatStroy_Zakaz.nRec    ==  SpecMTR_Zakaz.cObj     )) = tsOk 
               {
                 if GetFirst SpStep_Zakaz where ((SpecMTR_Zakaz.cSpec == SpStep_Zakaz.nRec)) = tsOk
                    {
                      if GetFirst StepDoc_Zakaz where ((SpStep_Zakaz.cStepDoc == StepDoc_Zakaz.nRec)) = tsOk
                         {
                           if GetFirst BaseDoc_Zakaz where ((StepDoc_Zakaz.cBaseDoc == BaseDoc_Zakaz.nRec)) = tsOk
                              {
                                if GetFirst _DogTmp where ((BaseDoc_Zakaz.cDogovor  == _DogTmp.cMainDog)) = tsOk
                                   {
                                      if GetFirst _Zakaz_Zakaz where ((KatStroy_Zakaz.nRec == _Zakaz_Zakaz.cKatStroy)) <> tsOk
                                         {
                                           ClearBuffer(#_Zakaz_Zakaz);
                                           _Zakaz_Zakaz.cDogovor  := _DogTmp.nRec;
                                           _Zakaz_Zakaz.cKatStroy := KatStroy_Zakaz.nRec;
                                           _Zakaz_Zakaz.ZakNo     := KatStroy_Zakaz.Name;
                                           insert current _Zakaz_Zakaz; 
                                         }
                                   }
                              }
                         }
                    }
               }
          }
     } while (GetNext KatStroy_Zakaz = tsOk and NextVisual);
!============================================================================================$$}
!====================  Себестоимость ===========================================================================
//  if GetFirst PLANSSCH = tsOk do
//     {
       _mySchet     := '890';
       _myKorSchet  := '820';
//       if PLANSSCH.KOD = 8
          {
            if GetFirst Oborot90_02 = tsOk do
               {
                 if (_myKorSchet = Oborot90_02.SCHETK) or ('843' = Oborot90_02.SCHETK)
                    {
                      if GetFirst _FaktStoim where ((Oborot90_02.KAUKS[1] == _FaktStoim.cKatStroy)) <> tsOk 
                         {
                           ClearBuffer(#_FaktStoim);
                           _FaktStoim.cKatStroy := Oborot90_02.KAUKS[1];
                           _FaktStoim.Sum       := Oborot90_02.SUMOB;
                           insert current _FaktStoim;
                         } else {
                                  _FaktStoim.Sum  := _FaktStoim.Sum + Oborot90_02.SUMOB;
                                  update current _FaktStoim;
                                }
                    }
               } while (GetNext Oborot90_02 = tsOk and NextVisual);
!====================  Фактическая прибыль =====================================================================
            if GetFirst Oborot90_09 = tsOk do
               {
                 if GetFirst _FaktPrib where ((Oborot90_09.KAUOS[2] == _FaktPrib.cKatStroy)) <> tsOk 
                    {
                      ClearBuffer(#_FaktPrib);
                      _FaktPrib.cKatStroy := Oborot90_09.KAUOS[2];
                      _FaktPrib.SumDt     := Oborot90_09.SUMOB;
                      insert current _FaktPrib;
                    } else {
                             _FaktPrib.SumDt := _FaktPrib.SumDt + Oborot90_09.SUMOB;
                             update current _FaktPrib;
                           }
                } while (GetNext Oborot90_09 = tsOk and NextVisual);

            if GetFirst KR_Oborot90_09 = tsOk do
               {
                 if GetFirst _FaktPrib where ((KR_Oborot90_09.KAUKS[2] == _FaktPrib.cKatStroy)) <> tsOk 
                    {
                      ClearBuffer(#_FaktPrib);
                      _FaktPrib.cKatStroy := KR_Oborot90_09.KAUKS[2];
                      _FaktPrib.SumKt     := KR_Oborot90_09.SUMOB;
                      insert current _FaktPrib;
                    } else {
                             _FaktPrib.SumKt := _FaktPrib.SumKt + KR_Oborot90_09.SUMOB;
                             update current _FaktPrib;
                           }
                } while (GetNext KR_Oborot90_09 = tsOk and NextVisual);

          }

       _mySchet     := '790';
       _myKorSchet  := '720';
//       if PLANSSCH.KOD = 8
          {
            if GetFirst Oborot90_02 = tsOk do
               {
                 if (_myKorSchet = Oborot90_02.SCHETK) or ('743' = Oborot90_02.SCHETK)
                    {
                      if GetFirst _FaktStoim where ((Oborot90_02.KAUKS[1] == _FaktStoim.cKatStroy)) <> tsOk 
                         {
                           ClearBuffer(#_FaktStoim);
                           _FaktStoim.cKatStroy := Oborot90_02.KAUKS[1];
                           _FaktStoim.Sum       := Oborot90_02.SUMOB;
                           insert current _FaktStoim;
                         } else {
                                  _FaktStoim.Sum  := _FaktStoim.Sum + Oborot90_02.SUMOB;
                                  update current _FaktStoim;
                                }
                    }
               } while (GetNext Oborot90_02 = tsOk and NextVisual); 
!====================  Фактическая прибыль =====================================================================
            if GetFirst Oborot90_09 = tsOk do
               {
                 if GetFirst _FaktPrib where ((Oborot90_09.KAUOS[2] == _FaktPrib.cKatStroy)) <> tsOk 
                    {
                      ClearBuffer(#_FaktPrib);
                      _FaktPrib.cKatStroy := Oborot90_09.KAUOS[2];
                      _FaktPrib.SumDt     := Oborot90_09.SUMOB;
                      insert current _FaktPrib;
                    } else {
                             _FaktPrib.SumDt := _FaktPrib.SumDt + Oborot90_09.SUMOB;
                             update current _FaktPrib;
                           }
                } while (GetNext Oborot90_09 = tsOk and NextVisual);

            if GetFirst KR_Oborot90_09 = tsOk do
               {
                 if GetFirst _FaktPrib where ((KR_Oborot90_09.KAUKS[2] == _FaktPrib.cKatStroy)) <> tsOk 
                    {
                      ClearBuffer(#_FaktPrib);
                      _FaktPrib.cKatStroy := KR_Oborot90_09.KAUKS[2];
                      _FaktPrib.SumKt     := KR_Oborot90_09.SUMOB;
                      insert current _FaktPrib;
                    } else {
                             _FaktPrib.SumKt := _FaktPrib.SumKt + KR_Oborot90_09.SUMOB;
                             update current _FaktPrib;
                           }
                } while (GetNext KR_Oborot90_09 = tsOk and NextVisual);
          }

       _mySchet     := '490';
       _myKorSchet  := '420';

//       if PLANSSCH.KOD = 8
          {
            if GetFirst Oborot90_02 = tsOk do
               {
                 if Oborot90_02.TBLKS[1] = 110 and _myKorSchet = Oborot90_02.SCHETK
                    {
                      if GetFirst SpKau where ((Oborot90_02.KAUKS[1] == SpKau.nRec)) = tsOk
                         {
                           if GetFirst _tmpUserKAU where ((if (inStr(' ',SpKau.Name) <> 0,SubStr(SpKau.Name,1,(inStr(' ',SpKau.Name)-1)),SpKau.Name) == _tmpUserKAU.Name)) = tsOk
                              {
                                if GetFirst _FaktStoim where ((_tmpUserKAU.cKatStroy == _FaktStoim.cKatStroy)) <> tsOk
                                   {
                                     ClearBuffer(#_FaktStoim);
                                     _FaktStoim.cKatStroy := _tmpUserKAU.cKatStroy;
                                     _FaktStoim.Sum       := Oborot90_02.SUMOB;
                                     insert current _FaktStoim;
                                   } else {
                                            _FaktStoim.Sum  := _FaktStoim.Sum + Oborot90_02.SUMOB;
                                            update current _FaktStoim;
 
                                          }
                              }
                           
                         }
                    }
               } while (GetNext Oborot90_02 = tsOk and NextVisual);
          }
//     } while GetNext PLANSSCH = tsOk;
!===============================================================================================================
}
Procedure _CreateExcelRep;
{
  _myPath := TranslatePath('%startpath%'+'Xls\AGAT_GOZ_Pr5v2.xlt');
  if FileExist(_myPath)
     {
       var xlREs : boolean;
       xlRes := xlCreateExcelWithTemplate(_myPath,true); 
       if (not xlRes)  { Message('Не открылся Excel'); Exit; }
       xlRes := xlIsExcelValid;
       if (not xlRes)  { Message('Не инициализирован Excel'); Exit; }
       xlRes := xlGetActiveWorkBookName(_myPath);
       if (not xlRes)
          { Message('Не активна книга Excel '+_myPath); Exit; }
!==============================================================================
       xlSetCellStringValue('Справка о заключенных ОАО "Концерн "Моринсис-Агат" Контрактах (договорах) с головными исполнителями(исполнителями) ГОЗ в '+Year(_dBeg)+'-'+Year(_dEnd)+' г.г.',1,1,1,1);
       if XLCREATEMATRIX(10000, 22) {};

       _Line := 1; _npp := 1;
       if GetFirst _DogRep = tsOk do
          {
            if GetFirst _Zakaz = tsOk{
            xlStWriteToMatrix (_Line, 1, _npp);

            if GetFirst SpGrSch = tsOk do
               {
                 if (GetFirst GroupSch = tsOk) and (GroupSch.cNode = comp(281474976710658)) xlStWriteToMatrix (_Line, 3, GroupSch.Name); //В настр
               } while (GetNext SpGrSch = tsOk and NextVisual);
            
            xlStWriteToMatrix (_Line, 4, _DogRep.OrgName);      // Заказчик
            xlStWriteToMatrix (_Line, 5, _DogRep.NoDoc+' от '+DateToStr(_DogRep.dBeg, 'DD.MM.YYYY')); //Договор № и дата
            xlStWriteToMatrix (_Line, 6, _DogRep.NoDoc_Ext+' '+_Zakaz.KatVidD);  //Наименование продукции
            xlStWriteToMatrix (_Line, 9, _DogRep.VidCen);       // вид цены
            xlStWriteToMatrix (_Line, 11, _DogRep.Summa/1000);  // Сумма

            _Line2 := _Line;
            if GetFirst _Zakaz = tsOk do
               {
                 xlStWriteToMatrix (_Line2, 2, _Zakaz.ZakNo); // Заказ
                 xlStWriteToMatrix (_Line2, 7, _Zakaz.Kol);   // Кол-во
                 xlStWriteToMatrix (_Line2, 8, _Zakaz.Price/1000); // цена
                 xlStWriteToMatrix (_Line2, 10, _Zakaz.PSC);  // Номер протокола
!===============================================================================================
                 if GetFirst Oborot62_K2013 = tsOk do
                    {
                      _cSoprDoc := Oborot62_K2013.CSOPRDOC;
                      if GetFirst PlPor = tsOk {};
                      if (GetFirst BuhSchet_K2013 = tsOk) 
                         {
                           if inStr('Аванс',BuhSchet_K2013.Name) <> 0
                              {
                                ClearBuffer(#_Avans);
                                _Avans.cKatStroy :=  _Zakaz.cKatStroy;
                                _Avans.Sum       :=  Oborot62_K2013.SumOb;
                                _Avans.dDoc      :=  Oborot62_K2013.DatOb;
                                _Avans.NoDoc     :=  PlPor.NODOK;
                                insert current _Avans;
                              }
                           ClearBuffer(#_FOpl);
                           _FOpl.cKatStroy :=  _Zakaz.cKatStroy;
                           _FOpl.Sum       :=  Oborot62_K2013.SumOb;
                           _FOpl.dDoc      :=  Oborot62_K2013.DatOb;
                           _FOpl.NoDoc     :=  PlPor.NODOK;
                           insert current _FOpl;
                         }
                    } while (GetNext Oborot62_K2013 = tsOk and NextVisual);

                 if GetFirst Oborot62_K2005 = tsOk do
                    {
                      _cSoprDoc := Oborot62_K2005.CSOPRDOC;
                      if GetFirst PlPor = tsOk {};

                      if (GetFirst BuhSchet_K2005 = tsOk) 
                         {
                           if inStr('Аванс',BuhSchet_K2005.Name) <> 0
                              {
                                ClearBuffer(#_Avans);
                                _Avans.cKatStroy :=  _Zakaz.cKatStroy;
                                _Avans.Sum       :=  Oborot62_K2005.SumOb;
                                _Avans.dDoc      :=  Oborot62_K2005.DatOb;
                                _Avans.NoDoc     :=  PlPor.NODOK;
                                insert current _Avans;
                              }
                           ClearBuffer(#_FOpl);
                           _FOpl.cKatStroy :=  _Zakaz.cKatStroy;
                           _FOpl.Sum       :=  Oborot62_K2005.SumOb;
                           _FOpl.dDoc      :=  Oborot62_K2005.DatOb;
                           _FOpl.NoDoc     :=  PlPor.NODOK;
                           insert current _FOpl;
                         }
                    } while (GetNext Oborot62_K2005 = tsOk and NextVisual);


                 if GetFirst Oborot62_D2013 = tsOk do
                    {
                      ClearBuffer(#_Akt);
                      _Akt.cKatStroy :=  _Zakaz.cKatStroy;
                      _Akt.Sum       :=  Oborot62_D2013.SumOb;
                      _Akt.dDoc      :=  Oborot62_D2013.DatOb;
                      insert current _Akt;
                    } while (GetNext Oborot62_D2013 = tsOk and NextVisual);

                 if GetFirst Oborot62_D2005 = tsOk do
                    {
                      ClearBuffer(#_Akt);
                      _Akt.cKatStroy :=  _Zakaz.cKatStroy;
                      _Akt.Sum       :=  Oborot62_D2005.SumOb;
                      _Akt.dDoc      :=  Oborot62_D2005.DatOb;
                      insert current _Akt;
                    } while (GetNext Oborot62_D2005 = tsOk and NextVisual);

!===============================================================================================

                 if GetFirst _FaktStoim = tsOk xlStWriteToMatrix (_Line2, 12, _FaktStoim.Sum/1000) else xlStWriteToMatrix (_Line2, 12, '-');
                 if GetFirst _FaktPrib = tsOk xlStWriteToMatrix (_Line2, 13, (_FaktPrib.SumDt/1000-_FaktPrib.SumKt/1000) ) else xlStWriteToMatrix (_Line2, 13, '-');

                 _Line3 := _Line2;
                 _Line4 := _Line2;
                 _Line5 := _Line2;
                 if GetFirst _Avans = tsOk do
                    {
                      xlStWriteToMatrix (_Line3, 14, _Avans.NoDoc);
                      xlStWriteToMatrix (_Line3, 15, DateToStr(_Avans.dDoc, 'DD.MM.YYYY'));
                      xlStWriteToMatrix (_Line3, 16, _Avans.Sum/1000);
                      xlStWriteToMatrix (_Line3, 21, _Zakaz.cDogovor);
                      xlStWriteToMatrix (_Line3, 22, _Zakaz.ZakNo); // Заказ
                      inc(_Line3);
                    } while (GetNext _Avans = tsOk and NextVisual);


                 if GetFirst _FOpl = tsOk do
                    {
                      xlStWriteToMatrix (_Line4, 17, _FOpl.NoDoc);
                      xlStWriteToMatrix (_Line4, 18, DateToStr(_FOpl.dDoc, 'DD.MM.YYYY'));
                      xlStWriteToMatrix (_Line4, 19, _FOpl.Sum/1000);
                      xlStWriteToMatrix (_Line4, 21, _Zakaz.cDogovor);
                      xlStWriteToMatrix (_Line4, 22, _Zakaz.ZakNo); // Заказ
                      inc(_Line4);

                    } while (GetNext _FOpl = tsOk and NextVisual);


                 if GetFirst _Akt = tsOk do
                    {
                      xlAddStrToMatrix (_Line5, 20, DateToStr(_Akt.dDoc, 'DD.MM.YYYY') + chr(10));
                      xlStWriteToMatrix (_Line5, 21, _Zakaz.cDogovor);
                      xlStWriteToMatrix (_Line5, 22, _Zakaz.ZakNo); // Заказ
                      inc(_Line5);
                    } while (GetNext _Akt = tsOk and NextVisual);

                 if _Line3 < _Line5 then _Line3 := _Line5;

                 if (_Line3 - _Line2 <> 0) or (_Line4 - _Line2 <> 0)
                    {
                      if _Line3 >= _Line4
                        {
                         _Line2:= _Line3-1;
                        } else
                        {
                         _Line2:= _Line4-1;
                        }
                    }

                 xlStWriteToMatrix (_Line2, 21, _Zakaz.cDogovor);
                 inc(_Line2);
                 _Line := _Line2-1;
               } while (GetNext _Zakaz = tsOk and NextVisual);
            inc(_Line); inc(_npp);
            } //if GetFirst _Zakaz = tsOk
          } while (GetNext _DogRep = tsOk and NextVisual);
!==============================================================================
       xlWriteMatrixToExcel(5,1);
       if xlFreeMatrix{};
       if xlRes:=xlRunMacro('Format') {};
       xlKillExcel;
!==============================================================================
     } else Message ('Шаблон "AGAT_GOZ_Pr5v2.xlt" не найден!')//if FileExist(myPath)
}

Panel pnl_RepDogGOZ;
 Table Dogovor;
  Screen scr1 ('...',,sci13Esc);
  Show at (,,,);
  Fields
   _dBeg         : noProtect;
   _dEnd         : noProtect;
  Buttons
  cmValue1,,,'Сформировать';
  cmValue2,,,'Выход';
<< 

`период с`.@@@@@@@@@@  `по`.@@@@@@@@@@

     <. Сформировать .>    <. Выход .>
>>
 end;
end; // Panel
HandleEvent
cmInit:{
  _PopAllCondition;
  _Item_KP  := comp(281474976711951);  // Конечный потребитель - Кооперац.               //В настр
  _Item_PrP := comp(281474976711945);  // Принадлежность продукции - Спецтехника      //В настр
  var datechanged : boolean = false;
  if ((not readmydsk(_dBeg,'AGAT_GOZ_Pr5v2_dBeg',datechanged)) or (datechanged)) _dBeg := Cur_date;
  if ((not readmydsk(_dEnd,'AGAT_GOZ_Pr5v2_dEnd',datechanged)) or (datechanged)) _dEnd := Cur_date;
} // cmInit

cmDone:{
  SaveMyDsk(_dBeg,'AGAT_GOZ_Pr5v2_dBeg');
  SaveMyDsk(_dEnd,'AGAT_GOZ_Pr5v2_dEnd');
}//cmDone

cmValue1:{
  StartNewVisual(vtRotateVisual,vfTimer + vfBreak + vfConfirm,''#3'Обработка договоров...'#13#13#3,1);
  
  Delete all _DogDel;
  Delete all _ZakazDel;
  Delete all _FaktStoimDel;
  Delete all _FaktPribDel;
  Delete all _AvansDel;
  Delete all _FOplDel;
  Delete all _AktDel;
 
  PushCondition(tcby_KP);
  PushCondition(tcby_PrP);
!======================================================================
! Собираем договоры по датам  
!======================================================================
  PushCondition(tcby_Date);
  if GetFirst Dogovor = tsOk do
     { 
       _InsDog;
     } while(GetNext Dogovor = tsOk and NextVisual);
!======================================================================
! Собираем договоры по статусу "исполняемый"
!======================================================================
  PopCondition(tcby_Date);
  PushCondition(tcby_Stat);
  if GetFirst Dogovor = tsOk do
     { 
       _InsDog;
     } while(GetNext Dogovor = tsOk and NextVisual);

  _PopAllCondition

  Delete all _tmpUserKAU;
  if GetFirst KatStroy2001 = tsOk do
     {
       ClearBuffer(#_tmpUserKAU);
       _tmpUserKAU.Name      := if (inStr(' ',KatStroy2001.Name) <> 0,SubStr(KatStroy2001.Name,1,(inStr(' ',KatStroy2001.Name)-1)),KatStroy2001.Name);
       _tmpUserKAU.cKatStroy := KatStroy2001.nRec;
       insert current _tmpUserKAU;
     } while (GetNext KatStroy2001 = tsOk and NextVisual);

  _CreateTmp;
  _CreateExcelRep;

  StopVisual('',0);
  CloseInterface(cmDefault);
}
end; // HandleEvent
end. // Interface

VipInterface UserReport_AGAT_GOZ_Pr5v2 Implements IUserReport licensed(free);
Interface UserReport_AGAT_GOZ_Pr5v2;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::AGAT_GOZ_Pr5v2);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Справка (приложение 5)';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    GetGroupName := 'Отчеты ГОЗ';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'DOGOVOR';
    end;
  end;
end.
