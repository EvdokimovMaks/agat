#component "AGAT"
!==============================================================
Interface AGAT_PSCtoSpDocs 'Привязка протоколов согласования цены к этапам' EscClose, cyan;

Show at (1,1,112,30);
!==============================================================
create view
var
_curSpdocs : comp
as Select 
if(SpDocs.PrMc=1,KatMC.Name,KatUsl.Name) (FieldName = _McUslName)
,*
from 
 SpDocs
,GroupSch
,synonym GroupSch PSC_Parent
,synonym GroupSch PSC
,synonym SpDocs SpDocsMark
,synonym KatOrg KatOrgPSC

, SpecMTR
, KatStroy

, KatMC
, KatUSL

,Pick
,ExClassVal
,ExClassSeg
,KlVal KlValPSC

,AttrNam
,AttrVal
//,SpDocs SpDocsVP
//,ExClassVal ExClassValVP
//,ExClassSeg ExClassSegVP

where ((
    word(777)                   ==  PSC_Parent.Kod
and PSC_Parent.nRec             ==  PSC.cNode
and PSC.cOrg                    ==  KatOrgPSC.nRec
and PSC.nRec                    ==  SpDocs.cMoveCell (noIndex)

and SpDocs.cDoc                 ==  Dogovor.nRec

and word(1723)                  ==  Pick.wList
and Pick.cRec                   ==  SpDocsMark.nRec

and word(1723)                  ==  SpecMTR.CoTable
and SpDocs.nRec                 ==  SpecMTR.cSpec
and SpecMTR.cObj                ==  KatStroy.nRec
                        
                        
and SpDocs.cMcUsl               ==  KatMC.nRec
and SpDocs.cMcUsl               ==  KatUsl.nRec

and SpDocs.nRec                 ==  ExClassVal.cRec
and word(124)                   ==  ExClassVal.ClassCode
and ExClassVal.cClassSeg        ==  ExClassSeg.nRec

and PSC.cVal                    ==  KlValPSC.nRec

and word(1125)                  ==  AttrNam.wTable
and 'Номер протокола согл. цены'==  AttrNam.Name
and word(1125)                  ==  AttrVal.wTable
and PSC.nRec                    ==  AttrVal.cRec
and AttrNam.nRec                ==  AttrVal.cAttrNam


//and Dogovor.nRec                ==  SpDocsVP.cDoc
//and SpDocsVP.nRec               ==  ExClassValVP.cRec
//and word(124)                   ==  ExClassValVP.ClassCode
//and ExClassValVP.cClassSeg      ==  ExClassSegVP.nRec

))Order by PSC.Kod;



Window wEdtProtSoglC 'Редактирование протокола';
  Show At(10,10,60,15)
  Screen Scr_edt1;
  Fields
   AttrVal.vString : NoProtect;
   PSC.dFinIsh     : [10,'DD/MM/YYYY'],NoProtect;
   PSC.Sum         : NoProtect;
   KlValPSC.SIMVOLV   : Protect,PickButton;
   KatOrgPSC.Name  : Protect,PickButton;
<<
`Номер`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
`Дата`         `Сумма`           `Валюта`
 .@@@@@@@@@@@@  .@@@@@@@@@@@@@@   .@@@@@@@@@@@
`Контрагент`
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
  End;
HandleEvent
cmInit:{
  if comp(PSC.Name) = PSC.nRec PSC.Name := '';
}
cmDone:{
  Update current AttrVal;
  if PSC.Name = '' PSC.Name := String(PSC.nRec);
  Update current PSC;
}

cmPick:{
  if (CurField =#KatOrgPSC.Name)
     {
      var tmpRec : comp;
      tmpRec := PSC.cOrg;

      if (RunInterface('L_KATORG::GETKATOR', tmpRec, 0, false, 0, 0) <> cmCancel)
         {
           if (tmpRec <> PSC.cOrg )
              {
                set PSC.cOrg := tmpRec;
                RescanPanel(#PSC);
              }
         }
     }
  if (CurField =#KlValPSC.SIMVOLV)
     {
      var tmpRec : comp;
      tmpRec := PSC.cVal;
      if (RunInterface('L_VAL::CURSVAL', tmpRec,0,date(0,0,0),word(1)) <> cmCancel)
         {
           if (tmpRec <> PSC.cVal )
              {
                set PSC.cVal := tmpRec;
                RescanPanel(#PSC);
              }
         }
     }

}
End; // HandleEvent Window
End; // Window


Panel myP001
Screen scr1;
 Show at (1,1,70,1);
<< 
        Список протоколов согласования цены
>>
 end;
end; //panel

Panel myP1
Screen scr2;
  Show at (71,1,,14);
buttons
  cmValue1,,,'';
  cmValue2,,,'';
  cmValue3,,,'';

<< 
  <.   Привязать протокол к этапам    .>

  <. Отвязать протокол от всех этапов .>

  <. Отвязать текущий этап от проток. .>


>>
 end;

end; //panel

Panel myP2;
Browse br_PSC ('',,sci1478Esc);
 Show at (,2,70,14);
 Table PSC;
   Fields
    AttrVal.vString    'Номер'      ('Номер',,sci1478Esc)        :[10],protect;
    PSC.dFinIsh        'Дата'       ('Дата',,sci1478Esc)         :[6],protect;
    PSC.Sum            'Сумма'      ('Сумма',,sci1478Esc)        :[5.2],protect;
    KatOrgPSC.Name     'Контрагент' ('Контрагент',,sci1478Esc)   :[20],protect;
End;

Screen scr3 ('...',,sci13Esc);
 Show at (,15,,15);
<< 
        Этапы привязанные к протоколу
>>
end;
end; //panel

Panel myP3
Browse br_SpDocs;
 Show at (,16,,);
 Table SpDocs
   Fields
    SpDocs.Code         '№ п/п'              :[3]  ,protect;
    KatStroy.Name       'Заказ/Этап'         :[20] ,protect;
    _McUslName          'Наименование'       :[20] ,protect;
    ExClassSeg.Name     'Вид цены'           :[20] ,protect;
    Dogovor.NoDoc       'Договор №'          :[15] ,protect;
    Dogovor.NoDoc_Ext   'Шифр'               :[15] ,protect;
    Dogovor.dDoc        'от'                 :[8]  ,protect;
    SpDocs.Kol          'Кол-во'             :[5.3],protect;
    SpDocs.Price        'Цена'               :[5.2],protect;
    SpDocs.Summa        'Сумма'              :[5.2],protect;
 End;
end; //panel



HandleEvent
cmInit:{
//  Message('1');
  if GetFirst PSC_Parent <> tsOk
     {
       if (Message('В каталоге групп документов отсутствует запись по умолчанию. Создать группу документов "Протоколы согласования цены"?'+ chr(13) +
                   'В случае отказа интерфейс будет закрыт.',YesNo + Confirmation) <> cmYes) 
          {
            Abort; Exit;
          }
          else 
          {
            Insert current PSC_Parent set PSC_Parent.Kod := word(777), PSC_Parent.Name := 'Протоколы согласования цены';
          }
     }
}
cmEdit:{
  RunWindowModal(wEdtProtSoglC);
}
cmInsert:{
  if (Curtable = #PSC)
     {
       ClearBuffer (#PSC);
       PSC.nRec    := GetNextNRec(#PSC,0);
       PSC.cNode   := PSC_Parent.nRec;
       PSC.Kod     := '';
       PSC.Name    := String(comp(PSC.nRec));
       PSC.dFinIsh := Cur_Date;
       insert current PSC;

       ClearBuffer (#AttrVal);
       AttrVal.nRec     := GetNextNRec(#AttrVal,0);
       AttrVal.wTable   := word(1125);
       AttrVal.cRec     := PSC.nRec;
       AttrVal.cAttrNam := AttrNam.nRec
       insert current AttrVal;
       RunWindowModal(wEdtProtSoglC);
     } 
  Abort; Exit;
}
     
cmDefault:{
//  Abort; Exit;
}

cmDelete:  if (GetFirst GroupSch where ((PSC_Parent.nRec == GroupSch.cNode))) = tsOk  {
  if (Curtable = #PSC)
     {
       if (Message('Удалить текущую запись?',YesNo + Confirmation) = cmYes) 
          {
            if GetFirst SpDocs = tsOk 
               { 
                 if (Message('Внимание! Данный протокол согласования цены привязан к спецификациям договоров! Продолжить удаление?',YesNo + Confirmation) = cmYes)
                    do {
                         Update current SpDocs Set SpDocs.cMoveCell := comp(0);
                         if GetFirst AttrVal = tsOk Delete current AttrVal;
                         Delete current PSC;
                       } while GetNext SpDocs = tsOk 
               } else Delete current PSC;
          }
       RescanPanel(#PSC);
     }
}
cmValue1:if isValid(tnPSC){
  if (RunInterface('AGAT::AGAT_GETSOMEDOGOVOR') = cmDefault)
     {
       if GetFirst Pick= tsOk do
          {
            SpDocsMark.cMoveCell := PSC.nRec;
            update current SpDocsMark;
          } while GetNext Pick = tsOk;
       if GetFirst SpDocs= tsOk{};

//       var isFact
//       if GetFirst SpDocsVP = tsOk do
//          {
//            
//          } while GetNext SpDocsVP = tsOk;
       RescanPanel(#SpDocs);
     }

}
cmValue2:  if isValid(tnSpDocs){
  if (Message('Внимание! Вы уверенны, что хотите отвязать протокол №'+PSC.Kod+' от '+PSC.dFinIsh+' от всех этапов?',YesNo + Confirmation) = cmYes) 
     { 
       if GetFirst SpDocs = tsOk do 
          {
            Update current SpDocs Set SpDocs.cMoveCell := comp(0);
          } while GetNext SpDocs = tsOk 
       ClearBuffer(#SpDocs);
       RescanPanel(#SpDocs);
     };
}
cmValue3:if isValid(tnSpDocs){
  _curSpdocs := SpDocs.nRec;
  if (Message('Внимание! Вы уверенны, что хотите отвязать этап '+KatStroy.Name+' от протокола '+PSC.Kod+' от '+PSC.dFinIsh+'?',YesNo + Confirmation) = cmYes) 
     { 

       Update SpDocs where ((_curSpdocs == SpDocs.nRec)) Set SpDocs.cMoveCell := comp(0);
       if GetFirst SpDocs <> tsOk{ClearBuffer(#SpDocs);};
       RescanPanel(#SpDocs);
     }
}
end; // HandleEvent Interface
end. // Interface

VipInterface UserReport_AGAT_PSCtoSpDocs Implements IUserReport licensed(free);
Interface UserReport_AGAT_PSCtoSpDocs;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::AGAT_PSCtoSpDocs);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Протоколы согласования цены';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    GetGroupName := 'Отчеты ГОЗ';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'DOGOVOR';
    end;
  end;
end.

/*
  p1         // код валюты
, p2         // ее курс
, dat_curse  // принимает дату, по которую вывести курсы, возвращает дату курса
, only_pick  // 1 - признак вызова интерфейса для выбора, 0 - для редактирования
;            // 1-только выбор валюты, 2 - валюта и курсы, 3- только курсы

*/
