#include Query.vih
#include QueryStr.vih
#include ExplosionQueryStr.vih

#component "UTILS"

interface iExplosionQueryStr_KauFilter;

var
  _formplNrec: comp;
  _typerec   : word;
  _kodgrKau  : word;

create view
as select *
from spformpl
where ((
  _formplNrec == spformpl.cformpl and
  _typerec    == spformpl.typerec
));

procedure CreateTmpTable(n: string);
{
  //Создаем временную таблицу с набором записей
  sqlDropTmpTable(n);
  sqlCreateTmpTable('table '+n+' ( cRec: comp) with index (i01 = cRec);', ctmNormal);

  //Готовим запрос для заполнения временной таблицы
  var q: IQuery;
  q := queryManager.createQuery('insert '+n+' (cRec) values (:cRec)');

  //Заполняем временную таблицу
  var i: integer;
  var c: comp;
  q.setParam('cRec', c);
  _loop spformpl
  {
    for(i := 1; i <= 20; i++)
    {
      if (spformpl.rescomp1[i] != 0)
      {
        //q.setParam('cRec', if(spformpl.rescomp1[i] = -1, 0, spformpl.rescomp1[i]));
        c := if(spformpl.rescomp1[i] = -1, 0, spformpl.rescomp1[i]);
        q.execute;
      }
    }
    for(i := 1; i <= 10; i++)
    {
      if (spformpl.rescomp2[i] != 0)
      {
        c := if(spformpl.rescomp2[i] = -1, 0, spformpl.rescomp2[i]);
        //q.setParam('cRec', if(spformpl.rescomp2[i] = -1, 0, spformpl.rescomp2[i]));
        q.execute;
      }
    }
  }
}

function GetTableName: string;
{
  result := 'tFormplKauFltr'+_typerec;
}

procedure Create(var l: longint);
{
  if (getfirst spformpl = tsOk)
  {
    //получаем имя временной таблицы с набором
    var n: string;
    n := GetTableName;
    //создаем и заполняем временную таблицу
    CreateTmpTable(n);
    //формируем доп условие для фильтрации по временной таблице
    sqlAddStr(l,'and');
    sqlAddStr(l,'case when psl.wkodgr1 = '+_kodgrKau+' then psl.canval1');
    sqlAddStr(l,'     when psl.wkodgr2 = '+_kodgrKau+' then psl.canval2');
    sqlAddStr(l,'     when psl.wkodgr3 = '+_kodgrKau+' then psl.canval3');
    sqlAddStr(l,'     else #comp(0)');
    sqlAddStr(l,'end in (select crec from '+n+')');
  }

}

property FormplNrec: comp   absolute _formplNrec;
property Typerec   : word   absolute _typerec;
property KodGrKau  : word   absolute _kodgrKau;

end.
