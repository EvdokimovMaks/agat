#component "M_TPP"

#doc interface M_TPP::INPRSED
<brief> Докомпиляция интерфейса редактирования строк производственных спецификаций (ПС)</brief>
<p>После выбора пользователем МЦ в аналитику 1 строки ПС устанавливается статья затрат из внешнего атрибута
"Статья" (код группы аналитик 10016) к типу МЦ.
<p>После выбора пользователем услуги в аналитику 1 строки ПС устанавливается статья затрат
"Затраты по работам соисполнителей" (код аналитики 20100101, код группы аналитик 10016)
#end
alter interface INPRSED;

sql query q1 =  select spkau.nrec
                from ps_lines
                join katmc on ps_lines.cdet = katmc.nrec
                join typemc on katmc.ctype = typemc.nrec
                join attrval av on 10004 = av.wtable and coalesce(typemc.nrec, #comp(0)) = av.crec and #comp(0001000000000426h) = av.cattrnam    // в боевой базе - 000100000000043Ch
                join spkau on av.vcomp = spkau.nrec
                where ps_lines.nrec = ?;

//tableevent table ps_lines
handleevent
cmPick:
{
  var oldDetNrec: comp;
  oldDetNrec := ps_lines.cdet;
  inherited::handleevent(cmpick);
  case curfield of
    #dkol:
    {
      // #docl <p>TODO: добавить проверку на соответствие производственной спецификации шаблону
      if ps_lines.cDet != oldDetNrec and ps_lines.tDoc = 0
      {
        case ps_lines.wDet of
          4: //для мц устанавливаем статью затрат из внешнего атрибута "Статья" к типу МЦ
          {
            var stmt: longint;
            var c: comp; c := 0;
            stmt := sqlAllocStmt;
            sqlPrepare(stmt, q1);
            sqlBindParam(stmt, 1, ps_lines.nrec);
            sqlBindCol(stmt, 1, c);
            sqlExecute(stmt);
            sqlFetch(stmt);
            sqlFreeStmt(stmt);
            if (c = 0)
              message('К типу МЦ не привязана статья. Установите статью вручную')
            else
            {
              ps_lines.wkodgr1 := 10016;
              ps_lines.canval1 := c;
            }
            ps_lines.wkodgr3 := 5008;
            ps_lines.canval3 := katmc.ctype;
            setmodified(true);
          }
          5: //для услуги устанавливаем статью затрат Затраты по работам соисполнителей 20100101
          {
            ps_lines.wkodgr1 := 10016;
            ps_lines.canval1 := 0001000000000987h;
            setmodified(true);
          }
        end;
      }
    }
  end;
}
end;
end.
