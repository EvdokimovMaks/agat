#include GlobalMatch.vih
#include GlobalDataProviderMatches.vih
#include GlobalGoods.vih
#include System.vih

#component "AGAT"

interface MatchLoaderGoodsByMC;

var
  _dataProvider: IGlobalDataProviderMatches;

function InitObjectsByDefaultIfNull: boolean;
{
  result := true;

  if _dataProvider = nullref
    result := result and getvipref(_dataProvider,'AGAT::GlobalDataProviderMatches');
}

constructor Init;
{
  result := InitObjectsByDefaultIfNull;
}

constructor MatchLoaderGoodsByMC(aDataProvider: IGlobalDataProviderMatches);
{
  _dataProvider := aDataProvider;
  result := InitObjectsByDefaultIfNull;
}

function IMatchLoaderGoodsByMC.LoadMatches(aMC: IMC): ICollection;
{
  var c: ICollection;
  c := ICollection(new(System::Collection));

  var rs: IResultSet;
  rs := _dataProvider.GetGoodsByKatmcNrec(aMC.Nrec);

  if rs.getFirst = tsOk do
  {
    var g: IGlobalGoods;
    g := IGlobalGoods(new(GlobalGoods));
    g.GoodsId     := rs.row.Val('GoodsId');
    g.GoodsName   := rs.row.Val('GoodsName');
    g.MeasureId   := rs.row.Val('MeasureId');
    g.MeasureName := rs.row.Val('MeasureName');
    c.Add(g);

  } while rs.getNext = tsOk;

  result := c;
}

property DataProvider: IGlobalDataProviderMatches absolute _dataProvider;

end.
