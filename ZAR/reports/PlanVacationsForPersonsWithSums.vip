#include Query.vih
#include SrDnZar.vih
#include TitledocHelper.vih
#include ComFiltr.vih
#include ExtAttr.vih
#include XLReport.vih

#component "AGAT"

interface PlanVacationsForPersonsWithSums 'Реестр сотрудников с запланированными отпусками за период', escClose, doAccept;
show(,,80,14);

sql query qFillMyTitledoc =

insert tMyTitledoc (ctitledoc, cperson, cZakaz, cRS, cplanotpusk, docdatef1, docdatef2, duration, spplbanNrec, KindOtp)

  select td.nrec as ctitledoc,
         cd.person as cperson,
         coalesce(avZakaz.vComp,#comp(0)) as cZakaz,
         coalesce(avRS.vComp,#comp(0)) as cRS,
         min(cd.cnew5) as cplanotpusk,
         min(cd.dat1) as docdatef1,
         max(cd.dat2) as docdatef2,
         sum(pvc.wdaycount) as duration,
         coalesce(otp.SpplbanNrec, #comp(0)) as spplbanNrec,
         klotp.notpus as KindOtp

  from titledoc td

  join partdoc pd on pd.cdoc = td.nrec
                 and pd.typeoper in (6,41)

  join contdoc cd on cd.cpart = pd.nrec

  join planotpusk plno on plno.nrec = cd.cnew5
                      and plno.planyearbeg between :startDate and :endDate

  join putvaccon pvc on pvc.wtable = 25045
                    and pvc.ctable = cd.nrec
                    and pvc.wdaycount <> 0

  join putvacation pvac on pvac.nrec = pvc.cputvacation

  join klotpusk klotp on klotp.nrec = pvac.cklotpusk

  left join attrval avZakaz on avZakaz.wtable = 25045
                           and avZakaz.crec = cd.nrec
                           and avZakaz.cattrnam = #comp(0001000000000921h)

  left join attrval avRS on avRS.wtable = 25045
                        and avRS.crec = cd.nrec
                        and avRS.cattrnam = #comp(0001000000000920h)

  left join (select fo.ccontdoc, max(spplban.nrec) as SpplbanNrec
             from factotpusk fo
             join otpusk otp on otp.cfactotpusk = fo.nrec
             join zaroper zo on zo.tipdoc=15028 and zo.clschet=otp.clsch and zo.cpodr=otp.nrec
             join spplban on spplban.nrec = zo.cplpor
             group by fo.ccontdoc
            ) otp on otp.ccontdoc=cd.nrec

  group by td.nrec,
           cd.person,
           coalesce(avZakaz.vComp,#comp(0)),
           coalesce(avRS.vComp,#comp(0)),
           coalesce(otp.SpplbanNrec, #comp(0)),
           klotp.notpus;

sql query qSelect =
select coalesce(ls.nrec,#comp(0)) as clschet,
       p.nrec as cperson,
       plno.nrec as cplanotpusk,
       cast(p.tabnmb as string) as tabnmb,
       p.fio,
       case when coalesce(lschet.tbld[1],0)=10057 then lschet.ckaud[1]
            when coalesce(lschet.tbld[2],0)=10057 then lschet.ckaud[2]
            when coalesce(lschet.tbld[3],0)=10057 then lschet.ckaud[3]
            when coalesce(lschet.tbld[4],0)=10057 then lschet.ckaud[4]
            when coalesce(lschet.tbld[5],0)=10057 then lschet.ckaud[5]
            when coalesce(lschet.tbld[6],0)=10057 then lschet.ckaud[6]
            else #comp(0)
       end as cpodgr,
       case when coalesce(lschet.tbld[1],0)=10036 then lschet.ckaud[1]
            when coalesce(lschet.tbld[2],0)=10036 then lschet.ckaud[2]
            when coalesce(lschet.tbld[3],0)=10036 then lschet.ckaud[3]
            when coalesce(lschet.tbld[4],0)=10036 then lschet.ckaud[4]
            when coalesce(lschet.tbld[5],0)=10036 then lschet.ckaud[5]
            when coalesce(lschet.tbld[6],0)=10036 then lschet.ckaud[6]
            else #comp(0)
       end as ckateg,
       coalesce(catdep.nrec, #comp(0)) as cdepartment,
       coalesce(catdep.name, '') as department,
       coalesce(catpost.nrec, #comp(0)) as cpost,
       coalesce(catpost.name, '') as post,
       lschet.tarif as salary,
       plno.planyearbeg,
       plno.planyearend,
       plno.duration as durationp,
       coalesce(td.duration,0) as durationf,
       plno.workyearbeg,
       plno.workyearend,
       coalesce(td.cnt,0) as doccnt,
       coalesce(td6.nrec,#comp(0)) as docnrec,
       coalesce(td6.docnmb,  '') as docnmb,
       coalesce(td6.docdate,  0) as docdate,
       coalesce(td6.wstatus,  0) as docstatus,
       coalesce(td.docdatef1, 0) as docdatef1,
       coalesce(td.docdatef2, 0) as docdatef2,
       td.KindOtp as KindOtp
from planotpusk plno
join persons p on p.nrec = plno.person
left join (select td.cplanotpusk,
                  count(*) as cnt,
                  max(td.ctitledoc) as ctitledoc,
                  min(td.docdatef1) as docdatef1,
                  max(td.docdatef2) as docdatef2,
                  sum(td.duration) as duration,
                  td.KindOtp
           from tMyTitledoc td
           group by td.cplanotpusk,
                    td.KindOtp
          ) td on td.cplanotpusk = plno.nrec
left join (select tperson, max(nrec) as nrec
           from lschet
           where datuv=0 or datuv>:startDate
           group by tperson
          ) ls on ls.tperson=p.nrec
left join (select person, max(nrec) as nrec
           from appointments
           where dismissdate=0 or dismissdate>:startDate
           group by person
          ) apps on apps.person = p.nrec
left join appointments app on app.nrec = coalesce(apps.nrec,#comp(0))
left join titledoc td6 on td6.nrec = coalesce(td.ctitledoc,#comp(0))
left join catalogs catdep on catdep.nrec = coalesce(app.department, p.department)
left join catalogs catpost on catpost.nrec = coalesce(app.post, #comp(0))
join lschet on lschet.tperson=p.nrec and ( lschet.datuv=0 or lschet.datuv>:startDate )
where plno.vactype=1
  and plno.planyearbeg between :startDate and :endDate
order by p.tabnmb
;

table struct tMyTitledoc (
  ctitledoc  : comp,
  cperson    : comp,
  cZakaz     : comp,
  cRS        : comp,
  cplanotpusk: comp,
  docdatef1  : date,
  docdatef2  : date,
  duration   : double,
  spplbanNrec: comp,
  KindOtp    : string
) with index (
  i01 = cplanotpusk + ctitledoc
);

table struct t (
  clschet    : comp,
  cperson    : comp,
  cdepartment: comp,
  department : string,
  cpost      : comp,
  post       : string,
  cplanotpusk: comp,
  tabnmb     : string[10],
  fio        : string,
  cpodgr     : comp,
  ckateg     : comp,
  salary     : double,
  planyearbeg: date,
  planyearend: date,
  durationp  : double,
  durationf  : double,
  srdnzar    : double,
  summa      : double,
  KindOtp    : string,
  workyearbeg: date,
  workyearend: date,
  doccnt     : byte,
  docnrec    : comp,
  docnmb     : string[20],
  docdate    : date,
  docstatus  : word,
  docdatef1  : date,
  docdatef2  : date
) with index (
  i01 = fio
);

var
  _d1: date;
  _d2: date;
  _srDnZar: iSrDnZar;
  _titledocHelper: TitledocHelper;
  _cf: CommonFiltr new;
  _pXL: XLSRepBuilder;
  _xltFileName: string;
  _ea: IExtAttr;

create view
as select *
from t, t brt, tMyTitledoc, tMyTitledoc brMyTitledoc, tMyTitledoc brMyTitledoc1, titledoc, spplban, sbban,
     spkau spkKateg, spkau spkPodgr
where ((
  brt.cplanotpusk          == brMyTitledoc.cplanotpusk and
  brMyTitledoc.ctitledoc   == titledoc.nrec and
  brMyTitledoc.spplbanNrec == spplban.nrec and
  spplban.csbban           == sbban.nrec and
  brt.cplanotpusk          == brMyTitledoc1.cplanotpusk and
  brt.docnrec              == brMyTitledoc1.ctitledoc and
  brt.ckateg               == spkKateg.nrec and
  brt.cpodgr               == spkPodgr.nrec
))
;

//#region GUI настройки интерфейса

screen sc1;
show at (,,,3);
fields
  _d1: noprotect;
  _d2: noprotect;
<<

  `Период с` .@@@@@@@@@@ `по` .@@@@@@@@@@

>>
end;

embedded sc2 interface _cf;
show at (,4,,10);
end;

screen sc3;
show at (,11,,13);
buttons
  cmDoOk, default;
  cmCancel;
<<

                                      <.    Продолжить    .> <.    Отмена    .>
>>
end;

//#endregion

//#region Вспомогательные функции для отображения полей в интерфейсе и отчете

function getPeriodView(aStartDate: date; aEndDate: date): string;
{
  result := datetostr(aStartDate,'DD.MM.YYYY')+' - '+datetostr(aEndDate,'DD.MM.YYYY');
}

function getPlanPeriodView: string;
{
  result := getPeriodView(brt.planyearbeg, brt.planyearend);
}

function getWorkPeriodView: string;
{
  result := getPeriodView(brt.workyearbeg, brt.workyearend);
}

function getDocNmbView(aDocNmb: string): string;
{
  result := if(trim(aDocNmb)='','<Без номера>',aDocNmb);
}

function getDocDateView(aDocDate: date; aPrefix:string=' от '): string;
{
  result := if(aDocDate=0, '', aPrefix+datetostr(aDocDate,'DD.MM.YYYY'));
}

function getDocStatusView(aDocStatus: word): string;
{
  result := if(aDocStatus=0,'не утвержден','утвержден');
}

function getDocView: string;
{
  case brt.doccnt of
    0: result := '';
    1: result := getDocNmbView(brt.docnmb)+getDocDateView(brt.docdate);
  else
    result := 'Кол-во приказов: '+brt.doccnt;
  end;
}

function getDocDateFact: string;
{
  case brt.doccnt of
    0: result := '';
    1: result := getPeriodView(brt.docdatef1, brt.docdatef2);
  else
    result := 'см. нижнюю панель';
  end;
}

function getDocZakazView(aDocNrec: comp): string;
{
  if aDocNrec = 0
    result := ''
  else
    result := if(
      getfirst brMyTitledoc1=tsOk,
      if(getanykau(1, 40, brMyTitledoc.cZakaz),givenanname(1),''),
      ''
    ); //_titledocHelper.getContdocExtAttrZakazVString(aDocNrec);
}

function getDocRSView(aDocNrec: comp): string;
{
  if aDocNrec = 0
    result := ''
  else
    result := if(
      getfirst brMyTitledoc1=tsOk,
      if(getanykau(1, 10545, brMyTitledoc.cRS),givenanname(1),''),
      ''
    ); //_titledocHelper.getContdocExtAttrRSVString(aDocNrec);
}

function getFontBackColorByDocStatus(aDocStatus: word): byte;
{
  result := if(aDocStatus=1, 78, 67);
}

function getBRTFontBackColor: byte;
{
  result := if(brt.doccnt=1, getFontBackColorByDocStatus(brt.docstatus), if(brt.doccnt=0, 0, 61));
}

function getSpplbanView: string;
{
  result := 'нет';
  if getfirst fastfirstrow spplban = tsOk
    if getfirst fastfirstrow sbban = tsOk
      result := sbban.name;
}

function ShowSalary: boolean;
{
  result := wGetTune('AGAT.ZAR.PLANVACREPSALARY')=1;
}

//#endregion

//#region Отчет

procedure ShowExcelReport(isCreateXLT: boolean);
{
  var _fname: string;
  _fname := if(isCreateXLT, _pXL.CreateXLT(_xltFileName, True),  _pXL.CreateReport(_xltFileName, True));
  _pXL.CreateVar(_fname);
  _pXL.SetStringVar('drep1', datetostr(_d1,'DD.MM.YYYY'));
  _pXL.SetStringVar('drep2', datetostr(_d2,'DD.MM.YYYY'));
  _pXL.PublishVar;
  _pXL.CreateTbls(_fname);
  _pXL.CreateTbl('Main');
  _pXL.CreateTblFld('ТабНомер');
  _pXL.CreateTblFld('ФИО');
  _pXL.CreateTblFld('Категория');
  _pXL.CreateTblFld('Подгруппа');
  _pXL.CreateTblFld('Подразделение');
  _pXL.CreateTblFld('Должность');
  if ShowSalary
    _pXL.CreateTblFld('Оклад');
  _pXL.CreateTblFld('ПериодОтпуска');
  _pXL.CreateTblFld('ПериодОтпускаФакт');
  _pXL.CreateTblFld('ВклВРеестр');
  _pXL.CreateTblFld('РабочийПериод');
  _pXL.CreateTblFld('ДнейВсего');
  _pXL.CreateTblFld('СреднедневнаяЗП');
  _pXL.CreateTblFld('СуммаВсего');
  _pXL.CreateTblFld('Приказ');
  _pXL.CreateTblFld('Статус');
  _pXL.CreateTblFld('ВидОтпуска');
  _pXL.CreateTblFld('Заказ');
  _pXL.CreateTblFld('Счет');
  _pXL.CreateTblFld('ДнейПоПриказу');
  _pXL.CreateTblFld('СуммаПоПриказу');

  if not isCreateXLT
  _loop brt
  {
    var fullcnt: integer;
    fullcnt := brt.durationp;
    _loop brMyTitledoc
    {
      _pXL.ClearTblBuffer;
      _pXL.SetTblStringFldValue('ТабНомер'       ,brt.Tabnmb);
      _pXL.SetTblStringFldValue('ФИО'            ,brt.FIO);
      _pXL.SetTblStringFldValue('Подразделение'  ,brt.Department);
      _pXL.SetTblStringFldValue('Категория'      ,if(getfirst spkKateg = tsOk, spkKateg.name, ''));
      _pXL.SetTblStringFldValue('Подгруппа'      ,if(getfirst spkPodgr = tsOk, spkPodgr.name, ''));
      _pXL.SetTblStringFldValue('Должность'      ,brt.Post);
      if ShowSalary
        _pXL.SetTblNumberFldValue('Оклад'        ,brt.salary);
      _pXL.SetTblStringFldValue('ПериодОтпуска'  ,getPlanPeriodView);
      _pXL.SetTblStringFldValue('ПериодОтпускаФакт',getDocDateFact);
      _pXL.SetTblStringFldValue('РабочийПериод'  ,getWorkPeriodView);
      _pXL.SetTblStringFldValue('ВклВРеестр'     ,getSpplbanView);
      _pXL.SetTblNumberFldValue('ДнейВсего'      ,brt.durationp);
      _pXL.SetTblNumberFldValue('СреднедневнаяЗП',brt.srdnzar);
      _pXL.SetTblNumberFldValue('СуммаВсего'     ,brt.summa);
      if getfirst titledoc = tsOk
      {
        _pXL.SetTblStringFldValue('Приказ'       ,getDocNmbView(titledoc.docnmb)+getDocDateView(titledoc.docdate,' от '));
        _pXL.SetTblStringFldValue('Статус'       ,getDocStatusView(titledoc.wStatus));
      }
      _pXL.SetTblStringFldValue('ВидОтпуска'    ,brt.KindOtp);
      _pXL.SetTblStringFldValue('Заказ'          ,if(getanykau(1, 40, brMyTitledoc.cZakaz), givenanname(1), ''));
      _pXL.SetTblStringFldValue('Счет'           ,if(getanykau(1, 10545, brMyTitledoc.cRS), givenanname(1), ''));

      //var cnt: integer; cnt := 0;
      //_loop putvaccon
      //  cnt += putvaccon.wdaycount;

      fullcnt -= brt.durationf;
      _pXL.SetTblNumberFldValue('ДнейПоПриказу'  ,brt.durationf);
      _pXL.SetTblNumberFldValue('СуммаПоПриказу' ,brt.durationf*brt.srdnzar);
      _pXL.InsTblRow;
    }

    if fullcnt > 0
    {
      _pXL.ClearTblBuffer;
      _pXL.SetTblStringFldValue('ТабНомер'       ,brt.Tabnmb);
      _pXL.SetTblStringFldValue('ФИО'            ,brt.FIO);
      _pXL.SetTblStringFldValue('Подразделение'  ,brt.Department);
      _pXL.SetTblStringFldValue('Категория'      ,if(getfirst spkKateg = tsOk, spkKateg.name, ''));
      _pXL.SetTblStringFldValue('Подгруппа'      ,if(getfirst spkPodgr = tsOk, spkPodgr.name, ''));
      _pXL.SetTblStringFldValue('Должность'      ,brt.Post);
      if ShowSalary
        _pXL.SetTblNumberFldValue('Оклад'        ,brt.salary);
      _pXL.SetTblStringFldValue('ПериодОтпуска'  ,getPlanPeriodView);
      _pXL.SetTblStringFldValue('РабочийПериод'  ,getWorkPeriodView);
      _pXL.SetTblStringFldValue('ПериодОтпускаФакт','');
      _pXL.SetTblStringFldValue('ВклВРеестр'     ,'');
      _pXL.SetTblStringFldValue('Приказ'         ,'');
      _pXL.SetTblStringFldValue('Статус'         ,'');
      _pXL.SetTblStringFldValue('ВидОтпуска'    ,'');
      _pXL.SetTblStringFldValue('Заказ'          ,'');
      _pXL.SetTblStringFldValue('Счет'           ,'');
      _pXL.SetTblNumberFldValue('ДнейВсего'      ,brt.durationp);
      _pXL.SetTblNumberFldValue('СреднедневнаяЗП',brt.srdnzar);
      _pXL.SetTblNumberFldValue('СуммаВсего'     ,brt.summa);
      _pXL.SetTblNumberFldValue('ДнейПоПриказу'  ,fullcnt);
      _pXL.SetTblNumberFldValue('СуммаПоПриказу' ,fullcnt*brt.srdnzar);
      _pXL.InsTblRow;
    }
  }
  _pXL.PublishTbl('Main');
  _pXL.LoadReport(_fname);
  _pXL.DisConnectExcel;
}
//#endregion

procedure ShowDoc(aDocNrec: comp);
{
  if aDocNrec != 0
    runinterfacenomodal(Z_STAFFORDERS::ORDERSLIST, aDocNrec);
}

window wData 'Реестр сотрудников с запланированными отпусками за период', escClose;

  browse brData (,,sci14Esc);
  show(,,,18);
  table brt;
  fields
    brt.tabnmb 'Таб. №': [10], protect;
    brt.fio 'ФИО': [20], protect;
    spkKateg.name 'Категория': [10], protect;
    spkPodgr.name 'Подгруппа': [10], protect;
    brt.department 'Подразделение': [20], protect;
    brt.post 'Должность': [20], protect;
    brt.salary 'Оклад': [,'\2p666`666`666`666.88'], protect;
    [planPeriodView] getPlanPeriodView 'План.период отпуска': [20], protect;
    getDocDateFact 'Факт.период отпуска': [20], protect;
    brt.durationp 'План.Длительность': [7], protect, browsesum;
    brt.durationf 'Факт.Длительность': [7], protect, browsesum;
    brt.srdnzar 'Ср.дн.зп': [,'\2p[|-]3666`666`666`666.88'], protect;
    brt.summa 'Сумма': [,'\2p[|-]3666`666`666`666.88'], protect, browsesum;
    getWorkPeriodView 'Рабочий период сотрудника': [20], protect;
    getSpplbanView 'Включено в реестр': [10], protect;
    getDocView 'Приказ': [20], protect, editButton, {font={backColor=getBRTFontBackColor}};
    brt.KindOtp 'Вид отпуска': [10], protect;
    if(brt.doccnt>1,'см. нижнюю панель',getDocZakazView(brt.docNrec)) 'Заказ': [10], protect;
    if(brt.doccnt>1,'см. нижнюю панель',getDocRSView(brt.docNrec)) 'Расчетный счет': [10], protect;
  end;

  browse brDoc (,,sci14Esc);
  show (,19,,) fixed_y;
  table brMyTitledoc;
  fields
    getDocNmbView(titledoc.docnmb) 'Приказ - Номер': [20], protect;
    getDocDateView(titledoc.docdate,'') 'Дата': [10], protect;
    getDocStatusView(titledoc.wstatus) 'Статус': [10], protect, {font={backColor=getFontBackColorByDocStatus(titledoc.wstatus)}};
    titledoc.doctext 'Краткое содержание': [30], protect;
    getPeriodView(brMyTitledoc.docdatef1, brMyTitledoc.docdatef2) 'Факт.перод отпуска': [20], protect;
    if(getanykau(1, 40, brMyTitledoc.cZakaz),givenanname(1),'') 'Заказ': [20], protect;
    if(getanykau(1, 10545, brMyTitledoc.cRS),givenanname(1),'') 'Расчетный счет': [30], protect;
  end;

  handleevent
  cmEdit:
  {
    case curtable of
      #brt         : ShowDoc(brt.docnrec);
      #brMyTitledoc: ShowDoc(titledoc.nrec);
    end;
  }
  cminit:
  {
    rereadrecord(#brt);
  }
  cmPrintDoc: ShowExcelReport(false);
  end;

end;

procedure ShowUI;
{
  SetWindowTitle(wData, 'Реестр сотрудников с запланированными отпусками за период с '+datetostr(_d1,'DD.MM.YYYY')+' по '+datetostr(_d2,'DD.MM.YYYY'));
  runwindow(wData);
}

function prepareData: boolean;
{
  var s: longint;
  sqlAddStr(s, 'table tMyTitledoc (');
  sqlAddStr(s, '  ctitledoc  : comp,');
  sqlAddStr(s, '  cperson    : comp,');
  sqlAddStr(s, '  cZakaz     : comp,');
  sqlAddStr(s, '  cRS        : comp,');
  sqlAddStr(s, '  cplanotpusk: comp,');
  sqlAddStr(s, '  docdatef1  : date,');
  sqlAddStr(s, '  docdatef2  : date,');
  sqlAddStr(s, '  duration   : double,');
  sqlAddStr(s, '  spplbanNrec: comp,');
  sqlAddStr(s, '  KindOtp    : string);');
  sqlCreateTmpTable(s,ctmNormal);

  var q: IQuery;

  q := queryManager.createQuery(qFillMyTitledoc);
  q.setParam('startDate', _d1);
  q.setParam('endDate', _d2);
  if q.execute.errorCode != tsOk
  {
    result := false;
    exit;
  }

  delete all tMyTitledoc;
  q := queryManager.createQuery('select * from tMyTitledoc');
  sqlCopyInto(q, #tMyTitledoc, false);

  q := queryManager.createQuery(qSelect);
  q.setParam('startDate', _d1);
  q.setParam('endDate', _d2);
  if q.Execute.errorCode = tsOk
  {
    startnewvisual(vtRotateVisual, vfTimer, 'Формирование реестра', 0);
    delete all t;
    result := sqlCopyInto(q, #t, true) = tsOk;

    var l: longint;
    l := _cf.GetPersonsMarker;
    if getmarkercount(l) > 0
    delete all t where not foundmarker(l, t.cperson);

    stopvisual('',0);

    var cnt: longint; cnt := recordsintable(#t);
    startnewvisual(vtIndicatorVisual, vfBreak+vfTimer, 'Формирование реестра'#13'Расчет среднедневного заработка'#13'', cnt);
    _loop t
    {
      setvisualheader('Формирование реестра'#13'Расчет среднедневного заработка'#13''+t.fio);
      t.srdnzar := _srDnZar.GetSrDnZar(t.clschet, t.planyearbeg);
      t.summa := t.srdnzar * if(t.durationf>t.durationp, t.durationf, t.durationp);
      update current t;
      if not nextvisual
        break;
    }
    stopvisual('',0);
  }
  else
    result := false;
}

procedure donedata;
{
  sqlDropTmpTable('tMyTitledoc');
}

handleevent
cmDoOk:
{
  _cf.EndSelect;
  if prepareData
    showUI;
  donedata;
}
cmInit:
{
  if wGetTune('AGAT.ZAR.PLANVACREPENABLED') != 1
  {
    message('Недостаточно прав для формирования отчетной формы', error);
    closeinterface(cmCancel);
  }

  if not ShowSalary
  {
    ClearFieldState(#brt.salary, sfVisible);
  }

  if not readmydsk(_d2, 'PlanVacationsForPersonsWithSums_d2', false) _d2 := cur_date;
  if not readmydsk(_d1, 'PlanVacationsForPersonsWithSums_d1', false) _d1 := sub_day(_d2, 15);
  _xltFileName := TranslatePath('%StartPath%xls\PlanVacationsForPersonsWithSums.xltm');
  _cf.ObjInit;
}
cmDone:
{
  savemydsk(_d1, 'PlanVacationsForPersonsWithSums_d1');
  savemydsk(_d2, 'PlanVacationsForPersonsWithSums_d2');
}
end;

end.

VipInterface UserReport_PlanVacForPersonsWithSums Implements IUserReport licensed(free);
Interface UserReport_PlanVacForPersonsWithSums;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::PlanVacationsForPersonsWithSums);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Агат. Реестр сотрудников с запланированными отпусками за период';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    GetGroupName := '';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'MenuZarp';
      2 : VisibleInModule := 'STAFFMAINMENU';
      3 : VisibleInModule := 'DOGOVOR';
    end;
  end;
end.
