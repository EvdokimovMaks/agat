#include Query.vih
#include Karantin65Validator.vih

#component "Z_LSCHET"

interface Karantin65Validator;

var _m: TPtr;

public function Valid(B: #MarksTLschet): Boolean;
{
  Result := FoundMarker(_m, B.Nrec);
}

sql query qLs =
  select ls.Nrec

  from lschet ls

  join persons p on p.Nrec = ls.tPerson

  outer apply (
    select count(*) as cnt
    from vacations v
    join klotpusk o on v.vactype = o.nrec
    where
        v.person = p.nrec
    and v.factyearbeg <= #date(30, 4, 2020)
    and v.factyearend >= #date(1, 4, 2020)
    and o.days in ('ОД','ДО','ОТ')
  ) tOtp

  outer apply (
    select count(*) as cnt
    from BlisNet bs
    where
        bs.clsch = ls.nrec
    and bs.choice in (0,3,10)
    and bs.DataN <= #date(30,4,2020)
    and bs.DatOk >= #date(1,4,2020)
  ) tBS

  where
      (ls.DatUv = 0 or ls.DatUv > #date(30,4,2020))
  and p.IsEmployee = 'С'
  and p.BornDate between 1 and #date(6,04,1955)
  and coalesce(tBS.cnt,0) = 0
  and coalesce(tOtp.cnt,0) = 0
;

HandleEvent
  cmOnVipLoad:
  {
    _m := InitMarker('', 8, 100, 100, true);
    var q: IQuery = queryManager.createQuery(qLs);
    var rs: IResultSet = q.getResultSet;
    if rs = NullRef
    {
      Message('Ошибка выполнения запроса ' + q.errorCode, error);
      exit;
    }
    if rs.getfirst = tsOk do
    {
      InsertMarker(_m, comp(rs.row.valat(1)));
    }
    while rs.getnext = tsOk;
  }
  cmOnVipUnload:
  {
    DoneMarker(_m, '');
  }
end; //handleevent
end. //interface Karantin65Validator

interface PersonalAccountValidatorBrowserPlugin_Karantin65 'Карантин. Возраст >= 65'
  doaccept, escclose, gray;

  public procedure ClearParameters;
  {
  }

  public function GetDescription: String;
  {
    Result := 'Карантин. Возраст >= 65';
  }

  public function HasParameters: Boolean;
  {
    Result := false;
  }

  public function SetupParameters: Boolean;
  {
    Result := true;
  }

  public function GetParametersStr: String;
  {
    result := '';
  }

  public function GetValidator: IPersonalAccountValidator;
  {
    var Validator: IPersonalAccountValidator = NullRef;
    LoadVipRef(Validator, 'Karantin65Validator');
    GetValidator := Validator;
  }

end.
