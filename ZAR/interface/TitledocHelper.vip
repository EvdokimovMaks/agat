#include TitledocHelper.vih
#include Query.vih

#component "AGAT"

interface TitledocHelper;

sql query qContdocExtAttrByTitledoc =
select distinct attrval.vstring
from partdoc
join contdoc on contdoc.cpart = partdoc.nrec
join attrval on attrval.wtable = 25045 and
                attrval.crec = contdoc.nrec and
                attrval.cattrnam = :AttrnamNrec and
                attrval.vcomp <> #comp(0)
where partdoc.cdoc = :TitledocNrec
;

sql query qContdocZakazByTitledoc =
select distinct substring(attrval.vstring,1,instr(' ', attrval.vstring+' ', 1)-1) as vstring
from partdoc
join contdoc on contdoc.cpart = partdoc.nrec
join attrval on attrval.wtable = 25045 and
                attrval.crec = contdoc.nrec and
                attrval.cattrnam = #comp(0001000000000921h) and
                attrval.vcomp <> #comp(0)
where partdoc.cdoc = :TitledocNrec
;

var
  _queryZakaz: IQuery;
  _queryExtAttr: IQuery;

function ConcatQueryRowsField(aQuery: IQuery; aFldNum: byte = 1; aDelimiter: string = '; '): string;
{
  var s: string; s := '';
  while (aQuery.fetch.errorCode = tsOk)
    s := if(s='', aQuery.row.ValAt(aFldNum), s + aDelimiter+aQuery.row.ValAt(aFldNum));
  result := s;
}

function ITitledocHelper.getContdocExtAttrVString(aTitledocNrec: comp; aAttrnamNrec: comp): string; cacheable;
{
  _queryExtAttr.setParam('TitledocNrec', aTitledocNrec);
  _queryExtAttr.setParam('AttrnamNrec', aAttrnamNrec);
  _queryExtAttr.Execute;
  result := ConcatQueryRowsField(_queryExtAttr);
}

function ITitledocHelper.getContdocExtAttrRSVString(aTitledocNrec: comp): string;    cacheable;
{
  result := getContdocExtAttrVString(aTitledocNrec, 0001000000000920h);
}

function ITitledocHelper.getContdocExtAttrZakazVString(aTitledocNrec: comp): string; cacheable;
{
  _queryZakaz.setParam('TitledocNrec', aTitledocNrec);
  _queryZakaz.Execute;
  result := ConcatQueryRowsField(_queryZakaz);
}

constructor Init;
{
  _queryZakaz := queryManager.createQuery(qContdocZakazByTitledoc);
  _queryExtAttr := queryManager.createQuery(qContdocExtAttrByTitledoc);
  result := true;
}

end.
