#include ZarAlg.vih

VipInterface ZarAlgorithm_AGAT_SrChas implements IzarAlgorithm;

interface ZarAlgorithm_AGAT_SrChas cacheable;

create view
var
  _cLSch  : comp;
  _wYearN : word;
  _wMesN  : word;
  _wVidOpl: word;
as select
  SumVidOp.Nrec
from
  LSchet
 ,SumVidOp SumVidOpBase
 ,KlVidOpl
 ,SumVidOp
where ((
      _cLSch  == LSchet.Nrec
  and _cLSch  == SumVidOpBase.cLSch
  and _wYearN == SumVidOpBase.YearN
  and _wMesN  == SumVidOpBase.MesN
  and 7       == SumVidOpBase.VidOpl

  and root    == KlVidOpl.Nrec
  and (KlVidOpl.VhVO[13] = '+')

  and _cLSch          == SumVidOp.cLSch
  and _wYearN         == SumVidOp.YearN
  and _wMesN          == SumVidOp.MesN
  and KlVidOpl.VidOpl == SumVidOp.VidOpl
));

function CalcSum(Parameter: String): Double;
{
  result := 0;

  _cLSch := uch_lsch;
  if getfirst LSchet != tsOk exit;

  var d: date = Sub_Months(uch_datan, 1);
  var cnt: integer = 0;
  var sum, sum_m: double = 0;
  var ch, ch_m: double = 0;

  while d >= Date(1, Month(LSchet.DatPos), Year(LSchet.DatPos)) and cnt < 12
  {
    _wYearN := Year(d);
    _wMesN  := Month(d);
    if getfirst SumVidOpBase = tsOk
    {
      sum_m := 0;
      ch_m := 0;
      cnt++;
      Displ('__________________________________________________________________________');
      _loop KlVidOpl
      {
        _loop SumVidOp
        {
          Displ('Вид оплаты = ' + LPad(SumVidOp.VidOplP, 4) +
                '; Часов = ' + LPad(SumVidOp.ChasF, 10) +
                '; Сумма = ' + LPad(SumVidOp.Summa, 15) +
                ' ' + d);
          sum_m += SumVidOp.Summa;
          ch_m += if(KlVidOpl.VhVO[14] = '+', SumVidOp.ChasF, 0);
        }
      }
      Displ('За месяц ' + cnt + ' Сумма ' + LPad(sum_m, 15) + ', Часов ' + LPad(ch_m, 10));
      sum += sum_m;
      ch  += ch_m;
    }
    d := Sub_Months(d, 1);
  }
  result := sum / ch;
  Displ('__________________________________________________________________________');
  Displ('Итого = ' + sum + ' / ' + ch + ' = ' + result);
}

function GetDescription: String;
{
  result := 'Агат. Среднечасовая сумма';
}

end.
