//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика ERP 5.50 - Заработная плата
// Процедуры и функции корректировки рабочего табеля отпускными больничными и другими отклонениями
//------------------------------------------------------------------------------

Procedure Reform_Ot(acLSchet : comp;
                   FirstD    : date;
                   DatR      : date);
!пометка рабочего табеля отпускными
var
    aYear: word; // Рабочий год
    aMont: byte; // Рабочий месяц
    RefrabDate: date;
    prYear: word;
    prMonth: byte;
    TSFlagNew: word;
{
  if (DatR = ZeroDate)
    DatR := DatRas;

  if (FirstD = ZeroDate)
    FirstD := FirstDayRas;

  aYear := Year(FirstD);
  aMont := Month(FirstD);
  RefRabDate := date(1, aMont, aYear);

  if not InitWorkingTable
    exit;

  Do
  {
    aYear := Year(RefRabDate);
    aMont := Month(RefRabDate);

    if (ReFormWorkingTable(acLSchet, aMont, aYear) = erInterrupted)
      break;

    RefRabDate := add_Months(RefRabDate,1);
    NextVisual;
  }
  while (RefRabDate <= DatR);      //конец цикла   and(NextVisual)

  if (LSchet.DatPos <> ZeroDate)
  {
    if getfirst Movelsc where (( LSchet.NRec == MoveLsc.cLSchet  and
                                 ZeroDate    << MoveLsc.DatPos )) = tsOk
    {
      prYear  := Year(MoveLsc.DatPos);
      prMonth := Month(MoveLsc.DatPos);
    }
    else
    {
      prYear  := Year(LSchet.DatPos);
      prMonth := Month(LSchet.DatPos);
    }

    TSFlagNew := delete LSTab where (( LSchet.NRec     == LSTab.cLSchet and
                                       integer(prYear) >> LSTab.YearN ));

    TSFlagNew := delete LSTab where (( LSchet.NRec     == LSTab.cLSchet and
                                       integer(prYear) == LSTab.YearN   and
                                       prMonth         >> LSTab.MesN ));
  }

  if (LSchet.DatUv <> ZeroDate)
  {
    prYear  := Year(LSchet.DatUv);
    prMonth := Month(LSchet.DatUv);

    TSFlagNew := delete LSTab where (( LSchet.NRec     == LSTab.cLSchet and
                                       integer(prYear) << LSTab.YearN ));

    TSFlagNew := delete LSTab where (( LSchet.NRec     == LSTab.cLSchet and
                                       integer(prYear) == LSTab.YearN   and
                                       prMonth         << LSTab.MesN  ));
  }

  DoneWorkingTable;
}
