#include Query.vih
#include "z:\exe\_SRC\src\Интерфейсы\SrDnZar.vih"

#component "AGAT"

interface SrDnZarReport 'Отчет о среднедневных заработках сотрудников на дату' escclose, doaccept;
show(,,31,5);

table struct t (
  tabNmb: longint,
  fio: string,
  podrName: string,
  srdnzar: double
) with index (
  i01 = fio
);

sql query query1 =
  select ls.nrec, p.tabnmb, p.fio, catdep.name as podr
  from lschet ls
  join persons p on p.nrec = ls.tperson
  left join catalogs catdep on catdep.nrec = p.department
  where ls.datuv=0 or ls.datuv>:dRep
  order by p.fio
;

var
  srdnzar: iSrDnZar;

create view
var
  dRep: date;
as select *
from t;

datastream dsRep auto;

screen sc1;
fields
  dRep: noprotect;
buttons
  cmDoOk, default;
  cmCancel;
<<

`Дата отчета` .@@@@@@@@@@

<. Продолжить .> <. Отмена .>
>>
end;

handleevent
cmDoOk:
{
  delete all t;
  var q: IQuery;
  q := queryManager.createQuery(query1);
  q.setParam('dRep', dRep);

  var rs: IResultSet;
  rs := q.getResultSet;

  if rs != nullref
  {
    startnewvisual(vtIndicatorVisual, vfTimer + vfBreak, '', rs.Count)
    if rs.getfirst = tsOk do
    {
      setvisualheader(rs.row.Val('fio'));
      clearbuffer(#t);
      t.tabNmb := rs.row.Val('tabnmb');
      t.fio := rs.row.Val('fio');
      t.podrName := rs.row.Val('podr');
      t.srdnzar := srdnzar.GetSrDnZar(rs.row.Val('nrec'), dRep, false);
      insert current t;
      if not nextvisual break;
    } while rs.getnext = tsOk
    StopVisual('',0);
  }
  runfreport(dsrep,'',false);
}
cmInit:
{
  dRep := date(31,12,2015);
}
end;
end.

