#include PD_Context.vih
#include PD_Row.vih
#include PD_ObjectList.vih
#include PD_IIterator.vih

#component "PD_AGAT"

interface PD_Main 'Распределение входящих платежей' escClose;

table struct TmpData (
  Nrec   : comp,
  cParent: comp,
  Name   : string[80],
  Code   : string[20],
  Npp    : string[20],
  RowId  : longint
) with index (
  i01 = Nrec (unique, surrogate)
 ,i02 = cParent + Npp
);

var
  _context: PD_IContext;
  _cKatStroy: comp;
  _cPlPor: comp;
  _cParent: comp;

create view
as select
  double(_context.cellValue(TmpData.RowId, 1))  (fieldname = vFld1)
 ,double(_context.cellValue(TmpData.RowId, 2))  (fieldname = vFld2)
 ,double(_context.cellValue(TmpData.RowId, 3))  (fieldname = vFld3)
 ,double(_context.cellValue(TmpData.RowId, 4))  (fieldname = vFld4)
 ,double(_context.cellValue(TmpData.RowId, 5))  (fieldname = vFld5)
 ,double(_context.cellValue(TmpData.RowId, 6))  (fieldname = vFld6)
 ,double(_context.cellValue(TmpData.RowId, 7))  (fieldname = vFld7)
 ,double(_context.cellValue(TmpData.RowId, 8))  (fieldname = vFld8)
 ,double(_context.cellValue(TmpData.RowId, 9))  (fieldname = vFld9)
 ,double(_context.cellValue(TmpData.RowId, 10)) (fieldname = vFld10)
 ,double(_context.cellValue(TmpData.RowId, 11)) (fieldname = vFld11)
 ,double(_context.cellValue(TmpData.RowId, 12)) (fieldname = vFld12)
 ,double(_context.cellValue(TmpData.RowId, 13)) (fieldname = vFld13)
 ,double(_context.cellValue(TmpData.RowId, 14)) (fieldname = vFld14)
 ,double(_context.cellValue(TmpData.RowId, 15)) (fieldname = vFld15)
 ,double(_context.cellValue(TmpData.RowId, 16)) (fieldname = vFld16)
 ,double(_context.cellValue(TmpData.RowId, 17)) (fieldname = vFld17)
from
  TmpData
 ,TmpData TmpData1
where ((
  _cParent == TmpData.cParent
));

parameters _cKatStroy, _cPlPor;

function getColByFld(f: longint): byte;
{
  result := case(f;
                 #vFld1 : 1 ,
                 #vFld2 : 2 ,
                 #vFld3 : 3 ,
                 #vFld4 : 4 ,
                 #vFld5 : 5 ,
                 #vFld6 : 6 ,
                 #vFld7 : 7 ,
                 #vFld8 : 8 ,
                 #vFld9 : 9 ,
                 #vFld10: 10,
                 #vFld11: 11,
                 #vFld12: 12,
                 #vFld13: 13,
                 #vFld14: 14,
                 #vFld15: 15,
                 #vFld16: 16,
                 #vFld17: 17;
                 0);
}

procedure loadData(rows: PD_ObjectList; cPrnt: comp);
{
  if rows = nullRef exit;
  var it: PD_IIterator = nullRef;
  for(it := rows.iterator; it.hasNext; it.next)
  {
    var r: PD_IRow = PD_IRow(it.get);
    if r = nullRef continue;
    ClearBuffer(#TmpData1);
    TmpData1.cParent := cPrnt;
    TmpData1.Name    := r.name;
    TmpData1.Code    := r.code;
    TmpData1.Npp     := r.npp;
    TmpData1.RowId   := r.id;
    insert current TmpData1;
    loadData(r.children, TmpData1.Nrec);
  }
}

procedure setCellValue(c: PD_ICell; v: variant);
{
  if c.schema != nullRef
  {
    if c.schema.isEditable(c, _context)
      c.schema.setValue(c, v, _context)
    else
      message('Установлен запрет на редактирование ячейки', warning);
  }
  else
    c.value := v;
  rescanPanel(#TmpData);
}

procedure setCellDoubleValue(c: PD_ICell; v: double);
{
  setCellValue(c, v);
}

tree trData (,,Sci13EscTreeI);
  table TmpData;
  fields
    TmpData.Name 'Статья': [20], protect;
    TmpData.Code 'Код': [5], protect, noAutoSize;
    vFld1  '1 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld2  '2 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld3  '3 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld4  '4 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld5  '5 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld6  '6 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld7  '7 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld8  '8 ': [10,'\2p[|-]366`666`666`666`666.88'], noprotect;
    vFld9  '9 ': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld10 '10': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld11 '11': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld12 '12': [10,'\2p[|-]366`666`666`666`666.88'], protect, editButton;
    vFld13 '13': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld14 '14': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld15 '15': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld16 '16': [10,'\2p[|-]366`666`666`666`666.88'], protect;
    vFld17 '17': [10,'\2p[|-]366`666`666`666`666.88'], protect;
end;

tableevent table TmpData;
  cmTreeTop:
    _cParent := 0;

  cmTreeUp:
    _cParent := TmpData.cParent;

  cmTreeDown:
    _cParent := TmpData.Nrec;

  cmTreeNodeType:
    if TreeIsTerminal(trData)
        TreeSetNodeType(trData, ntfText);

  cmTreeNeedOwner:
    TreeJumpToRecord(trData, TmpData.cParent);
end;

handleevent

  cmInit:
  {
    _context := PD_IContext(new(PD_Context, _PD_Context(17)));

    var p: PD_IParams = _context.params;
    p.cKatStroy := _cKatStroy;
    p.cPlPor := _cPlPor;
    if RunInterface(PD_AGAT::PD_ParamsView, p) != cmDefault {abort; FreeVipInterface(_context); exit;}

    loadData(_context.rows, 0);
    _context.recalcAll;

    RereadRecord(#TmpData);
  }

  cmExprFieldChanged:
  {
    setCellDoubleValue(_context.cellByRowId(TmpData.RowId, getColByFld(curField)), double(ExprFieldValue));
  }

  cmDone:
  {
    var p: PD_IParams = _context.params;
    SaveMyDsk(p.cKatStroy, 'PD_cKatStroy');
    SaveMyDsk(p.cPlPor, 'PD_cPlPor');
  }

  cmOnVipUnLoad:
  {
    _context := nullRef;
  }

end;


end.




VipInterface UserReport_Main Implements IUserReport licensed(free);
Interface UserReport_Main;
  create view dummy;
  procedure Run;
  begin
    var cKatStroy, cPlPor: comp = 0;
    ReadMyDsk(cKatStroy, 'PD_cKatStroy', false);
    ReadMyDsk(cPlPor, 'PD_cPlPor', false);
    runinterfacenomodal(PD_AGAT::PD_Main, cKatStroy, cPlPor);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Распределение платежей';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    if (Level = 1)
      GetGroupName := 'Распределение платежей'
    else
      GetGroupName := '';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := 'B_Finans';
    end;
  end;
end.
