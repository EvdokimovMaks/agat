#include PD_Object.vih
#include PD_ICellSchema.vih
#include Query.vih
#include PD_Context.vih
#include PD_CellSchemaBase.vih

#component "PD_AGAT"

vipInterface PD_CellSchema_SqlQuery(PD_CellSchemaBase) implements PD_ICellSchema, PD_InitByFormPl;

interface PD_CellSchema_SqlQuery;

var
  _isEditable: boolean;
  _q: IQuery;

file f;

create view
var
  _cFormPl: comp;
  _cPlPor: comp;
as select
  FormPl.NRec
from
  FormPl
 ,PlPor
where ((
      _cFormPl == FormPl.NRec
  and _cPlPor  == PlPor.Nrec
));

function isEditable(row: PD_IRow; col: byte; context: PD_IContext): boolean;
{
  #__UNUSED__(row, col, context)
  result := _isEditable;
}

function getValue(row: PD_IRow; col: byte; context: PD_IContext): variant;
{
  if row = nullRef or context = nullRef or col = 0 { result := 0; exit; }

  var d: date = 0;
  var wTiDk: word = 0;

  _cPlPor := context.params.cPlPor;
  if getfirst PlPor = tsOk
  {
    d := PlPor.DatOb;
    wTiDk := PlPor.TiDk;
  }
  else
  {
    d := Cur_Date;
    wTiDk := 0;
  }

  result := _q.setParam('cKatStroy', context.params.cKatStroy)
              .setParam('cPlPor', context.params.cPlPor)
              .setParam('cKatStroyKredit', context.params.cKatStroyKredit)
              .setParam('cPlPorKredit', context.params.cPlPorKredit)
              .setParam('cAddSumTune', row.cAddSumTune)
              .setParam('rowType', row.wType)
              .setParam('date', d)
              .setParam('tidk', wTiDk)
              .setParam('col', col)
              .getResultValue;
}

function setValue(row: PD_IRow; col: byte; value: variant; context: PD_IContext): boolean;
{
  #__UNUSED__(context)
  result := row.setValue(col, value);
}

function initByFormPl(cFormPl: comp): boolean;
{
  result := false;
  _cFormPl := cFormPl;
  if getfirst FormPl != tsOk
    exit;
  _isEditable := FormPl.ResWord[1] = 1;
  if f.OpenMemo(FormPl.Comment)
  {
    var l: longint = 0;
    var s: string = '';
    while not f.EOF
    {
      f.ReadLn(s);
      sqlAddStr(l, s);
    }
    f.Close;
    _q := queryManager.createQuery(l);
  }
  result := _q != nullRef;
}

function setupIfcName: string;
{
  result := 'PD_AGAT::PD_CellSchemaSetup_SqlQuery';
}

property name: string read 'Sql запрос';

end.
