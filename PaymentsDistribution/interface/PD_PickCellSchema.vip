#include PD_ICellSchema.vih

#component "PD_AGAT"

interface PD_PickCellSchema 'Алгоритмы настроек ячеек' escClose, doAccept;
show(,,80, 10);

table struct TmpSchema (
  Nrec: comp,
  Name: string,
  IfcName: string[60]
) with index (
  IDX0 = Nrec (unique, surrogate)
);

var
  _selectedIfcName: string;

create view as
select TmpSchema.Name
from TmpSchema;

parameters _selectedIfcName;

browse brSchema (,,sci1EnEsc);
  table TmpSchema;
  fields
    TmpSchema.Name 'Наименование': [20], protect;
end;

handleevent
  cmDefault:
  {
    _selectedIfcName := TmpSchema.IfcName;
  }
  cmInit:
  {
    if not loadImplementationListByUniquePrefix('PD_CellSchema_')
    {
      message('Ошибка загрузки доступных настроек ячейки', error);
      abort;
      exit;
    }

    var cnt: integer = GetImplementationCount;
    if cnt = 0
    {
      message('Не найдено ни одной настройки ячейки', warning);
      unloadImplementationList;
      abort;
      exit;
    }

    var i: integer = 0;
    var s: string = '';
    for(i := 0; i < cnt; i++)
    {
      s := getImplementationName(i);
      var schema: PD_ICellSchema;
      if getVipRef(schema, s)
        insert TmpSchema
        set TmpSchema.Name := schema.name,
            TmpSchema.IfcName := s;
    }

    unloadImplementationList;
  }
end;

end.
