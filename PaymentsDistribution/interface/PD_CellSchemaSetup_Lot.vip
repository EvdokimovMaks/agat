#component "PD_AGAT"

interface PD_CellSchemaSetup_Lot;
  show(,,80,5);

create view
var
  _cFormPl: comp;
as select
  FormPl.ResName[1] + FormPl.ResName[2] + FormPl.ResName[3] (fieldName=FormulaFld)
from
  FormPl
where ((
  _cFormPl == FormPl.NRec
));

parameters _cFormPl;

screen scTop (,,sci1Esc);
  table FormPl;
  fields
    FormPl.ResWord[1]: noProtect;
    FormulaFld: noProtect;
<<

     [.] Разрешено редактирование ячейки`

 `Формула Lot`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end;

procedure setFormula(s: string);
{
  set FormPl.ResName[1] := SubStr(s,   1, 80);
  set FormPl.ResName[2] := SubStr(s,  81, 80);
  set FormPl.ResName[3] := SubStr(s, 161, 80);
}

handleevent
  cmExprFieldChanged:
  {
    setFormula(ExprFieldValue);
  }
  cmInit:
  {
    if getfirst FormPl != tsOk
    {
      message('Не найдена запись с настройками: FormPl.Nrec = ' + string(_cFormPl), error);
      abort;
      exit;
    }
  }
  cmDone:
  {
    update current FormPl;
  }
end;

end.
