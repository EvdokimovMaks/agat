#component "PD_AGAT"

interface PD_CellSchemaSetup_Lot 'Выражение Lot (Формула)' escClose doAccept;
  show(,,100,5);

create view
var
  _cFormPl: comp;
  _isReadOnly: boolean;
as select
  FormPl.ResName[1] + FormPl.ResName[2] + FormPl.ResName[3] (fieldName=FormulaFld)
from
  FormPl
where ((
  _cFormPl == FormPl.NRec
));

parameters _cFormPl, _isReadOnly;

function isReadOnly: boolean;
{
  result := _isReadOnly;
}

function windowTitle(s: string): string;
{
  result := s + if(isReadOnly, ' (Режим просмотра: изменения не сохраняются)', '');
}

screen scTop (,,sci1Esc);
  table FormPl;
  fields
    FormPl.ResWord[1]: noProtect;
    FormulaFld: noProtect;
<<

     [.] Разрешено редактирование ячейки`

 `Формула Lot`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end;

procedure setFormula(s: string);
{
  set FormPl.ResName[1] := SubStr(s,   1, 80);
  set FormPl.ResName[2] := SubStr(s,  81, 80);
  set FormPl.ResName[3] := SubStr(s, 161, 80);
}

handleevent
  cmExprFieldChanged:
  {
    setFormula(ExprFieldValue);
  }
  cmInit:
  {
    if getfirst FormPl != tsOk
    {
      message('Не найдена запись с настройками: FormPl.Nrec = ' + string(_cFormPl), error);
      abort;
      exit;
    }
    ToggleFieldProtected(#FormPl.ResWord[1], isReadOnly);
    ToggleFieldProtected(#FormulaFld, isReadOnly);
    SetWindowTitle(wnMainWindow, windowTitle('Выражение Lot (Формула)'));
  }
  cmDone:
  {
    if not isReadOnly
      update current FormPl;
    if isModified and isReadOnly
      Message('Режим просмотра: изменения не сохраняются.');
  }
end;

end.
