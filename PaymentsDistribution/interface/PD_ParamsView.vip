#include PD_Params.vih
#include GetKau.vih
#include ExtAttr.vih

#component "PD_AGAT"

interface PD_ParamsView 'Параметры' doAccept, escClose;
show(,,60, 11);

var
  _params: PD_IParams;
#include GetKau.var
#include ExtAttr.var

create view
as select
  ShowKau(cgKau_ObjBuild, _params.cKatStroy) (fieldname = KatStroyFld)
 ,ShowKau(cgKau_ObjBuild, _params.cKatStroyKredit) (fieldname = KatStroyKreditFld)
 ,ShowKau(cgKau_PlPor, _params.cPlPor) (fieldname = PlPorFld)
 ,ShowKau(cgKau_PlPor, _params.cPlPorKredit) (fieldname = PlPorKreditFld)
;

parameters _params;

screen scParams (,,sci1Esc);
  fields
    KatStroyFld: protect, pickButton;
    PlPorFld: protect, pickButton;
    KatStroyKreditFld: protect;
    PlPorKreditFld: protect, pickButton;
    _params.flags: noProtect;
  buttons
    cmOk, default;
    cmCancel;
<<

 `Заказ`              .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 `Платежное поручение`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 `Кредитный заказ`    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 `Пл.пор. кр.заказа  `.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

  Отображать строки   [.] Основной заказ`
                      [.] Кредитный заказ`
                      [.] Итого`

                               <. Продолжить .> <. Отмена .>
>>
end;


procedure updateFields;
{
  if _params.cKatStroy = 0
    _params.cKatStroyKredit := 0;
  else
  {
    var c: comp = oExtAttr.coGetAttrID(coKatStroy, _params.cKatStroy, ATTRNAM_NREC_KATSTROY_KREDIT);
    if _params.cKatStroyKredit = 0 and c != 0
      _params.flags := 1 + 2 + 4;
    _params.cKatStroyKredit := c;
  }

  if _params.cKatStroyKredit = 0
  {
    _params.cPlPorKredit := 0;
    ClearFieldState(#PlPorKreditFld, sfVisible);
    _params.flags := 1;
  }
  else
    SetFieldState(#PlPorKreditFld, sfVisible);

  if (_params.flags and 4) = 4
    _params.flags := 1 + 2 + 4
  else if (_params.flags and 2) = 2
    _params.flags := 1 + 2
  else
    _params.flags := 1;
}


handleevent

  cmOk:
    PutCommand(cmDefault);

  cmPick:
  {
    case CurField of
      #KatStroyFld:
      {
        var c: comp = _params.cKatStroy;
        if iGetKau.GetCodeKau(cgiPick, cgKau_ObjBuild, c) > 0
        {
          _params.cKatStroy := c;
          updateFields;
          RereadRecord;
        }
      }
      #PlPorFld:
      {
        if runinterface(PD_AGAT::PD_PickPlPor, _params.cKatStroy, _params.cPlPor) = cmDefault
        {
          updateFields;
          RereadRecord;
        }
      }
      #PlPorKreditFld:
      {
        if runinterface(PD_AGAT::PD_PickPlPorKredit, _params.cKatStroyKredit, _params.cPlPorKredit) = cmDefault
        {
          updateFields;
          RereadRecord;
        }
      }
    end;
  }

  cmDelOnProtect:
  {
    case CurField of
      #KatStroyFld:
      {
        _params.cKatStroy := 0;
        updateFields;
        RereadRecord;
      }
      #PlPorFld:
      {
        _params.cPlPor := 0;
        updateFields;
        RereadRecord;
      }
      #PlPorKreditFld:
      {
        _params.cPlPorKredit := 0;
        updateFields;
        RereadRecord;
      }
    end;
  }

  cmCheckField:
  {
    updateFields;
    RereadRecord;
  }

  cmInit:
  {
    if _params = nullRef
      _params := PD_IParams(new(PD_Params));
    updateFields;
    RereadRecord;
  }

end;

end.
