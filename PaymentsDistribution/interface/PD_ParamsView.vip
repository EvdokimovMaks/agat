#include PD_Params.vih
#include GetKau.vih
#include ExtAttr.vih

#component "PD_AGAT"

interface PD_ParamsView 'Параметры' doAccept, escClose;
show(,,60, 10);

var
  _params: PD_IParams;
#include GetKau.var
#include ExtAttr.var

create view
as select
  ShowKau(cgKau_ObjBuild, _params.cKatStroy) (fieldname = KatStroyFld)
 ,ShowKau(cgKau_ObjBuild, _params.cKatStroyKredit) (fieldname = KatStroyKreditFld)
 ,ShowKau(cgKau_PlPor, _params.cPlPor) (fieldname = PlPorFld)
;

parameters _params;

screen scParams (,,sci1Esc);
  fields
    KatStroyFld: protect, pickButton;
    KatStroyKreditFld: protect;
    PlPorFld: protect, pickButton;
    _params.flags: noProtect;
  buttons
    cmOk, default;
    cmCancel;
<<

 `Заказ`              .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 `Кредитный заказ`    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 `Платежное поручение`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

  Отображать строки   [.] Основной заказ`
                      [.] Кредитный заказ`
                      [.] Итого`

                               <. Продолжить .> <. Отмена .>
>>
end;

handleevent

  cmOk:
    PutCommand(cmDefault);

  cmPick:
  {
    case CurField of
      #KatStroyFld:
      {
        var c: comp = _params.cKatStroy;
        if iGetKau.GetCodeKau(cgiPick, cgKau_ObjBuild, c) > 0
        {
          _params.cKatStroy := c;
          _params.cKatStroyKredit := oExtAttr.coGetAttrID(coKatStroy, _params.cKatStroy, ATTRNAM_NREC_KATSTROY_KREDIT);
        }
        RereadRecord;
      }
      #PlPorFld:
      {
        if runinterface(PD_AGAT::PD_PickPlPor, _params.cKatStroy, _params.cPlPor) = cmDefault
          RereadRecord;
      }
    end;
  }

  cmDelOnProtect:
  {
    case CurField of
      #KatStroyFld:
      {
        _params.cKatStroy := 0;
        _params.cKatStroyKredit := 0;
        RereadRecord;
      }
      #PlPorFld:
      {
        _params.cPlPor := 0;
        RereadRecord;
      }
    end;
  }

  cmInit:
  {
    if _params = nullRef
      _params := PD_IParams(new(PD_Params));
    RereadRecord;
  }

end;

end.
