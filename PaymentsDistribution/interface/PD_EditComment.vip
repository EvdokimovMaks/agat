#component "PD_AGAT"

interface PD_EditComment 'Редактирование комментария' escClose;
show(,,100,18);

var
  _cRow: comp;
  _cKatStroy: comp;
  _colIndex: comp;

create view
as select
  FormPl.Nrec
from
  FormPl
 ,KatStroy
 ,PD_Comment Comm
 ,PD_Comment CommLast
where ((
      _cRow      == FormPl.Nrec

  and _cKatStroy == KatStroy.Nrec

  and _cRow      == Comm.cRow
  and _cKatStroy == Comm.cKatStroy
  and _colIndex  == Comm.ColIndex

  and _cRow      == CommLast.cRow
  and _cKatStroy == CommLast.cKatStroy
  and _colIndex  == CommLast.ColIndex
));

parameters _cKatStroy, _cRow, _colIndex;

screen scParams;
  show(,,,3) fixed_y;
  fields
    FormPl.Name: Skip;
    KatStroy.Name: Skip;
    if(_colIndex > 0, 'Столбец ' + _colIndex, ''): Skip;
<<
  `Строка`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  `Заказ `.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end;

panel pnComment;
  table Comm;

  browse brComment (,,sci178Esc);
    show(,4,,9);
    fields
      Comm.Npp '№ п/п': [5], NoProtect, NoAutoSize;
      Comm.Name 'Наименование': [50], NoProtect;
  end; // browse brComment

  text Comm.Mem: OnlyOneDoc;
    show(,10,,);
end; //panel pnComment

function lastNPP: word;
{
  result := if(getLast CommLast = tsOk, CommLast.Npp, 0);
}

tableevent table Comm;
  cmSetDefault:
  {
    ClearBuffer(#Comm);
    Comm.cRow := _cRow;
    Comm.cKatStroy := _cKatStroy;
    Comm.ColIndex := _colIndex;
    Comm.Npp := lastNPP + 1;
  }
  cmInsertRecord: insert current Comm;
  cmUpdateRecord: update current Comm;
  cmDeleteRecord: delete current Comm;
end;

handleevent

  cmInit:
  {
    if getfirst FormPl != tsOk
    {
      Message('Не найдена строка FormPl.Nrec=' + string(_cRow,0,0), error);
      Abort;
    }
    if getfirst KatStroy != tsOk
    {
      Message('Не найден заказ KatStroy.Nrec=' + string(_cKatStroy,0,0), error);
      Abort;
    }
    SetWindowTitle(wnMainWindow, 'Редактирование комментария: ')
  }

end;

end.
