#include GlobalGoods.vih
#include Params.vih

#component "AGAT"

interface GlobalGoodsColFillerKatsopr;

sql query qSelect =
  select distinct sps.cmcusl as nrec
  from katsopr ks
  join spsopr sps on sps.csopr = ks.nrec and sps.prmc = 1
  where ks.nrec = :katsoprNrec
;

function GetParam(aParams: IParams; aName: string; aDefaultValue: variant): variant;
{
  if aParams = nullref
    result := aDefaultValue
  else
    result := aParams.GetParam(aName, aDefaultValue);
}

procedure IGlobalGoodsCollectionFiller.FillCollection(aCollection: IGlobalGoodsCollection; aParams: IParams = nullref);
{
  if aCollection = nullref exit;

  var katsoprNrec: comp;
  katsoprNrec := GetParam(aParams, 'Nrec', 0);
  if katsoprNrec = 0 exit;

  var q: IQuery;
  q := queryManager.createQuery(qSelect).setParam('katsoprNrec', katsoprNrec);

  sql_loop q
  {
    aCollection.ClearCurrent;
    aCollection.KatmcNrec := q.Row.Val('nrec');
    aCollection.doInsert;
  }
}
end.
