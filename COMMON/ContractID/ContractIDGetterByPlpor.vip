#include ContractID.vih
#include  Query.vih
#component "AGAT"

vipinterface ContractIDGetterByPlpor implements IContractIDGetter;

handler RegContractIDGetterByPlpor on extensionpoint epRegisterContractIDGetter(factory: ContractIDGetterFactory) [1]
action
{
  factory.RegisterGetterIfc(GetContractIDKey_ByPlpor, 'AGAT::ContractIDGetterByPlpor');
  result := true;
}

interface ContractIDGetterByPlpor;

create view
var
  plporNrec: comp;

  (ContractIdFld)
as select
  if(isValidAll(tnExClassSeg), exclassseg.Name, '')
from plpor, usersdoc, cashbank, katbank, attrval, exclassseg
where ((
  plporNrec                == plpor.Nrec        and
  plpor.tidkgal            == usersdoc.tipgal   and
  plpor.tidk               == usersdoc.tipusers and
  usersdoc.cCashBank       == cashbank.nrec     and
  cashbank.cpodr           == katbank.nrec      and
  coKatBank                == attrval.wTable    and
  katbank.nrec             == attrval.cRec      and
  ATTRNAM_NREC_KATBANK_IGK == attrval.cAttrNam  and
  attrval.vComp            == exclassseg.nrec
));

function GetContractID(cRec: comp): string;
{
  result := '';
  set plporNrec := cRec;
  rereadrecord(tnExClassSeg);
  result := ContractIdFld;
}
end.
