#include ContractID.vih
#include  Query.vih
#component "AGAT"

vipinterface ContractIDGetterByDogovor implements IContractIDGetter;

handler RegContractIDGetterByDogovor on extensionpoint epRegisterContractIDGetter(factory: ContractIDGetterFactory) [1]
action
{
  factory.RegisterGetterIfc(GetContractIDKey_ByDogovor, 'AGAT::ContractIDGetterByDogovor');
  result := true;
}

interface ContractIDGetterByDogovor;

sql query query1 =
select trim(avIK.vstring) as IK
from attrval avZakaz
join attrnam anIK on anIK.wtable = 2101 and anIK.name = :eaNameIK
join attrval avIK on avIK.wtable = 2101 and avIK.crec = avZakaz.vcomp and avIK.cattrnam = anIK.nrec and trim(avIK.vstring) <> ''
where avZakaz.wtable = 1707 and avZakaz.crec = :cRec and avZakaz.cattrnam = #comp(00010000000000ABh) and avZakaz.vcomp <> #comp(0)
union
select trim(avIK.vstring)
from dogovor d
join spdocs spd on d.tidk = spd.tidk and d.nrec = spd.cdoc
join specmtr spm on spm.csaldtune=#comp(0001000000000001h) and spm.cotable=1723 and spm.cspec=spd.nrec and spm.cobj<>#comp(0)
join attrnam anIK on anIK.wtable = 2101 and anIK.name = :eaNameIK
join attrval avIK on avIK.wtable = 2101 and avIK.crec = spm.cobj and avIK.cattrnam = anIK.nrec and trim(avIK.vstring) <> ''
where d.nrec = :cRec ;

var
  q: IQuery;
  eaNameIK: string;
  dogovorNrec: comp;

function GetContractID(cRec: comp): string;
{
  var IK: string; IK := '';

  if (q = nullref)
  {
    eaNameIK := EXTATTRNAME_CONTRACTID;
    q := queryManager.createQuery(query1);
    q.setParam('eaNameIK', eaNameIK);
    q.setParam('cRec', dogovorNrec);
  }

  dogovorNrec := cRec;

  if q.Execute.errorCode = tsOk
  {
    while q.fetch.errorCode = tsOk
    {
      IK := IK + if(IK='', '', ', ') + q.row.valat(1);
    }
    result := IK;
  }
  else
    result := 'Ошибка выполнения запроса '+q.errorCode;
}
end.
