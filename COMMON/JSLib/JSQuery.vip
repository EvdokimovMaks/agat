#include Query.vih
#include JSQuery.vih

#component ""

interface JSQuery;
var
  _query: IQuery;
  _paramNames : array[0..0] of string;
  _paramValues: array[0..0] of variant;
  _resultSet: IResultSet;

procedure Init(sqlStr: variant);
{
  _query := queryManager.createQuery(sqlStr);
  _resultSet := nullref;
  setLimit(_paramNames, 0);
  setLimit(_paramValues, 0);
}

function  GetResultSet: IResultSet;
{
  _resultSet := nullref;

  if _query != nullref
    _resultSet := _query.getResultSet;

  result := _resultset;
}

procedure SetParam(paramName: string; paramValue: variant);
{
  var ind: longint;
  ind := FindItem(_paramNames, paramName);

  if ind = -1
  {
    ind := count(_paramNames);
    _paramNames[ind] := paramName;
  }

  _paramValues[ind] := paramValue;

  if _query != nullref
  {
    case VType(_paramValues[ind]) of
      VT_I8  : _query.setParam(paramName, comp(_paramValues[ind]));
      VT_UI2 : _query.setParam(paramName, word(_paramValues[ind]));
      VT_R8  : _query.setParam(paramName, double(_paramValues[ind]));
      VT_DATE: _query.setParam(paramName, strtodate(string(_paramValues[ind]),'DD/MM/YYYY'));
      VT_BOOL: _query.setParam(paramName, boolean(_paramValues[ind]));
    else
      _query.setParam(paramName, string(_paramValues[ind]));
    end;
  }
}

property Query: IQuery absolute _query read;
property ResultSet: IResultSet absolute _resultSet read;

end.
