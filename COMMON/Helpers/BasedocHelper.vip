#include BasedocHelper.vih
#include Query.vih
#include AtlProtocol.vih

#component "AGAT"


interface BasedocHelper;

const
  BASEDOC_CNOTE_FULLPAID = 0001000000000077h;
  BASEDOC_CNOTE_ISPOLN = 400056A46A9AD669h;
end;

sql query qSelectSumOplNoExcludes =
  select sum(basefin.summa)
  from basefin
  join plpor on plpor.nrec = basefin.cplpor and plpor.datob > 0
  where ( basefin.cbasedoc=:BasedocNrec or basefin.cpredopldo=:BasedocNrec )
;

var
  _qSumOplNoExcludes: IQuery;
  _sGroupSch: string;

create view
as select *
from basedoc, katnotes, statlog, GroupSch, IerGrSch, SpGrSch;

constructor Init;
{
  _qSumOplNoExcludes := queryManager.createQuery(qSelectSumOplNoExcludes);
  result := true;
}

function RecalcBasedocStatusPrepareQuery(aExcludeBasefin: TCompArray): IQuery;
{
  var cnt: longint; cnt := count(aExcludeBasefin);
  if cnt = 0
    result := _qSumOplNoExcludes;
  else
  {
    var s: longint; s := 0;
    sqlAddStr(s, qSelectSumOplNoExcludes);
    if cnt = 1
      sqlAddStr(s, 'and basefin.nrec <> #comp('+string(aExcludeBasefin[0],0,0)+')');
    else
    {
      var i: longint; i := 0;
      sqlAddStr(s, 'and basefin.nrec not in (');
      for(i := 0; i < cnt; i++)
        sqlAddStr(s, if(i>0,',','') + '#comp(' + string(aExcludeBasefin[i],0,0) + ')');
      sqlAddStr(s, ')');
    }
    result := queryManager.createQuery(s);
  }
}

procedure IBasedocHelper.RecalcBasedocStatus(aBasedocNrec: comp; aExcludeBasefin: TCompArray);
{
  if getfirst fastfirstrow basedoc where ((aBasedocNrec == basedoc.nrec)) = tsOk
  {
    var q: IQuery;
    q := RecalcBasedocStatusPrepareQuery(aExcludeBasefin);
    q.setParam('BasedocNrec', aBasedocNrec);
    var sumOpl: double;
    sumOpl := q.getResultValue;
    var fullPaid: boolean;
    fullPaid := (sumOpl-basedoc.total) > -1;
    if fullPaid and basedoc.cnote != BASEDOC_CNOTE_FULLPAID
    {
      basedoc.cnote := BASEDOC_CNOTE_FULLPAID;
      update current basedoc;
    }
    else if (not fullPaid) and basedoc.cnote = BASEDOC_CNOTE_FULLPAID
    {
      if getlast StatLog where ((40 == StatLog.DocType and basedoc.nrec == StatLog.cDoc)) = tsOk
        basedoc.cnote := StatLog.cNewNote;
      else
        basedoc.cnote := BASEDOC_CNOTE_ISPOLN;
      update current basedoc;
    }
  }
}

function IBasedocHelper.GetBasedocViza(aBasedocNrec: comp; aBasedocVizaAttrnamNrec: comp): IBasedocViza;
{
  result := IBasedocViza(new(BasedocVizaBase, BasedocVizaBase(aBasedocNrec, aBasedocVizaAttrnamNrec)));
}

function IBasedocHelper.GetBasedocVizas(aBasedocNrec: comp): IBasedocVizas;
{
  result := IBasedocVizas(new(BasedocVizasBase, BasedocVizasBase(aBasedocNrec)));
}

function IBasedocHelper.GetDeferredDaysCount(aBasedocNrec: comp): word;
{
  result := 0;

  if getfirst fastfirstrow basedoc where ((aBasedocNrec == basedoc.nrec)) = tsOk
  {
    if  basedoc.cnote != 000100000000007Eh //когда статус ДО не "включен в реестр"
    and basedoc.cnote != 0001000000000077h //и не "оплачено"
    {
      var vizas: IBasedocVizas;
      vizas := GetBasedocVizas(aBasedocNrec);
      if vizas.Soglasovany
      {
        var d: date; d := 0;

        //if (d < vizas.BUH.LastDate) d := vizas.BUH.LastDate;
        //if (d < vizas.KAZ.LastDate) d := vizas.KAZ.LastDate;
        //if (d < vizas.PEU.LastDate) d := vizas.PEU.LastDate;
        //if (d < vizas.DIR.LastDate) d := vizas.DIR.LastDate;

        d := vizas.PEU.LastDate;

        if d > 0
          result := calcdaysbetweendates(d, cur_date, true);
      }
    }
  }

}

function IBasedocHelper.SetStatus(aBasedocNrec: comp; aKatnotesNrec: comp; aCreateHistory: boolean = false; aComment: string = ''): boolean;
{

  result := false;

  if getfirst basedoc where ((aBasedocNrec == basedoc.nrec)) = tsOk
  {

    var wNewStatus: word = 0;
    if getfirst katnotes where ((aKatnotesNrec == katnotes.nrec)) = tsOk
      wNewStatus := katnotes.status;
    else
      wNewStatus := 0;

    var oldKatnotesNrec: comp;
    oldKatnotesNrec := basedoc.cnote;

    var wOldStatus: word;
    wOldStatus := basedoc.status;

    var rslt: integer = 0;
    basedoc.cnote := aKatnotesNrec;
    basedoc.status := wNewStatus;
    rslt := update current basedoc;

    if rslt = tsOk and aCreateHistory
    {
      clearbuffer(#statlog);
      statlog.cdoc      := basedoc.nrec;
      statlog.cnewnote  := aKatnotesNrec;
      statlog.coldnote  := oldKatnotesNrec;
      statlog.comment   := aComment;
      statlog.descr     := sGetTune('USER.DESCR');
      statlog.desgr     := sGetTune('USER.DESGR');
      statlog.doctype   := 40;
      statlog.doper     := cur_date;
      statlog.newstatus := wNewStatus;
      statlog.oldstatus := wOldStatus;
      statlog.timeoper  := cur_time;
      insert current statlog;
    }

    result := rslt = tsOk;

  }

}

function IBasedocHelper.SetStatusValidate(aBasedocBuffer: type$basedoc; aKatnotesNrec: comp; protocol: IAtlProtocol = nullref): boolean;
{

  result := true;

  //Если пользователь - админ - ничего не проверяем
  if pr_CurUserAdmin exit;

  //инициализируем протокол, если он не передан в качестве параметра
  if protocol = nullref
    protocol := IAtlProtocol(new(AtlProtocol));

  //Проверяем корректность ссылки на статус
  if (getfirst katnotes where ((aKatnotesNrec == katnotes.nrec)) != tsOk) and aKatnotesNrec != 0
  {
    result := false;
    protocol.LogLine(APE_ERROR, 'Установка несуществующего статуса');
  }

  case aKatnotesNrec of

    KATNOTES_NREC_BASEDOC_REESTR:
    {

      var vizas: IBasedocVizas;
      vizas := GetBasedocVizas(aBasedocBuffer.nrec);

      if not (vizas.BUH.Soglasovana and vizas.DIR.Soglasovana and vizas.KAZ.Soglasovana)
        result := false;

      if not vizas.BUH.Soglasovana
        protocol.LogLine(APE_ERROR, 'Запрещено включать в реестр счет без визы бухгалтерии');

      if not vizas.DIR.Soglasovana
        protocol.LogLine(APE_ERROR, 'Запрещено включать в реестр счет без визы дирекции');

      if not vizas.KAZ.Soglasovana
        protocol.LogLine(APE_ERROR, 'Запрещено включать в реестр счет без визы казначейства');

    }

  end;

}

function IBasedocHelper.GetBuffer(aNrec: comp): type$basedoc;
{

  var buf: type$basedoc;

  if getfirst basedoc where ((aNrec == basedoc.nrec)) = tsOk
    buf := type$basedoc(basedoc.buffer);
  else
    clearadvrecord(buf);

  result := buf;

}


//--------------------------------------------------------------
window wAddToGroup 'Включение ДО в группу' escClose doAccept;
show(,,50,5);

screen scAddToGroup;
fields
  _sGroupSch: noprotect, pickbutton;
buttons
  cmOk, default;
  cmCancel;
<<

`Наименование группы` .@@@@@@@@@@@@@@@@@@@@@@@@@@

                          <.Включить.> <.Отмена.>
>>
end;

handleevent
  cmOk: PutCommand(cmDefault);
  cmPick:
  {
    if curfield = #_sGroupSch
    {
      var c: comp = 0;
      if RunInterface(L_GRSCH::GETGROUPSCH, 3, 3, 3, false, c, false, 0) = cmDefault
        if getfirst GroupSch where ((c == GroupSch.Nrec)) = tsOk
          _sGroupSch := GroupSch.Name;
      rereadrecord;
    }
  }
end;

end;

procedure AddBasedocToGroupUI(sGroupSch: string; basedocMarker: TPtr);
{
  if RunWindowModal(wAddToGroup) = cmDefault
  {
    if getfirst GroupSch where ((_sGroupSch == GroupSch.Name)) != tsOk
    {
      ClearBuffer(#GroupSch);
      GroupSch.Name := _sGroupSch;
      GroupSch.cNode := GROUPSCH_NREC_USERSETS;
      insert current GroupSch;
      ClearBuffer(#IerGrSch);
      IerGrSch.cUpRec := GroupSch.Nrec;
      IerGrSch.cRec := GroupSch.Nrec;
      IerGrSch.IsLeaf := 1;
      insert current IerGrSch;
      ClearBuffer(#IerGrSch);
      IerGrSch.cUpRec := GROUPSCH_NREC_USERSETS;
      IerGrSch.cRec := GroupSch.Nrec;
      IerGrSch.IsLeaf := 1;
      insert current IerGrSch;
    }

    var i: longint = 0;
    var c: comp = 0;
    for(i := 0; GetMarker(basedocMarker, i, c); i++)
    {
      if getfirst SpGrSch where ((GroupSch.Nrec == SpGrSch.cGroupSch and
                                  0             == SpGrSch.wList and
                                  c             == SpGrSch.cBaseDoc)) != tsOk
      {
        ClearBuffer(#SpGrSch);
        SpGrSch.cGroupSch := GroupSch.Nrec;
        SpGrSch.cBaseDoc := c;
        insert current SpGrSch;
      }
    }
    message('Установка группы завершена');
  }
}

end.
