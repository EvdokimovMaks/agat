#include BaseDocHelper.vih
#include PD_ChangeStatusTxo.vih
#include AccessRightsHelper.vih
#include Query.vih
#include AtlProtocol.vih
#include ExtAttr.vih
#include SDFuns.vih
#include ClndrDog.vih
#include Notes.vih
#include StatLog.vih
#include GetSNote.vih
#include SpDocs.vih

#component "AGAT"


interface BaseDocHelper;

const
  BaseDoc_CNOTE_FULLPAID = 0001000000000077h;
  BaseDoc_CNOTE_ISPOLN = 400056A46A9AD669h;
end;

sql query qSelectSumOplNoExcludes =
  select sum(basefin.summa)
  from basefin
  join plpor on plpor.nrec = basefin.cplpor and plpor.datob > 0
  where ( basefin.cBaseDoc=:BaseDocNrec or basefin.cpredopldo=:BaseDocNrec )
;

var
  _cBaseDoc: comp;
  _cKatNotes: comp;
  _qSumOplNoExcludes: IQuery;
  _sGroupSch: string;
  _changeStatusTxo: PD_ChangeStatusTxo;
  _accessRights: AccessRightsHelper;
  #include ExtAttr.var
  #include SDFuns.var
  #include ClndrDog.var
  #include Notes.var
  #include StatLog.var
  #include SpDocs.var

create view
as select
  BaseDoc.nrec
from
  BaseDoc
 ,Dogovor
 ,StepDoc
 ,KatSopr
 ,KatNotes
 ,StatLog
 ,GroupSch
 ,IerGrSch
 ,SpGrSch
 ,SpStep
where ((
      _cBaseDoc    == BaseDoc.Nrec
  and _cKatNotes   == KatNotes.Nrec
  and BaseDoc.Nrec == StepDoc.cBaseDoc
  and Stepdoc.Nrec == SpStep.cStepDoc
  and Stepdoc.Nrec == KatSopr.cStepDoc
  and 40           == StatLog.DocType
  and BaseDoc.nrec == StatLog.cDoc
));

function sBaseDoc(sNoDoc: string; dDDoc: date): string;
{
  result := 'ДО №' + sNoDoc + ' от ' + dDDoc;
}

constructor Init;
{
  _qSumOplNoExcludes := queryManager.createQuery(qSelectSumOplNoExcludes);
  result := true;
}

function RecalcBaseDocStatusPrepareQuery(aExcludeBasefin: TCompArray): IQuery;
{
  var cnt: longint; cnt := count(aExcludeBasefin);
  if cnt = 0
    result := _qSumOplNoExcludes;
  else
  {
    var s: longint; s := 0;
    sqlAddStr(s, qSelectSumOplNoExcludes);
    if cnt = 1
      sqlAddStr(s, 'and basefin.nrec <> #comp('+string(aExcludeBasefin[0],0,0)+')');
    else
    {
      var i: longint; i := 0;
      sqlAddStr(s, 'and basefin.nrec not in (');
      for(i := 0; i < cnt; i++)
        sqlAddStr(s, if(i>0,',','') + '#comp(' + string(aExcludeBasefin[i],0,0) + ')');
      sqlAddStr(s, ')');
    }
    result := queryManager.createQuery(s);
  }
}

procedure IBaseDocHelper.RecalcBaseDocStatus(aBaseDocNrec: comp; aExcludeBasefin: TCompArray);
{
  _cBaseDoc := aBaseDocNrec;
  if getfirst fastfirstrow BaseDoc != tsOk exit;

  var q: IQuery = RecalcBaseDocStatusPrepareQuery(aExcludeBasefin);
  q.setParam('BaseDocNrec', aBaseDocNrec);
  var sumOpl: double = q.getResultValue;

  var fullPaid: boolean = (sumOpl-BaseDoc.total) > -1;

  if fullPaid and BaseDoc.cNote != BaseDoc_CNOTE_FULLPAID
  {
    _changeStatusTxo.process(40, BaseDoc.nrec, BaseDoc.cNote, BASEDOC_CNOTE_FULLPAID);
    BaseDoc.cNote := BASEDOC_CNOTE_FULLPAID;
    update current BaseDoc;
  }
  else if (not fullPaid) and BaseDoc.cNote = BASEDOC_CNOTE_FULLPAID
  {
    if getlast StatLog where ((40 == StatLog.DocType and BaseDoc.nrec == StatLog.cDoc)) = tsOk
    {
      _changeStatusTxo.process(40, BaseDoc.nrec, BaseDoc.cNote, StatLog.cNewNote);
      BaseDoc.cNote := StatLog.cNewNote;
    }
    else
    {
      _changeStatusTxo.process(40, BaseDoc.nrec, BaseDoc.cNote, BaseDoc_CNOTE_ISPOLN);
      BaseDoc.cNote := BaseDoc_CNOTE_ISPOLN;
    }
    update current BaseDoc;
  }
}

function IBaseDocHelper.GetBaseDocViza(aBaseDocNrec: comp; aBaseDocVizaAttrnamNrec: comp): IBaseDocViza;
{
  result := IBaseDocViza(new(BaseDocVizaBase, BaseDocVizaBase(aBaseDocNrec, aBaseDocVizaAttrnamNrec)));
}

function IBaseDocHelper.GetBaseDocVizas(aBaseDocNrec: comp): IBaseDocVizas;
{
  result := IBaseDocVizas(new(BaseDocVizasBase, BaseDocVizasBase(aBaseDocNrec)));
}

function IBaseDocHelper.GetDeferredDaysCount(aBaseDocNrec: comp): word;
{
  result := 0;

  _cBaseDoc := aBaseDocNrec;
  if getfirst fastfirstrow BaseDoc != tsOk exit;

  if  BaseDoc.cnote != 000100000000007Eh //когда статус ДО не "включен в реестр"
  and BaseDoc.cnote != 0001000000000077h //и не "оплачено"
  {
    var vizas: IBaseDocVizas = GetBaseDocVizas(aBaseDocNrec);
    if vizas.Soglasovany
    {
      var d: date; d := 0;

      //if (d < vizas.BUH.LastDate) d := vizas.BUH.LastDate;
      //if (d < vizas.KAZ.LastDate) d := vizas.KAZ.LastDate;
      //if (d < vizas.PEU.LastDate) d := vizas.PEU.LastDate;
      //if (d < vizas.DIR.LastDate) d := vizas.DIR.LastDate;

      d := vizas.PEU.LastDate;

      if d > 0
        result := CalcDaysBetweenDates(d, cur_date, true);
    }
  }
}

function RunPostStatusChangeAlg(ccNote: comp; isBefore: boolean): boolean;
{
  result := false;

  var pAlg: comp = if(BaseDoc.Direct = 1,
                      coGetTune(if(IsBefore, 'Doc.Alg.Before.SellDO', 'Doc.Alg.After.SellDO')),
                      coGetTune(if(IsBefore, 'Doc.Alg.Before.BuyDO' , 'Doc.Alg.After.BuyDO')));

  if (pAlg <> 0)
  {
    StartNewVisual(vtRotateVisual, vfScreenBottom, '', 0);

    var wResult: word = RunInterface('M_MnPlan::SimpleWayToRunAlgorithm', pAlg, 1201, BaseDoc.nRec, ccNote);

    StopVisual('', 0);

    case wResult of
      cmNo, cmDefault, cmCancel: exit;
    end;
  }
  result := true;
}

function IBaseDocHelper.SetStatus(aBaseDocNrec: comp; aKatnotesNrec: comp; aCreateHistory: boolean = false; aComment: string = ''): boolean;
{

  result := false;

  _cBaseDoc := aBaseDocNrec;
  if getfirst BaseDoc != tsOk exit;

  _cKatNotes := aKatnotesNrec;
  var wNewStatus: word = if(getfirst KatNotes = tsOk, KatNotes.Status, 0);

  var oldKatnotesNrec: comp = BaseDoc.cnote;
  var wOldStatus: word = BaseDoc.status;

  var rslt: integer = 0;
  BaseDoc.cnote  := aKatnotesNrec;
  BaseDoc.status := wNewStatus;
  update current BaseDoc;

  if not RunPostStatusChangeAlg(BaseDoc.cNote, False)
  {
    BaseDoc.cNote := oldKatnotesNrec;
    BaseDoc.Status := wOldStatus;
    update current BaseDoc;
    exit;
  }

  if rslt = tsOk and aCreateHistory
    oStatLog.StatLog_InsNewStat(40, BaseDoc.nRec, wOldStatus, oldKatnotesNrec, BaseDoc.Status, BaseDoc.cNote, aComment);

  if oNotes.Notes_InUseStatus(BaseDoc.cNote) <> oNotes.Notes_InUseStatus(oldKatnotesNrec)
  {
    if getfirst StepDoc = tsOk
      _loop SpStep
        if SpStep.cSpDocs <> 0
          oSpDocs.ReCalcOstatok(SpStep.cSpDocs);
  }

  if rslt = tsOk
    _changeStatusTxo.process(40, BaseDoc.nrec, oldKatnotesNrec, aKatnotesNrec);

  result := rslt = tsOk;
}

function IBaseDocHelper.SetStatusValidate(aBaseDocBuffer: type$BaseDoc; aKatnotesNrec: comp; protocol: IAtlProtocol = nullref): boolean;
{

  result := true;

  //Если пользователь - админ - ничего не проверяем
  if pr_CurUserAdmin exit;

  //инициализируем протокол, если он не передан в качестве параметра
  if protocol = nullref
    protocol := IAtlProtocol(new(AtlProtocol));

  //Проверяем корректность ссылки на статус
  _cKatNotes := aKatnotesNrec;
  if (getfirst katnotes != tsOk) and aKatnotesNrec != 0
  {
    result := false;
    protocol.LogLine(APE_ERROR, 'Установка несуществующего статуса');
  }

  case aKatnotesNrec of
    KATNOTES_NREC_BASEDOC_REESTR:
    {
      if not _accessRights.isUserInGroup(_accessRights.GroupByName('s_реестр'))
      {
        var vizas: IBaseDocVizas = GetBaseDocVizas(aBaseDocBuffer.nrec);

        if not (vizas.BUH.Soglasovana and vizas.DIR.Soglasovana and vizas.KAZ.Soglasovana)
          result := false;

        if not vizas.BUH.Soglasovana
          protocol.LogLine(APE_ERROR, 'Запрещено включать в реестр счет без визы бухгалтерии');

        if not vizas.DIR.Soglasovana
          protocol.LogLine(APE_ERROR, 'Запрещено включать в реестр счет без визы дирекции');

        if not vizas.KAZ.Soglasovana
          protocol.LogLine(APE_ERROR, 'Запрещено включать в реестр счет без визы казначейства');
      }
    }
  end;

  if (aBaseDocBuffer.Status = 1) // оформляемый
  {
    if not oSDfuns.ControlDogovorRef(aBaseDocBuffer.Nrec, 3)
    {
      protocol.logLine(APE_ERROR, sBaseDoc(aBaseDocBuffer.NoDoc, aBaseDocBuffer.DDoc) + ': ошибка контроля наличия договора');
      result := false;
    }

    if not oCF.CtrlBaseDocUnderDog(aBaseDocBuffer, TRUE, TRUE)
    {
      protocol.logLine(APE_ERROR, sBaseDoc(aBaseDocBuffer.NoDoc, aBaseDocBuffer.DDoc) + ': ошибка контроля превышения суммы по договору');
      result := false;
    }
  }

  if not RunPostStatusChangeAlg(aBaseDocBuffer.cNote, True)
  {
    protocol.logLine(APE_WARNING, sBaseDoc(aBaseDocBuffer.NoDoc, aBaseDocBuffer.DDoc) + ': Изменение статуса запрещено по результатам выполнения алгоритма.');
    result := false;
  }
}

function IBaseDocHelper.GetBuffer(aNrec: comp): type$BaseDoc;
{

  var buf: type$BaseDoc;

  _cBaseDoc := aNrec;
  if getfirst BaseDoc = tsOk
    buf := type$BaseDoc(BaseDoc.buffer);
  else
    clearadvrecord(buf);

  result := buf;

}

procedure IBaseDocHelper.SetNaznPl(aBaseDocNrec: comp);
{
  _cBaseDoc := aBaseDocNrec;
  if getfirst BaseDoc != tsOk exit;

  var s1,s2,s3: string = '';

  var cDDS: comp = oExtAttr.coGetAttrID(coBaseDoc, BaseDoc.nrec, ATTRNAM_NREC_BASEDOC_DDS);
  var DDS: string = trim(if(getanykau(cgAkSimple, KODGRKAU_DDS, cDDS), givenanname(cgAnStrKod), ''));

  var cIsp: comp = oExtAttr.coGetAttrID(coBaseDoc, BaseDoc.nrec, ATTRNAM_NREC_BASEDOC_ISPOLNITEL_DOGOVORA);
  var Isp: string = trim(if(getanykau(cgAkSimple, cgKau_KatPodr, cIsp), givenanname(cgAnAbbr), ''));

  var cAvans: comp = oExtAttr.coGetAttrID(coBaseDoc, BaseDoc.nrec, ATTRNAM_NREC_BASEDOC_AVANS);
  var Avans: boolean = upcase(trim(if(getanykau(cgAkSimple, KODGRKAU_PRINADLEJNOST_AVANSU, cAvans), givenanname(cgAnStrKod),''))) = 'А';

  var Predopl: boolean = BaseDoc.VidDoc = 111;

  var Prim: string = Trim(BaseDoc.Name);
  //преобразуем "Примечание" в "примечание"
  Prim := LoCase(Prim[1]) + SubStr(Prim, 2, 255);

  var Dog: string = if(getfirst Dogovor where ((BaseDoc.cDogovor == Dogovor.Nrec)) = tsOk,
                       'договор ' + Dogovor.NoDoc + ' от ' + DateToStr(Dogovor.DDoc, 'DD/MM/YY'),
                       '');

  var Schet: string = 'По счету №' + BaseDoc.Nodoc + ' от ' + DateToStr(BaseDoc.DDoc,'DD/MM/YY');

  var Etap: string = '';

  var Akt: string = '';
  if getfirst StepDoc = tsOk
    _loop KatSopr
      Akt += if(Akt = '', '', ', ') +
             if(KatSopr.VidSopr = 101, 'накладная', 'акт') +
             ' №' + KatSopr.NSopr +
             ' от ' + DateToStr(KatSopr.DSopr,'DD/MM/YY');

  case Isp of
  '44201':
  {
    case DDS of
      '204001','204002','204003','204004','204005','204006','204007','204008','204009','210301', '210303', '210800', '403002':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := if(Avans and Predopl, 'аванс', '');
      }
    end;
  }
  '44202':
  {
    case DDS of
      '204001':
      {
        s1 := Schet + if(Dog <> '', ' ' + Dog, '');
        s2 := Prim;
        s3 := if(Avans, 'аванс', '');
      }
      '206006':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := if(Avans and Predopl, 'аванс', '');
      }
    end;
  }
  '100', '300', '480', '490', '900':
  {
    case DDS of
      '205000':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := Dog + if(Etap<>'', ' ' + Etap, '') + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
      }
    end;
  }
  '090':
  {
    case DDS of
      '205000':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := Dog + if(Etap<>'', ' ' + Etap, '') + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
      }
      '210407', '210701', '210702':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := if(Avans and Predopl, 'аванс', '');
      }
      '206002':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := Dog + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
      }
    end;
  }
  '777':
  {
    case DDS of
      '210305', '210306':
      {
        s1 := Schet;
        s2 := Prim;
      }
      '210404', '210407':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := Dog + if(Avans, ' аванс', '') + if(Akt<>'', ' ' + Akt, '');
      }
    end;
  }
  '77706':
  {
    case DDS of
      '209001', '209002', '209004':
      {
        s1 := Schet;
        s2 := Prim;
      }
    end;
  }
  '77703':
  {
    case DDS of
      '210602':
      {
        s1 := Schet + if(Dog<>'', ' '+Dog, '');
        s2 := Prim;
        s3 := if(Avans, 'аванс', '') + if(Akt<>'', ' ' + Akt, '');
      }
    end;
  }
  '762':
  {
    case DDS of
      '207101', '207102', '207103', '208002':
      {
        s1 := Schet;
        s2 := Prim;
        s3 := Dog;
      }
    end;
  }
  '640':
  {
    case DDS of
      '401201', '401202', '401400':
      {
        s1 := Schet + if(Dog<>'', ' '+Dog, '');
        s2 := Prim;
        s3 := if(Avans, 'аванс', '');
      }
      '401101', '401102', '401301', '401302', '403001', '403003', '404000':
      {
        s1 := Schet + if(Dog<>'', ' '+Dog, '');
        s2 := Prim;
        s3 := if(Avans, 'аванс', '') + if(Akt<>'', ' ' + Akt, '');
      }
    end;
  }
  end;

  if s1='' and s2='' and s3=''
  {
    s1 := Schet;
    s2 := Prim;
  }

  if (BaseDoc.Nds > 0)
    s3 += if(s3<>'',' ','') + 'НДС ' + doubletostr(round(BaseDoc.nds,2),'[|-]3666 666 666 666.88')
  else
    s3 += if(s3<>'',' ','') + 'НДС не облагается';

  oExtAttr.sSetAttrID(coBaseDoc, aBaseDocNrec, ATTRNAM_NREC_BASEDOC_NAZNPL,
                      s1+if(s2<>'', ' '+s2,'')+if(s3<>'',' '+s3,''));
}


//--------------------------------------------------------------
window wAddToGroup 'Включение ДО в группу' escClose doAccept;
show(,,50,5);

screen scAddToGroup;
fields
  _sGroupSch: noprotect, pickbutton;
buttons
  cmOk, default;
  cmCancel;
<<

`Наименование группы` .@@@@@@@@@@@@@@@@@@@@@@@@@@

                          <.Включить.> <.Отмена.>
>>
end;

handleevent
  cmOk: PutCommand(cmDefault);
  cmPick:
  {
    if curfield = #_sGroupSch
    {
      var c: comp = 0;
      if RunInterface(L_GRSCH::GETGROUPSCH, 3, 3, 3, false, c, false, false, 0) = cmDefault
        if getfirst GroupSch where ((c == GroupSch.Nrec)) = tsOk
          _sGroupSch := GroupSch.Name;
      rereadrecord;
    }
  }
end;

end;

procedure AddBaseDocToGroupUI(sGroupSch: string; BaseDocMarker: TPtr);
{ #__UNUSED__(sGroupSch)
  if RunWindowModal(wAddToGroup) = cmDefault
  {
    if getfirst GroupSch where ((_sGroupSch == GroupSch.Name)) != tsOk
    {
      ClearBuffer(#GroupSch);
      GroupSch.Name := _sGroupSch;
      GroupSch.cNode := GROUPSCH_NREC_USERSETS;
      insert current GroupSch;
      ClearBuffer(#IerGrSch);
      IerGrSch.cUpRec := GroupSch.Nrec;
      IerGrSch.cRec := GroupSch.Nrec;
      IerGrSch.IsLeaf := 1;
      insert current IerGrSch;
      ClearBuffer(#IerGrSch);
      IerGrSch.cUpRec := GROUPSCH_NREC_USERSETS;
      IerGrSch.cRec := GroupSch.Nrec;
      IerGrSch.IsLeaf := 1;
      insert current IerGrSch;
    }

    var i: longint = 0;
    var c: comp = 0;
    for(i := 0; GetMarker(BaseDocMarker, i, c); i++)
    {
      if getfirst SpGrSch where ((GroupSch.Nrec == SpGrSch.cGroupSch and
                                  0             == SpGrSch.wList and
                                  c             == SpGrSch.cBaseDoc)) != tsOk
      {
        ClearBuffer(#SpGrSch);
        SpGrSch.cGroupSch := GroupSch.Nrec;
        SpGrSch.cBaseDoc := c;
        insert current SpGrSch;
      }
    }
    message('Установка группы завершена');
  }
}

end.
