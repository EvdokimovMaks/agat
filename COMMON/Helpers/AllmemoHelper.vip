#include AllmemoHelper.vih

#component "AGAT"

interface AllmemoHelper;

create view vByNrec
var
  _allmemoNrec: comp;
as select *
from allmemo
where ((
  _allmemoNrec == allmemo.nrec
));

create view v
var
  _wtable: word;
  _crec: comp;
as select *
from allmemo
where ((
  _wtable == allmemo.wtable and
  _crec == allmemo.crec
));


file f;

function GetAllmemoTextByNrec(allmemoNrec: comp): string;
{
  result := '';
  _allmemoNrec := allmemoNrec;
  if vByNrec.getfirst allmemo = tsOk
  {
    if f.OpenMemo(vByNrec.allmemo.mem)
    {
      var s: string = '';
      var s1: string = '';
      while not f.Eof
      {
        f.Readln(s1);
        s += if(s='','',' ') + s1;
      }
      f.Close;
      result := s;
    }
  }
}

function GetAllmemoNrec(aWTable: word; aCRec: comp): comp;
{
  v._wtable := aWTable;
  v._crec := aCRec;
  if v.getfirst allmemo = tsOk
    result := v.allmemo.nrec
  else
    result := 0;
}

function GetAllmemoText(aWTable: word; aCRec: comp): string;
{
  result := GetAllmemoTextByNrec(GetAllmemoNrec(aWTable, aCRec));
}

end.
