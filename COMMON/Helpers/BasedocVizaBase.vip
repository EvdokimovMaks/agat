#include BasedocHelper.vih
#include Query.vih

#component "AGAT"

interface BasedocVizaBase;

sql query qLoad =
  select av.vcomp           as ValueNrec,
         coalesce(spk.name) as ValueName,
         coalesce(spk.code) as ValueCode,
         av.atl_lastdate    as LastDate,
         av.atl_lasttime    as LastTime,
         av.atl_lastuser    as LastUserNrec
  from attrval av
  left join spkau spk on spk.nrec = av.vcomp
  where av.wtable = 1102 and av.crec = :BasedocNrec and av.cattrnam = :AttrnamNrec
;

var
  _basedocNrec : comp;
  _attrnamNrec : comp;
  _valueNrec   : comp;
  _valueName   : string;
  _valueCode   : string;
  _lastDate    : date;
  _lastTime    : time;
  _lastUserNrec: comp;

function IBasedocViza.Soglasovana : boolean;
{
  result := ValueNrec = 00010000000001CEh    //"Согласовано"
         or ValueNrec = 00010000000013CCh    //"Не требуется"
         or ValueNrec = 0001000000001423h;   //"С общих р/счет"
}

property IBasedocViza.BasedocNrec : comp     absolute _basedocNrec ;
property IBasedocViza.AttrnamNrec : comp     absolute _attrnamNrec ;
property IBasedocViza.ValueNrec   : comp     absolute _valueNrec   ;
property IBasedocViza.ValueName   : string   absolute _valueName   ;
property IBasedocViza.ValueCode   : string   absolute _valueCode   ;
property IBasedocViza.LastDate    : date     absolute _lastDate    ;
property IBasedocViza.LastTime    : time     absolute _lastTime    ;
property IBasedocViza.LastUserNrec: comp     absolute _lastUserNrec;

procedure InitFieldsByDefault;
{
  _basedocNrec   := 0;
  _attrnamNrec   := 0;
  _valueNrec     := 0;
  _valueName     := '';
  _valueCode     := '';
  _lastDate      := 0;
  _lastTime      := 0;
  _lastUserNrec  := 0;
}

constructor BasedocVizaBase(aBasedocNrec: comp; aAttrnamNrec: comp);
{
  InitFieldsByDefault;
  _basedocNrec   := aBasedocNrec;
  _attrnamNrec   := aAttrnamNrec;

  var rs: IResultSet;
  rs := queryManager.createQuery(qLoad)
                    .setParam('BasedocNrec', aBasedocNrec)
                    .setParam('AttrnamNrec', aAttrnamNrec)
                    .getResultSet;

  if rs != nullref
    if rs.getFirst = tsOk
    {
      _valueNrec     := rs.row.Val('ValueNrec');
      _valueName     := rs.row.Val('ValueName');
      _valueCode     := rs.row.Val('ValueCode');
      _lastDate      := rs.row.Val('LastDate');
      _lastTime      := rs.row.Val('LastTime');
      _lastUserNrec  := rs.row.Val('LastUserNrec');
    }

  result := true;
}

constructor Init;
{
  InitFieldsByDefault;
  result := true;
}

end.
