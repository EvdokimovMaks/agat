#include PlporHelper.vih
#include BasedocHelper.vih
#include ContractId.vih
#include Query.vih

#component "AGAT"

interface PlporHelper;

sql query qSelectLinkedBasedoc =
  select distinct basedoc.nrec
  from basefin
  join basedoc on basedoc.nrec = basefin.cbasedoc or basedoc.nrec = basefin.cpredopldo
  where basefin.cplpor=:PlporNrec
;

var
  _basedocHelper: IBasedocHelper;
  _qLinkedBasedoc: IQuery;
  _contractIdHelper: ContractIDHelper;

create view vSetUIN
var
  _plporNrec: comp;
as select *
from plpor
where ((
  _plporNrec == plpor.nrec
));

procedure IPlporHelper.RecalcLinkedBasedocStatuses(aPlporNrec: comp; aExcludeBasefin: TCompArray);
{
  var rs: IResultSet;
  rs := _qLinkedBasedoc.setParam('PlporNrec', aPlporNrec).getResultSet;
  if _qLinkedBasedoc.errorCode = tsOk and rs != nullref
  {

    if rs.getFirst = tsOk do
    {
      _basedocHelper.RecalcBasedocStatus(rs.row.ValAt(1), aExcludeBasefin);
    } while rs.getNext = tsOk;

  }
}

type ttaxArray = array[1..10] of string;

function GetTaxArray(taxString: string): ttaxArray;
{
  var a: ttaxArray;
  var i, p: integer;
  for(i := 1; i<=count(a); i++)
  {
    p := pos(';', taxString+';');
    if p > 0
    {
      a[i] := substr(taxString, 1, p-1);
      taxString := substr(taxString,p+1,255);
    }
    else
      a[i] := '';
  }
}

function GetTaxString(a: ttaxArray): string;
{
  var s: string = '';
  var i: integer;
  for(i := 1; i<=count(a); i++)
    s += a[i] + ';';
  result := s;
}

procedure IPlporHelper.SetPlporUIN(aPlporNrec: comp);
{
  vSetUIN._plporNrec := aPlporNrec;
  if vSetUIN.getfirst plpor = tsOk
  {
    var cid: string;
    cid := _contractIdHelper.GetContractID(GetContractIDKey_ByPlpor, aPlporNrec);
    var a: ttaxArray;
    a := GetTaxArray(vSetUIN.plpor.tax);
    if a[9] != cid
    {
      a[9] := cid;
      vSetUIN.plpor.tax := GetTaxString(a);
      vSetUIN.update current plpor;
    }
  }
}

constructor Init;
{
  result := getvipref(_basedocHelper, 'AGAT::BasedocHelper');
  _qLinkedBasedoc := queryManager.createQuery(qSelectLinkedBasedoc);
}

property BasedocHelper: IBasedocHelper absolute _basedocHelper;

end.
