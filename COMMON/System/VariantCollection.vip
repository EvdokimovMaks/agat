#include System.vih

#component "System"

interface VariantCollection;

var
  _list: IVariantList;
  _count: longint;

//#Region IEnumerable

function GetEnumerator: IVariantEnumerator;
{
  result := IVariantEnumerator( new( System::VariantListEnumerator, VariantListEnumerator(IVariantList(self)) ) );
}

function First: Variant;
{
  result := _list.First;
}

function Last: Variant;
{
  result := _list.Last;
}

//#EndRegion

//#Region ICollection

function Add(value: variant): longint;
{
  InsertItem(_count, Value);
  result := _count;
}

procedure Remove(value: variant);
{
  var i: longint;
  i := IndexOf(value);
  if (i <> -1) RemoveAt(i);
}

procedure Clear;
{
  ClearItems;
}

function Contains(value: variant): boolean;
{
  result := IndexOf(value) <> -1;
}

property Count: longint read _count;

//#EndRegion

//#Region IList

function ItemGet(index: longint): variant;
{
  result := GetItem(index);
}

procedure ItemSet(index: longint; value: variant);
{
  SetItem(index, value);
}

function IndexOf(value: variant): longint;
{
  result := -1;
  var i: longint;
  for (i:=0; i < _count; i++)
  {
    if GetItem(i) = value
    {
      result := i;
      break;
    }
  }
}

procedure Insert(index: longint; value: variant);
{
  InsertItem(index, value);
}

procedure RemoveAt(index: longint);
{
  RemoveItem(index);
}

//#EndRegion

//#Region Collection private metods

constructor Init;
{
  _count := 0;
  result := loadvipref(_list, 'system::VariantList');
}

function  GetItem(index: longint): variant;
{
  result := _list.ItemGet(index);
}

procedure SetItem(index: longint; value: variant);
{
  _list.ItemSet(index, value);
  _count := _list.Count;
}

procedure InsertItem(index: longint; value: variant);
{
  _list.Insert(index, value);
  _count := _list.Count;
}

procedure RemoveItem(index: longint);
{
  _list.RemoveAt(index);
  _count := _list.Count;
}

procedure ClearItems;
{
  _list.Clear;
}

//#EndRegion

end.
