#include docs2obj.vih
#include Query.vih

#component "AGAT"

interface FixRegradeSpschem 'Восстановление схем связей в актах на пересортицу';
show (,,45,5);

sql query q1 =  select sps.nrec
                from katsopr ks
                join spsopr sps on ks.nrec = sps.csopr
                join specmtr spm on 1110=spm.cotable and sps.nrec = spm.cspec
                join katstroy kstr on spm.cobj = kstr.nrec
                left join spschem s on ks.vidsopr=s.tidk and ks.nrec=s.cdoc and 454=s.tidkbase and kstr.nrec=s.cbasedoc
                where ks.vidsopr=630 and ks.dsopr between :d1 and :d2 and s.nrec is null;

var
  _d1, _d2: date;
  _docToObj: L_UKS::DocToObj;

create view dummy;

screen sc1;
fields
  _d1: noprotect;
  _d2: noprotect;
buttons
  cmDoOk, default;
  cmCancel;
<<

 Период анализа с .@@@@@@@@@ по .@@@@@@@@@@

              <. Продолжить .> <. Отмена .>
>>
end;

handleevent
  cmDoOk:
  {
    var q: IQuery;
    q := queryManager.CreateQuery(q1);
    q.setParam('d1', _d1);
    q.setParam('d2', _d2);

    var rs: IResultSet;
    rs := q.getResultSet;

    if (rs!=nullref)
      if (rs.getfirst = tsOk) do
      {
        _docToObj.AddSpDoc(1110, rs.row.Val('nrec'));
      } while (rs.getnext = tsOk)
  }
end;

end.
