Table struct _tmpFpOborot_old
 (
   Name         : string,
   cNode        : comp,
   dBeg         : date,
   dEnd         : date,
   isTested     : boolean,
   DatOb        : double,
   Summa        : double,
   cPlansSch    : comp,
   cPeriod      : comp,
   cCO          : comp,
   cStBud       : comp
 )
with index
 (
   _tmpFpOborot_old01 = cPlansSch+cPeriod+cCO+cStBud,
   _tmpFpOborot_old02 = cPlansSch+cCO+cStBud+dEnd,
   _tmpFpOborot_old03 = dEnd
 );
#component "AGAT"
Interface AGAT_TPbyEyars 'Параметры отчета' (,,)   EscClose, cyan;
Show (,,70,8);
!===============================================================================================================
create view
var 
 _PeriodFakt
,_Line
,_Column
,_lvl
,_MonthCur
,_YearCur            : word;

 _fpPeriod_Name
,_FieldName
,_myPath             : string;

 _fpPeriodCurr_Nrec
,_cTF
,_cfpCO
,_cPlansSchF
,_cPlansSchP
,_cParent
                     : comp;

 _dBeg
,_dEnd               
,_dEnd2              : date;

 _FaktPrevPer
,_PlanCurrPer
,_FaktCurrPer
,_PlanNextPer
                     : double;

as select * from 
 fpPeriod
,fpTForm
,fpCO

,fpAxTF Axis1

,fpSpAxTF SpAxis1
,fpSpAxTF SpAxis2

,fpOborot fpOborotP
,fpOborot fpOborotF
,_tmpFpOborot_old

,_tmpFpOborot_old _tmpOborotPrevP
,_tmpFpOborot_old _tmpOborotPrevF

,_tmpFpOborot_old _tmpOborotCurrP
,_tmpFpOborot_old _tmpOborotCurrF

,_tmpFpOborot_old _tmpOborotNextP
,_tmpFpOborot_old _tmpOborotNextF

,_tmpFpOborot_old _myLog

where ((
    _cTF                          ==  fpTForm.nRec
and _cfpCO                        ==  fpCO.nRec
!===========================================================
and fpTForm.nRec                  ==  Axis1.cMain
and word(1)                       ==  Axis1.wAxisId
!===========================================================
and Axis1.nRec                    ==  SpAxis1.cMain
and _cParent                      ==  SpAxis1.cNode
and Axis1.nRec                    ==  SpAxis2.cMain
and SpAxis1.nRec                  ==  SpAxis2.cNode
!===========================================================
and _cPlansSchP                   ==  fpOborotP.cPlansSch
and _cfpCO                        ==  fpOborotP.cCO

and _cPlansSchF                   ==  fpOborotF.cPlansSch
and _cfpCO                        ==  fpOborotF.cCO

!===========================================================
!   Предыдущие периоды
!===========================================================
and _cPlansSchP                   ==  _tmpOborotPrevP.cPlansSch
and _cfpCO                        ==  _tmpOborotPrevP.cCO
and SpAxis1.cMean                 ==  _tmpOborotPrevP.cStBud
and date(1,1,1900)                <<= _tmpOborotPrevP.dEnd
and _dBeg                         >>  _tmpOborotPrevP.dEnd

and _cPlansSchF                   ==  _tmpOborotPrevF.cPlansSch
and _cfpCO                        ==  _tmpOborotPrevF.cCO
and SpAxis1.cMean                 ==  _tmpOborotPrevF.cStBud
and date(1,1,1900)                <<= _tmpOborotPrevF.dEnd
and _dBeg                         >>  _tmpOborotPrevF.dEnd

!===========================================================
!   Текущий период
!===========================================================
and _cPlansSchP                   ==  _tmpOborotCurrP.cPlansSch
and _cfpCO                        ==  _tmpOborotCurrP.cCO
and SpAxis1.cMean                 ==  _tmpOborotCurrP.cStBud
and _dBeg                         <<= _tmpOborotCurrP.dEnd
and _dEnd                         >>= _tmpOborotCurrP.dEnd

and _cPlansSchF                   ==  _tmpOborotCurrF.cPlansSch
and _cfpCO                        ==  _tmpOborotCurrF.cCO
and SpAxis1.cMean                 ==  _tmpOborotCurrF.cStBud
and _dBeg                         <<= _tmpOborotCurrF.dEnd
and _dEnd                         >>= _tmpOborotCurrF.dEnd

!===========================================================
!   Будущий период
!===========================================================
and _cPlansSchP                   ==  _tmpOborotNextP.cPlansSch
and _cfpCO                        ==  _tmpOborotNextP.cCO
and SpAxis1.cMean                 ==  _tmpOborotNextP.cStBud
and _dEnd                         <<  _tmpOborotNextP.dEnd
and _dEnd2                        >>= _tmpOborotNextP.dEnd

and _cPlansSchF                   ==  _tmpOborotNextF.cPlansSch
and _cfpCO                        ==  _tmpOborotNextF.cCO
and SpAxis1.cMean                 ==  _tmpOborotNextF.cStBud
and _dEnd                         <<  _tmpOborotNextF.dEnd
and _dEnd2                        >>= _tmpOborotNextF.dEnd

));

Procedure _GetFpPeriod;
{
  if GetFirst fpPeriod where ((_fpPeriodCurr_Nrec == fpPeriod.nRec)) = tsOk
     {
       _fpPeriod_Name := fpPeriod.Name;
       _dBeg  := fpPeriod.dBeg;
       _dEnd  := fpPeriod.dEnd;
     }
}

Window _myTmpWindow;
Tree treeSpTF (,,sci147EnEscTree);
  Table SpAxis1;
Fields
  SpAxis1.Name      ''#3'Наименование'  ('Наименование элемента оси',,)   : [30], Protect;
end; // Tree
TableEvent table SpAxis1;
cmTreeNodeType:{
  if (IsValid(#SpAxis2)) TreeSetNodeType(treeSpTF,if(TreeNodeIsOpen(treeSpTF),ntfOpen,ntfClose));
     else TreeSetNodeType(treeSpTF,ntfText);
}
cmTreeTop : _cParent := comp(0) // текущий корень - вершина
cmTreeDown: _cParent := SpAxis1.nRec; // текущий корень - текущая запись
cmTreeUp  : _cParent := SpAxis1.cNode; // текущий корень - ссылка вверх
cmTreeNeedOwner:{
   TreeJumpToRecord(treeSpTF,SpAxis1.cNode);
}
end; // TableEvent
end; // Window

Panel pnl_ParamRep;
  Screen scr1 ('...',,sci13Esc);
  Table fpPeriod;
  Fields
   _fpPeriod_Name     : Protect,PickButton;
   _PeriodFakt        : [List '1-й квартал','Полугодие','Девять месяцев'],protect;
   fpTForm.Name       : Protect,PickButton;
   fpCO.Name          : Protect,PickButton;
  Buttons
  cmValue1,,,'Сформировать';
  cmValue2,,,'Выход';
<< 
`Отчет за` .@@@@@@@@@@@@ `  фактические показатели`.@@@@@@@@@@@@@@@@@
`Типовая форма`
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
`Заказ (центр ответственности)`
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                       <. Сформировать .> <. Выход .>
>>
 end;
end; // Panel

HandleEvent
cmPick:{
  if CurField = #_fpPeriod_Name
     {
       if ( RunInterface('F_fpCatalog::GetPeriod', word(1), 0, comp(0), _fpPeriodCurr_Nrec, word(0) ) <> cmCancel )
          {
            _GetFpPeriod;
            RescanPanel(#FpPeriod);
          }
     }
  if CurField = #fpTForm.Name
     {
       if ( RunInterface('F_fpBUDGET::GETTFORM', word(1), comp(0), _cTF ) <> cmCancel )
          {
            if GetFirst fpTForm = tsOk RescanPanel(#fpPeriod);
          }
     }
  if CurField = #fpCO.Name
     {
       if ( RunInterface('F_FPCATALOG::GETCO', word(1), comp(0), comp(0), _cfpCO ) <> cmCancel )
          {
            if GetFirst fpCO = tsOk RescanPanel(#fpPeriod);
          }
     }
}

cmValue1:{
  _cPlansschP  := comp(281474976710666);  // Операции план
  _cPlansschF  := comp(281474976710667);  // Операции факт
  _dEnd2 := date(31,12,2100);
//  _cPlansschP  := comp(14);  // Операции план
//  _cPlansschF  := comp(15);  // Операции факт


  StartNewVisual(vtRotateVisual,vfTimer + vfBreak + vfConfirm,''#3'Формирование шаблона '#13#13#3,1);
  _myPath := TranslatePath('%startpath%'+'Xls\TPbyEyars.xlt');
  if FileExist(_myPath)
     {
       var xlREs : boolean;
       xlRes := xlCreateExcelWithTemplate(_myPath,true) 
       if (not xlRes)  { Message('Не открылся Excel'); Exit; }
       xlRes := xlIsExcelValid;
       if (not xlRes)  { Message('Не инициализирован Excel'); Exit; }
       xlRes := xlGetActiveWorkBookName(_myPath);
       if (not xlRes)
          { Message('Не активна книга Excel '+_myPath); Exit; }
!==============================================================================
       xlSetCellStringValue('Тематический план ' + _fpPeriod_Name ,1,1,1,1);
       xlSetCellStringValue(fpCO.Name ,2,7,2,7);
!=============================================================================================
       Delete All _tmpFpOborot_old;
       if GetFirst fpOborotP = tsOk do
          {
            if GetFirst _tmpFpOborot_old where ((
                                                 fpOborotP.cPlansSch == _tmpFpOborot_old.cPlansSch
                                             and fpOborotP.cPeriod   == _tmpFpOborot_old.cPeriod
                                             and fpOborotP.cCO       == _tmpFpOborot_old.cCO
                                             and fpOborotP.cStBud    == _tmpFpOborot_old.cStBud
                                           )) <> tsOk
               {
                 if GetFirst fpPeriod where ((fpOborotP.cPeriod == fpPeriod.nRec)) = tsOk
                      {
                        ClearBuffer(#_tmpFpOborot_old);
                        _tmpFpOborot_old.cPeriod   := fpPeriod.nRec;
                        _tmpFpOborot_old.Name      := fpPeriod.Name;
                        _tmpFpOborot_old.cNode     := fpPeriod.cNode;
                        _tmpFpOborot_old.dBeg      := fpPeriod.dBeg;
                        _tmpFpOborot_old.dEnd      := fpPeriod.dEnd;
                        _tmpFpOborot_old.DatOb     := fpOborotP.DatOb;
                        _tmpFpOborot_old.Summa     := fpOborotP.Summa;
                        _tmpFpOborot_old.cCO       := fpOborotP.cCO;
                        _tmpFpOborot_old.cStBud    := fpOborotP.cStBud;
                        _tmpFpOborot_old.cPlansSch := fpOborotP.cPlansSch;
                        insert current _tmpFpOborot_old;
                      } else
                      {
                        _tmpFpOborot_old.Summa     := _tmpFpOborot_old.Summa + fpOborotP.Summa;
                        update current _tmpFpOborot_old;
                      }
               }
          } while GetNext fpOborotP = tsOk and NextVisual;

       if GetFirst fpOborotF = tsOk do
          {
            if GetFirst _tmpFpOborot_old where ((
                                                 fpOborotF.cPlansSch == _tmpFpOborot_old.cPlansSch
                                             and fpOborotF.cPeriod   == _tmpFpOborot_old.cPeriod
                                             and fpOborotF.cCO       == _tmpFpOborot_old.cCO
                                             and fpOborotF.cStBud    == _tmpFpOborot_old.cStBud
                                           )) <> tsOk
               {
                 if GetFirst fpPeriod where ((fpOborotF.cPeriod == fpPeriod.nRec)) = tsOk
                      {
                        ClearBuffer(#_tmpFpOborot_old);
                        _tmpFpOborot_old.cPeriod   := fpPeriod.nRec;
                        _tmpFpOborot_old.Name      := fpPeriod.Name;
                        _tmpFpOborot_old.cNode     := fpPeriod.cNode;
                        _tmpFpOborot_old.dBeg      := fpPeriod.dBeg;
                        _tmpFpOborot_old.dEnd      := fpPeriod.dEnd;
                        _tmpFpOborot_old.DatOb     := fpOborotF.DatOb;
                        _tmpFpOborot_old.Summa     := fpOborotF.Summa;
                        _tmpFpOborot_old.cCO       := fpOborotF.cCO;
                        _tmpFpOborot_old.cStBud    := fpOborotF.cStBud;
                        _tmpFpOborot_old.cPlansSch := fpOborotF.cPlansSch;
                        insert current _tmpFpOborot_old;
                      } else 
                      {
                        _tmpFpOborot_old.Summa     := _tmpFpOborot_old.Summa + fpOborotF.Summa;
                        update current _tmpFpOborot_old;
                      }
               }
          } while GetNext fpOborotF = tsOk and NextVisual;

       if XLCREATEMATRIX(10000, 45) {};
       _Line := 1;
       if xlsetActiveSheetByName('Тематический план') {};
       if (TreeGetFirstEx(treeSpTF)) do
          {
            _lvl := TreeLevel(treeSpTF)+1;
            if SpAxis1.KodGrKau = 20
               {
                 xlStWriteToMatrix (_Line, _lvl, SpAxis1.Name);

                 xlStWriteToMatrix (_Line, 44, SpAxis1.nRec);
                 xlStWriteToMatrix (_Line, 45, SpAxis1.cNode);

!=====================================================================================================
!                Столбцы
!=====================================================================================================
                 _GetFpPeriod;
                 _FaktPrevPer := 0;
                 if GetFirst _tmpOborotPrevF = tsOk do
                    {
                      _FaktPrevPer := _FaktPrevPer + _tmpOborotPrevF.Summa;
                    } while GetNext  _tmpOborotPrevF = tsOk;

                 _PlanCurrPer := 0;
                 if GetFirst _tmpOborotCurrP = tsOk do
                    {
                      _PlanCurrPer := _PlanCurrPer + _tmpOborotCurrP.Summa;
                    } while GetNext _tmpOborotCurrP = tsOk;

                 _PlanNextPer := 0;
                 if GetFirst _tmpOborotNextP = tsOk do
                    {
                      _PlanNextPer := _PlanNextPer + _tmpOborotNextP.Summa;
                    } while GetNext _tmpOborotNextP = tsOk;

                 xlStWriteToMatrix (_Line, 21, (_FaktPrevPer+_PlanCurrPer+_PlanNextPer)); //Плн по заказу

                 _FieldName := 'НЗП  на '+ add_day(_dBeg,-1)+' по показателю выручка показана частичная отгрузка';
                 xlSetCellStringValue(_FieldName,5,22,5,22); //Заголовок столбца НЗП прошлых периодов
                 xlStWriteToMatrix (_Line, 22, _FaktPrevPer); //НЗП прошлых периодов

                 _FieldName := 'План на '+_fpPeriod_Name;
                 xlSetCellStringValue(_FieldName,5,23,5,23); //Заголовок столбца План на тек. период
                 xlStWriteToMatrix (_Line, 23, _PlanCurrPer); //План тек. период

                 case _PeriodFakt of 
                   0: {_dEnd := date(31,3,Year(_dEnd)); _FieldName := 'Факт 1-й квартал '+_fpPeriod_Name; _MonthCur := 4;}
                   1: {_dEnd := date(30,6,Year(_dEnd)); _FieldName := 'Факт за полугодие '+_fpPeriod_Name; _MonthCur := 7;}
                   2: {_dEnd := date(30,9,Year(_dEnd)); _FieldName := 'Факт за девять месяцев '+_fpPeriod_Name; _MonthCur := 10;}
                 end

                 _FaktCurrPer := 0;
                 if GetFirst _tmpOborotCurrF = tsOk do
                    {
                      _FaktCurrPer := _FaktCurrPer + _tmpOborotCurrF.Summa;
                    } while GetNext  _tmpOborotCurrF = tsOk;

                 xlSetCellStringValue(_FieldName,5,24,5,24); //Заголовок столбца Факт тек. период
                 xlStWriteToMatrix (_Line, 24, _FaktCurrPer); //Факт тек. период

                 _Column := 25;
                 for(_MonthCur:=_MonthCur;_MonthCur<=12;inc(_MonthCur))
                    {

                      _dBeg := date(01,_MonthCur,Year(_dBeg));
                      _dEnd := date(Last_Day(_dBeg),_MonthCur,Year(_dBeg));
                      _PlanCurrPer := 0;

                      if GetFirst _tmpOborotCurrP = tsOk do
                         {
                           _PlanCurrPer := _PlanCurrPer + _tmpOborotCurrP.Summa;
                         } while GetNext  _tmpOborotCurrP = tsOk;

                      case _MonthCur of
                       1:  _FieldName := 'январь '+_fpPeriod_Name;
                       2:  _FieldName := 'февраль '+_fpPeriod_Name;
                       3:  _FieldName := 'март '+_fpPeriod_Name;
                       4:  _FieldName := 'апрель '+_fpPeriod_Name;
                       5:  _FieldName := 'май '+_fpPeriod_Name;
                       6:  _FieldName := 'июнь '+_fpPeriod_Name;
                       7:  _FieldName := 'июль '+_fpPeriod_Name;
                       8:  _FieldName := 'август '+_fpPeriod_Name;
                       9:  _FieldName := 'сентябрь '+_fpPeriod_Name;
                       10: _FieldName := 'октябрь '+_fpPeriod_Name;
                       11: _FieldName := 'ноябрь '+_fpPeriod_Name;
                       12: _FieldName := 'декабрь '+_fpPeriod_Name;
                      end
                      
                      xlSetCellStringValue(_FieldName,5,_Column,5,_Column); //Заголовок столбца Факт тек. период
                      xlStWriteToMatrix (_Line, _Column, _PlanCurrPer); //Факт тек. период
                      inc(_Column);

                    }
                 _GetFpPeriod;
                 for(_YearCur := Year(_dBeg);_YearCur<Year(_dBeg)+10;inc(_YearCur))
                    {
                      _dEnd  := date(31,12,_YearCur);
                      _dEnd2 := date(31,12,_YearCur+1);
                      _PlanNextPer := 0;
                      if GetFirst _tmpOborotNextP = tsOk do
                         {
                           _PlanNextPer := _PlanNextPer + _tmpOborotNextP.Summa;
                         } while GetNext _tmpOborotNextP = tsOk;
                      if _PlanNextPer <> 0
                         {
                           _FieldName := Year(_dEnd2) + ' год.'
                           xlSetCellStringValue(_FieldName,5,_Column,5,_Column); //Заголовок столбца Факт тек. период
                           xlStWriteToMatrix (_Line, _Column, _PlanNextPer); //Факт тек. период
                           inc(_Column);
                         } else if GetFirst _myLog where ((    _dEnd  <<  _myLog.dEnd
                                                           and _dEnd2 >>= _myLog.dEnd)) = tsOk xlStWriteToMatrix (_Line, _Column, double(0));
                     }
                 inc(_Line);
!=====================================================================================================
               }
          } While (TreeGetNextEx(treeSpTF)) and NextVisual;

       xlWriteMatrixToExcel(8,1);
       if xlFreeMatrix{};

       xlSetCellStringValue(_Column,1,21,1,21);
       xlSetCellStringValue(_Line+6,1,22,1,22);
!=====================================================================================================
!=====================================================================================================
       if XLCREATEMATRIX(10000, 15) {};
       _Line := 1;
       if xlsetActiveSheetByName('Log') {};
       if GetFirst _myLog = tsOk do
          {
            xlStWriteToMatrix (_Line, 1, _myLog.Name);
            xlStWriteToMatrix (_Line, 2, _myLog.cNode);
            xlStWriteToMatrix (_Line, 3, _myLog.dBeg);
            xlStWriteToMatrix (_Line, 4, _myLog.dEnd);
            xlStWriteToMatrix (_Line, 5, _myLog.isTested);
            xlStWriteToMatrix (_Line, 6, _myLog.DatOb);
            xlStWriteToMatrix (_Line, 7, _myLog.Summa);
            xlStWriteToMatrix (_Line, 8, _myLog.cPlansSch);
            xlStWriteToMatrix (_Line, 9, _myLog.cPeriod);
            xlStWriteToMatrix (_Line, 10, _myLog.cCO);
            xlStWriteToMatrix (_Line, 11, _myLog.cStBud);
            inc(_Line);
          } while GetNext _myLog = tsOk;

       xlWriteMatrixToExcel(1,1);
       if xlFreeMatrix{};
!=====================================================================================================
!=====================================================================================================

       if xlsetActiveSheetByName('Тематический план') {};
       if xlRes:=xlRunMacro('FormatIt') {};
       xlKillExcel;
!==============================================================================
     } else Message ('Шаблон "TPbyEyars.xlt" не найден!')//if FileExist(myPath)
  StopVisual('',0);
}

cmValue2:{
  CloseInterface(cmDefault);
}
end; //HandleEvent
end. //Interface  

VipInterface UserReport_AGAT_TPbyEyars Implements IUserReport licensed(free);
Interface UserReport_AGAT_TPbyEyars;
  create view dummy;
  procedure Run;
  begin
    runinterfacenomodal(AGAT::AGAT_TPbyEyars);
  end;

  function GetReportName: String;
  begin
    GetReportName := 'Анализ тематического плана';
  end;

  function GetGroupName (Level : Word) : String;
  begin
    GetGroupName := 'Тематический план';
  end;

  function GetPriority : Integer;
  begin
    GetPriority := 0;
  end;

  function VisibleInModule(Ind : Byte) : String;
  begin
    VisibleInModule := '';
    case Ind of
      1 : VisibleInModule := '*';
    end;
  end;
end.


