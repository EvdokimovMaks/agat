#include ContractId.vih
#include BasedocReeReport.vih
#include extattr.vih
#include Query.vih
#include XLREPORT.vih

#component "AGAT"

interface BasedocReeReport_Hier_Rs_Zakaz_Rejim;

//#region tables

table struct trs (
  ObshiySchet : byte,
  spkRSNrec   : comp,
  zakazVibNrec: comp,
  rejimNrec   : comp,
  name        : string,
  summa       : double,
  total       : double,
  sumopl      : double,
  sumsaldo    : double
) with index (
  i01 = ObshiySchet+spkRSNrec+zakazVibNrec+rejimNrec,
  i02 = name
);

table struct tOpl (
  basedocNrec: comp,
  rsNrec: comp,
  summa: double,
  dopl: date
) with index (
  i01 = basedocNrec + rsNrec (unique)
);

table struct tbd (
  nrec        : comp,
  viddoc      : word,
  ddoc        : date,
  dVklRee     : date,
  reestr      : string[100],
  nodoc       : string,
  bdds        : string,
  dform       : date,
  ObshiySchet : byte,
  spkRSNrec   : comp,
  zakazVibNrec: comp,
  rejimNrec   : comp,
  summa       : double,
  dopl        : date,
  total       : double,
  sumopl      : double,
  sumsaldo    : double,
  val         : string,
  avans       : string,
  status      : string,
  ispoln      : string,
  dds         : string,
  buhsch      : string,
  dsogl       : date,
  sogl        : string,
  org         : string,
  bank        : string,
  bankrs      : string,
  cfo         : string,
  zakaz       : string,
  dognum      : string,
  dogdate     : date,
  procav      : string,
  nazn        : string,
  sud         : string[20],
  vizaPEUNrec : comp,
  vizaKAZNrec : comp,
  vizaKOMNrec : comp,
  vizaBUHNrec : comp,
  vizaPEUName : string,
  vizaKAZName : string,
  vizaKOMName : string,
  vizaBUHName : string,
  vizaPEUDate : date,
  vizaKAZDate : date,
  vizaKOMDate : date,
  vizaBUHDate : date,
  vizaPEUUser : string,
  vizaKAZUser : string,
  vizaKOMUser : string,
  vizaBUHUser : string,
  vizaPEUPrim : string,
  vizaKAZPrim : string,
  vizaKOMPrim : string,
  vizaBUHPrim : string
) with index (
  i01 = nrec,
  i02 = ObshiySchet + spkRSNrec + zakazVibNrec + rejimNrec + sumsaldo,
  i03 = nrec + spkRSNrec,
  i04 = nrec + sumsaldo
);

//#endregion

//#region Queries

sql query qSpstepData =
select coalescen(katbank.nrec, spkRS.nrec, #comp(0)) as RSNrec,
       coalescen(katbank.name, spkRS.name, '')       as BankName,
       coalescen(katbank.schet1, spkRS.name, '')     as RSName,
       coalesce(spkIGK.nrec,#comp(0))                as IGKNrec,
       coalesce(spkIGK.name,'')                      as IGKName,
       coalesce(spkRejim.nrec,#comp(0))              as RejimNrec,
       coalesce(spkRejim.name,'')                    as RejimName,
       coalesce(ksVib.nrec,#comp(0))                 as ZakazVibNrec,
       coalesce(stVib.nrec,#comp(0))                 as StVibNrec,
       coalesce(stVib.name,#comp(0))                 as StVibName,
       substring(coalesce(ksVib.name,''),1,instr(' ',coalesce(ksVib.name,'')+' ',1)-1) as ZakazVibName,
       coalesce(ks.nrec,#comp(0))                    as ZakazNrec,
       substring(coalesce(ks.name,''),1,instr(' ',coalesce(ks.name,'')+' ',1)-1) as ZakazName,
       coalesce(cfo.nrec,#comp(0))                   as CFONrec,
       coalesce(cfo.kod,'')                          as CFOKod
  from basedoc bd
  join stepdoc sd on sd.cbasedoc = bd.nrec
  join spstep sps on sps.cstepdoc = sd.nrec
  left join attrval avVizaPEU on avVizaPEU.wtable = 1102 and avVizaPEU.cRec = bd.nrec and avVizaPEU.cattrnam = #comp(0001000000000088h)
  left join attrval avRS on avRS.wtable = 1104 and avRS.cRec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)
  left join attrval avZakVib on avZakVib.wtable = 1104 and avZakVib.cRec = sps.nrec and avZakVib.cattrnam = #comp(0001000000000A87h)
  left join attrval avRejim on avRejim.wtable = 1104 and avRejim.cRec = sps.nrec and avRejim.cattrnam = #comp(000100000000079Eh)
  left join attrval avStVib on avStVib.wtable = 1104 and avStVib.cRec = sps.nrec and avStVib.cattrnam = #comp(0001000000000A88h)
  left join specmtr spm on spm.csaldtune = #comp(0001000000000001h) and spm.cotable = 1104 and spm.cspec = sps.nrec
  left join (select spkRS.nrec as spkRSNrec, max(coalesce(spkIGK.nrec,#comp(0))) as spkIGKNrec, max(cb.cpodr) as katbankNrec
             from spcash
             join spkau spkRS on spkRS.nrec = case when spcash.tblos[1] = 10545 then spcash.kauos[1]
                                                   when spcash.tblos[2] = 10545 then spcash.kauos[2]
                                                   when spcash.tblos[3] = 10545 then spcash.kauos[3]
                                                   when spcash.tblos[4] = 10545 then spcash.kauos[4]
                                                   when spcash.tblos[5] = 10545 then spcash.kauos[5]
                                                   when spcash.tblos[6] = 10545 then spcash.kauos[6]
                                                   else #comp(0)
                                              end
             join cashbank cb on cb.nrec = spcash.ccashbank
             left join spkau spkIGK on spkIGK.nrec = case when spcash.tblos[1] = 10527 then spcash.kauos[1]
                                                          when spcash.tblos[2] = 10527 then spcash.kauos[2]
                                                          when spcash.tblos[3] = 10527 then spcash.kauos[3]
                                                          when spcash.tblos[4] = 10527 then spcash.kauos[4]
                                                          when spcash.tblos[5] = 10527 then spcash.kauos[5]
                                                          when spcash.tblos[6] = 10527 then spcash.kauos[6]
                                                          else #comp(0)
                                                     end
             where spcash.cplanssch=#comp(000100000000000Eh)
             group by spkRS.nrec
            ) tCashbank on tCashbank.spkRSNrec = avRS.vcomp
  left join spkau spkIGK on spkIGK.nrec = coalesce(tCashbank.spkIGKNrec,#comp(0))
  left join katbank on katbank.nrec = coalesce(tCashbank.katbankNrec,#comp(0))
  left join spkau spkRejim on spkRejim.nrec = coalesce(avRejim.vComp,#comp(0))
  left join addsumtune stVib on stVib.nrec = coalesce(avStVib.vComp,#comp(0))
  left join spkau spkRS on spkRS.nrec = coalesce(avRS.vComp,#comp(0))
  left join katstroy ksVib on ksVib.nrec = coalesce(avZakVib.vComp,#comp(0))
  left join katstroy ks on ks.nrec = coalesce(spm.cobj,#comp(0))
  left join katpodr cfo on cfo.nrec = coalesce(spm.ckau[2],#comp(0))
  where bd.nrec = :basedocNrec and ( coalesce(avRS.vcomp,#comp(0)) = :rsNrec or coalesce(avVizaPEU.vcomp,#comp(0)) = #comp(0001000000001423h) )
;

//суммы оплаты по реестру ДО в разрезе ДО и р/с списания
//отбираются только те платежки р/с которых совпадает с р/с списания в спецификации ДО
sql query qSelectOpl =

select tbd.basedocNrec as basedocNrec,
       tbd.rsNrec      as rsNrec,
       max(bf.ddoc)    as dopl,
       sum(bf.summa)   as summa

from (
      select distinct bdreeagat.crec as basedocNrec, spkRS.nrec as rsNrec

      from bdreeagat

      join stepdoc sd on sd.cbasedoc = bdreeagat.crec

      join spstep sps on sps.cstepdoc = sd.nrec

      join attrval avRS on avRS.wtable = 1104 and avRS.cRec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)

      join spkau spkRS on spkRS.nrec = avRS.vcomp

     ) tbd

join basefin bf on bf.tidkgal = 1 and (bf.cbasedoc = tbd.basedocNrec or bf.cpredopldo = tbd.basedocNrec)

join usersdoc ud on ud.tipgal=bf.tidkgal and ud.tipusers=bf.tipdoc

join cashbank cb on cb.nrec = ud.ccashbank

join spcash on spcash.ccashbank = cb.nrec and
               spcash.cplanssch = #comp(000100000000000Eh) and
               case when spcash.tblos[1] = 10545 then spcash.kauos[1]
                    when spcash.tblos[2] = 10545 then spcash.kauos[2]
                    when spcash.tblos[3] = 10545 then spcash.kauos[3]
                    when spcash.tblos[4] = 10545 then spcash.kauos[4]
                    when spcash.tblos[5] = 10545 then spcash.kauos[5]
                    when spcash.tblos[6] = 10545 then spcash.kauos[6]
                    else #comp(0)
               end = tbd.rsNrec

group by tbd.basedocNrec, tbd.rsNrec
;

//суммы оплаты по реестру ДО в разрезе ДО
//отбираются только те платежки р/с которых НЕ совпадает с р/с списания в спецификации ДО
sql query qSelectOplAnotherRS =

select bdreeagat.crec as basedocNrec, sum(bf.summa) as summa

from bdreeagat

join basefin bf on bf.tidkgal = 1 and (bf.cbasedoc = bdreeagat.crec or bf.cpredopldo = bdreeagat.crec)

join usersdoc ud on ud.tipgal=bf.tidkgal and ud.tipusers=bf.tipdoc

join cashbank cb on cb.nrec = ud.ccashbank

join spcash on spcash.ccashbank = cb.nrec and spcash.cplanssch = #comp(000100000000000Eh)

left join (select distinct bdreeagat.crec as basedocNrec, spkRS.nrec as rsNrec

           from bdreeagat

           join stepdoc sd on sd.cbasedoc = bdreeagat.crec

           join spstep sps on sps.cstepdoc = sd.nrec

           join attrval avRS on avRS.wtable = 1104 and avRS.cRec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)

           join spkau spkRS on spkRS.nrec = avRS.vcomp

          ) tBdRs on tBdRs.basedocNrec = bdreeagat.crec and
                     case when spcash.tblos[1] = 10545 then spcash.kauos[1]
                          when spcash.tblos[2] = 10545 then spcash.kauos[2]
                          when spcash.tblos[3] = 10545 then spcash.kauos[3]
                          when spcash.tblos[4] = 10545 then spcash.kauos[4]
                          when spcash.tblos[5] = 10545 then spcash.kauos[5]
                          when spcash.tblos[6] = 10545 then spcash.kauos[6]
                          else #comp(0)
                     end = tBdRs.rsNrec

where tBdRs.rsNrec is Null

group by bdreeagat.crec
;


sql query querymain =
select bd.nrec,
       bd.viddoc                                   as viddoc,
       bd.ddoc                                     as ddoc,
       bd.nodoc                                    as nodoc,
       coalesce(statlog.comment,'')                as reestr,
       coalesce(date(lastStatlog.dt), 0)           as dVklRee,
       coalesce(spkBdds.Name,'')                   as bdds,
       bd.dform                                    as dform,
       tbd.obshiySchet                             as ObshiySchet,
       tbd.spkRSNrec                               as spkRSNrec,
       tbd.RejimNrec                               as rejimNrec,
       tbd.ZakazVibNrec                            as zakazVibNrec,
       tbd.summa                                   as summa,
       round(round(tbd.total, 6), 2)               as total,
       //coalesce(bfall.dopl,0)                      as dopl,
       //coalesce(bfall.summa, 0)                    as sumopl,
       coalesce(klval.simvolv,'')                  as val,
       coalesce(spkAvans.code,'')                  as avans,
       coalesce(katnotes.name,'')                  as status,
       coalesce(podrIspoln.kod,'')                 as ispoln,
       coalesce(spkDds.code,'')                    as dds,
       coalesce(substring(buhschet.schet,2,10),'') as buhsch,
       coalesce(avSogl.atl_lastdate,0)             as dsogl,
       coalesce(avSogl.vstring,'')                 as sogl,
       coalesce(org.name, '')                      as org,
       coalesce(bank.name, '')                     as bank,
       coalesce(bank.schet1, '')                   as bankrs,
       coalesce(getdocfo(bd.nrec),'-')             as cfo,
       coalesce(getdozakaz(bd.nrec),'-')           as zakaz,
       coalesce(dog.nodoc,'')                      as dognum,
       coalesce(dog.ddoc,0)                        as dogdate,
       bd.procopl                                  as procav,
       case when coalesce(avNaznPl.vString,'') = ''
            then bd.name
            else avNaznPl.vString
       end                                         as nazn,
       coalesce(spkSud.Code, '')                   as sud,
       coalesce(spkPEU.nrec, #comp(0))             as vizaPEUNrec,
       coalesce(spkKAZ.nrec, #comp(0))             as vizaKAZNrec,
       coalesce(spkKOM.nrec, #comp(0))             as vizaKOMNrec,
       coalesce(spkBUH.nrec, #comp(0))             as vizaBUHNrec,
       coalesce(spkPEU.name, '')                   as vizaPEUName,
       coalesce(spkKAZ.name, '')                   as vizaKAZName,
       coalesce(spkKOM.name, '')                   as vizaKOMName,
       coalesce(spkBUH.name, '')                   as vizaBUHName,
       coalesce(avVizaPEU.atl_lastdate, 0)         as vizaPEUDate,
       coalesce(avVizaKAZ.atl_lastdate, 0)         as vizaKAZDate,
       coalesce(avVizaKOM.atl_lastdate, 0)         as vizaKOMDate,
       coalesce(avVizaBUH.atl_lastdate, 0)         as vizaBUHDate,
       coalesce(uPEU.xu$loginname, '')             as vizaPEUUser,
       coalesce(uKAZ.xu$loginname, '')             as vizaKAZUser,
       coalesce(uKOM.xu$loginname, '')             as vizaKOMUser,
       coalesce(uBUH.xu$loginname, '')             as vizaBUHUser,
       coalesce(avPrimPEU.vString, '')             as vizaPEUPrim,
       coalesce(avPrimKAZ.vString, '')             as vizaKAZPrim,
       coalesce(avPrimKOM.vString, '')             as vizaKOMPrim,
       coalesce(avPrimBUH.vString, '')             as vizaBUHPrim

from (select bd.nrec,
             case when avVizaPEU.vcomp = #comp(0001000000001423h) then 1 else 0 end as obshiySchet,  //если виза ПЭУ = "с общих сч.", ставим признак общего счета
             coalesce(spkRS.nrec,#comp(0))       as spkRSNrec,
             coalesce(avZakazVib.vcomp,#comp(0)) as ZakazVibNrec,
             coalesce(avRejim.vcomp,#comp(0))    as RejimNrec,
             sum(sps.summa + case when bd.vhodnal=2 then sps.nds else 0 end) as summa,
             sum(case when bd.summa <> 0 then cast(sps.summa as double) / bd.summa * bd.total else 0 end) as total
      from bdreeagat
      join basedoc bd on bd.nrec = bdreeagat.crec
      join stepdoc sd on sd.cbasedoc = bd.nrec
      join spstep sps on sps.cstepdoc = sd.nrec
      left join attrval avRS on avRS.wtable = 1104 and avRS.cRec = sps.nrec and avRS.cattrnam = #comp(0001000000000A86h)
      left join spkau spkRS on spkRS.nrec = coalesce(avRS.vcomp,#comp(0))
      left join attrval avVizaPEU on avVizaPEU.wtable = 1102 and avVizaPEU.cRec = bd.nrec and avVizaPEU.cattrnam = #comp(0001000000000088h)
      left join attrval avZakazVib on avZakazVib.wtable = 1104 and avZakazVib.cRec = sps.nrec and avZakazVib.cattrnam = #comp(0001000000000A87h)
      left join attrval avRejim on avRejim.wtable = 1104 and avRejim.cRec = sps.nrec and avRejim.cattrnam = #comp(000100000000079Eh)
      group by bd.nrec,
               case when avVizaPEU.vcomp = #comp(0001000000001423h) then 1 else 0 end,
               coalesce(spkRS.nrec,#comp(0)),
               coalesce(avZakazVib.vcomp,#comp(0)),
               coalesce(avRejim.vcomp,#comp(0))
     ) tbd

join basedoc bd on bd.nrec = tbd.nrec

left join attrval avSogl on avSogl.wtable = 1102 and avSogl.cRec = bd.nrec and avSogl.cattrnam = #comp(000100000000008Bh)
left join attrval avVizaPEU on avVizaPEU.wtable = 1102 and avVizaPEU.cRec = bd.nrec and avVizaPEU.cattrnam = #comp(0001000000000088h)
left join attrval avVizaKAZ on avVizaKAZ.wtable = 1102 and avVizaKAZ.cRec = bd.nrec and avVizaKAZ.cattrnam = #comp(0001000000000087h)
left join attrval avVizaKOM on avVizaKOM.wtable = 1102 and avVizaKOM.cRec = bd.nrec and avVizaKOM.cattrnam = #comp(000100000000008Ah)
left join attrval avVizaBUH on avVizaBUH.wtable = 1102 and avVizaBUH.cRec = bd.nrec and avVizaBUH.cattrnam = #comp(0001000000000089h)
left join spkau   spkPEU    on spkPEU.nrec = coalesce(avVizaPEU.vcomp,#comp(0))
left join spkau   spkKAZ    on spkKAZ.nrec = coalesce(avVizaKAZ.vcomp,#comp(0))
left join spkau   spkKOM    on spkKOM.nrec = coalesce(avVizaKOM.vcomp,#comp(0))
left join spkau   spkBUH    on spkBUH.nrec = coalesce(avVizaBUH.vcomp,#comp(0))
left join x$users uPEU      on uPEU.atl_nrec = coalesce(avVizaPEU.atl_lastuser,#comp(0))
left join x$users uKAZ      on uKAZ.atl_nrec = coalesce(avVizaKAZ.atl_lastuser,#comp(0))
left join x$users uKOM      on uKOM.atl_nrec = coalesce(avVizaKOM.atl_lastuser,#comp(0))
left join x$users uBUH      on uBUH.atl_nrec = coalesce(avVizaBUH.atl_lastuser,#comp(0))
left join attrval avPrimPEU on avPrimPEU.wtable = 1102 and avPrimPEU.cRec = bd.nrec and avPrimPEU.cattrnam = #comp(0001000000000099h)
left join attrval avPrimKAZ on avPrimKAZ.wtable = 1102 and avPrimKAZ.cRec = bd.nrec and avPrimKAZ.cattrnam = #comp(0001000000000098h)
left join attrval avPrimKOM on avPrimKOM.wtable = 1102 and avPrimKOM.cRec = bd.nrec and avPrimKOM.cattrnam = #comp(000100000000009Ah)
left join attrval avPrimBUH on avPrimBUH.wtable = 1102 and avPrimBUH.cRec = bd.nrec and avPrimBUH.cattrnam = #comp(0001000000000097h)

left join attrval avAvans on avAvans.wtable=1102 and avAvans.crec=bd.nrec and avAvans.cattrnam=#comp(000100000000008Dh)
left join spkau spkAvans on spkAvans.nrec = coalesce(avAvans.vComp, #comp(0))

left join attrval avBdds on avBdds.wtable=1102 and avBdds.crec=bd.nrec and avBdds.cattrnam=#comp(000100000000008Eh)
left join spkau spkBdds on spkBdds.nrec = coalesce(avBdds.vComp, #comp(0))

left join attrval avNaznPl on avNaznPl.wtable=1102 and avNaznPl.crec=bd.nrec and avNaznPl.cattrnam=#comp(000100000000010Dh)

left join klval on klval.nrec = bd.cval

left join katnotes on katnotes.nrec = bd.cnote

left join attrval avIspoln on avIspoln.wtable=1102 and avIspoln.crec=bd.nrec and avIspoln.cattrnam=#comp(000100000000005Bh)
left join katpodr podrIspoln on podrIspoln.nrec=coalesce(avIspoln.vComp,#comp(0))

left join attrval avDds on avDds.wtable=1102 and avDds.crec=bd.nrec and avDds.cattrnam=#comp(000100000000008Fh)
left join spkau spkDds on spkDds.nrec = coalesce(avDds.vComp, #comp(0))

left join attrval avSch on avSch.wtable=1102 and avSch.crec=bd.nrec and avSch.cattrnam=#comp(0001000000000094h)
left join buhschet on buhschet.nrec = coalesce(avSch.vComp, #comp(0))

left join attrval avSud on avSud.wtable=1707 and avSud.crec=bd.cdogovor and avSud.cattrnam=#comp(0001000000001979h)
left join spkau spkSud on spkSud.nrec = avSud.vComp

left join katorg org on org.nrec = bd.corg

left join katbank bank on bank.nrec = bd.cbank

left join dogovor dog on dog.nrec = bd.cdogovor

left join (select cbasedoc, max(ddoc) as dopl, sum(summa) as summa
           from (select cbasedoc, summa, ddoc
                 from basefin
                 where tidkgal = 1 and cbasedoc in (select crec from bdreeagat)
                 union all
                 select cpredopldo, summa, ddoc
                 from basefin
                 where tidkbase = 0 and cpredopldo in (select crec from bdreeagat) and cpredopldo<>cbasedoc and tidkgal = 1
                ) bf
           group by cbasedoc
          ) bfall on bfall.cbasedoc = bd.nrec

left join (select sl.cdoc, max(datetime(sl.doper, sl.timeoper)) as dt
           from bdreeagat
           join statlog sl on sl.doctype=40 and sl.cdoc = bdreeagat.crec and sl.cnewnote=#comp(000100000000007Eh)
           group by sl.cdoc) lastStatlog on lastStatlog.cdoc = bd.nrec  //последний статус "в реестре"

left join statlog on statlog.doctype=40 and statlog.cdoc=bd.nrec and statlog.doper=date(lastStatlog.dt) and statlog.timeoper=time(lastStatlog.dt)
                     and (bd.cnote = #comp(000100000000007Eh) or bd.cnote = #comp(0001000000000077h)) //и тек. статус ДО "включен в реестр" или "оплачен"

;

//#endregion

var
  _qOpl: IQuery;
  _qSpstepData: IQuery;
  _contractIDHelper: ContractIDHelper;
  pXL: XLSRepBuilder;
  ea: iextattr;
  sXLSFileName, sXLTFileName: string;

create view
as select *
from tbd, trs,
     trs brrs (i02), tbd brbd, basedoc brbasedoc, katorg brorg, katbank brbank, dogovor brdog,
     katnotes brnote, statlog brstatlog(STATLOG02), klval brval, stepdoc brsd, basefin brbf
where ((
  brrs.ObshiySchet   == brbd.ObshiySchet  and
  brrs.spkRSNrec     == brbd.spkRSNrec    and
  brrs.zakazVibNrec  == brbd.zakazVibNrec and
  brrs.rejimNrec     == brbd.rejimNrec    and
  brbd.nrec          == brbasedoc.nrec    and
  brbasedoc.corg     == brorg.nrec        and
  brbasedoc.cbank    == brbank.nrec       and
  brbasedoc.cdogovor == brdog.nrec        and
  brbasedoc.cnote    == brnote.nrec       and
  40                 == brstatlog.doctype and
  brbasedoc.nrec     == brstatlog.cdoc    and
  brbasedoc.cval     == brval.nrec        and
  brbd.nrec          == brsd.cbasedoc     and
  brbd.nrec          == brbf.cbasedoc
));

create view vExtAttrInfo
var
  _wtable: word;
  _crec: comp;
  _cattrnam: comp;
as select *
from attrval, x$users
where ((
  _wtable   == attrval.wtable   and
  _crec     == attrval.crec     and
  _cattrnam == attrval.cattrnam and
  attrval.atl_lastuser == x$users.atl_nrec
));

create view vRs
var
  _obshiySchet : byte;
  _spkRSNrec   : comp;
  _zakazVibNrec: comp;
  _rejimNrec   : comp;
as select *
from trs, katstroy, spkau spkRejim, spkau spkRs
where ((
  _obshiySchet  == trs.ObshiySchet and
  _spkRSNrec    == trs.spkRSNrec and
  _zakazVibNrec == trs.zakazVibNrec and
  _rejimNrec    == trs.rejimNrec and
  _spkRSNrec    == spkRs.nrec and
  _rejimNrec    == spkRejim.nrec and
  _zakazVibNrec == katstroy.nrec
));

function isVizaSObshihSchetov(vizaNrec: comp): boolean;
{
  result := vizaNrec = VIZA_S_OBSHIH_SCHETOV;
}

function isStatusVklVReestr(_statusNrec: comp): boolean;
{
  result := _statusNrec = STATUS_VKL_V_REESTR or _statusNrec = STATUS_OPLACHEN;
}

function isVisaSet(_visaValueNrec: comp): boolean;
{
  result := _visaValueNrec = VIZA_SOGLASOVANO
         or _visaValueNrec = VIZA_NE_TREBUETSA
         or _visaValueNrec = VIZA_S_OBSHIH_SCHETOV;
}

function maxDateIn(d1: date; d2: date; d3: date = 0; d4: date = 0; d5: date = 0; d6: date = 0): date;
{
  var d: date; d := 0;
  if (d1 > d) d := d1;
  if (d2 > d) d := d2;
  if (d3 > d) d := d3;
  if (d4 > d) d := d4;
  if (d5 > d) d := d5;
  if (d6 > d) d := d6;
  result := d;
}

function LastVizaDateExceptPEU: date; //функция возвращает дату последней проставленной визы
{
  if (isVisaSet(brbd.vizaBUHNrec) and
      isVisaSet(brbd.vizaKAZNrec) and
      isVisaSet(brbd.vizaKOMNrec)) // все визы кроме ПЭУ есть
    result := maxDateIn(brbd.vizaKAZDate, brbd.vizaKOMDate, brbd.vizaBUHDate);
  else
    result := 0;
}

function DaysFromLastVizaToVklVReestr: integer; //функция возвращает кол-во дней с последней визы до включения в реестр
{
  var d1, d2: date;
  d1 := LastVizaDateExceptPEU;
  if d1 != 0
  {
    d2 := if(brbd.dvklree=0, cur_date, brbd.dvklree);

    var preres: integer;
    preres := CalcDaysBetweenDates(d1, d2, false) - 1;

    if preres < 0
      result := 0
    else
      result := preres;
  }
  else
  {
    result := 0;
  }
}

function DaysVklVReestrWithoutVizaPEU: integer; //функция возвращает кол-во дней с "в реестре" до даты визы пэу или до тек.даты, если виза не установлена
{
    var d1, d2: date;
    d1 := brbd.dvklree;
    if (not isVisaSet(brbd.vizaPEUNrec))
      d2 := if(brbasedoc.cnote = STATUS_OPLACHEN, brbd.dopl, Cur_Date)
    else
      d2 := brbd.vizaPEUDate;
    result := if(d2 > d1 and d1 > 0, CalcDaysBetweenDates(d1, d2, false) - 1, 0);
}

procedure CreateTmpTableForMarker(lMarker: longint; tableName: string);
{
  sqlCreateTmpTable('table '+tableName+'(crec: comp);', ctmNormal);
  sqlExecDirect('delete '+tableName);
  var q: IQuery;
  q := queryManager.createQuery('insert '+tableName+' (crec) values (:crec)');

  var i: longint = 0;
  var c: comp;
  for(i := 0; getmarker(lMarker, i, c); i++)
    q.SetParam('crec', c).execute;
}

procedure PrepareData(lBasedocMarker: longint);
{

  //создаем в sql временную таблицу для маркера. она используется в следующих запросах
  CreateTmpTableForMarker(lBasedocMarker, 'bdreeagat');

  //заполняем tbd данными по ДО из маркера в разрезе р/с выбытия, режимов исп, заказов выбытия
  delete all tbd;
  sqlCopyInto(queryManager.createQuery(querymain).getResultSet, #tbd, true);

  //заполняем tOpl данными по оплатам в разрезе ДО и р/с выбытия
  delete all tOpl;
  sqlCopyInto(queryManager.createQuery(qSelectOpl).getResultSet, #tOpl, true);

  //распределяем суммы из tOpl по tBd, т.е. из разреза ДО/рс  в  разрез ДО/рс/заказ/режим
  _loop tOpl
  {

    //сумма для распределения по текущему ДО/рс
    var sumForDistr: double;
    sumForDistr := tOpl.summa;

    //распределяем последовательно
    _loop tbd where (( tOpl.basedocNrec == tbd.nrec and tOpl.rsNrec == tbd.spkRsNrec ))
    {

      if tbd.total <= sumForDistr
      {
        tbd.sumopl  := tbd.total;
        sumForDistr -= tbd.total;
      }
      else
      {
        tbd.sumopl  := sumForDistr;
        sumForDistr := 0;
      }

      update current tbd;

      if sumForDistr = 0 break;

    }

    //в случае, если сумма для распределения > суммы ДО (переплата)
    if sumForDistr > 0
    {

      //Сумма, которую удалось распределить последовательно
      var sumDistributed: double;
      sumDistributed := tOpl.summa - sumForDistr;

      if sumDistributed <> 0
      {

        //распределяем сумму переплаты пропорционально распределенной
        _loop tbd where (( tOpl.basedocNrec == tbd.nrec and tOpl.rsNrec == tbd.spkRsNrec ))
        {
          tbd.sumopl += tbd.sumopl / sumDistributed * sumForDistr;
          update current tbd;
        }

      } //if sumDistributed <> 0

    } //if sumForDistr > 0

  } //_loop tOpl

  //считаем sumsaldo
  update tbd set tbd.sumsaldo := tbd.total - tbd.sumopl;

  //распределяем платежи по ДО, которые прошли не по тем рс, которые указаны в спецификации ДО
  var rs: IResultSet;
  rs := queryManager.createQuery(qSelectOplAnotherRS).getResultSet;

  if rs.getFirst = tsOk do
  {

    var curBasedocNrec: comp;
    curBasedocNrec := rs.row.Val('basedocNrec');

    var sumForDistr: double;
    sumForDistr := rs.row.Val('summa');

    _loop tbd where (( curBasedocNrec == tbd.nrec and (tbd.sumsaldo > 0) )) ordered by tbd.sumsaldo
    {

      if tbd.sumsaldo <= sumForDistr
      {
        tbd.sumopl   += tbd.sumsaldo;
        sumForDistr  -= tbd.sumsaldo;
        tbd.sumsaldo := 0;
      }
      else
      {
        tbd.sumopl   += sumForDistr;
        tbd.sumsaldo -= sumForDistr;
        sumForDistr  := 0;
      }

      update current tbd;

      if sumForDistr = 0 break;

    }

    //если после распределения сумма еще осталась - кидаем ее на первый попавшийся разрез по ДО
    if sumForDistr > 0
      if getfirst tbd where (( curBasedocNrec == tbd.nrec )) = tsOk
      {
        tbd.sumopl += sumForDistr;
        tbd.sumsaldo -= sumForDistr;
        update current tbd;
      }

  } while rs.getNext = tsOk;

  //заполняем trs группами р/с выбытия, режимов исп, заказов выбытия
  delete all trs;

  _loop tbd
  {

    vRs._obshiySchet  := tbd.ObshiySchet;
    vRs._spkRSNrec    := tbd.spkRSNrec;
    vRs._zakazVibNrec := tbd.zakazVibNrec;
    vRs._rejimNrec    := tbd.rejimNrec;

    if vRs.getfirst trs != tsOk
    {
      vRs.clearbuffer(#vRs.trs);
      vRs.trs.name := if(vRs.getfirst spkRs != tsOk, 'Р/с не установлен', vRs.spkRs.name)
                      + if((vRs.getfirst katstroy != tsOk and vRs.getfirst spkRejim != tsOk), '',  //если и заказ и режим не установлен - не выводим их (Акимов 25.12.17)
                           if(vRs.getfirst katstroy != tsOk, '; Заказ не установлен',
                              '; Заказ №' + substr(vRs.katstroy.name, 1, pos(' ', vRs.katstroy.name + ' ') - 1))
                           + if(vRs.getfirst spkRejim != tsOk, '; Режим использования счета не установлен',
                                '; ' + vRs.spkRejim.name));
      vRs.trs.summa    := tbd.summa;
      vRs.trs.total    := tbd.total;
      vRs.trs.sumopl   := tbd.sumopl;
      vRs.trs.sumsaldo := tbd.sumsaldo;
      vRs.insert current trs;
    }
    else
    {
      vRs.trs.summa    += tbd.summa;
      vRs.trs.total    += tbd.total;
      vRs.trs.sumopl   += tbd.sumopl;
      vRs.trs.sumsaldo += tbd.sumsaldo;
      vRs.update current trs;
    }
  }
}

function IBasedocReeReport.Title: string;
{
  result := 'Реестр ДО с группировкой по счетам, заказам, режимам использования счетов';
}

function vizafld(vizaName: string; dSogl: date; sogl: string): string; cacheable;
{
  if vizaName != ''
    result := vizaName
  else if dSogl > 0 and sogl != ''
    result := calcdaysbetweendates(dSogl, cur_date, true)+ ' дн.'
  else
    result := '';
}

function GetExtAttrSetInfo(aWTable: word; aCRec: comp; aCAttrnam: comp): string;
{
  vExtAttrInfo._wtable := aWTable;
  vExtAttrInfo._crec := aCRec;
  vExtAttrInfo._cattrnam := aCAttrnam;
  if vExtAttrInfo.getfirst attrval = tsOk
  {
    result := datetostr(vExtAttrInfo.attrval.atl_lastdate,'DD.MM.YYYY')
              + ' ' + timetostr(vExtAttrInfo.attrval.atl_lasttime, 'HH:MM')
              + ' ' + if(vExtAttrInfo.getfirst x$users = tsOk,
                         vExtAttrInfo.x$users.xu$loginname,
                         'неизвестный пользователь '+string(vExtAttrInfo.attrval.atl_lastuser,0,0));
  }
  else
    result := '';
}

function GetBDReeExtAttrInfo(aTitle: string; aCAttrnam: comp): string;
{
  result := aTitle+': '+GetExtAttrSetInfo(WT_BASEDOC, brbd.nrec, aCAttrnam);
}

procedure AddSpstepInfo(spstepDataResultSet: IResultSet;
                        xlsFieldName       : string;
                        queryFieldNrec     : string;
                        queryFieldName     : string;
                        delimiter          : string = ';');
{
  pXL.SetTblStringFldValue(xlsFieldName, '');

  var isFirst: boolean = true;
  var l: longint;
  l := initmarker('', 8, 10, 10, false);
  if spstepDataResultSet.getFirst = tsOk do
  {
    var c: comp;
    c := spstepDataResultSet.row.val(queryFieldNrec);
    if not foundmarker(l, c) and c != 0
    {
      insertmarker(l, c);

      if isFirst
        isFirst := false;
      else
        pXL.AddTblStringFldValue(xlsFieldName, delimiter+' ');

      pXL.AddTblStringFldValue(xlsFieldName, string(spstepDataResultSet.row.val(queryFieldName)));
    }
  } while spstepDataResultSet.getNext = tsOk;
  donemarker(l, '');
}

function IBasedocReeReport.PrintReport(lBasedocMarker: longint; params: IParams = nullref): boolean;
{

  var stopReport: boolean;

  startnewvisual(vtRotateVisual, vfBreak+vfTimer, ''#3'Формирование отчета'#13'Выборка данных', 0)

  _qOpl := queryManager.createQuery(qSelectOpl);

  _qSpstepData := queryManager.createQuery(qSpstepData);

  PrepareData(lBasedocMarker);

  stopReport := not nextvisual;
  if stopReport
  {
    StopVisual('', 0);
    exit;
  }
  SetVisualHeader(''#3'Формирование отчета'#13'Подготовка excel');

  var szakaz, scfo, _dds: string;

  sXLTFileName := TranslatePath('%StartPath%xls\agat_basedoc_ree_hier_rs_zakaz_rejim.xlt');
  sXLSFileName := pXL.CreateReport(sXLTFileName, True)
  //sXLSFileName := pXL.CreateXLT(sXLTFileName, True);

  pXL.CreateTbls(sXLSFileName);
  pXL.CreateTbl('Main');
  pXL.CreateTblFld('rowtype');
  pXL.CreateTblFld('viddoc');
  pXL.CreateTblFld('ddoc'  );
  pXL.CreateTblFld('nodoc' );
  pXL.CreateTblFld('bdds'  );
  pXL.CreateTblFld('dform' );
  pXL.CreateTblFld('summa' );
  pXL.CreateTblFld('total' );
  pXL.CreateTblFld('sumopl');
  pXL.CreateTblFld('sumsaldo');
  pXL.CreateTblFld('val'   );
  pXL.CreateTblFld('avans' );
  pXL.CreateTblFld('status');
  pXL.CreateTblFld('ispoln');
  pXL.CreateTblFld('dds'   );
  pXL.CreateTblFld('bdr'   );
  pXL.CreateTblFld('buhsch');
  pXL.CreateTblFld('dsogl' );
  pXL.CreateTblFld('sogl'  );
  pXL.CreateTblFld('vbuh'  );
  pXL.CreateTblFld('vkaz'  );
  pXL.CreateTblFld('vpeu'  );
  pXL.CreateTblFld('vdir'  );
  pXL.CreateTblFld('pbuh'  );
  pXL.CreateTblFld('pkaz'  );
  pXL.CreateTblFld('ppeu'  );
  pXL.CreateTblFld('pdir'  );
  pXL.CreateTblFld('vrez'  );
  pXL.CreateTblFld('soglinfo');
  pXL.CreateTblFld('org'   );
  pXL.CreateTblFld('bank'  );
  pXL.CreateTblFld('bankrs');
  pXL.CreateTblFld('init'  );
  pXL.CreateTblFld('ContractIdVib');
  pXL.CreateTblFld('BankRSVib');
  pXL.CreateTblFld('BankVib');
  pXL.CreateTblFld('ZakazVib');
  pXL.CreateTblFld('StVib');
  pXL.CreateTblFld('Rejim');
  pXL.CreateTblFld('cfo'   );
  pXL.CreateTblFld('zakaz' );
  pXL.CreateTblFld('contractId');
  pXL.CreateTblFld('viddoc');
  pXL.CreateTblFld('dognum' );
  pXL.CreateTblFld('dogdate');
  pXL.CreateTblFld('procav');
  pXL.CreateTblFld('nazn');
  pXL.CreateTblFld('npp');
  pXL.CreateTblFld('statusName');
  pXL.CreateTblFld('statusComment');
  pXL.CreateTblFld('daystoree');
  pXL.CreateTblFld('daysinree');
  pXL.CreateTblFld('sud');

  stopReport := not nextvisual;
  StopVisual('', 0);
  if stopReport exit;

  StartNewVisual(vtIndicatorVisual, vfBreak+vfTimer, ''#3'Формирование отчета'#13'Вывод в excel', recordsintable(#tbd));

  var _npp: longint = 0;

  _loop brrs {
    pXL.ClearTblBuffer;
    pXL.SetTblStringFldValue('rowtype'  , 'СЧЕТ');
    pXL.SetTblStringFldValue('npp'      , if(brrs.spkRSNrec!=0, '', if(brrs.ObshiySchet=1,'Общий счет ','Отдельный счет ')) + brrs.name);
    pXL.SetTblNumberFldValue('summa'    , brrs.summa                       );
    pXL.SetTblNumberFldValue('total'    , brrs.total                       );
    pXL.SetTblNumberFldValue('sumopl'   , brrs.sumopl                      );
    pXL.SetTblNumberFldValue('sumsaldo' , brrs.sumsaldo                    );
    pXL.InsTblRow;

    _loop brbd  {
      _npp += 1;
      //выводим строки
      if getfirst brbasedoc = tsOk {};
      if getfirst brorg     = tsOk {};
      if getfirst brbank    = tsOk {};
      if getfirst brnote    = tsOk {};
      pXL.ClearTblBuffer;
      pXL.SetTblStringFldValue('npp'      , _npp);
      pXL.SetTblStringFldValue('rowtype'  , 'ДО');
      pXL.SetTblStringFldValue('ddoc'     , datetostr(brbd.ddoc,'DD.MM.YYYY'));
      pXL.SetTblStringFldValue('nodoc'    , brbd.nodoc                       );
      pXL.SetTblStringFldValue('viddoc'   , if(brbd.viddoc=111,'ДО на предоплату закупок','ДО на закупку'));
      pXL.SetTblStringFldValue('nazn'     , brbd.nazn                        );
      pXL.SetTblStringFldValue('bdds'     , brbd.bdds                        );
      pXL.SetTblStringFldValue('dform'    , datetostr(brbd.dform,'DD.MM.YYYY'));
      pXL.SetTblNumberFldValue('summa'    , brbd.summa                       );
      pXL.SetTblNumberFldValue('total'    , brbd.total                       );
      pXL.SetTblNumberFldValue('sumopl'   , brbd.sumopl                      );
      pXL.SetTblNumberFldValue('sumsaldo' , brbd.sumsaldo                    );
      pXL.SetTblNumberFldValue('dopl'     , brbd.dopl                        );

      pXL.SetTblStringFldValue('avans'    , brbd.avans                       );
      pXL.SetTblStringFldValue('status'   , brbd.status                      );
      pXL.SetTblStringFldValue('ispoln'   , brbd.ispoln                      );
      if pos(' ',brbd.dds) > 0 {
        _dds := replace(substr(brbd.dds,1,pos(' ',brbd.dds)), '.', '');
      } else {
        _dds := brbd.dds;
      }
      pXL.SetTblStringFldValue('dds'      , _dds                             );
      pXL.SetTblStringFldValue('buhsch'   , brbd.buhsch                      );
      pXL.SetTblStringFldValue('dsogl'    , datetostr(brbd.dsogl,'DD.MM.YYYY'));
      pXL.SetTblStringFldValue('sogl'     , if(brbd.sogl='',
                                               calcdaysbetweendates(brbd.dform, cur_date, true)+' дн.',
                                               brbd.sogl));
      pXL.SetTblStringFldValue('vbuh'     , vizafld(brbd.vizaBUHName, brbd.dsogl, brbd.sogl));
      pXL.SetTblStringFldValue('vkaz'     , vizafld(brbd.vizaKAZName, brbd.dsogl, brbd.sogl));
      pXL.SetTblStringFldValue('vpeu'     , vizafld(brbd.vizaPEUName, brbd.dsogl, brbd.sogl));
      pXL.SetTblStringFldValue('vdir'     , vizafld(brbd.vizaKOMName, brbd.dsogl, brbd.sogl));

      pXL.SetTblStringFldValue('pbuh'     , brbd.vizaBUHPrim);
      pXL.SetTblStringFldValue('pkaz'     , brbd.vizaKAZPrim);
      pXL.SetTblStringFldValue('ppeu'     , brbd.vizaPEUPrim);
      pXL.SetTblStringFldValue('pdir'     , brbd.vizaKOMPrim);

      pXL.AddTblStringFldValue('soglinfo' ,         GetBDReeExtAttrInfo('Передано на согл.    ', ATTRNAM_SOGL));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('Казначейство         ', ATTRNAM_KAZN));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('Бухгалтерия          ', ATTRNAM_BUH));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('ПЭУ                  ', ATTRNAM_PEU));
      pXL.AddTblStringFldValue('soglinfo' , chr(10)+GetBDReeExtAttrInfo('Коммерческая дирекция', ATTRNAM_DIR));

      pXL.SetTblStringFldValue('org'      , brorg.name                       );
      pXL.SetTblStringFldValue('bank'     , brbank.name                      );
      pXL.SetTblStringFldValue('bankrs'   , brbank.schet1                    );

      var rsSpstepData: IResultSet;
      _qSpstepData.setParam('basedocNrec' , brbd.nrec);
      _qSpstepData.setParam('rsNrec'      , brrs.spkRSNrec);
      rsSpstepData := _qSpstepData.getResultSet;

      AddSpstepInfo(rsSpstepData, 'ContractIdVib'   , 'IGKNrec'     , 'IGKName');
      AddSpstepInfo(rsSpstepData, 'BankRSVib'       , 'RSNrec'      , 'RSName');
      AddSpstepInfo(rsSpstepData, 'BankVib'         , 'RSNrec'      , 'BankName');
      AddSpstepInfo(rsSpstepData, 'ZakazVib'        , 'ZakazVibNrec', 'ZakazVibName');
      AddSpstepInfo(rsSpstepData, 'Rejim'           , 'RejimNrec'   , 'RejimName');
      AddSpstepInfo(rsSpstepData, 'StVib'           , 'StVibNrec'   , 'StVibName');
      /*
      pXL.SetTblStringFldValue('zakaz'    , brbd.zakaz                       );
      pXL.SetTblStringFldValue('cfo'      , brbd.cfo                         );
      */
      AddSpstepInfo(rsSpstepData, 'zakaz'        , 'ZakazNrec', 'ZakazName');
      AddSpstepInfo(rsSpstepData, 'cfo'          , 'CFONrec'  , 'CFOKod');
      if (getfirst brsd = tsOk)
        pXL.SetTblStringFldValue('contractId', _contractIDHelper.GetContractId(GetContractIDKey_ByStepdoc, brsd.nrec));

      if getfirst brdog     = tsOk {
        pXL.SetTblStringFldValue('dognum' , brdog.nodoc                    );
        pXL.SetTblStringFldValue('dogdate', datetostr(brdog.ddoc,'DD.MM.YYYY'));
      }
      if getfirst brval     = tsOk {
        pXL.SetTblStringFldValue('val'    , brval.dollar);
      } else {
        pXL.SetTblStringFldValue('val'    , '');
      }
      pXL.SetTblStringFldValue('procav'   , if(brbasedoc.procopl>0, brbasedoc.procopl, ''));
      if getfirst brnote = tsOk
        pXL.SetTblStringFldValue('statusName', brnote.name);
      else
        pXL.SetTblStringFldValue('statusName', '');

      pXL.SetTblStringFldValue('statusComment', brbd.reestr);

      pXL.SetTblNumberFldValue('daystoree', DaysFromLastVizaToVklVReestr);
      pXL.SetTblNumberFldValue('daysinree', DaysVklVReestrWithoutVizaPEU);

      pXL.SetTblStringFldValue('sud', brbd.sud);

      pXL.InsTblRow;

      stopReport := not nextvisual;
      if stopReport break;

    } //loop brbd

    if stopReport break;

  }//loop brrs

  pXL.PublishTbl('Main');
  pXL.LoadReport(sXLSFileName);
  pXL.DisConnectExcel;
  result := true;

  stopvisual('', 0);

}

end.
