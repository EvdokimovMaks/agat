#include FeeSigners.vih
#include ExtAttr.vih

#component "AGAT"

interface FinDirSlZapParams 'Финансовая дирекция. Служебная записка' escClose doAccept;
show(,,40, 5);

var
  _basedocNrec: comp;
  _extAttr: iExtAttr;
  _feeSigners: FeeSigners;
  _feeSignersFld: string;
  _feeSignersNrec: comp;

create view
var
  _katpodrNrec: comp;
from KatPodr, Selection, Selection CurrentSelection
where ((
  _katpodrNrec == KatPodr.Nrec and
  FEESIGNERS_CONSTREP_FINDIR_SLZAP == Selection.vRep and
  (Selection.Name like (KatPodr.Kod + ':%')) and
  _feeSignersNrec == CurrentSelection.Nrec
));

parameters _basedocNrec, _feeSignersNrec;

screen sc1;
fields
  _feeSignersFld: protect, pickButton;
buttons
  cmDoOk, default;
  cmCancel;
<<

 `Кому`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

         <. Продолжить .> <. Отмена .>
>>
end;

procedure RefreshFeeSignersFld;
{
  if GetFirst CurrentSelection = tsOk
    _feeSignersFld := CurrentSelection.Name;
  else
    _feeSignersFld := '';
}

procedure SetFeeSignersNrec(aValue: comp);
{
  _feeSignersNrec := aValue;
  _feeSigners.FindFeeSignersByNRec(aValue);
  RefreshFeeSignersFld;
}

handleevent

cmDoOk: PutCommand(cmDefault);

cmPick:
{
  case curfield of
    #_feeSignersFld:
    {
      _feeSigners.RunWindowFeeSigners(1);
      SetFeeSignersNrec(_feeSigners.GetFeeSigners);
      rereadrecord;
    }
  end;
}

cmInit:
{
  _feeSigners.InitFeeSigners(FEESIGNERS_CONSTREP_FINDIR_SLZAP);

  _katpodrNrec := _extAttr.coGetAttrId(coBaseDoc, _basedocNrec, ATTRNAM_NREC_BASEDOC_ISPOLNITEL_DOGOVORA);
  if getfirst KatPodr = tsOk
    if getfirst Selection = tsOk
      SetFeeSignersNrec(Selection.Nrec);

  if _feeSignersNrec = 0
  {
    ReadMyDsk(_feeSignersNrec, 'FinDirSlZapParams_feeSignersNrec', false);
    SetFeeSignersNrec(_feeSignersNrec);
  }
}

cmDone:
{
  SaveMyDsk(_feeSignersNrec, 'FinDirSlZapParams_feeSignersNrec');
}

end; //handleevent

end.
