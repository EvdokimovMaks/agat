#include AtlProtocol.vih
#include GetKau.vih
#include ExtAttr.vih
#include StatLog.vih
#include PD_TXOGlobalState.vih
#include PD_ChangeStatusTxo.vih
#include SetSpstepAttributesBatch.vih

#component "AGAT"

interface SetSpstepAttributesBatch;

var
  _flags        : byte;
  _zakVibNrec   : comp;
  _rsSpisNrec   : comp;
  _rejIspNrec   : comp;
  _stVibNrec    : comp;
  _overwriteFlag: boolean;
  _getkau: GetKau;
  _ea: iExtAttr;
  _PDTXOGlobalState: PD_ITXOGlobalState(PD_TXOGlobalState);
  _PDChangeStatusTxo: PD_ChangeStatusTxo;
  #include StatLog.var

function GetFldValue(aNrec: comp; aKodgrkau: word; aFlagSet: boolean): string; forward;

create view
var
  _currentBasedocNrec: comp;

as select GetFldValue(_zakVibNrec, 40   , ((_flags and 1) > 0)) (fieldname=ZakVibFld),
          GetFldValue(_rsSpisNrec, 10545, ((_flags and 2) > 0)) (fieldname=RsSpisFld),
          GetFldValue(_rejIspNrec, 10695, ((_flags and 4) > 0)) (fieldname=RejIspFld),
          GetFldValue(_stVibNrec , 73   , ((_flags and 8) > 0)) (fieldname=StVibFld)

from basedoc, stepdoc, spstep
where ((
  _currentBasedocNrec == basedoc.nrec and
  _currentBasedocNrec == stepdoc.cbasedoc and
  stepdoc.nrec        == spstep.cstepdoc
))
;

window wConfig 'Установка атрибутов строк ДО' doaccept, escclose;
show(,,68,10);

screen sc1;
fields
  _flags   : noprotect;
  ZakVibFld: protect, pickbutton;
  RsSpisFld: protect, pickbutton;
  RejIspFld: protect, pickbutton;
  StVibFld : protect, pickbutton;
  _overwriteFlag: noprotect;
buttons
  cmDoOk, default;
  cmCancel;
<<

 [.] Заказ выбытия`           .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 [.] Р/счет списания`         .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 [.] Реж.исп. счета`          .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 [.] Статья по заказу выбытия`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

 [:] Заменять заполненные атрибуты`

 <. Продолжить .> <. Отмена .>
>>
end;

procedure PickKau(aBit: byte; aKodgrKau: word; var aNrec: comp);
{
  var c: comp;
  c := aNrec;
  if _getkau.GetCodeKau(1, aKodgrKau, c) > 0
  {
    aNrec := c;
    _flags := _flags or word(2 ** aBit);
  }
}

handleevent

cmDoOk: putcommand(cmDefault);

cmPick:
{

  case curfield of
    #ZakVibFld: PickKau(0, 40   , _zakVibNrec);
    #RsSpisFld: PickKau(1, 10545, _rsSpisNrec);
    #RejIspFld: PickKau(2, 10695, _rejIspNrec);
    #StVibFld : PickKau(2, 73   , _stVibNrec);
  end;
  rereadrecord;

}

end; //handleevent window

end; //window

function GetFldValue(aNrec: comp; aKodgrkau: word; aFlagSet: boolean): string;
{
  if aNrec = 0
    result := if(aFlagSet, 'Очистить', 'Не устанавливать');
  else
    result := if(getanykau(1, aKodgrkau, aNrec), givenanname(1), '');
}

handleevent

cmDelOnProtect:
{

  case curfield of
    #ZakVibFld: _zakVibNrec := 0;
    #RsSpisFld: _rsSpisNrec := 0;
    #RejIspFld: _rejIspNrec := 0;
    #StVibFld : _stVibNrec  := 0;
  end;
  rereadrecord;

}

cmCheckField:
{

  case curfield of
    #_flags: rereadrecord;
  end;

}

end; //handleevent

procedure SetSpstepExtattr(attrnamNrec: comp; valueNrec: comp; valueName: string; aProtocol: IAtlProtocol; aCaption: string);
{

  var isModifying: boolean;
  isModifying := _overwriteFlag;

  if _overwriteFlag = false
  {
    if _ea.coGetAttrId(1104, spstep.nrec, attrnamNrec) = 0
      isModifying := true;
    else
      aProtocol.LogLine(APE_WARNING, 'Строка ' + spstep.npp + ' ' + aCaption + '. Флаг "Заменять заполненный атрибут" не установлен, значение атрибута заполнено. Строка пропущена.');
  }

  if isModifying
  {

    if valueNrec = 0
    {
      if _ea.DeleteValueID(1104, spstep.nrec, attrnamNrec) = tsOk
        aProtocol.LogLine(APE_MESSAGE, 'Строка ' + spstep.npp + ' ' + aCaption + '. Значение очищено.');
    }
    else
    {
      if _ea.coSetAttrID(1104, spstep.nrec, attrnamNrec, valueNrec, valueName)
        aProtocol.LogLine(APE_MESSAGE, 'Строка ' + spstep.npp + ' ' + aCaption + '. Значение установлено.');
      else
        aProtocol.LogLine(APE_ERROR, 'Строка ' + spstep.npp + ' ' + aCaption + '. Ошибка при установке значения.');
    }

  }

}

//#region vipinterface public methods

function ShowConfig: word;
{

  result := runwindowmodal(wConfig);

}

procedure SaveToDSK(aPrefix: string = 'SetSpstepAttributesBatch');
{

  savemydsk(_flags        , aPrefix + '_flags');
  savemydsk(_zakVibNrec   , aPrefix + '_zakVibNrec');
  savemydsk(_rsSpisNrec   , aPrefix + '_rsSpisNrec');
  savemydsk(_rejIspNrec   , aPrefix + '_rejIspNrec');
  savemydsk(_stVibNrec    , aPrefix + '_stVibNrec');
  savemydsk(_overwriteFlag, aPrefix + '_overwriteFlag')

}

procedure LoadFromDSK(aPrefix: string = 'SetSpstepAttributesBatch');
{

  if not readmydsk(_zakVibNrec   , aPrefix + '_zakVibNrec'   , false) _zakVibNrec    := 0;
  if not readmydsk(_rsSpisNrec   , aPrefix + '_rsSpisNrec'   , false) _rsSpisNrec    := 0;
  if not readmydsk(_rejIspNrec   , aPrefix + '_rejIspNrec'   , false) _rejIspNrec    := 0;
  if not readmydsk(_stVibNrec    , aPrefix + '_stVibNrec'    , false) _stVibNrec     := 0;
  if not readmydsk(_flags        , aPrefix + '_flags'        , false) _flags         := 0;
  if not readmydsk(_overwriteFlag, aPrefix + '_overwriteFlag', false) _overwriteFlag := true;

}

procedure SetSpstepAttributes(aBasedocMarker: longint; aProtocol: IAtlProtocol);
{

  if aProtocol = nullref
    aProtocol := IAtlProtocol(new(AtlProtocol));

  var i, cnt: longint = 0;
  cnt := GetMarkerCount(aBasedocMarker);

  //Отключаем формирование проводок в техническом плане счетов, срабатывающее из
  //триггера изменения ВА к спецификации ДО. Иначе если в счете 100 позиций -
  //переформировывание проводок выполняется для счета 100 раз
  _PDTXOGlobalState.value := PD_TXOGLOBALSTATE_DONT_MAKE_TXO;

  for(i := 0; i < cnt; i++)
  {

    if not getmarker(aBasedocMarker, i, _currentBasedocNrec)
      continue;

    if getfirst fastfirstrow basedoc = tsOk
    {

      aProtocol.LogMessage(APE_MESSAGE, 'ДО №' + basedoc.nodoc + ' от ' + basedoc.ddoc);

      if basedoc.cnote = KATNOTES_NREC_BASEDOC_OPLACH and not pr_CurUserAdmin
      {
        aProtocol.LogLine(APE_WARNING, 'ДО в статусе "Оплачено". ДО пропущено');
      }
      else
      {

        _loop stepdoc
        {

          _loop spstep
          {

            var rslt: boolean = true;

            if (_flags and 1) > 0 SetSpstepExtattr(ATTRNAM_NREC_SPSTEP_ZAKAZ_VIBITIYA, _zakVibNrec, ZakVibFld, aProtocol, 'Заказ выбытия');

            if (_flags and 2) > 0 SetSpstepExtattr(ATTRNAM_NREC_SPSTEP_RS_SPISANIYA, _rsSpisNrec, RsSpisFld, aProtocol, 'Р/с списания');

            if (_flags and 4) > 0 SetSpstepExtattr(ATTRNAM_NREC_SPSTEP_REJIM_ISPOLZOVANIYA_SCHETA, _rejIspNrec, RejIspFld, aProtocol, 'Реж.исп. счета');

            if (_flags and 8) > 0 SetSpstepExtattr(ATTRNAM_NREC_SPSTEP_STATIA_VIBITIYA, _stVibNrec, StVibFld, aProtocol, 'Статья по заказу выбытия');

          } //spstep

        } //stepdoc

      }

      //Для счета в статусе "В реестре"
      //переформировываем проводки в техническом плане счетов.
      if basedoc.cNote = KATNOTES_NREC_BASEDOC_REESTR
      {
        var wOldNote: word = 0;
        var cOldNote: comp = 0;
        oStatLog.StatLog_GetLast(40, _currentBasedocNrec, wOldNote, cOldNote);
        _PDChangeStatusTxo.process(40, _currentBasedocNrec, cOldNote, basedoc.cNote);
      }

      aProtocol.LogCommit;

    } //basedoc

  }

  //Включаем формирование проводок в техническом плане счетов, срабатывающее из
  //триггера изменения ВА к спецификации ДО
  _PDTXOGlobalState.reset;

}

//#endregion

end.
