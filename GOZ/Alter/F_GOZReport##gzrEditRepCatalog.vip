#include v001_gzrF47Editor.vih

#component "F_GOZReport"

alter interface gzrEditRepCatalog;

overload
  function IgzrEditRepCatalog.GetIHotKey: string;
end;

function IgzrEditRepCatalog.GetIHotKey: string;
{
  result := inherited::GetIHotKey +
            ';SEPARATOR;F_GOZReport::AGAT_gzrEditRepCatalog';
}

function getRepNameToFileName(sRepName: string): string;
{
  result := Trim(Replace(Replace(sRepName, '\', '_'), '/', '_')) + '.xml';
}

function myRunExportReport: boolean;
{
  result := false;
  var isAllOk: boolean = True;
  do
  {
    var isNotMarker: boolean = (GetMarkerCount(MainMID) = 0);
    if (not isNotMarker)
    {
      if Message('Экспортировать помеченные отчеты?', YesNo+mfSwapButtons) != cmYes
      { Break; }
    }
    else
    {
      if Message('Экспортрировать отчет?', YesNo+mfSwapButtons) != cmYes
      { Break; }
      InsertMarker(MainMID, otTablRepCatalog.NRec);
    }
    var editor: v001_gzrF47Editor new;
    var sExportPath: string = '';
    var i: LongInt = 0;
    var cRec: comp = 0;
    StartNewVisual(vtIndicatorVisual, vfTimer + vfBreak, 'Экспорт', GetMarkerCount(MainMID));
    for (i:=0; GetMarker(MainMID, i, cRec); i++)
    {
      if (GetFirst FastFirstRow otTablRepCatalog where ((cRec == otTablRepCatalog.NRec)) = tsOk)
      {
        SetVisualHeader('Экспорт ' + otTablRepCatalog.Name);
        if not editor.InitParam(cgiNo, otTablRepCatalog.cReport)
        {
          FpLog(#(__InterfaceName__)+'.Editor.InitParam: ');
          isAllOk := false;
          break;
        }
        if sExportPath = ''
          sExportPath := GetDirName('Папка для экспорта отчетов', '', 0, 'Папка для экспорта отчетов');
        if sExportPath = ''
          break;
        SaveMyDsk(TranslatePath(sExportPath + '\' + getRepNameToFileName(otTablRepCatalog.Name)), 'v001_gzrF46Editor_sExportPath');
        editor.RunExportData;
        ClearMyDsk(sExportPath);
      }
      if not NextVisual break;
    }
    FreeVipInterface(editor);
    StopVisual;
  }
  while false;
  if (not isAllOk)  RunShowError;
  result := isAllOk;
}

handleevent

  cmValue59: myRunExportReport

end;

end.

AGAT_gzrEditRepCatalog menu {
  - 'Экспорт отчетов в XML', cmValue59;
}
