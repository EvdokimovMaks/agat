// Галактика 8.1
// Печатная форма корректировочного счет-фактуры
!--------------------------------------------------------------------------------------------------------------------
#doc
Корректировочный счет-фактура
#end
.linkform 'Schfrees_80' prototype is 'Schfrees'
.NameInList 'Корректировочный счет-фактура (старый вариант)'
.Group 'Бланки корректировочных счетов-фактур'
.Group 'Country' subGroup 'Russia'
.defo landscape
.set filler '-'
!-------------------------------------------------------------------
.create view vSchf
From
  SchFact
where
((
  nSchFact == SchFact.nRec
));
!-------------------------------------------------------------------
.var
  StrPrim1, StrPrim2 : String;
  is2004 : boolean;
  EmptyField : string;

  NumSigner  : word;
  KolSigners : word;
  arrFIO_Dir      : array[1..10] of string;
  arrPosition_Dir : array[1..10] of string;
  arrDover_Dir    : array[1..10] of string;
  arrFIO_Buh      : array[1..10] of string;
  arrPosition_Buh : array[1..10] of string;
  arrDover_Buh    : array[1..10] of string;
  isDirector      : boolean;
  isBuh           : boolean;
  wTipUser        : word;
  Sign            : double;
  isCorrV         : boolean;
.endvar
.begin
  is2004 := (ppPrintParam and 64) <> 0;
  EmptyField := if(wCountry=0,'-','');
end.
!-------------------------------------------------------------------
.if SetDateRange
.end
.{CheckEnter Schf
.if HeaderGroup
.end
.begin
  if (vSchf.GetFirst SchFact = 0)
    wTipUser := SchFact.TipUser;
  isCorrV := (wTipUser = 7241 or wTipUser = 7341);

  for (NumSigner := 1; NumSigner <= 10; NumSigner++)
  {
    arrFIO_Dir      [NumSigner] := '';
    arrPosition_Dir [NumSigner] := '';
    arrDover_Dir    [NumSigner] := '';
  }

  for (NumSigner := 1; NumSigner <= 10; NumSigner++)
  {
    arrFIO_Buh      [NumSigner] := '';
    arrPosition_Buh [NumSigner] := '';
    arrDover_Buh    [NumSigner] := '';
  }

  NumSigner := 0;
end.
.fields
  NSF_CorrV
  if (longint(dFact_CorrV)<>0, DateToStr(dFact_CorrV, '"DD" Mon YYYY г.'), '')

  NSF
  if (longint(dFact)<>0, DateToStr(dFact, '"DD" Mon YYYY г.'), '')

  if (Direct = 1,
      #f_(MyOrgName),
      #f_(OrgName))

  if (Direct = 1,
      #f_(MyOrgAddr),
      #f_(OrgAddr))

  if (Direct = 1, UnnORInn, OrgUnnORInn) + if (is2004, '/КПП', '') + ' продавца'
  if (Direct = 1,
      #f_(MyOrgINN + if (is2004 and (trim(MyOrgKPP) <> ''), ' / ' + MyOrgKPP, '')),
      #f_(OrgINN   + if (is2004 and (trim(OrgKPP)   <> ''), ' / ' + OrgKPP  , '')))

  if (Direct <> 1,
      #f_(MyOrgName),
      #f_(OrgName))

  if (Direct <> 1,
      #f_(MyOrgAddr),
      #f_(OrgAddr))

  if (Direct <> 1, UnnORInn, OrgUnnORInn) + if (is2004, '/КПП', '') + ' покупателя'
  if (Direct <> 1,
      #f_(MyOrgINN + if (is2004 and (trim(MyOrgKPP) <> ''), ' / ' + MyOrgKPP, '')),
      #f_(OrgINN   + if (is2004 and (trim(OrgKPP)   <> ''), ' / ' + OrgKPP  , '')))

.endfields


 Ш                                                                                                                                                                                                 Приложение №1 Ш
 Ш                                                                                                                                                                                           к письму ФНС России Ш
 Ш                                                                                                                                                                                  от  Д28.09.2011 Д №  ДЕД-4-3/15927 Ь



 Й                  КОРРЕКТИРОВОЧНЫЙ СЧЕТ-ФАКТУРА №  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д от  Д@@@@@@@@@@@@@@@@@@@@@ Д,                (1) Й

 Й                      к СЧЕТУ-ФАКТУРЕ №  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д от  Д@@@@@@@@@@@@@@@@@@@@@ Д                        (1.1) Й


 Й Продавец  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (2) Й
 Й Адрес  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (2а) Й
 Й @@@@@@@@@@@@@@@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (2б) Й
 Й Покупатель  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (3) Й
 Й Адрес  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (3а) Й
 Й @@@@@@@@@@@@@@@@@@  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (3б) Й
.if Dopol
.fields
  Nazn
.endfields
 Й Дополнение (условия оплаты по договору (контракту), способ отправления и т.п.) Й
 Й @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Й
.end
.fields
  Dollar
.endfields

 Ш                                                                                                                                                                                          Валюта: @@@@@@@@@@@@@@@@@@@@@@@@ Ш
 Ш┌────────────────────────┬─────────┬───────────────┬───────────────┬─────────────────────────────────────┬───────────────────────────────────┬──────┬───────────────────────────────────┬─────────────────────────────────────┐ Ш
 Ш│ Наименование товара    │ Единица │   Количество  │  Цена (тариф) │      Стоимость товаров (работ,      │          В том числе акциз        │Нало- │           Сумма налога            │  Стоимость товаров (работ, услуг),  │ Ш
 Ш│(описание выполненных   │измерения│               │ за единицу из-│     услуг), имущественных прав,     │                                   │говая │                                   │         имущественных прав,         │ Ш
 Ш│ работ, оказанных       │         │               │    мерения    │          всего без налога           │                                   │ставка│                                   │        всего с учетом налога        │ Ш
 Ш│ услуг), имущественного │         ├───────┬───────┼───────┬───────┼────────┬────────┬───────────────────┼───────┬───────┬───────────────────┤      ├───────┬───────┬───────────────────┼────────┬────────┬───────────────────┤ Ш
 Ш│ права                  │         │  до   │ после │  до   │ после │   до   │  после │      разница      │  до   │ после │      разница      │      │  до   │ после │      разница      │   до   │  после │      разница      │ Ш
 Ш│                        │         │измене-│измене-│измене-│измене-│ измене-│ измене-├─────────┬─────────┤измене-│измене-├─────────┬─────────┤      │измене-│измене-├─────────┬─────────┤ измене-│ измене-├─────────┬─────────┤ Ш
 Ш│                        │         │  ния  │  ния  │  ния  │  ния  │   ния  │   ния  │ к умень-│к доплате│  ния  │  ния  │ к умень-│к доплате│      │  ния  │  ния  │ к умень-│к доплате│   ния  │   ния  │ к умень-│к доплате│ Ш
 Ш│                        │         │       │       │       │       │        │        │  шению  │         │       │       │  шению  │         │      │       │       │  шению  │         │        │        │  шению  │         │ Ш
 Ш├────────────────────────┼─────────┼───────┼───────┼───────┼───────┼────────┼────────┼─────────┼─────────┼───────┼───────┼─────────┼─────────┼──────┼───────┼───────┼─────────┼─────────┼────────┼────────┼─────────┼─────────┤ Ш
 Ш│          1             │    2    │   3   │   3а  │   4   │   4а  │    5   │    5а  │    5б   │    5в   │   6   │   6а  │    6б   │    6в   │   7  │   8   │   8а  │    8б   │    8в   │    9   │   9а   │    9б   │    9в   │ Ш
 Ш├────────────────────────┼─────────┼───────┼───────┼───────┼───────┼────────┼────────┼─────────┼─────────┼───────┼───────┼─────────┼─────────┼──────┼───────┼───────┼─────────┼─────────┼────────┼────────┼─────────┼─────────┤ Ш
.{ CheckEnter FeeSignersMember_Schfrees_Dir
.begin
  NumSigner++;
  arrFIO_Dir      [NumSigner] := Signer_FIO_Director;
  arrPosition_Dir [NumSigner] := Signer_Position_Director;
  if (Signer_Dover_Director <> '')
    arrDover_Dir[NumSigner] := '(' + Signer_Dover_Director + ')';
end.
.}
.begin
  NumSigner := 0;
end.
.{ CheckEnter FeeSignersMember_Schfrees_Buh
.begin
  NumSigner++;
  arrFIO_Buh      [NumSigner] := Signer_FIO_MainBuh;
  arrPosition_Buh [NumSigner] := Signer_Position_MainBuh;
  if (Signer_Dover_MainBuh <> '')
    arrDover_Buh[NumSigner] := '(' + Signer_Dover_MainBuh + ')';
end.
.}
.{CheckEnter SchfKatSopr
.{CheckEnter SchfSpSopr
.{CheckEnter SchfSpSoprNalog
.} // SchfSpSoprNalog
.} // SchfSpSopr
.} // SchfKatSopr
.{CheckEnter SpSchf
.begin
  Sign := if (SpSumma < 0, -1.0, 1.0);
end.
.fields
  #f_(SpName)
  #f_(if (SpPrMC = 3, '', SpOtpEd))

  if (SpPrMC = 101 or SpPrMC = 102, '-',
    if (SpPrMC = 3, '0.0', if (abs(SpKol_Src) >= dMinKol, string(SpKol_Src, 0, Kol_Pr), '-')))
  if (SpPrMC = 101 or SpPrMC = 102, '-',
    if (SpPrMC = 3, '0.0', if (abs(SpKol_Src + SpKol) >= dMinKol, string(SpKol_Src + SpKol, 0, Kol_Pr), '-')))

#ifdef _RoundInDoc_
  if (SpPrMC = 3 or #isPrnprice, 0.0, SpPrNoNds_Src)
  if (SpPrMC = 3 or #isPrnPrice, 0.0, if (abs(SpKol) >= dMinKol,
                                                         (SpPrNoNds_Src * SpKol_Src + SpPrNoNds * SpKol) / (SpKol_Src + SpKol),
                                                         (SpPrNoNds_Src + SpPrNoNds)))
#else
  if (SpPrMC = 3 or #isPrnPrice, 0.0, SpSumNoNds_Src / SpKol_Src)
  if (SpPrMC = 3 or #isPrnPrice, 0.0, (SpSumNoNds_Src + SpSumNoNds) / (SpKol_Src + SpKol))
#end
  SpSumNoNds_Src
  (SpSumNoNds_Src + SpSumNoNDS)
  if (Sign < 0, Sign * SpSumNoNDS, 0.0)
  if (Sign > 0, Sign * SpSumNoNDS, 0.0)

  SpAkciz_Src
  (SpAkciz_Src + SpAkciz)
  if (Sign < 0, Sign * SpAkciz, 0.0)
  if (Sign > 0, Sign * SpAkciz, 0.0)

  if (SpPrMC = 101, '-',
    if (SpPrMC = 102, if (SpPerc = -1, '-', String(SpPerc, 2, 0) + '/' + String(100 + SpPerc, 3, 0)),
      if (SpExistNDS or Abs(SpPerc) >= 0.01,
          String(SpPerc, if (SpPerc >= 10, 2, 1), 0) + ' %', 'Без налога (НДС)')))

  if (SpPrMC = 101, '-',
      if (SpPrMC = 102, if (SpPerc = -1, '-', DoubleToStr(SpNDS_Src, '\2p[|-]3666666666666666666.88')),
          if ((SpExistNDS_Src or Abs(SpPerc) >= 0.01),
              DoubleToStr(SpNDS_Src, '\2p[|-]3666666666666666666.88'), 'Без налога (НДС)')))

  if (SpPrMC = 101, '-',
      if (SpPrMC = 102, if (SpPerc = -1, '-', DoubleToStr((SpNDS_Src + SpNDS), '\2p[|-]3666666666666666666.88')),
          if (SpExistNDS or Abs(SpPerc) >= 0.01,
              DoubleToStr((SpNDS_Src + SpNDS), '\2p[|-]3666666666666666666.88'), 'Без налога (НДС)')))

  if (Sign < 0, if (SpPrMC = 101, '-',
                    if (SpPrMC = 102, if (SpPerc = -1, '-', DoubleToStr(Sign * SpNDS, '\2p[|-]3666666666666666666.88')),
                        if (SpExistNDS or Abs(SpPerc) >= 0.01,
                            DoubleToStr(Sign * SpNDS, '\2p[|-]3666666666666666666.88'), '-'))), '-')


  if (Sign > 0, if (SpPrMC = 101, '-',
                         if (SpPrMC = 102, if (SpPerc = -1, '-', DoubleToStr(Sign * SpNDS, '\2p[|-]3666666666666666666.88')),
                             if (SpExistNDS or Abs(SpPerc) >= 0.01,
                                 DoubleToStr(Sign * SpNDS, '\2p[|-]3666666666666666666.88'), '-'))), '-')

  SpSumma_Src
  (SpSumma_Src + SpSumma)
  if (Sign < 0, Sign * SpSumma, 0.0)
  if (Sign > 0, Sign * SpSumma, 0.0)
.endfields
 Ш│@@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@│&#&&&&&│&#&&&&&│&#&&.&&│&#&&.&&│&#&&&.&&│&#&&&.&&│&#&&&&.&&│&#&&&&.&&│&#&&.&&│&#&&.&&│&#&&&&.&&│&#&&&&.&&│@~#@@@│&#&&.&&│&#&&.&&│&#&&&&.&&│&#&&&&.&&│&#&&&.&&│&#&&&.&&│&#&&&&.&&│&#&&&&.&&│ Ш
.} // SpSchf
.{CheckEnter KazSt
.}
.{CheckEnter NalogFinSchf
.} // NalogFinSchf
.if ItogoGroup       // итого по группе. Если true, то не печатать Итого
.end
.fields
 if (((SubTip=16) and wCountry=0), 0.0,
  if (#isPrnValue
     , if (isCorrV, ItSumNoNDS, 0.0), 0.0))

 if (((SubTip=16) and wCountry=0), 0.0,
  if (#isPrnValue
     , if (isCorrV, ItSumNoNDS_Dop, ItSumNoNDS), 0.0))

 if (isCorrV, ItNDS, 0.0)
 if (isCorrV, ItNDS_Dop, ItNDS)
 if (isCorrV, ItSumma, 0.0)
 if (isCorrV, ItSumma_Dop, ItSumma)
.endfields
 Ш├────────────────────────┴─────────┴───────┴───────┴───────┴───────┼────────┼────────┼─────────┼─────────┼───────┴───────┴─────────┴─────────┴──────┼───────┼───────┼─────────┼─────────┼────────┼────────┼─────────┼─────────┤ Ш
 Ш│  Итого по корректировочному счету-фактуре                        │    X   │    X   │&#&&&&.&&│&#&&&&.&&│                     X                    │   X   │   X   │&#&&&&.&&│&#&&&&.&&│    X   │    X   │&#&&&&.&&│&#&&&&.&&│ Ш
 Ш└──────────────────────────────────────────────────────────────────┴────────┴────────┴─────────┴─────────┴──────────────────────────────────────────┴───────┴───────┴─────────┴─────────┴────────┴────────┴─────────┴─────────┘ Ш
.begin
  NumSigner  := 0;
  KolSigners := if(KolSigners_Director >= KolSigners_MainBuh, KolSigners_Director, KolSigners_MainBuh);
end.
.fields
   Director
   Main_Buh
.endfields


 Ш       Руководитель организации           ______________    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       Главный бухгалтер  _____________    ^ Ш
 Ш                                            (подпись)                  (Ф.И.О.)                                                  (подпись)           (Ф.И.О.) Ш


.{?internal; (KolSigners_Director > 0 or KolSigners_MainBuh > 0)
 Ш       За руководителя организации                                                                          За главного бухгалтера Ш

.}
.{While (NumSigner < KolSigners)
.begin
  NumSigner++;
  IsDirector := if(KolSigners_Director >= NumSigner, true, false);
  IsBuh := if(KolSigners_MainBuh >= NumSigner, true, false);
end.
.{?internal; (isDirector and isBuh)
.fields
  arrPosition_Dir[NumSigner]
  arrFIO_Dir     [NumSigner]
  arrPosition_Buh[NumSigner]
  arrFIO_Buh     [NumSigner]
  arrDover_Dir   [NumSigner]
  arrDover_Buh   [NumSigner]
.endfields
 Ш        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  _______________  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ ______________  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ш
 Ш                                                (подпись)             (Ф.И.О.)                                                                     (подпись)             (Ф.И.О.) Ш
 Ш                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ш

.}
.{?internal; (isDirector and not isBuh)
.fields
  arrPosition_Dir[NumSigner]
  arrFIO_Dir     [NumSigner]
  arrDover_Dir   [NumSigner]
.endfields
 Ш        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ ________________  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ш
 Ш                                                (подпись)             (Ф.И.О.) Ш
 Ш                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ш

.}
.{?internal; (not isDirector and isBuh)
.fields
  arrPosition_Buh[NumSigner]
  arrFIO_Buh     [NumSigner]
  arrDover_Buh   [NumSigner]
.endfields
 Ш                                                                                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ ______________  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ш
 Ш                                                                                                                                                   (подпись)             (Ф.И.О.) Ш
 Ш                                                                                                                                              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Ш

.}
.}
 Ш       Индивидуальный предприниматель _______________  ____________________________  _______________________________________________________________________ Ш
 Ш                                        (подпись)              (Ф.И.О.)                   (реквизиты свидетельства о государственной регистрации Ш
 Ш                                                                                                     индивидуального предпринимателя) Ш



 Ш       Примечание. Первый экземпляр - покупателю, второй экземпляр - продавцу. Ш


.} // Schf
.endform
