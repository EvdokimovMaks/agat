//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Электронная отчетность"
// Поток данных : "Книга продаж для функционала Электронная отчетность"
//------------------------------------------------------------------------------

#doc
Поток данных : "Книга продаж для функционала Электронная отчетность"
#end
.linkform 'SFTR_BkSel' prototype is 'BkPrZkF'
.NameInList 'Данные "Книга продаж" => "Электронная отчетность"'
.Group 'SFTR_GroupBkSel'
.Group 'Country' subGroup 'Russia'

.declare
#include SFTR_BkListener.Vih
.enddeclare
.var
  pSpy : SFTR_BkListener;
  Buff : TSFTR_BkSelSpyBuffer;
.endvar

.{ CheckEnter FiltKontr
.} // CheckEnter FiltKontr
.{ CheckEnter FiltTip
.} // CheckEnter FiltTip
.{ CheckEnter FiltPodr
.} //CheckEnter FiltPodr
.fields
  UnnORInn + '/КПП'
  UnnORInn + '/КПП'
.endfields
Книга продаж
┌───┬────────┬───────────┬──────────┬──────────┬──────────┬───────────────┬──────────┬─────────────────────────┬────────────┬─────────┬───────────────────────────┬─────────────────────────────────────────┬───────────────────────────┬─────────────────┐
│ № │Код вида│  Номер и  │  Номер и │  Номер и │  Номер и │  Наименование │@~@@@@@@@@│  Сведения о посреднике  │Номер и дата│  Наиме- │ Стоимость продаж по счету-│ Стоимость продаж, облагаемых налогом, по│Сумма НДС по счету-фактуре,│Стоимость продаж,│
│п/п│операции│   дата    │   дата   │   дата   │   дата   │   покупателя  │покупателя│  (комиссионере, агенте) │ документа, │ нование │ фактуре, разница стоимости│   счету-фактуре, разница стоимости по   │    разница суммы НДС по   │ освобождаемых от│
│   │        │   счета-  │ исправле-│ корректи-│ исправле-│               │          │                         │подтверждаю-│  и код  │по корректировочному счету-│     корректировочному счету-фактуре     │  корректировочному счету- │налога, по счету-│
│   │        │  фактуры  │    ния   │ ровочного│    ния   │               │          │                         │щего уплату │  валюты │  фактуре (включая НДС) в  │ (без НДС) в рублях и копейках, по ставке│ фактуре, принимаемая к вы-│ фактуре, разница│
│   │        │ продавца  │  счета-  │  счета-  │ корректи-│               │          │                         │            │         │   валюте счета-фактуры    │                                         │ чету, в рублях и копейках,│   стоимости по  │
│   │        │           │  фактуры │  фактуры │ ровочного│               │          │                         │            │         │                           │                                         │         по ставке         │корректировочному│
│   │        │           │ продавца │ продавца │  счета-  │               │          ├────────────┬────────────┤            │         ├─────────────┬─────────────┼─────────────┬─────────────┬─────────────┼─────────────┬─────────────┤ счету-фактуре в │
│   │        │           │          │          │  фактуры │               │          │наименование│@~@@@@@@@@@@│            │         │   в валюте  │ в рублях и  │ 18 процентов│ 10 процентов│ 0 процентов │ 18 процентов│ 10 процентов│рублях и копейках│
│   │        │           │          │          │ продавца │               │          │ посредника │ посредника │            │         │счета-фактуры│  копейках   │             │             │             │             │             │                 │
├───┼────────┼───────────┼──────────┼──────────┼──────────┼───────────────┼──────────┼────────────┼────────────┼────────────┼─────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────────┤
│ 1 │    2   │     3     │     4    │     5    │     6    │       7       │     8    │      9     │     10     │     11     │    12   │     13а     │     13б     │      14     │      15     │      16     │      17     │      18     │        19       │
├───┼────────┼───────────┼──────────┼──────────┼──────────┼───────────────┼──────────┼────────────┼────────────┼────────────┼─────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────────┤
.{ // GLOBAL
.{ CheckEnter BkPrZkVal
.{ CheckEnter BkPrZkMain
.if TipUsersGroup
.end //if
.if PodrGroup
.end //if
.if GroupBKObjectStroi
.end //if
.if KatOrgGroup
.end //if
.if DesGrGroup
.end //if
.{ CheckEnter BookZk
.} // CheckEnter BookZk
.Fields
  NumPP                      // 01. Порядковый номер записи
  KodOp                      // 02. Код вида операции
  DatNomSF                   // 03. Номер и дата счета-фактуры продавца
  if(longint(DatRevSF) = 0 and NomRevSF = ''
  , '-'
  , if(ZeroPrint
    , NomRevSF
    , LTrim(NomRevSF, '0')) + ' ' + DateToStr(DatRevSF, 'DD/MM/YYYY'))        // 04. Номер и дата исправления счета-фактуры продавца
  if(longint(DatCorrV) = 0 and NomCorrV = ''
  , '-'
  , if(ZeroPrint
    , NomCorrV
    , LTrim(NomCorrV, '0')) + ' ' + DateToStr(DatCorrV, 'DD/MM/YYYY'))        // 05. Номер и дата корректировочного счета-фактуры продавца
  if(longint(DatRevCorrV) = 0 and NomRevCorrV = ''
  , '-'
  , if(ZeroPrint
    , NomRevCorrV
    , LTrim(NomRevCorrV, '0')) + ' ' + DateToStr(DatRevCorrV, 'DD/MM/YYYY'))  // 06. Номер и дата исправления корректировочного счета-фактуры продавца
  KontrNameNazn              // 07. Наименование покупателя
  KontrINN + '/' + KontrKPP  // 08. ИНН/КПП покупателя
  MediatorName               // 09. Наименование посредника
  MediatorINN_KPP            // 10. ИНН/КПП посредника
  sSomeDateOplata            // 11. Номер и дата документа, подтверждающего уплату налога
  KodNameVal                 // 12. Наименование и код валюты
  SumVal                     // 13а Стоимость продаж по счету-фактуре, разница стоимости по корректировочному счету фактуре (включая НДС) в валюте счета-фактуры в валюте счета-фактуры
  SummA                      // 13б Стоимость продаж по счету-фактуре, разница стоимости по корректировочному счету фактуре (включая НДС) в валюте счета-фактуры в рублях и копейках
  if (not isNDS20, Summ1, 0) // 14. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 18 процентов
  Summ4                      // 15. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 10 процентов
  Summ9                      // 16. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 0 процентов
  if (not isNDS20, Summ2, 0) // 17. Сумма НДС по счету-фактуре, разница стоимости по корректировочному счету- фактуре, в рублях и копейках, по ставке 18 процентов
  Summ5                      // 18. Сумма НДС по счету-фактуре, разница стоимости по корректировочному счету- фактуре, в рублях и копейках, по ставке 10 процентов
  Summ7                      // 19. Стоимость продаж, освобождаемых от налога, по счету-фактуре, разница стоимости по корректировочному счету-фактуре в рублях и копейках
.EndFields
.begin
  var SumValSF : double;  SumValSF := SumVal                   ;
  var SumRubSF : double;  SumRubSF := SummA                    ;
  var SumRub18 : double;  SumRub18 := if(not isNDS20, Summ1, 0);
  var SumRub10 : double;  SumRub10 := Summ4                    ;
  var SumRub00 : double;  SumRub00 := Summ9                    ;
  var SumNDS18 : double;  SumNDS18 := if(not isNDS20, Summ2, 0);
  var SumNDS10 : double;  SumNDS10 := Summ5                    ;
  var SumNDSNo : double;  SumNDSNo := Summ7                    ;

  var SumRub18_Vsego : double;  SumRub18_Vsego := if(not isNDS20 and IsIncItog, Summ1, 0);
  var SumNDS18_Vsego : double;  SumNDS18_Vsego := if(not isNDS20 and IsIncItog, Summ2, 0);

  ClearAdvRecord(Buff);
  Buff.NumPP           := NumPP          ;  // 01. Порядковый номер записи
  Buff.KodOp           := KodOp          ;  // 02. Код вида операции
  Buff.DatSF           := DatFact        ;  // 03. Дата счета-фактуры продавца
  Buff.NomSF           := NomSF          ;  // 03. Номер счета-фактуры продавца
  Buff.DatRevSF        := DatRevSF       ;  // 04. Дата исправления счета-фактуры продавца
  Buff.NomRevSF        := NomRevSF       ;  // 04. Номер исправления счета-фактуры продавца
  Buff.DatCorrV        := DatCorrV       ;  // 05. Дата корректировочного счета-фактуры продавца
  Buff.NomCorrV        := NomCorrV       ;  // 05. Номер корректировочного счета-фактуры продавца
  Buff.DatRevCorrV     := DatRevCorrV    ;  // 06. Дата исправления корректировочного счета-фактуры продавца
  Buff.NomRevCorrV     := NomRevCorrV    ;  // 06. Номер исправления корректировочного счета-фактуры продавца
  Buff.KontrNameNazn   := KontrNameNazn  ;  // 07. Наименование покупателя
  Buff.KontrINN        := KontrINN       ;  // 08. ИНН покупателя
  Buff.KontrKPP        := KontrKPP       ;  // 08. КПП покупателя
  Buff.MediatorName    := MediatorName   ;  // 09. Наименование посредника
  Buff.MediatorINN_KPP := MediatorINN_KPP;  // 10. ИНН/КПП посредника
  Buff.sSomeDateOplata := sSomeDateOplata;  // 11. Номер и дата документа, подтверждающего уплату налога
  Buff.KodNameVal      := KodNameVal     ;  // 12. Наименование и код валюты
  Buff.SumValSF        := SumValSF       ;  // 13а Стоимость продаж по счету-фактуре, разница стоимости по корректировочному счету фактуре (включая НДС) в валюте счета-фактуры в валюте счета-фактуры
  Buff.SumRubSF        := SumRubSF       ;  // 13б Стоимость продаж по счету-фактуре, разница стоимости по корректировочному счету фактуре (включая НДС) в валюте счета-фактуры в рублях и копейках
  Buff.SumRub18        := SumRub18       ;  // 14. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 18 процентов
  Buff.SumRub10        := SumRub10       ;  // 15. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 10 процентов
  Buff.SumRub00        := SumRub00       ;  // 16. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 0 процентов
  Buff.SumNDS18        := SumNDS18       ;  // 17. Сумма НДС по счету-фактуре, разница стоимости по корректировочному счету- фактуре, в рублях и копейках, по ставке 18 процентов
  Buff.SumNDS10        := SumNDS10       ;  // 18. Сумма НДС по счету-фактуре, разница стоимости по корректировочному счету- фактуре, в рублях и копейках, по ставке 10 процентов
  Buff.SumNDSNo        := SumNDSNo       ;  // 19. Стоимость продаж, освобождаемых от налога, по счету-фактуре, разница стоимости по корректировочному счету-фактуре в рублях и копейках
  Buff.SumRub18_Vsego  := SumRub18_Vsego ;  // ВСЕГО 14. Стоимость продаж, облагаемых налогом, по счету-фактуре, разница стоимости по корректировочному счету-фактуре (без НДС) в рублях и копейках, по ставке 18 процентов
  Buff.SumNDS18_Vsego  := SumNDS18_Vsego ;  // ВСЕГО 17. Сумма НДС по счету-фактуре, разница стоимости по корректировочному счету- фактуре, в рублях и копейках, по ставке 18 процентов

  pSpy.ReportRecDataBkSel(Buff);
end.
│@@@│@@@@@@@@│@@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@@@@@@│@~@@@@@@@@│@~@@@@@@@@@@│@~@@@@@@@@@@│@~@@@@@@@@@@│@~@@@@@@@│&'#&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&&&&&.&&│
.} // CheckEnter BkPrZkMain

.} // CheckEnter BkPrZkVal

.{ CheckEnter BkPrZkAllVal
.} // CheckEnter BkPrZkAllVal

.} // GLOBAL

.endform
