//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Электронная отчетность"
// Поток данных : "Книга покупок для функционала Электронная отчетность"
//------------------------------------------------------------------------------

#doc
Поток данных : "Книга покупок для функционала Электронная отчетность"
#end
.linkform 'SFTR_BkBuy' prototype is 'BkPrZkF'
.NameInList 'Данные "Книга покупок" => "Электронная отчетность"'
.Group 'SFTR_GroupBkBuy'
.Group 'Country' subGroup 'Russia'

.declare
#include SFTR_BkListener.Vih
.enddeclare
.var
  pSpy : SFTR_BkListener;
  Buff : TSFTR_BkBuySpyBuffer;
.endvar

.{ CheckEnter FiltKontr
.} // CheckEnter FiltKontr
.{ CheckEnter FiltTip
.} // CheckEnter FiltTip
.{ CheckEnter FiltPodr
.} //CheckEnter FiltPodr
.fields
  UnnORInn + '/КПП'
  UnnORInn + '/КПП'
.endfields
Книга покупок
┌───┬────────┬───────────┬──────────┬──────────┬──────────┬────────────┬──────────┬───────────────┬──────────┬─────────────────────────┬─────────┬─────────┬─────────────────┬─────────────────┐
│ № │Код вида│  Номер и  │  Номер и │  Номер и │  Номер и │Номер и дата│   Дата   │  Наименование │@~@@@@@@@@│  Сведения о посреднике  │  Номер  │ Наиме-  │Стоимость покупок│    Сумма НДС    │
│п/п│операции│   дата    │   дата   │   дата   │   дата   │ документа, │ принятия │    продавца   │ продавца │  (комиссионере, агенте) │  тамо-  │ нование │по счету-фактуре,│по счету-фактуре,│
│   │        │   счета-  │ исправле-│ корректи-│ исправле-│подтверждаю-│ на учет  │               │          ├────────────┬────────────┤  женной │  и код  │разница стоимости│разница суммы НДС│
│   │        │  фактуры  │    ния   │ ровочного│    ния   │щего уплату │ товаров  │               │          │            │            │  декла- │  валюты │       по        │       по        │
│   │        │ продавца  │  счета-  │  счета-  │ корректи-│   налога   │(работ,ус-│               │          │            │            │  рации  │         │корректировочному│корректировочному│
│   │        │           │  фактуры │  фактуры │ ровочного│            │луг), иму-│               │          │наименование│@~@@@@@@@@@@│         │         │  счету-фактуре  │  счету-фактуре, │
│   │        │           │ продавца │ продавца │  счета-  │            │щественных│               │          │ посредника │ посредника │         │         │  (включая НДС)  │  принимаемая к  │
│   │        │           │          │          │  фактуры │            │  прав    │               │          │            │            │         │         │     в валюте    │вычету,в рублях и│
│   │        │           │          │          │ продавца │            │          │               │          │            │            │         │         │  счета-фактуры  │     копейках    │
├───┼────────┼───────────┼──────────┼──────────┼──────────┼────────────┼──────────┼───────────────┼──────────┼────────────┼────────────┼─────────┼─────────┼─────────────────┼─────────────────┤
│ 1 │    2   │     3     │     4    │     5    │     6    │      7     │     8    │       9       │    10    │     11     │     12     │   13    │    14   │        15       │        16       │
├───┼────────┼───────────┼──────────┼──────────┼──────────┼────────────┼──────────┼───────────────┼──────────┼────────────┼────────────┼─────────┼─────────┼─────────────────┼─────────────────┤
.{ // GLOBAL
.{ CheckEnter BkPrZkVal
.{ CheckEnter BkPrZkMain
.if TipUsersGroup
.end //if
.if PodrGroup
.end //if
.if GroupBKObjectStroi
.end //if
.if KatOrgGroup
.end //if
.if DesGrGroup
.end //if
.{ CheckEnter BookZk
.} // CheckEnter BookZk
.Fields
  NumPP                      // 01. Порядковый номер записи
  KodOp                      // 02. Код вида операции
  DatNomSF                   // 03. Номер и дата счета-фактуры продавца
  if(longint(DatRevSF) = 0 and NomRevSF = ''
  , '-'
  , if(ZeroPrint
    , NomRevSF
    , LTrim(NomRevSF, '0')) + ' ' + DateToStr(DatRevSF, 'DD/MM/YYYY'))        // 04. Номер и дата исправления счета-фактуры продавца
  if(longint(DatCorrV) = 0 and NomCorrV = ''
  , '-'
  , if(ZeroPrint
    , NomCorrV
    , LTrim(NomCorrV, '0')) + ' ' + DateToStr(DatCorrV, 'DD/MM/YYYY'))        // 05. Номер и дата корректировочного счета-фактуры продавца
  if(longint(DatRevCorrV) = 0 and NomRevCorrV = ''
  , '-'
  , if(ZeroPrint
    , NomRevCorrV
    , LTrim(NomRevCorrV, '0')) + ' ' + DateToStr(DatRevCorrV, 'DD/MM/YYYY'))  // 06. Номер и дата исправления корректировочного счета-фактуры продавца
  sSomeDateOplata            // 07. Номер и дата документа, подтверждающего уплату налога
  DatOpr                     // 08. Дата принятия на учет товаров (работ,услуг), имущественных прав
  KontrNameNazn              // 09. Наименование продавца
  KontrINN + '/' + KontrKPP  // 10. ИНН/КПП продавца
  MediatorName               // 11. Наименование посредника
  MediatorINN_KPP            // 12. ИНН/КПП посредника
  GTDCountry                 // 13. Номер таможенной декларации
  GTDNomer                   // 13. Номер таможенной декларации
  KodNameVal                 // 14. Наименование и код валюты
  SumValSF                   // 15. Стоимость покупок по счету-фактуре, разница стоимости по корректировочному счету-фактуре (включая НДС) в валюте счета-фактуры
  SumNDSRub                  // 16. Сумма НДС по счету-фактуре, разница суммы НДС по корректировочному счету-фактуре, принимаемая к вычету, в рублях и копейках
.EndFields
.begin
  var SumNDSRub_Vsego : double;  SumNDSRub_Vsego := if (IsIncItog, SumNDSRub, 0);

  ClearAdvRecord(Buff);
  Buff.NumPP           := NumPP          ;  // 01. Порядковый номер записи
  Buff.KodOp           := KodOp          ;  // 02. Код вида операции
  Buff.DatSF           := DatFact        ;  // 03. Дата счета-фактуры продавца
  Buff.NomSF           := NomSF          ;  // 03. Номер счета-фактуры продавца
  Buff.DatRevSF        := DatRevSF       ;  // 04. Дата исправления счета-фактуры продавца
  Buff.NomRevSF        := NomRevSF       ;  // 04. Номер исправления счета-фактуры продавца
  Buff.DatCorrV        := DatCorrV       ;  // 05. Дата корректировочного счета-фактуры продавца
  Buff.NomCorrV        := NomCorrV       ;  // 05. Номер корректировочного счета-фактуры продавца
  Buff.DatRevCorrV     := DatRevCorrV    ;  // 06. Дата исправления корректировочного счета-фактуры продавца
  Buff.NomRevCorrV     := NomRevCorrV    ;  // 06. Номер исправления корректировочного счета-фактуры продавца
  Buff.sSomeDateOplata := sSomeDateOplata;  // 07. Номер и дата документа, подтверждающего уплату налога
  Buff.DatOpr          := DatOpr         ;  // 08. Дата принятия на учет товаров (работ,услуг), имущественных прав
  Buff.KontrNameNazn   := KontrNameNazn  ;  // 09. Наименование продавца
  Buff.KontrINN        := KontrINN       ;  // 10. ИНН продавца
  Buff.KontrKPP        := KontrKPP       ;  // 10. КПП продавца
  Buff.MediatorName    := MediatorName   ;  // 11. Наименование посредника
  Buff.MediatorINN_KPP := MediatorINN_KPP;  // 12. ИНН/КПП посредника
  Buff.GTDCountry      := GTDCountry     ;  // 13. Номер таможенной декларации
  Buff.GTDNomer        := GTDNomer       ;  // 13. Номер таможенной декларации
  Buff.KodNameVal      := KodNameVal     ;  // 14. Наименование и код валюты
  Buff.SumValSF        := SumValSF       ;  // 15. Стоимость покупок по счету-фактуре, разница стоимости по корректировочному счету-фактуре (включая НДС) в валюте счета-фактуры
  Buff.SumNDSRub       := SumNDSRub      ;  // 16. Сумма НДС по счету-фактуре, разница суммы НДС по корректировочному счету-фактуре, принимаемая к вычету, в рублях и копейках
  Buff.SumNDSRub_Vsego := SumNDSRub_Vsego;  // ВСЕГО 16. Сумма НДС по счету-фактуре, разница суммы НДС по корректировочному счету-фактуре, принимаемая к вычету, в рублях и копейках

  pSpy.ReportRecDataBkBuy(Buff);
end.
│@@@│@@@@@@@@│@@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@@@@@@│@~@@@@@@@@│@~@@@@@@@@@@│@~@@@@@@@@@@│@~@@ @~@@│@~@@@@@@@│&'#&&&&&&&&&&&.&&│&'#&&&&&&&&&&&.&&│
.} // CheckEnter BkPrZkMain

.} // CheckEnter BkPrZkVal

.{ CheckEnter BkPrZkAllVal
.} // CheckEnter BkPrZkAllVal

.} // GLOBAL

.endform
