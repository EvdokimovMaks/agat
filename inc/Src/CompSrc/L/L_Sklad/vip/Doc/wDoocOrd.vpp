!╔═══════════════════════════════════════════════════════════════════════════╗
!║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
!║ Проект        : ГАЛАКТИКА                                                 ║
!║ Система       : Оперативный контур                                        ║
!║ Версия        : 5.20                                                      ║
!║ Назначение    : Окно расширенной информации специф. ордера по дооценке    ║
!║ Ответственный : Лебедев Сергей Владимирович                               ║
!║ Параметры     : есть                                                      ║
!╚═══════════════════════════════════════════════════════════════════════════╝
!╔═══════════════════════════════════════════════════════════════════════════╗
!║ Используют :                                                              ║
!╚═══════════════════════════════════════════════════════════════════════════╝
#doc
Спецификация ордера переоценки
#end
Window wDoocOrd 'Спецификация ордера переоценки' EscClose;
  Show at (,,104,27);

Panel pDoocOrd
  Show at (,,,15);
  Table SpOrder;

Screen scDoocOrd (,hcSkladWDoocOrdExtSpec,sci178Esc)
  Bevel b1 {2, 5, 102, 6};// bsHorizontal};

Fields
  KatMC.Name      ('Наименование МЦ'        ,,sci1378Esc) : protect;
  SpOrder.sNameVar('Наименование для печати',,          ) : NoProtect;
  KatMC.BarKod    ('Номенклатурный номер МЦ',,sci1378Esc) : protect;
  KatParty.Kod    ('Партия МЦ',,sci1378Esc);
  KatParty.Name   ('Партия МЦ',,sci1378Esc);
  KolMC           : skip;
  KatEd.abbr      : skip;
  OldSrPrice                                                  : [brForSpOrder_Price , '\2p[|-]36`666`666`666`666.88'];
  oValFunc.GetValSymbol(0): skip;
  OldVprice                                                   : [brForSpOrder_VPrice, '\2p[|-]36`666`666`666`666.88'];
  KlVal.SimvolV   : skip;
  
  NewSrPrice      ('Новая отпускная цена'                 ,,) : [brForSpOrder_Price , '\2p[|-]36`666`666`666`666.88'], NoProtect;
  oValFunc.GetValSymbol(0): skip;
  NewVPrice       ('Новая валютная отпускная цена'        ,,) : [brForSpOrder_VPrice, '\2p[|-]36`666`666`666`666.88'], NoProtect;
  NewSimvolV      ('Символ валюты',,sci1378Esc);
  SpOrder.SrPrice ('Сумма переоценки'                     ,,) : [brForSpOrder_Price , '\2p[|-]36`666`666`666`666.88'], NoProtect;
  oValFunc.GetValSymbol(0): skip;
  SpOrder.VPrice  ('Сумма переоценки в валюте'            ,,) : [brForSpOrder_VPrice, '\2p[|-]36`666`666`666`666.88'], NoProtect;
  NewSimvolV      : skip;
  oldPriceN       ('Цена МЦ для налогового учета'         ,,) : [brForSpOrder_PriceE, '\2p[|-]36`666`666`666`666.88'], skip;
  strNDE          : skip;
  newPriceN       ('Новая цена для налогового учета'      ,,) : [brForSpOrder_PriceE, '\2p[|-]36`666`666`666`666.88'], NoProtect;
  strNDE1         : skip;
  SpOrder.priceN  ('Сумма переоценки для налогового учета',,) : [brForSpOrder_PriceE, '\2p[|-]36`666`666`666`666.88'], NoProtect;
  strNDE2         : skip;
<<

  `Наименование МЦ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
        `для печати`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  `Ном. номер МЦ`   .@@@@@@@@@@@@@@@@
  `Партия`          .@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

  `Количество `     .@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@
  `Цена `           .@@@@@@@@@@@@@@@@.@@@@ `в валюте `.@@@@@@@@@@@@@@@.@@@@@

  `Новая цена      `.@@@@@@@@@@@@@@@@.@@@@ `в валюте `.@@@@@@@@@@@@@@@.@@@@@
  `Сумма переоценки`.@@@@@@@@@@@@@@@@.@@@@ `в валюте `.@@@@@@@@@@@@@@@.@@@@@

  `Цена для НУ`            .@@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@
  `Новая цена для НУ`      .@@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@
  `Сумма переоценки для НУ`.@@@@@@@@@@@@@@@@@@@@@@@@@.@@@@@
>>
end; // screen
end; // panel

Panel pDoocOrd2;
  show (,16,,);
  table SpOrder;

TabbedSheet Bottom tsSpOrdscrExtendetInfo2

Embedded scKauEdit2 'Целевой учет' Interface;
end;

end;

HandleEvent
cmPositionChanged :
{
   if ((CurTable = #SpOrder) and (CurWindow = wDoocOrd) and (isDoocen))
   {
     NewSrPrice := NewVPrice := 0;

     if (not GetMCInfo)
       Exit;
   }
}

cmOpenSearch: abort;

End; // handleevent Panel
End; // panel;

HandleEvent //window

cmInit :
{
  strNDE := strNDE1 := strNDE2 := oValFunc.GetValSymbol(0);

  SetFieldAndLabelVisible(#oldPriceN     , boGetTune('Oper.NalUch') and (SpOrder.vidOrder = 0));
  SetFieldAndLabelVisible(#newPriceN     , boGetTune('Oper.NalUch') and (SpOrder.vidOrder = 0));
  SetFieldAndLabelVisible(#SpOrder.PriceN, boGetTune('Oper.NalUch') and (SpOrder.vidOrder = 0));
  SetFieldAndLabelVisible(#strNDE        , boGetTune('Oper.NalUch') and (SpOrder.vidOrder = 0));
  SetFieldAndLabelVisible(#strNDE1       , boGetTune('Oper.NalUch') and (SpOrder.vidOrder = 0));
  SetFieldAndLabelVisible(#strNDE2       , boGetTune('Oper.NalUch') and (SpOrder.vidOrder = 0));

  DisableCommand(cmAttrib); //блокируем вызов этого окна в меню

  if not getMCinfo
    Exit;

  isPick:=False;
}


cmInsert:
{
  if isProtected(#SklOrder)
  {
    stop;
    ProcessCommand(cmProtectedInput);
  }
}


cmOpenSearch:
{
  if isProtected(#SklOrder) or isProtected(#SpOrder)
  {
    stop;
    ProcessCommand(cmProtectedInput);
    Exit;
  }

 case curField of
   #SpOrder.SrPrice:
   {
     Stop;

     var GetSum: double; GetSum := SpOrder.SrPrice;

     if (RunDialog('GetPriceTov', GetSum) <> cmCancel)
     {
       set SpOrder.SrPrice  := GetSum;
       set SpOrder.rSrPrice := GetSum;

       ProcessCommand(cmCheckField);
     }
   }
 end;
}

cmSaveDoc :
  GetSaveDoc;

cmNal :
  if (CurTable = #SpOrder)
    if (UpdateTable)
      RunInterface('GetSaldoMC', SkPr, SpOrder.dOrd, SpOrder.cMC, 0,0,0);

cmShowAll :
  RunInterface('GSaldDat', SklOrder.dOrd, word(0), SpOrder.cMC,
               SpOrder.ccPodr, SpOrder.ccMOL, SpOrder.cParty, Comp(0));

cmDone :
{
  EnableCommand(cmAttrib); //разблокируем вызов этого окна в меню

  if ( UpdateTable )
    RereadRecord(#SpOrder);
}

End; // HandleEvent for Window
End; // window wSpOrder
