//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.1
// Тиражирование объектов недвижимости
//------------------------------------------------------------------------------
#ifndef _RltObjReplicator_vih_
#define _RltObjReplicator_vih_

#component "L_REALTING"

// Данное исключение может быть выброшено, если в процессе репликации будут обнаружены проблемы со структурой
// дерева объектов. Это означает, что где-то есть ошибки в алгоритмах. Я решил не прятать такие ошибки.
exception ExBadDataStructure;

objinterface IRltObjReplicator;
  #doc
  Выполнить одну репликацию объекта - источника, разместив копию в заданный узел дерева.</brief>
  #end
  function Replicate(
    _srcObjTreeBuf:  type$RltObjTree; //#docl Буфер записи объекта источника
    _dstRltOwner:    comp;    //#docl Зависит от типа объекта (см. параметр pRltOwner методов doInsertObj соотв. объектов).
    _dstObjTreeNode: comp;    //#docl Ссылка на узел rltObjTree, в который выполняется копирование исходного объекта
    _strVal1:        string;  //#docl Для комнат - фактический номер комнаты, для остальных объектов - наименование представления объекта в иерархии
    _strVal2:        string;  //#docl Номер комнаты в БТИ, номер этажа, номер помещения, для остальных - наименование объекта
    _longVal:        longint; //#docl Для этажа - уровень, для остальных - не используется
    _isCopyChild:    boolean  //#docl Нужно ли копировать дочерние объекты
  ): boolean;                 //#docl В случае ошибки возвращается false.

  #doc
  Для объекта "Комната" ищет этаж, на котором расположена комната и соответствующий элемент дерева.
  #end
  function GetOwnersOfRoom(_objTreeBuf: type$RltObjTree; out _floor: comp; out _treeOwner: comp): boolean;

  #doc
  Для указанного узла дерева ищет и возвращает объект "Этаж".</brief>
  Либо возвращает 0, если объект не находится ни на каком этаже.
  (на этажах могут находиться "Комната", "Комнаты", "Помещение", "Помещения")
  #end
  function GetFloorOf(_objTreeBuf: type$RltObjTree): comp;

  #doc
  Для указанного узла дерева ищет и возвращает ссылку на земельный участок, либо 0, если объект
  найти не удалось (это возможно только в случае разрушенных структур данных в rltObjTree).
  #end
  function GetZemComplOf(_objTreeBuf: type$RltObjTree): comp;
end;

vipinterface RltObjReplicator implements IRltObjReplicator licensed(free);

#endif
