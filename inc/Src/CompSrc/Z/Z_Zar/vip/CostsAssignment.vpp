/*
                       (c) 1994,2004 корпорация ГАЛАКТИКА
  Проект        : ГАЛАКТИКА
  Система       : Заработная плата
  Назначение    : Макроопределения для работы с объектным интерфейсом "Отнесение затрат"
  Ответственный : Корзюк Виталий Францевич
*/

//------------------------------------------------------------------------------
// описание вычисляемых полей для отображения ШПЗ (для скринов)
//------------------------------------------------------------------------------
#declare DeclareCostsAssignmentFields(CA,FieldPrefix=Debit)
  [#(FieldPrefix)Code] (#CA.GetCode) ('Код отнесения затрат',, sci13Esc): protect, quickchoice,
    {font = {color = ColorSysGray}};
  [#(FieldPrefix)Account] (#CA.GetAccount) ('Счет',, sci13Esc): protect, quickchoice;
  [#(FieldPrefix)ControlAccount] (#CA.GetControlAccount) ('Субсчет счета',, sci13Esc): protect, quickchoice;
  [#(FieldPrefix)AnalyticsStr] (#CA.GetAnalyticsStr) ('Аналитика',, sci13Esc): protect;
  [#(FieldPrefix)BusinessTransactionStr] (#CA.GetBusinessTransactionStr) ('Типовая хозяйственная операция',, sci13Esc): protect;
  [#(FieldPrefix)BusinessTransactionParam] (#CA.GetBusinessTransactionParam) ('Параметр типовой хозяйственной операции',, sci13Esc): noprotect;
#end

//------------------------------------------------------------------------------
// для включения в обработчик cmPick
//------------------------------------------------------------------------------
#declare PickCostsAssignment(CAF,CA,OpenQuickChoice,FieldPrefix=Debit)
  ##(FieldPrefix)Code:
    if #CAF.PickCode(#CA, #OpenQuickChoice)
      ReDrawPanel(CurTable);

  ##(FieldPrefix)Account:
  {
    if #CAF.PickAccount(#CA, #OpenQuickChoice)
    {
      if #CA.GetControlAccount <> ''
        SelectField(##(FieldPrefix)AnalyticsStr);
      else
        SelectField(##(FieldPrefix)ControlAccount);
      ReDrawPanel(CurTable);
    }
  }

  ##(FieldPrefix)ControlAccount:
  {
    if #CAF.PickControlAccount(#CA, #OpenQuickChoice)
    {
      if #CA.GetControlAccount <> ''
        SelectField(##(FieldPrefix)AnalyticsStr);
      ReDrawPanel(CurTable);
    }
  }

  ##(FieldPrefix)AnalyticsStr:
    if #CAF.PickAnalytics(#CA)
      ReDrawPanel(CurTable);

  ##(FieldPrefix)BusinessTransactionStr:
    if #CAF.PickBusinessTransaction(#CA, #OpenQuickChoice)
      ReDrawPanel(CurTable);

  ##(FieldPrefix)BusinessTransactionParam:
    if #CAF.PickBusinessTransactionParam(#CA)
      ReDrawPanel(CurTable);
#end

//------------------------------------------------------------------------------
// для включения в обработчик cmDelOnProtect
//------------------------------------------------------------------------------
#declare ClearCostsAssignment(CA,FieldPrefix=Debit)
  ##(FieldPrefix)Code:
  {
    #CA.ClearCode;
    ReDrawPanel(CurTable);
  }

  ##(FieldPrefix)Account:
  {
    #CA.ClearAccount;
    ReDrawPanel(CurTable);
  }

  ##(FieldPrefix)ControlAccount:
  {
    #CA.ClearControlAccount;
    ReDrawPanel(CurTable);
  }

  ##(FieldPrefix)AnalyticsStr:
  {
    #CA.ClearAnalytics;
    ReDrawPanel(CurTable);
  }

  ##(FieldPrefix)BusinessTransactionStr:
  {
    #CA.ClearBusinessTransaction;
    ReDrawPanel(CurTable);
  }
#end

//------------------------------------------------------------------------------
// для включения в обработчик cmExprFieldChanged
//------------------------------------------------------------------------------
#declare OnCostsAssignmentChanged(CA,FieldPrefix=Debit)
  ##(FieldPrefix)BusinessTransactionParam: {#CA.SetBusinessTransactionParam(ExprFieldValue); ReDrawPanel(CurTable)}
#end

//------------------------------------------------------------------------------
// скрыть ШПЗ
//------------------------------------------------------------------------------
#declare HideCostsAssignmentFields(FieldPrefix=Debit)
{
  HideLabel4Field(##(FieldPrefix)Code);
  HideLabel4Field(##(FieldPrefix)Account);
  HideLabel4Field(##(FieldPrefix)ControlAccount);
  HideLabel4Field(##(FieldPrefix)AnalyticsStr);
  HideLabel4Field(##(FieldPrefix)BusinessTransactionStr);
  HideLabel4Field(##(FieldPrefix)BusinessTransactionParam);
  ClearFieldState(##(FieldPrefix)Code, sfVisible);
  ClearFieldState(##(FieldPrefix)Account, sfVisible);
  ClearFieldState(##(FieldPrefix)ControlAccount, sfVisible);
  ClearFieldState(##(FieldPrefix)AnalyticsStr, sfVisible);
  ClearFieldState(##(FieldPrefix)BusinessTransactionStr, sfVisible);
  ClearFieldState(##(FieldPrefix)BusinessTransactionParam, sfVisible);
}
#end

//------------------------------------------------------------------------------
// показать ШПЗ
//------------------------------------------------------------------------------
#declare ShowCostsAssignmentFields(FieldPrefix=Debit)
{
  SetFieldState(##(FieldPrefix)Code, sfVisible);
  SetFieldState(##(FieldPrefix)Account, sfVisible);
  SetFieldState(##(FieldPrefix)ControlAccount, sfVisible);
  SetFieldState(##(FieldPrefix)AnalyticsStr, sfVisible);
  SetFieldState(##(FieldPrefix)BusinessTransactionStr, sfVisible);
  SetFieldState(##(FieldPrefix)BusinessTransactionParam, sfVisible);
  ShowLabel4Field(##(FieldPrefix)Code);
  ShowLabel4Field(##(FieldPrefix)Account);
  ShowLabel4Field(##(FieldPrefix)ControlAccount);
  ShowLabel4Field(##(FieldPrefix)AnalyticsStr);
  ShowLabel4Field(##(FieldPrefix)BusinessTransactionStr);
  ShowLabel4Field(##(FieldPrefix)BusinessTransactionParam);
}
#end

//------------------------------------------------------------------------------
// копирование отнесения затрат с суффиксом Sfx из CA: ICostsAssignment в таблицу DstTable
//------------------------------------------------------------------------------
#declare CopyInfoFromCostsAssignment(CA,DstTable,Sfx=D)
{
  if (not NullVipRef(#CA))
  {
    #DstTable.CSCHET#Sfx := #CA.GetAccount;
    #DstTable.CSUBSCH#Sfx := #CA.GetControlAccount;
    var I : Integer;
    for (I := cFirstKAU; I <= cLastKAU; I := I + 1)
    {
      #DstTable.TBL#Sfx[I]  := #CA.GetAnalytics.GetTbl(I);
      #DstTable.CKAU#Sfx[I] := #CA.GetAnalytics.GetKAU(I);
    }
    #DstTable.CHOZOPER := #CA.GetBusinessTransaction;
    #DstTable.PARAM := #CA.GetBusinessTransactionParam;
  }
}
#end


//------------------------------------------------------------------------------
// копирование отнесения затрат с суффиксом Sfx из таблицы SrcTable в CA: ICostsAssignment
//------------------------------------------------------------------------------
#declare CopyInfoToCostsAssignment(SrcTable,CA,Sfx=D)
{
  if (not NullVipRef(#CA))
  {
    #CA.SetAccount(#SrcTable.CSCHET#Sfx);
    #CA.SetControlAccount(#SrcTable.CSUBSCH#Sfx);
    var I: Integer;
    for (I := cFirstKAU; I <= cLastKAU; Inc(I))
    {
      #CA.GetAnalytics.SetTbl(I, #SrcTable.TBL#Sfx[I]);
      #CA.GetAnalytics.SetKAU(I, #SrcTable.CKAU#Sfx[I]);
    }
    #CA.SetBusinessTransaction(#SrcTable.CHOZOPER);
    #CA.SetBusinessTransactionParam(#SrcTable.PARAM);
  }
}
#end

