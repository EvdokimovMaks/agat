//-----------------------------------------------------------------------------
// Галактика 8.10 - заработная плата                   (c) корпорация ГАЛАКТИКА
// XML форма на зачисление заработной платы
//-----------------------------------------------------------------------------
#doc
XML форма на зачисление заработной платы
#end

.set name = 'TransferSalaryXml'
.hide
.set filler = '-'
.fields
    DateForm         // Дата формирования реестра
    NumDog           // Номер договора
    DateDog          // Дата договора
    NameOrg          // Наименование организации
    INN              // ИНН
    RasSchet         // Расчетный счет организации
    BIK              // БИК
    IdDocument       // ИдПервичногоДокумента
    NumRees          // Номер реестра
    DateRees         // Дата реестра
    SumAll           // СуммаИтого
    TypeOfTransfer   // Вид зачисления
    NumPaymentOrder  // Номер платежного поручения
    DatePaymentOrder // Дата платежного поручения
.endfields
^^^^^^^^^^^^^^
.fields              // поля по циклу
    SurName          // Фамилия
    FirstName        // Имя
    SecName          // Отчество
    OtdBank          // Отделение банка
    FilBank          // Филиал Отделения Банка
    LSchet           // Лицевой счет
    Summa            // Сумма
    Currency         // Код валюты
.endfields
.{ TransferSalaryXml_Persons CheckEnter
   ^^^^^^^^
.}
.begin
 TransferSalaryXml.fExit;
end.
.endform
!
.Linkform 'TransferSalaryXml_2012' prototype 'TransferSalaryXml'
.nameinlist 'XML форма на зачисление заработной платы'
.F 'TransferSalaryXml.XML'
!
.var
  Npp   : word; //  Номер по порядку
.endvar
!
.fields
  DateForm                       // Дата формирования реестра
  NumDog                         // Номер договора
  DateDog                        // Дата договора
  Replace(NameOrg, '"', '&quot;')// Наименование организации
  INN                            // ИНН
  RasSchet                       // Расчетный счет организации
  BIK                            // БИК
  IdDocument                     // ИдПервичногоДокумента
  NumRees                        // Номер реестра
  DateRees                       // Дата реестра
.endFields
<?xml version="1.0" encoding="windows-1251"?>
<СчетаПК ДатаФормирования="^"
         НомерДоговора="^"
         ДатаДоговора="^"
         НаименованиеОрганизации="^"
         ИНН="^"
.{?INTERNAL; ( RasSchet <> '' )
         РасчетныйСчетОрганизации="^"
.}
.{?INTERNAL; ( BIK <> '' )
         БИК="^"
.}
         ИдПервичногоДокумента="^"
         НомерРеестра="^"
         ДатаРеестра="^">
!
  <ЗачислениеЗарплаты>
!---------------------
.begin
  Npp := 0;
end.
!
.{ TransferSalaryXml_Persons CheckEnter
!
.begin
  Inc(Npp);
end.
!
.fields
  Npp              //  Номер по порядку
  SurName          //  Фамилия
  FirstName        //  Имя
  SecName          //  Отчество
  OtdBank          //  Отделение банка
  FilBank          //  Филиал Отделения Банка
  LSchet           //  Лицевой счет
  Summa            //  Сумма
  Currency         //  Код валюты
!
.endfields
      <Сотрудник Нпп="^">
        <Фамилия>^</Фамилия>
        <Имя>^</Имя>
        <Отчество>^</Отчество>
        <ОтделениеБанка>^</ОтделениеБанка>
        <ФилиалОтделенияБанка>^</ФилиалОтделенияБанка>
        <ЛицевойСчет>^</ЛицевойСчет>
        <Сумма>^</Сумма>
        <КодВалюты>^</КодВалюты>
      </Сотрудник>
.}
!
.fields
  TypeOfTransfer   // Вид зачисления
  NumPaymentOrder  // Номер платежного поручения
  DatePaymentOrder // Дата платежного поручения
  Npp              // Количество записей
  SumAll           // СуммаИтого
.endFields
!
  </ЗачислениеЗарплаты>
  <ВидЗачисления>^</ВидЗачисления>
.{?INTERNAL; ( trim(NumPaymentOrder) <> '' )
  <ПлатежноеПоручение>^</ПлатежноеПоручение>
  <ДатаПлатежногоПоручения>^</ДатаПлатежногоПоручения>
.}
!
  <КонтрольныеСуммы>
    <КоличествоЗаписей>^</КоличествоЗаписей>
    <СуммаИтого>^</СуммаИтого>
  </КонтрольныеСуммы>
</СчетаПК>
.endform
