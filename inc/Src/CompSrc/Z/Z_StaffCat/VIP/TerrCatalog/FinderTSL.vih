//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика ERP 8.1 - Управление персоналом
// Интерфейсы организации поиска в Справочниках АТД
//------------------------------------------------------------------------------

#ifndef _TerrCat_vih_INCLUDED_
#define _TerrCat_vih_INCLUDED_

#component "Z_StaffCat"

//------------------------------------------------------------------------------
#doc
Запускатель поиска
#end
ObjInterface ISearchRunner;

  #doc
  Инициировать поиск
  #end
  procedure Run(Entity: comp; Code: word);

End;

//------------------------------------------------------------------------------
#doc
Поиск данных по заданному критерию
#end
ObjInterface IEventOpenSearch;

  #doc
  Возникает при попытке изменения владельца при переходе вверх по иерархии.
  Предоставляет возможность разрешить (true) или запретить (false) переход.
  По умолчанию переход запрещен. В обработчике необходимо перейти на сущность
  (NREC), переданную в параметре Entity, записать в параметр ссылку (CREF) на
  нового владельца (имеющуюся у данной сущности), и вернуть true. В случае,
  если обработчик события вернет false, либо параметр Entity будет возвращен
  не измененным, переподчинение новому владельцу в интерфейсе поиска не произойдет.
  #end
  event function GetLinkKey(var Entity: comp; var Code: word): boolean;

  #doc
  Возникает при попытке изменения владельца при переходе вниз по иерархии.
  Предоставляет возможность разрешить (true) или запретить (false) переход.
  По умолчанию переход запрещен. В обработчике необходимо перейти на любую из
  сущностей, имеющих ссылку (CREF) на владельца, переданного в параметре Entity,
  записать в параметр ссылку на себя (NREC), и вернуть true. Если обработчик
  события вернет false, либо параметр Entity будет возвращен неизмененным,
  переход на подчиненный уровень в интерфейсе поиска не произойдет.
  #end
  event function GetOwnKey(var Entity: comp; var Code: word): boolean;

  #doc
  Возникает при изменении уровня поиска. В обработчике события необходимо
  вернуть данные, ассоциированные с переданной сущностью.
  #end
  event function DataEntity(Entity: comp; Code: word): string;

  #doc
  Возникает, если найдена первая запись, удовлетворяющая критериям поиска.
  В обработчике события необходимо проверить, имеется ли запись с переданным
  NREC и cпозиционироваться на нем.
  #end
  event function GoToFirst(Entity: comp; Code: word): boolean;

  #doc
  Возникает, если найдена следующая запись, удовлетворяющая условию поиска.
  В обработчике события необходимо проверить, имеется ли запись с переданным
  NREC и cпозиционироваться на нем.
  #end
  event function GoToNext(Entity: comp; Code: word): boolean;

End;


//------------------------------------------------------------------------------
// специальные макроопределения для запуска процесса поиска в cmOpenSearch
// <TableName> - имя таблицы, по которой осуществляется поиск,
// <SearchField> - поле поиска,
// <LinkField> - поле связи,
// <OwnerNode> - владелец текущего узла
//------------------------------------------------------------------------------
#declare OnOpenSearch(OS, TableName, SearchField, LinkField, OwnerNode)
{
  var #OS: IEventOpenSearch;
  var RUN#TableName#SearchField: ISearchRunner;

  _try
  {
    GetVipRef(RUN#TableName#SearchField, '#__COMPONENT__::FINDER#TableName#SearchField#LinkField');
    #OS := IEventOpenSearch(RUN#TableName#SearchField);

    if not (NullVipRef(RUN#TableName#SearchField) and NullVipRef(#OS))
    {
      //связываем нашу переменную с событиями
      if not NullVipRef(#OS)
      {
        BindEvent(GoToFirst, #OS.GoToFirst);
        BindEvent(GoToNext,  #OS.GoToNext);
        BindEvent(GetLinkKey, #OS.GetLinkKey);
        BindEvent(GetOwnKey, #OS.GetOwnKey);
        BindEvent(DataEntity, #OS.DataEntity);
      }

      RUN#TableName#SearchField.Run(#OwnerNode,co#TableName);

      UnBindAllEvents(#OS);
    }
  }
  _except
    on ExRef:
    {
      Message('Методы для поиска по данному полю отсутствуют.'#13 +
              'Поиск невозможен.', Error);
      exit;
    }
  _finally
  {
    #OS := NullRef;
    RUN#TableName#SearchField := NullRef;
  }
}
#end

#end
