//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.1 - Управление персоналом
// Разграничение прав доступа к подразделениям
//------------------------------------------------------------------------------

#ifndef _DepartmentsLimiter_vih_INCLUDED_
#define _DepartmentsLimiter_vih_INCLUDED_

#component "Z_StaffCat"

//------------------------------------------------------------------------------
// Объект для ограничения доступа пользователей к подразделениям
//------------------------------------------------------------------------------
ObjInterface IDepartmentsLimiter;

  #doc
    Доступность (с учетом групп) пользователю подразделения ADepartment по его
    идентификатору AUser. Если AUser = 0, считается, что текущему пользователю.
  #end
  function Accessibility(ADepartment: comp; AUser: comp): word;

  #doc
    Открывает список доступных пользователю (группе) подразделений
  #end
  function OpenListDepartments(AOwner: comp; // #docl Ссылка на пользователя/группу (0 = текущий пользователь)
                               ASource,      // #docl Источник данных о владельце (0 - пользователи, 1 - группы)
                               ACode: word   // #docl режим открытия (0 - в режиме просмотра/редактирования; 1 - в режиме редактирования; 2 - в режиме выбора(возвращает маркер пометки подразделений)
                              ): TPtr;

end;

//------------------------------------------------------------------------------
// Обработчик режимов при встраивании интерфейса
//------------------------------------------------------------------------------
objinterface IEmbeddedModeHandler;

  #doc
    Настраивает браузер на просмотр прав данного пользователя (группы).
  #end
  procedure LinkTo(AOwner: comp; ASource: word);

  #doc
    Возникает после обновления, удаления, обновления записи/группы записей.
  #end
  event procedure OnListChanged;

  #doc
    Устанавливает режим редактирования (Value = true) или просмотра (Value = false).
  #end
  procedure SetEditMode(Value: boolean);

  #doc
    Возникает при изменении режима работы в интерфейсе (просмотр/редактирование)
  #end
  event procedure OnModeChanged(AEditMode: boolean);

end;


//------------------------------------------------------------------------------
#doc
  Итератор по доступным пользователю подразделениям
#end
objinterface IUserDepartmentsList;
  #doc
    Ссылка на доступное подразделение
  #end
  property Department: comp read;

  #doc
    Ссылка на вышестоящее подразделение в собственной иерархии подразделений
    без учета его доступности
  #end
  property Parent: comp read;

  #doc
    Ссылка на вышестоящее подразделение в дереве доступных подразделений
    (то есть с учетом доступности вышестоящего)
  #end
  property HereParent: comp read;

  index IDXDEPARTMENT = DEPARTMENT(unique);
  index IDXPARENT = PARENT, DEPARTMENT;
  index IDXHEREPARENT = HEREPARENT, DEPARTMENT;

end;

//------------------------------------------------------------------------------
#doc
  Итератор по доступным пользователю подразделениям
#end
objinterface IDepartmentsLimiterIterator;
  #doc
    Настройка итератора по доступным пользователю подразделениям
  #end
  function Init(AUser: comp; // #docl Идентификатор пользователя (0 = текущий пользователь)
                #doc
                Комбинация кодов доступа к подразделениям, доступных в пределах комбинации.
                Применяется в случаях, если надо использовать итератор с иными параметрами,
                чем заданы по умолчанию. По умолчанию итератор настроен на текущего пользователя и
                на все виды доступа.
                #end
                ACode: word
               ): boolean;   // #docl true, если у пользователя имеются доступные подразделения

  #doc
    Выбор порядка сортировки подразделений
  #end
  procedure SelectOrder(AOrder: word);

  #doc
    Наличие подразделения в перечне разрешенных
  #end
  function Exists(ADepartment: comp): boolean;

  #doc
    Перейти на первое из доступных подразделений
  #end
  function First: comp; // #docl Ссылка на подразделение или 0

  #doc
    Перейти на следующее доступное подразделение
  #end
  function Next: comp; // #docl Ссылка на подразделение или 0

end;

//------------------------------------------------------------------------------
VipInterface UserDepartmentsLimiter
  implements
    IDepartmentsLimiter, IEmbeddedModeHandler
  parameters
    (Owner: comp; Source, Mode: word; Marker: TPtr)
  licensed(free);

VipInterface UserDepartmentsIterator
  implements
    IUserDepartmentsList, IDepartmentsLimiterIterator
  licensed(free);

#end
