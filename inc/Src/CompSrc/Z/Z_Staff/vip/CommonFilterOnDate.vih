//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.1 - Управление персоналом
//
// Фильтр по подразделениям и сотрудникам на дату с разграничением прав доступа
//------------------------------------------------------------------------------

#ifndef _CommonFilterOnDate_INCLUDED_
#define _CommonFilterOnDate_INCLUDED_

#ifdef ComponentVersion
#component "Z_Staff"
#end

#include Tables4FilterOnDate.vih //Таблицы для фильтра по подразделениям и сотрудникам на дату

#doc
Объектный интерфейс с процедурами для фильтра по сотрудникам/подразделениям
#end
objinterface ICommonFilterOnDate;

#doc
 Запуск интерфейса выбора подразделений с набивкой соответствующего маркера DeptMarker
#end
procedure SelectPodr;

#doc
Запуск интерфейса выбора сотрудников с набивкой соответствующего маркера PsnMarker
#end
procedure SelectPsn;

#doc
  Очистка фильтра по подразделениям
#end
procedure ClearDepartmentsFilter;

#doc
  Очистка маркера по персонам
#end
procedure ClearPersonsFilter;

#doc
  Получить ссылку на маркер по подразделениям
#end
function GetDepartmentsMarker: TPtr;

#doc
  Получить ссылку на маркер по сотрудникам
#end
function GetPersonsMarker: TPtr;

#doc
  Получить строковое значение установленного фильтра по подразделениям
#end
function GetDepartmentsFilterStr: string;

#doc
  Получить строковое значение установленного фильтра по сотрудникам
#end
function GetPersonsFilterStr: string;

#doc
  Получить кол-во записей в маркере по подразделениям
#end
function GetDeptCount: longint;

#doc
  Получить кол-во записей в маркере по сотрудникам
#end
function GetPsnCount: longint;

#doc
  Получить дату фильтрации (в случае фильтра за период - дата окончания периода)
#end
function GetFilterDate: date;

#doc
  Сохранить настройки фильтрации
#end
procedure StoreAllParams;

#doc
  Получить данные о сотруднике по NRec
#end
//! так делать нельзя, т.к. теперь нельзя изменить структуру TmpPsn4FilterOnDate
function GetPsnRecord(cNrec: comp): record as table TmpPsn4FilterOnDate;

end;

#doc
Расширение объектного интерфейса с процедурами для фильтра по сотрудникам/подразделениям
#end
objinterface ICommonFilterOnDate_2;

#doc
  Получить дату начала периода фильтрации (в случае фильтра на дату - указанная дата)
#end
function GetFilterDateBeg: date;

#doc
  Получить дату окончания периода фильтрации (в случае фильтра на дату - указанная дата)
#end
function GetFilterDateEnd: date;

#doc
  Установить дату начала периода или дату фильтрации в случае фильтра на дату
#end
procedure SetFilterDateBeg(_dPeriodBeg: date);

#doc
  Установить дату окончания периода или дату фильтрации в случае фильтра на дату
#end
procedure SetFilterDateEnd(_dPeriodEnd: date);

end;

#doc
Расширение объектного интерфейса с процедурами для фильтра по сотрудникам/подразделениям
#end
objinterface ICommonFilterOnDate_3;

#doc
  Установить видимость поля/полей с датой/периодом и их подписей в фильтре.
#end
procedure SetDatesVisible(_bVisible: boolean);

#doc
  Установить период фильтра. Для фильтра по дате возвращает <code>false</code>.
#end
function SetFilterPeriod(_dPeriodBeg, _dPeriodEnd: date): boolean;

end;

vipinterface CommonFilterOnDate implements ICommonFilterOnDate, ICommonFilterOnDate_2, ICommonFilterOnDate_3
Licensed (free)
;
public:
#doc
  Конструктор по умолчанию
#end
constructor Init;

#doc
  Конструктор с параметрами:
    _bPeriodFilter - для true - фильтра за период
    _bCorrectDate - для true - даты фильтрации только начало/конец месяца
      Для фильтра за период в таком случае датой начала всегда устанавливается первый день месяца, а датой
      окончания - последний день месяца. Для фильтра на дату - последний день месяца.
#end
constructor InitParams(_bPeriodFilter, _bCorrectDate: boolean);
end;

#end
