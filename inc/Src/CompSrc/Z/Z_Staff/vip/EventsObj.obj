//------------------------------------------------------------------------------
//                                           (c) корпорация ГАЛАКТИКА
// Версия     : 7.12
// Назначение : Объектный интерфейс, декларирующий методы реализации
//              событий системы
//------------------------------------------------------------------------------

#ifndef _IEventsObj_OBJ_INCLUDED_
#define _IEventsObj_OBJ_INCLUDED_

table struct tmpTemp
(
  Nrec: comp,
  cPsn: comp,           // ссылка на сотрудника
  bMarked: boolean,     // отмечена ли запись для обработки
  sDescription: string, // описание если что-то нужно
  vDopDate: variant,
  sNameVipInt: string,  // название реализации, которая добавила данные в данную таблицу
  cSRCRec: comp         // Nrec записи, которую нужно обработать
)
with index
(
  ind1 = Nrec(Unique),
  ind2 = cPsn
)
;


#doc
Объектный интерфейс, декларирующий методы реализации событий системы
#end
objinterface IEventsObj;

#doc
Наименование события
#end
function GetNameEvent: string;

#doc
запускает на выполнение событие
Параметры:
  cNumber - Nrec сотрудника
  _Days - количество дней для анализа
#end
function ExecuteEvent(cNumber: comp; _Days: Word): boolean;

#doc
функция, которая возвращает результат обработки данных
#end
function ReturnResultStr: string;

#doc
функция, которая возвращает результирующую дату
#end
function ReturnResultDate: Date;

#doc
процедура запуска окна с дополнительными настройками события
#end
procedure RunWindowWithAddSettings;

#doc
Функция обработки сотрудников, которые внесены во временную таблицу
#end
function ExecuteTableEvent(cTblNrec: comp): boolean;

end;

#doc
Расширение новыми методами обработки событий
#end
private objinterface IEventsObj_Ex;

#doc
Функция запуска события с учетом сдвига даты до начала анализа и сдвига даты до окончания анализа </brief>

К текущей дате прибавляется <code> wDaysFrom </code> календарных дней.                                          </br>
С полученной даты начинается проверка наступления событий у сотрудника.                                         </br>
Проверяются все события до наступления даты, которая больше текущей на <code> wDaysTo </code> календарных дней. </br>
Остальные события не проверяются.

<h3> Параметры: </h3>
<ul>
  <li><code> cNumber   </code> - nRec сотрудника                                                   </li>
  <li><code> wDaysFrom </code> - количество дней, через которое начнется анализ событий системы    </li>
  <li><code> wDaysTo   </code> - количество дней, через которое прекратится анализ событий системы </li>
</ul>

<h3> Результат: </h3>
<ul>
  <li><code> true  </code> - в случае, если сотрудник обработан </li>
  <li><code> false </code> - если произошла ошибка обработки    </li>
</ul>

#end
function ExecuteEventShift(cNumber: comp; wDaysFrom, wDaysTo: Word): boolean;

#doc
Определить есть ли у события дополнительная настройка</brief>
Иными словами запускается ли для события процедура RunWindowWithAddSettings
#end
function funReturnIsTune: boolean;

#doc
Определить есть ли у события дополнительная обработка или только вывод сообщений</brief>
#end
function funReturnIsAct: boolean;

#doc
Определить может ли для события быть задан интервал поиска </brief>
#end
function funReturnIsShiftDate: boolean;

end;

#end
