//******************************************************************************
//                                                      (с) корпорация Галактика
// Галактика 8.1
// Специальные объекты для макроинтерфейсов поиска таблицы STERR по привязке к
// вышестоящему
//******************************************************************************

#ifndef _TerrCat_vih_INCLUDED_
#define _TerrCat_vih_INCLUDED_

#ifdef ComponentVersion
  #component "Z_Staff"
#end



//------------------------------------------------------------------------------
#doc
  Запускатель поиска
#end
objinterface ISearchRunner;

  #doc
    инициировать поиск
  #end
  procedure Run(Entity: comp; Code: word);

end;

//------------------------------------------------------------------------------
#doc
  Декларация событийного интерфейса поиска данных по заданному критерию.
#end
objinterface IEventOpenSearch;
  #doc
    Возникает при попытке изменения владельца при переходе вверх по иерархии.
    Предоставляет возможность разрешить (true) или запретить (false) переход.
    По умолчанию переход запрещен. В обработчике необходимо перейти на сущность
    (NREC), переданную в параметре Entity, записать в параметр ссылку (CREF) на
    нового владельца (имеющуюся у данной сущности), и вернуть true. В случае,
    если обработчик события вернет false, либо параметр Entity будет возвращен
    неизмененным, переподчинение новому владельцу в интерфейсе поиска не произойдет.
  #end
  event function GetLinkKey(var Entity: comp; var Code: word): boolean;

  #doc
    Возникает при попытке изменения владельца при переходе вниз по иерархии.
    Предоставляет возможность разрешить (true) или запретить (false) переход.
    По умолчанию переход запрещен. В обработчике необходимо перейти на любую из
    сущностей, имеющих ссылку (CREF) на владельца, переданного в параметре Entity,
    записать в параметр ссылку на себя (NREC), и вернуть true. Если обработчик
    события вернет false, либо параметр Entity будет возвращен неизмененным,
    переход на подчиненный уровень в интерфейсе поиска не произойдет.
  #end
  event function GetOwnKey(var Entity: comp; var Code: word): boolean;

  #doc
    Возникает при изменении уровня поиска. В обработчике события необходимо
    вернуть данные, ассоциированные с переданной сущностью.
  #end
  event function DataEntity(Entity: comp; Code: word): string;

  #doc
    Возникает, если найдена первая запись, удовлетворяющая критериям поиска.
    В обработчике события необходимо проверить, имеется ли запись с переданным
    NREC и cпозиционироваться на нем.
  #end
  event function GoToFirst(Entity: comp; Code: word): boolean;

  #doc
    Возникает, если найдена следующая запись, удовлетворяющая условию поиска.
    В обработчике события необходимо проверить, имеется ли запись с переданным
    NREC и cпозиционироваться на нем.
  #end
  event function GoToNext(Entity: comp; Code: word): boolean;

end;


//------------------------------------------------------------------------------
// специальные макроопределения для запуска процесса поиска в cmOpenSearch
// <TABLENAME> - имя таблицы, по которой осуществляется поиск,
// <SEARCHFIELD> - поле поиска,
// <LINKFIELD> - поле связи,
// <OWNERNODE> - владелец текущего узла
//------------------------------------------------------------------------------
#declare OnOpenSearch(OS,TABLENAME,SEARCHFIELD,LINKFIELD,OWNERNODE)
{
  var #OS: IEventOpenSearch;
  var RUN#TABLENAME#SEARCHFIELD: ISearchRunner;

  _try
  {
    GetVipRef(RUN#TABLENAME#SEARCHFIELD, '#__COMPONENT__::FINDER#TABLENAME#SEARCHFIELD#LINKFIELD');
    #OS := IEventOpenSearch(RUN#TABLENAME#SEARCHFIELD);

    if not (NullVipRef(RUN#TABLENAME#SEARCHFIELD) and NullVipRef(#OS))
    {
      //связываем нашу переменную с событиями
      if not NullVipRef(#OS)
      {
        BindEvent(GoToFirst, #OS.GoToFirst);
        BindEvent(GoToNext,  #OS.GoToNext);
        BindEvent(GetLinkKey, #OS.GetLinkKey);
        BindEvent(GetOwnKey, #OS.GetOwnKey);
        BindEvent(DataEntity, #OS.DataEntity);
      }

      RUN#TABLENAME#SEARCHFIELD.Run(#OWNERNODE,co#TABLENAME);

      UnBindAllEvents(#OS);
    }
  }
  _except
    on ExRef:
    {
      Message('Методы для поиска по данному полю '#13 + 'отсутствуют. Поиск невозможен.', Error);
      exit;
    }
  _finally
  {
    #OS := NullRef;
    RUN#TABLENAME#SEARCHFIELD := NullRef;
  }
}
#end

//------------------------------------------------------------------------------
// компиляция интерфейса поиска по полю STERR.SGNI_CODE (для tree и browse)
//------------------------------------------------------------------------------
#define TABLENAME STERR
#define SEARCHFIELD SGNI_CODE
#define LINKFIELD CPARENT
  #include TerrCat.vpp
#undef LINKFIELD
#define LINKFIELD CREC
  #include TerrCat.vpp
#undef LINKFIELD
#undef SEARCHFIELD

//------------------------------------------------------------------------------
// компиляция интерфейсов поиска по полю STERR.CATDATA (для tree и browse)
//------------------------------------------------------------------------------
#define SEARCHFIELD CATDATA
#define LINKFIELD CPARENT
  #include TerrCat.vpp
#undef LINKFIELD
#define LINKFIELD CREC
  #include TerrCat.vpp
#undef LINKFIELD
#undef SEARCHFIELD
#undef TABLENAME

#end


