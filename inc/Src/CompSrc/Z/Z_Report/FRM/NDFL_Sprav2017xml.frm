//-----------------------------------------------------------------------------
//                                                     (c) корпорация ГАЛАКТИКА
// Галактика 9.10 - заработная плата
// Форма прототип для формирования справки 2-НДФЛ
//-----------------------------------------------------------------------------
#doc
НДФЛ-2
#end
.Linkform 'NDFL2_2017XML' prototype 'NDFL2_prototype'
.nameinlist 'Файл обмена в формате XML c 2017 года'
.F 'NdflX2017.XML'
.group 'Ndfl_2015'
.var 
 Долг1             : LongInt;
 Долг2             : LongInt; 
 СумНалУд          : LongInt; 
 boPrint, 
 VichPrint         : boolean;
.endvar
.fields
ANSI2OEM(Имя_файла)
.endFields
.{ Main_Cycle_2NDFL CheckEnter
<?xml version="1.0" encoding="windows-1251"?>
<Файл ИдФайл="^" ВерсПрог="Галактика 9.10" ВерсФорм="5.05">
.fields
 АббрОКТМОилиОКАТО
 ОКАТО
 Год
 Признак
.endfields
  <СвРекв ^="^" ОтчетГод="^" ПризнакФ = "^">
.{?INTERNAL; (ТипОрг = 0)
.fields
 ИНН
 КПП 
.endfields
    <СвЮЛ ИННЮЛ="^" КПП="^"></СвЮЛ>
.}
.{?INTERNAL; (ТипОрг <> 0)
.fields
 ИНН
.endfields
    <СвФЛ ИННФЛ="^"></СвФЛ>
.}
.fields
  НалАгент
  ФамСост
  ИмяСост
  ОтчСост
  ДоверенностьСоставителя   
  Replace(ОргУпПредст, '"', '&quot;')
  ДоверенностьСоставителя
.endfields
    <Подписант ПрПодп="^">
.{?INTERNAL; (НалАгент = 2 or ИНН <> '')
      <ФИО Фамилия="^" Имя="^"
.{?INTERNAL; (Trim(ОтчСост) <> '')
      Отчество="^"
.}
      ></ФИО>
.}
.{?INTERNAL; (НалАгент = 2 and ОргУпПредст <> '')
      <СвПред НаимДок="^" НаимОрг="^"></СвПред>
.}
.{?INTERNAL; (НалАгент = 2 and ОргУпПредст = '')
      <СвПред НаимДок="^"></СвПред>
.}    
    </Подписант>
  </СвРекв>
.{ Person_Cycle_2NDFL CheckEnter
.fields  
  Дата
  Номер
  Год    
  Признак
  НомерКоррект
  ИФНС
  ОКАТО  
  Телефон  
.endfields
  <Документ КНД="1151078" ДатаДок="^" НомСпр="^" ОтчетГод="^" Признак="^" НомКорр="^" КодНО="^">
    <СвНА ОКТМО="^"
.{?INTERNAL; (Trim(Телефон) <> '')
    Тлф="^"
.}
    >
.{?INTERNAL; (ТипОрг = 0)
.fields
  Replace(Организация, '"', '&quot;')
  ИНН
  КПП
.endfields
      <СвНАЮЛ НаимОрг="^" ИННЮЛ="^" КПП="^">
.{?INTERNAL; (КодРЕОРГ = '0' or КодРЕОРГ = '1'  or КодРЕОРГ = '2' or КодРЕОРГ = '3' or КодРЕОРГ = '5' or КодРЕОРГ = '6')
.fields
  КодРЕОРГ
.endfields
        <СвРеоргЮЛ ФормРеорг="^"
.{?INTERNAL; (КодРЕОРГ = '1'  or КодРЕОРГ = '2' or КодРЕОРГ = '3' or КодРЕОРГ = '5' or КодРЕОРГ = '6')
.fields
  ИННРЕОРГ
  КППРЕОРГ
.endfields
         ИННЮЛ="^" КПП="^"
.}
        />
.}
      </СвНАЮЛ>
.}
.{?INTERNAL; (ТипОрг <> 0)
.fields
  ИНН
  ФамРук
  ИмяРук
  ОтчРук
.endfields 
      <СвНАФЛ ИННФЛ="^">
        <ФИО Фамилия="^" Имя="^"
.{?INTERNAL; (Trim(ОтчРук) <> '')
      Отчество="^"
.}
      ></ФИО>
      </СвНАФЛ>
.}
    </СвНА>
.fields  
  ИНН_налогоплательщика
  ИНН2_налогоплательщика
  Статус
  Дата_рождения
  Гражданство
.endfields
    <ПолучДох
.{?internal; (Trim(ИНН_налогоплательщика) <> '')
      ИННФЛ="^"
.}
.{?internal; (Trim(ИНН2_налогоплательщика) <> '')
      ИННИно="^"
.}
      Статус="^" ДатаРожд="^" Гражд="^">
.fields
  Фамилия
  Имя
  Отчество
  Код_документа
  Серия_номер_документа
.endFields
      <ФИО Фамилия="^" Имя="^"
.{?INTERNAL; (Trim(Отчество) <> '')
      Отчество="^"
.}
      ></ФИО>
      <УдЛичнФЛ КодУдЛичн="^" СерНомДок="^"></УдЛичнФЛ>
    </ПолучДох>
.fields
 Ставка_Налога
 Месяц_Ст1 Код_дохода_Ст1 Сумма_дохода_Ст1 Код_Вычета_Ст1 Сумма_вычета_Ст1
 Месяц_Ст2 Код_дохода_Ст2 Сумма_дохода_Ст2 Код_Вычета_Ст2 Сумма_вычета_Ст2
.endfields
.{ Percent_Cycle_2NDFL CheckEnter
   <СведДох  Ставка="^">
      <ДохВыч>
.{ Incom_Cycle_2NDFL CheckEnter
.{?INTERNAL; (Double(Сумма_дохода_Ст1) <> 0)
        <СвСумДох Месяц="^" КодДоход="^" СумДоход="^">
.{?INTERNAL; (Double(Сумма_вычета_Ст1) <> 0)
          <СвСумВыч КодВычет="^" СумВычет="^"/>
.}
        </СвСумДох>
.}
.{?INTERNAL; (Double(Сумма_дохода_Ст2) <> 0)
        <СвСумДох Месяц="^" КодДоход="^" СумДоход="^">
.{?INTERNAL; (Double(Сумма_вычета_Ст2) <> 0)
          <СвСумВыч КодВычет="^" СумВычет="^"/>
.}
        </СвСумДох>
.}
.}//Incom_Cycle_2NDFL CheckEnter
      </ДохВыч>
.fields
 Код_Вычета_1     Сумма_вычета_1
 Код_Вычета_2     Сумма_вычета_2
 Код_Вычета_3     Сумма_вычета_3
 Код_Вычета_4     Сумма_вычета_4
.endfields
.begin
 boPrint := false;
 VichPrint := false;
end.
.{ Discount_Cycle_2NDFL CheckEnter
.begin
 boPrint  := false;  
 if (Double(Сумма_вычета_1) <> 0)
   boPrint := true
 else
   if (Double(Сумма_вычета_2) <> 0) 
     boPrint := true
     else
       if (Double(Сумма_вычета_3) <> 0)
         boPrint := true
           else
             if (Double(Сумма_вычета_4) <> 0) 
               boPrint := true;
end.
.{?INTERNAL; (boPrint = true) and (VichPrint = false);
       <НалВычССИ>
.begin 
 VichPrint:= true;
end.
.}
.{?INTERNAL; (Double(Сумма_вычета_1) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_вычета_2) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_вычета_3) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_вычета_4) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.}//Discount_Cycle_2NDFL CheckEnter
.fields
  НомерУведСоцВыч
  ДатаВыдачиСоцУвед
  КемВыданоСоцУвед
.endfields
.{ Info_DiscountSoc_Cycle_2NDFL CheckEnter
.{?INTERNAL; (trim(НомерУведСоцВыч) <> '') or (trim(ДатаВыдачиСоцУвед) <> '') or (trim(КемВыданоСоцУвед) <> '')
        <УведСоцВыч НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведСоцВыч>
.}
.}
.fields
  НомерУведИмВыч
  ДатаВыдачиУвед
  КемВыданоУвед
.endfields
.{ Info_Discount_Cycle_2NDFL CheckEnter
.{?INTERNAL; (trim(НомерУведИмВыч) <> '') or (trim(ДатаВыдачиУвед) <> '') or (trim(КемВыданоУвед) <> '')
        <УведИмущВыч НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведИмущВыч>
.}       
.}//Info_Discount_Cycle_2NDFL CheckEnter
.{?INTERNAL; (VichPrint = true);
       </НалВычССИ>
.}
.begin
 Долг1 := 0;
 Долг2 := 0;
 if ( Ставка_Налога = 13 )
   СумНалУд := if (УчетДатыВыплаты = 1, СуммаНалогаУдВыпл, СуммаНалогаУд) - СуммаАвПлат;
 else
   СумНалУд := if (УчетДатыВыплаты = 1, СуммаНалогаУдВыпл, СуммаНалогаУд);
 if ( СуммаНалогаИсч > СуммаНалогаУд )
   Долг2 := СуммаНалогаИсч - СуммаНалогаУд
 else
   if ( СуммаНалогаИсч < СуммаНалогаУд and УчетДатыВыплаты = 0)
     Долг1 := СуммаНалогаУд  - СуммаНалогаИсч
end.
.fields
 СуммаДохода     
 НалБаза         
 СуммаНалогаИсч  
 СуммаАвПлат
 СумНалУд   
 СуммаНалогаПереч
 Долг1
 Долг2
.endfields
      <СумИтНалПер
        СумДохОбщ="^" НалБаза="^" НалИсчисл="^" АвансПлатФикс="^" НалУдерж="^"
        НалПеречисл="^"
        НалУдержЛиш="^" НалНеУдерж="^">
.fields
  НомерУведАвПлатВыч   
  ДатаВыдачиУведАвПлат   
  КемВыданоУведАвПлат     
.endfields
.{ Info_DiscountAvPlat_Cycle_2NDFL CheckEnter
.{?INTERNAL; (trim(НомерУведАвПлатВыч) <> '') or (trim(ДатаВыдачиУведАвПлат) <> '') or (trim(КемВыданоУведАвПлат) <> '')
        <УведФиксПлат НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведФиксПлат>
.}       
.}
      </СумИтНалПер>
    </СведДох>
.}
  </Документ>
.}
</Файл>
.}
.endform
