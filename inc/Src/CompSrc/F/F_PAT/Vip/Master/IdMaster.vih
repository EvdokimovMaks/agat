//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.0 - Бухгалтерский контур
// Мастер ПАТ-идентификаторов
//------------------------------------------------------------------------------

#ifndef __IdMaster__vih__
#define __IdMaster__vih__

#ifdef ComponentVersion
#Component "F_Pat"
#end

#include MasterAllIdent.vih

#doc
Мастер ПАТ-идентификаторов
#end
type TPatParams = record
  FilialModeSet : word;       // включать ли код филиала
  cFilial       : comp;       // филиал
  TypeIdent     : word;       // тип идентификатора сальдо/обороты/проводки
  TypeDirect    : word;       // тип идентификатора дебет/кредит
  TypeSaldo     : word;       // тип сальдо исходящее/входящее
  CalcSaldoFromOborot: boolean;
  PeriodCode    : string;     // период расчетов (код диапазона)
  BegDate       : date;
  EndDate       : date;
  BaseDate      : word;       // базовая дата;
  FilterModeSet : word;       // включать ли фильтры (на пользователя, группу пользователя, валюту)
  Descr         : tDescr;     // дескриптор пользователя для фильтра
  DesGr         : tDesGr;     // дескриптор группы пользователей для фильтра
  IsStorno      : word;
  cVal          : comp;       // валюта для фильтра
  RezModeSet    : word;       // результат в валюте/количество
  cValRez       : comp;       // валюта результата

  cPlanSch      : comp;       // текущий выбранный план счетов
  SchetO        : tSchet3;
  SchetK        : tSchet3;
  SubSchetO     : tSubSch;
  SubSchetK     : tSubSch;
  modSubSchetO  : word;
  modSubSchetK  : word;

  modPodrO      : word;
  modPodrK      : word;
  flPodrO       : word;
  flPodrK       : word;
  sPodrO        : string;
  sPodrK        : string;
  cPodrO        : comp;
  cPodrK        : comp;

  wKauO         : tTabKau;
  wKauK         : tTabKau;
  modKauO       : tTabKau;
  modKauK       : tTabKau;
  flKauO        : array [1..6] of word;
  flKauK        : array [1..6] of word;
  sKauO         : array[1..6] of string;
  sKauK         : array[1..6] of string;
  cKauO         : tGetKau;
  cKauK         : tGetKau;
  #ifdef __FastClose__
  modTipDoc     : word;     //зарезервировал на случай расширения типов фильтрации
  sTipDoc       : string;
  modStatus     : word;      //зарезервировал на случай расширения типов фильтрации
  sStatus       : string;
  #end
  //104.19554 Фильтр по названию ТХО в ПАТ- идентификаторах
  modTxo        : word;
  cTxo          : comp;

  //2.202596 доработка PAT идентификаторов в рамках филиальности
  modFilial2    : word;
  sFilial2      : String;
  cFilial2      : Comp;
end;
ObjInterface ObjIdMaster;

  #doc
  <pre>
    Инициализировать интерфейс, задать входные параметры. Возвращает true, если инициализация прошла успешно.
    _PickMode - режим запуска интерфейса (используется для ограничения доступа к некоторым полям в окне формирования ПАТ-идентификатора)
                0 - обычные отчеты
                1 - консолидированные (доступен выбор филиала)
                2 - обычные отчеты, но нельзя выбирать период
    _TiDk     - тип документа
    _SysOper  - тип хозяйственной операции (для операций ОС, НМА, Производство, Автотранспорт)
  </pre>
  #end
  function InitIdMaster(_PickMode, _TiDk, _SysOper : word) : boolean;

  #doc
  <pre>
    Сформировать строку ПАТ-идентификатор на основании переданных значений параметров с помощью функций SetParam_...
    Возвращает ПАТ-идентификатор.
  </pre>
  #end
  function AutoCreatePAT : string;

  #doc
  <pre>
    Проверка корректности ПАТ-идентификатора (синтаксический и семантический анализ).
    Функция возвращает false, если были ошибки при разборе строки ПАТ-идентификатора
    (при этом на экран выводится сообщение с содержанием ошибки).
    _sPAT - строка, содержащая ПАТ-идентификатор
  </pre>
  #end
  function CheckPAT(_sPAT : string) : boolean;

  #doc
  <pre>
    Открыть окно мастера для формрования ПАТ-идентификатора
    Функция возвращает true, если операция успешно выполнена.
    _sPAT - строка, содержащая ПАТ-идентификатор
  </pre>
  #end
  function PickPAT(var _sPAT : string) : boolean;


  #doc
  <pre>
   очистить все параметры ПАТ-идентификатора

   Параметры для построения формулы ПАТ-идентификатора задаются следующими функциями,
   которые возвращают false, если в функцию был передан недопустимый параметр.
  </pre>
  #end
  procedure ClearAllParams;

  #doc
  <pre>
    Включить в формулу результат в валюте/количество
    _wMode может иметь значения:
      cgPAT_ResVal - включить результат в валюте
      cgPAT_ResKol - включить результат количество
    _cVal - ссылка на валюту.
  </pre>
  #end
  function SetParam_Result(_wMode : word; _cVal : comp) : boolean;

  #doc
  <pre>
    Передать для постоения формулы параметры типа идентификатора
    _wMode может иметь значения:
      cgPAT_Saldo  - сальдо
      cgPAT_Oborot - обороты
      cgPAT_Provod - проводки
    _wDebitKredit может иметь значения:
      cgPAT_Debit  - дебет
      cgPAT_Kredit - кредит
    _wSaldoOutIn может иметь значения:
      cgPAT_GoOut  - исходящее сальдо
      cgPAT_GoIn   - входящее сальдо
  </pre>
  #end
  function SetParam_SaldOborProv(_wMode : word; _wDebitKredit : word; _wSaldoOutIn : word) : boolean;

  #doc
  <pre>
    Включить в формулу филиал (для консолидированных отчетов).
    _wMode может иметь значение:
      cgPat_Code  - включить код филиала
    _cFilial - ссылка на филиал
  </pre>
  #end
  function SetParam_Filial(_wMode : word; _cFilial : comp) : boolean;

  #doc
  <pre>
    Включить в формулу фильтр по пользователю или группе пользователей. Имеет смысл только для оборотов и проводок.
    _wMode может иметь значения:
      cgPAT_Descr - включить фильтр по дескриптору пользователя
      cgPAT_DesGr - включить фильтр по дескриптору группы пользователей
    _Descr - дескриптор пользователя
    _DesGr - дескриптор группы пользователей
  </pre>
  #end
  function SetParam_FilterDescrDesgr(_wMode : word; _Descr : tDescr; _DesGr : tDesGr) : boolean; // только для оборотов/проводок!

  #doc
  <pre>
    Включить в формулу фильтр по валюте.
    _wMode может иметь значение:
      cgPat_Code - включить фильтр по валюте
    _cVal - ссылка на валюту
  </pre>
  #end
  function SetParam_FilterVal(_wMode : word; _cVal : comp) : boolean;

  #doc
  </pre>
    Включить в формулу период расчетов и базовую дату.
    _wMode может иметь значение:
      cgPAT_CodePeriod - включить код диапазона
    _sPeriod   - код диапазона. Формируется вручную или с помощью методов объектного интерфейса ObjDateRangeParam.
    _wBaseDate - базовая дата для относительного периода, то есть периода, зависящего от точки отсчета (текущий, предыдущий и т.д.).
                 Может иметь  значения:
      cgPAT_AutoDate - автоматически подставляется при расчете в зависимости от места использования
      cgPAT_BegCur   - начало текущего отчетного периода
      cgPAT_BegPer   - начало периода из настройки
      cgPAT_EndPer   - конец периода из настройки
      cgPAT_HozDoc   - дата хоздокумента (для ТХО)
  </pre>
  #end
  function SetParam_Period(_wMode : word; _sPeriod : string; _wBaseDate : word) : boolean;

  #doc
  <pre>
    Включить в формулу план счетов. Если заданный план счетов совпадает с текущим (из настройки), то он в формулу не подставится.
    _wMode может иметь значение:
      cgPAT_Code - включить код плана счетов
    _cPlanSch - ссылка на план счетов
  </pre>
  #end
  function SetParam_PlanSch(_wMode : word; _cPlanSch : comp) : boolean;

  #doc
  <pre>
    Передать для постоения формулы основной счет.
    _wMode может иметь значения:
      cgPAT_Code - включить счет
    _Schet - основной счет
  </pre>
  #end
  function SetParam_SchetO(_wMode : word; _Schet : tSchet3) : boolean;

  #doc
  <pre>
    Передать для постоения формулы субсчет основного счета.
    _wMode может иметь значения:
      cgPAT_Code - включить субсчет
      cgPAT_Nul  - включить символ неопределенного счета
    _SubSch - субсчет основного счета
  </pre>
  #end
  function SetParam_SubSchetO(_wMode : word; _SubSch : tSubSch) : boolean;

  #doc
  <pre>
    Передать для постоения формулы аналитику основного счета.
    _Num - уровень (от 1 до 6)
    _wMode может иметь значения:
      cgPAT_All    - по всем объектам учета
      cgPAT_Nrec   - включить Nrec объекта
      cgPAT_Code   - включить маску на код объекта
      cgPAT_Auto   - включить символ автоподстановки объекта (для ТХО)
      cgPAT_AutoReg- включить символы автоподстановки объекта из заданного режима аналитики (для ТХО)
      cgPAT_Nul    - включить символ неопределенного объекта
      cgPAT_Filter - включить объекты учета по фильтру
    _cKau  - ссылка на аналитику (для _wMode = cgPAT_Nrec)
    _flKau - режим аналитики из ТХО (для _wMode = cgPAT_AutoReg)
    _wKau  - 0 или код каталога аналитики-синонима, если _flKau передается для синонима (для cgPAT_AutoReg)
    _sMask - маска на код аналитики (для _wMode = cgPAT_Code)
  </pre>
  #end
  function SetParam_KauO(_Num : word; _wMode : word; _cKau : comp; _flKau : word; _wKau : word; _sMask : string) : boolean;

  #doc
  <pre>
    Передать для постоения формулы подразделение основного счета.
    _wMode может иметь значения:
      cgPAT_All    - по всем подразделениям
      cgPAT_Nrec   - включить Nrec подразделения
      cgPAT_Code   - включить маску на код подразделения
      cgPAT_Auto   - включить символ автоподстановки подразделения (для ТХО)
      cgPAT_AutoReg- включить символы автоподстановки подразделелния из заданного режима аналитики (для ТХО)
      cgPAT_Nul    - включить символ неопределенного подразделения
      cgPAT_Filter - включить объекты учета по фильтру
    _cPodr  - ссылка на подразделение (для _wMode = cgPAT_Nrec)
    _flPodr - режим подразделения из ТХО (для _wMode = cgPAT_AutoReg)
    _sMask - маска на код подразделения (для _wMode = cgPAT_Code)
  </pre>
  #end
  function SetParam_PodrO(_wMode : word; _cPodr : comp; _flPodr : word; _sMask : string) : boolean;

  #doc
  <pre>
    Передать для постоения формулы корреспондирующий счет.
    _wMode может иметь значения:
      cgPAT_Code - включить счет
    _Schet - корреспондирующий счет
  </pre>
  #end
  function SetParam_SchetK(_wMode : word; _Schet : tSchet3) : boolean;

  #doc
  <pre>
    Передать для постоения формулы субсчет корреспондирующего счета.
    _wMode может иметь значения:
      cgPAT_Code - включить субсчет
      cgPAT_Nul  - включить символ неопределенного счета
    _SubSch - субсчет корреспондирующего счета
  </pre>
  #end
  function SetParam_SubSchetK(_wMode : word; _SubSch : tSubSch) : boolean;

  #doc
  <pre>
    Передать для постоения формулы аналитику корреспондирующего счета.
    _Num - уровень (от 1 до 6)
    _wMode может иметь значения:
      cgPAT_All    - по всем объектам учета
      cgPAT_Nrec   - включить Nrec объекта
      cgPAT_Code   - включить маску на код объекта
      cgPAT_Auto   - включить символ автоподстановки объекта (для ТХО)
      cgPAT_AutoReg- включить символы автоподстановки объекта из заданного режима аналитики (для ТХО)
      cgPAT_Nul    - включить символ неопределенного объекта
      cgPAT_Filter - включить объекты учета по фильтру
    _cKau  - ссылка на аналитику (для _wMode = cgPAT_Nrec)
    _flKau - режим аналитики из ТХО (для _wMode = cgPAT_AutoReg)
    _wKau  - 0 или код каталога аналитики-синонима, если _flKau передается для синонима (для cgPAT_AutoReg)
    _sMask - маска на код аналитики (для _wMode = cgPAT_Code)
  </pre>
  #end
  function SetParam_KauK(_Num : word; _wMode : word; _cKau : comp; _flKau : word; _wKau : word; _sMask : string) : boolean;

  #doc
  <pre>
    Передать для постоения формулы подразделение корреспондирующего счета.
    _wMode может иметь значения:
      cgPAT_All    - по всем подразделениям
      cgPAT_Nrec   - включить Nrec подразделения
      cgPAT_Code   - включить маску на код подразделения
      cgPAT_Auto   - включить символ автоподстановки подразделения (для ТХО)
      cgPAT_AutoReg- включить символы автоподстановки подразделелния из заданного режима аналитики (для ТХО)
      cgPAT_Nul    - включить символ неопределенного подразделения
      cgPAT_Filter - включить объекты учета по фильтру
    _cPodr  - ссылка на подразделение (для _wMode = cgPAT_Nrec)
    _flPodr - режим подразделения из ТХО (для _wMode = cgPAT_AutoReg)
    _sMask - маска на код подразделения (для _wMode = cgPAT_Code)
  </pre>
  #end
  function SetParam_PodrK(_wMode : word; _cPodr : comp; _flPodr : word; _sMask : string) : boolean;
//  function FormulaToParams(PSI_Service: longint; master: IdMaster;  Formula: string; var Params: TPatParams): boolean;
  // to do сделать Get... если понадобится
end;

ObjInterface ObjIdMaster2;
  #doc
    Установка режима работы
  #end
  procedure SetObjMode(mode: word);
  #doc
    Установка периода по двум датам
  #end
  procedure SetParam_PeriodByDates(_BegDate, _EndDate: date);

  #doc
    Установка ссылки на объект обработки формул в паскале TSI_Service (ServPat.Pas)
  #end
  procedure SetParam_SiServHandle(SiServHandle: TPtr);

  #doc
    Вычислить сформированную формулу через DirectSQL и вернуть результат
  #end
  function GetDSQLResult(var Value: double; var ValueValRep: double; var ValueVal: double; var ValueKol: double): boolean;

  #doc
    Запустить интерактивный аналитический отчет по установленным параметрам
  #end
  function FormulaToParams(PSI_Service: longint; Formula: string; var Params: TPatParams): boolean;
  // TODO: разбор периодов. пока поддерживается только метод SetParam_PeriodByDates
end;

#ifdef __FastClose__
ObjInterface ObjIdMaster3;
  #doc
    Установить фильтр/маску на тип документа
  #end
  function SetParam_TipDoc(_wMode : word; _sMask : string) : boolean;

  #doc
    Установить фильтр/маску на тип документа
  #end
  function SetParam_Status(_wMode : word; _sMask : string) : boolean;
end;
#end

ObjInterface ObjIdMaster4;
  #doc
    Установить фильтр по TXO
  #end
  function SetParam_TXO(_wMode : word; _sMask : string; _cTxo : comp) : boolean;
end;

//2.202596 доработка PAT идентификаторов в рамках филиальности
ObjInterface ObjIdMaster5;
  #doc
    Установить фильтр по Филиалу
  #end
  function SetParam_Filial2(_wMode : word; _sMask : string; _cFilial : comp) : boolean;
end;

ObjInterface ObjIdMaster6;
  #doc
    Установить свертку аналитики до указанного уровня
  #end
  function SetParam_RollLevel(_wMode : word; _wLevel : word) : boolean;

  #doc
    Не учитывать субсчет в разрезе свертки сальдо
  #end
  function SetParam_ExcludeSub(_wMode : word) : boolean;
end;

ObjInterface ObjIdMaster7;
  #doc
    Установить свертку аналитики по выбранной группировке
  #end
  function SetParam_RollByGroup(_wMode : word; _sGroup : string) : boolean;
end;


VipInterface IdMaster
  Implements ObjIdent, ObjIdMaster, ObjIdMaster2
  #ifdef __FastClose__
  ,ObjIdMaster3
  #end
  , ObjIdMaster4
  , ObjIdMaster5
  , ObjIdMaster6
  , ObjIdMaster7
  #ifdef Atl51
  #Licensed_Free
  Parameters (PickMode : word; sResult : string)
  #end
;

#end // __IdMaster__vih__
