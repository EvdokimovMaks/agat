//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Спецодежда
// Печать Акта выбытия СФО
//------------------------------------------------------------------------------

#doc
Формирование различных печатных вариантов документа Акт выбытия.<br>
#end
#include Commission.frn // комиссии
.Set Name = 'SFO_Act'
.Hide
.Fields
  OrgName : String  OrgOKPO : String  ActNo : String  dDate : Date
  FIO : String  TabN : String  spisReason : String
  frPodr : String  frMol : String
  toPodr : String  toMol : String
#CommissionFields
.endFields
.{ sfoNxtAct checkenter
  ^ ^ ^ ^
  ^ ^ ^
  ^ ^ ^ ^
#CommissionBody(SFO_Act)
.{ spcSfoAct checkenter
.Fields
  sfName : String  sfKod : String  Ed : String  KodEd : String
  giveDate : Date  KolVo : Double  Price : Double
  dopInfo : String percNach : Double
  spPrKod : String spPrName : String
! Для нового Акта выбытия (МБ-4) расчет амортизации по алгоритму Справки о задолженности
  newPrice : Double  endDate : Date  DolgSum : Double
! Для Акта группового выбытия
  WorkerFIO : String  WorkerTabN : String
! Ссылка на текущую спецификацию документа
  sfoRec : Comp iznKoef : Double
.endFields
 ^ ^ ^ ^ ^ ^ ^ ^ ^  ^ ^  ^ ^ ^  ^ ^  ^  ^
.}
.}
.endForm

!----------------------------------------------------------------------------

.LinkFORM SFOActOfOut Prototype Is SFO_Act
.Group 'Акт выбытия СФО'
.NameInList 'Акт выбытия СФО'
.var
  totSum : Double
  tStr, tStr1, tStr2, tStr3 : String
.endVar
#CommissionLinkVar
.Fields
  CommonFormHeader
  OrgName  ActNo
  DateToStr(dDate, 'DD')
  DateToStr(dDate, 'Mon')
  DateToStr(dDate, 'YYYY')
  tStr1
  tStr2
  tStr3
  frMol  FIO  spisReason
.endFields
.{ sfoNxtAct checkenter
#CommissionLinkInit(SFO_Act)
.begin
  totSum := 0;
  tStr1 := '';
  tStr2 := '';
  tStr3 := '';
  tStr := Информация_о_председателе + if (Информация_о_председателе = '', '', ' (председатель)');
  tStr := tStr + if (Список_членов_комиссии = '', '', if (tStr = '', '', ', ') + Список_членов_комиссии);

  // 1-я строка
  if (tStr <> '')
  {
    tStr1 := GroupWrap(tStr, 1, 51);
    tStr := SubStr(tStr, Length(tStr1) + 1, Length(tStr) - Length(tStr1));
  }

  // 2-я и 3-я строки
  if (tStr <> '')
  {
    tStr2 := GroupWrap(tStr, 1, 72);
    tStr3 := SubStr(tStr, Length(tStr2) + 1, Length(tStr) - Length(tStr2));
  }
end.
 ^
 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                                      Утверждаю


                             АКТ №  Б^ Б

        на выбытие малоценных и быстроизнашивающихся предметов,
                 форменной и специальной одежды, обуви

  Б"^" ^ ^ г. Б

 Комиссия в составе :  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
 назначенная приказом (распоряжением) от "__"_____________20__г. №_______
 произвела проверку состояния нижепоименованных предметов, находящихся на
 ответственном хранении у  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е
 (в использовании у  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е),
 и установила непригодность их к дальнейшей эксплуатации по причине:
  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е
 Р┌─────────────────────┬────────────┬─────────┬────────┬──────────┬──────────┬──────────┬────────────┐
│     Наименование    │ Номенкл. № │ Ед.изм. │ Кол-во │   Цена   │  Сумма   │Дата выдач│ Примечание │
├─────────────────────┼────────────┼─────────┼────────┼──────────┼──────────┼──────────┼────────────┤
.{ spcSfoAct CheckEnter
.fields
  sfName  sfKod  Ed  KolVo  Price  KolVo*Price  DateToStr( giveDate, 'DD/MM/YYYY')
.endFields
.begin
  totSum := totSum + (Price * KolVo);
end.
│@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@│@@@@@@@@@│&#&&&&&&│&#&&&&&.&&│&#&&&&&.&&│&&&&&&&&&&│            │
.}
└─────────────────────┴────────────┴─────────┴────────┴──────────┴──────────┴──────────┴────────────┘ Р
.fields
  Председатель_комиссии_Должность
  Председатель_комиссии_ФИО
  if (Номер_члена_комиссии = Word(1), 'Члены комиссии:', '')
  Массив_Член_комиссии_Должность [Номер_члена_комиссии]
  Массив_Член_комиссии_ФИО       [Номер_члена_комиссии]
  frMol
.endFields
 И
 Председатель комиссии:  Д@@@@@@@@@@@@@@@@@@@@@ Д ___________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                              должность         подпись        расшифровка подписи
.{While (Номер_члена_комиссии < Количество_членов_комиссии) OR (Номер_члена_комиссии < 1)
.begin
  Номер_члена_комиссии := Номер_члена_комиссии + 1;
end.
 @@@@@@@@@@@@@@@         Д@@@@@@@@@@@@@@@@@@@@@ Д ___________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                              должность         подпись        расшифровка подписи
.}

 Материально-ответственное лицо:  Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б И

.}
.endForm

!----------------------------------------------------------------------------

.linkform 'SFOActOfOutRos' prototype is 'SFO_Act'
.Group 'Акт выбытия СФО'
.Group 'Акт группового выбытия'
.Group 'Акт выбытия СФО (Россия)'
.Group 'Расчеты при увольнении'
.NameInList 'Акт выбытия (форма № МБ-4 Россия)'
.P 40
.PAPER PA4
.ORIENT PORTRAIT
.LL 80
.declare
#include FeeSigners.vih
.endDeclare
.var
  CurSum :  Double;
  TotSum :  Double;
  IznSum :  Double;
  CountPage,
  i, j,
  KolStr   : Integer;
  arFIOStr : array[1..2] of String;
  arTabStr : array[1..2] of String;
  arKolStr : array[1..2] of integer;
  arDate   : array[1..2] of String;
  arPerc   : array[1..2] of Double;
  arOstSt  : array[1..2] of Double;
  arDolg   : array[1..2] of Double;
  sReason1,
  sReason2,
  sReason3 : String;
  iFeeSigners : FeeSigners;
  sDirect,
  sPost,
  sBuh,
  sMaster : String;
.endvar
#CommissionLinkVar
.begin
  iFeeSigners.InitFeeSigners(cgReport_MBP);
  iFeeSigners.FindFeeSignersByNRec(cSelectionCommissionNRec);
  sDirect := sPost := sBuh := sMaster := '';

  if (iFeeSigners.FeeSignersIsValid)
  {
    if (iFeeSigners.RestrictFeeByRole(1, cgRole_Direct))
    {
      sDirect := iFeeSigners.GetSignerFIO(1);
      sPost := iFeeSigners.GetSignerPosition(1);
    }

    if (iFeeSigners.RestrictFeeByRole(1, cgRole_Bux))
      sBuh := iFeeSigners.GetSignerFIO(1);

    if (iFeeSigners.RestrictFeeByRole(3, 'Исполнитель'))
      sMaster := iFeeSigners.GetSignerFIO(1);

    iFeeSigners.ClearRestriction;
  }
end.
.fields
 CommonFormHeader
 ActNo  OrgName  orgOKPO
 frPodr
 DateToStr(dDate, 'DD/MM/YYYY')
 frPodr
.endfields
.{ sfoNxtAct checkenter
#CommissionLinkInit(SFO_Act)
.begin
  for (i := 1; i <= Count(arFIOStr); i := i + 1)
    arFIOStr[i] := '';

  for (i := 1; i <= Count(arTabStr); i := i + 1)
    arTabStr[i] := '';

  for (i := 1; i <= Count(arKolStr); i := i + 1)
    arKolStr[i] := 0;

  for (i := 1; i <= Count(arDate); i := i + 1)
    arDate[i] := '';

  for (i := 1; i <= Count(arPerc); i := i + 1)
    arPerc[i] := 0;

  for (i := 1; i <= Count(arOstSt); i := i + 1)
    arOstSt[i] := 0;

  for (i := 1; i <= Count(arDolg); i := i + 1)
    arDolg[i] := 0;

  KolStr := 0;
  TotSum := 0;
  IznSum := 0;
  CountPage := 0;
end.
.{ spcSfoAct CheckEnter
.[h
 Ш ^
                                                                                                             Типовая межотраслевая форма № МБ-4
                                                                                                             Утверждена постановлением Госкомстата России
                                                                                                             от 30.10.97 № 71а
                                                  БАКТ №  Д@@@@@@@@@@ Е                                                                          ┌─────────────┐
                               Бвыбытия малоценных и быстроизнашивающихся предметов Б                                                          │    Коды     │
                                                                                                                                           ├─────────────┤
                                                                                                                             Форма по ОКУД │   0320002   │
                                                                                                                                           ├─────────────┤
 Организация   Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е          по ОКПО │@~@@@@@@@@@@@│
 Структурное подразделение   Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е                  └─────────────┘
                            ┌──────────┬──────────┬──────────────────────────────────────────┬──────────────┬─────────────────────────────┬──────────────┐
                            │   Дата   │   Код    │              Структурное                 │     Вид      │    Корреспондирующий счет   │ Учетная еди- │
                            │  состав- │   вида   │             подразделение                │ деятельности ├───────────┬─────────────────┤ ница выпуска │
                            │   ления  │ операции │                                          │              │   счет,   │  код аналити-   │  продукции   │
                            │          │          │                                          │              │  субсчет  │  ческого учета  │(работ, услуг)│
                            ├──────────┼──────────┼──────────────────────────────────────────┼──────────────┼───────────┼─────────────────┼──────────────┤
                            Б │@@@@@@@@@@│          │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│              │           │                 │              │
                            └──────────┴──────────┴──────────────────────────────────────────┴──────────────┴───────────┴─────────────────┴──────────────┘ Б
┌───────────────────────────────────────────────────────┬───────────────────┬──────┬──────────────┬───────────────────────────┬──────────────────────────┐
│                        Предмет                        │ Единица измерения │      │              │      Сумма, руб. коп.     │      Причина выбытия     │
├────────────────────────────────────────┬──────────────┼─────┬─────────────┤ Коли-│    Цена,     ├─────────────┬─────────────┼─────────┬────────────────┤
│             Наименование,              │ Номенклатур- │ Код │ Наименование│чество│   руб.коп.   │  Без учета  │   Аморти-   │         │                │
│          сорт, размер, марка           │  ный  номер  │     │             │      │              │     НДС     │   зации     │   Код   │  Наименование  │
├────────────────────────────────────────┼──────────────┼─────┼─────────────┼──────┼──────────────┼─────────────┼─────────────┼─────────┼────────────────┤
│                   1                    │      2       │  3  │      4      │   5  │      6       │      7      │      8      │    9    │       10       │
├────────────────────────────────────────┼──────────────┼─────┼─────────────┼──────┼──────────────┼─────────────┼─────────────┼─────────┼────────────────┤
.]h
.begin
  KolStr := KolStr + 1;

  CurSum := KolVo * Price;
  TotSum := TotSum + CurSum;
  IznSum := IznSum + (CurSum * percNach / 100);

  arFIOStr [KolStr] := WorkerFIO;
  arTabStr [KolStr] := WorkerTabN;
  arKolStr [CountPage + 1] := arKolStr [CountPage + 1] + 1;
  arDate   [KolStr] := if(LongInt(giveDate) > 0, DateToStr(giveDate, 'DD/MM/YYYY'), '');
  arPerc   [KolStr] := percNach;
  arOstSt  [KolStr] := CurSum - (CurSum * percNach / 100);
  arDolg   [KolStr] := DolgSum;
end.
.fields
  sfName  sfKod  KodEd Ed  KolVo  Price  CurSum  (CurSum * percNach / 100)  spPrKod  spPrName
.endfields
│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@│@@@@@│@@@@@@@@@@@@@│&&&.&&│&'&&&&&&&&&.&&│&'#&&&&&&&.&&│&'#&&&&&&&.&&│@@@@@@@@@│@@@@@@@@@@@@@@@@│
.[f
.begin
  sReason1 := GroupWrap(spisReason, 1, 72);
  sReason3 := SubStr(spisReason, Length(sReason1) + 1, Length(spisReason) - Length(sReason1));
  sReason2 := GroupWrap(sReason3, 1, 136);
  sReason3 := GroupWrap(sReason3, 2, 136);
end.
.fields
  TotSum
  IznSum
  sReason1
  sReason2
  sReason3
  sMaster
.endfields
└────────────────────────────────────────┴──────────────┴─────┴─────────────┴──────┴──────────────┼─────────────┼─────────────┼─────────┴────────────────┘
                                                                                           БИтого Б   │ Б&'#&&&&&&&.&& Б│ Б&'#&&&&&&&.&& Б│
 Причина выбытия  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е         └─────────────┴─────────────┘
                  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е
                  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е
  БМастер (исполнитель работ) Б  _____________________    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                    подпись                   расшифровка подписи Ш
.begin
  TotSum := 0;
  IznSum := 0;
  CountPage := CountPage + 1;
end.

.]f
.}
.begin
  i := 0;
  KolStr := 0;
end.
.{While (i < CountPage)
 Ш                                                                                                                           Оборотная сторона формы № МБ-4
┌────────────────────────────────────────────┬───────────┬──────────┬──────────────┬───────────────────────────────────────────────────────┬─────────────┐
│                                            │           │          │              │               Расчет удержания с виновных             │             │
│                                            │           │          │              │                 Вид и способ удержания                │             │
│              Фамилия, И., О.               │ Табельный │   Дата   │   Подпись    ├───────┬────────────┬──────────────────────┬───────────┤   Подпись   │
│                                            │   номер   │  выдачи  │   сдавшего   │       │ стоимость  │  код вида удержания  │   сумма   │  виновного  │
│                                            │           │          │  малоценные  │процент│ за вычетом ├──────────────────────┤  ежеме-   │  в поломке  │
│                                            │           │          │  и быстроиз- │аморти-│амортизации,├─────────┬────────────┤  сячного  │   (утере)   │
│                                            │           │          │ нашивающиеся │ зации │ руб. коп.  │ размер  │   cумма    │  взноса,  │             │
│                                            │           │          │   предметы   │       │            │(процент)│ удержания, │  руб.коп  │             │
│                                            │           │          │              │       │            │удержания│  руб. коп. │           │             │
├────────────────────────────────────────────┼───────────┼──────────┼──────────────┼───────┼────────────┼─────────┼────────────┼───────────┼─────────────┤
│                     1                      │     2     │    3     │       4      │   5   │     6      │    7    │      8     │     9     │     10      │
├────────────────────────────────────────────┼───────────┼──────────┼──────────────┼───────┼────────────┼─────────┼────────────┼───────────┼─────────────┤
.begin
  j := 0;
  i := i + 1;
end.
.{While (j < arKolStr [i])
.begin
  j := j + 1;
  KolStr := KolStr + 1;
end.
.fields
  arFIOStr[KolStr]
  arTabStr[KolStr]
  arDate  [KolStr]
  arPerc  [KolStr]
  arOstSt [KolStr]
  arDolg  [KolStr]
.endfields
│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@│@~@@@@@@@@│              │&'#&.&&│&'#&&&&&&.&&│         │&'#&&&&&&.&&│           │             │
.}
└────────────────────────────────────────────┴───────────┴──────────┴──────────────┴───────┴────────────┴─────────┴────────────┴───────────┴─────────────┘
.fields
  sPost
  sDirect
  frMol
  sBuh
.endfields
  БРуководитель подразделения Б  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  _____________   Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                      должность               подпись             расшифровка подписи
  БКладовщик Б  _____________    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
               подпись              расшифровка подписи
  ББухгалтер Б  _____________    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
               подпись              расшифровка подписи
    "  " ___________20   г. Ш

.}
.}
.endform

!----------------------------------------------------------------------------

.linkform 'SFOReturn' prototype is SFO_ACT
.Group 'Приходная накладная'
.NameInList 'Приходная накладная'
.declare
#include FeeSigners.vih
.endDeclare
.var
  Npp, totSum, totSumIzn : Double;
  iFeeSigners : FeeSigners;
  sDirect, sBuh : String;
.endVar
#CommissionLinkVar
.begin
  iFeeSigners.InitFeeSigners(cgReport_MBP);
  iFeeSigners.FindFeeSignersByNRec(cSelectionCommissionNRec);
  sDirect := sBuh := '';

  if (iFeeSigners.FeeSignersIsValid)
  {
    if (iFeeSigners.RestrictFeeByRole(1, cgRole_Direct))
      sDirect := iFeeSigners.GetSignerFIO(1);

    if (iFeeSigners.RestrictFeeByRole(1, cgRole_GlBux))
      sBuh := iFeeSigners.GetSignerFIO(1);

    iFeeSigners.ClearRestriction;
  }
end.
.fields
 CommonFormHeader
 ActNo
 DateToStr(dDate, 'DD')
 DateToStr(dDate, 'Mon')
 DateToStr(dDate, 'YYYY')
 toPodr  toMol
.endfields
.{ sfoNxtAct checkenter
#CommissionLinkInit(SFO_Act)
.begin
  Npp := 0; totSum := 0; totSumIzn := 0;
end.
 Р  ^

                                                 ПРИХОДНАЯ НАКЛАДНАЯ  № ^
                                                      "^" ^ ^ г.

  Подразделение : @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  МОЛ           : @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Р Ш
┌───┬────────────┬───────────────────────────┬───────────┬──────────┬────────────┬───────────────┬────────────────┬───────┬────────────────┬──────────────┐
│ № │ Номенклат. │       Наименование        │  Единица  │  Размер  │ Количество │     Цена      │      Сумма     │ Износ │     Сумма      │  Примечание  │
│п/п│   номер    │                           │ измерения │  (рост)  │            │               │                │   %   │     износа     │              │
├───┼────────────┼───────────────────────────┼───────────┼──────────┼────────────┼───────────────┼────────────────┼───────┼────────────────┼──────────────┤
.{ spcSfoAct checkenter
.Fields
  Npp  sfKod  sfName  Ed  dopInfo  KolVo  Price
  Price * KolVo * if (wGetTune('Fin.MBP.OstStoim') = 0, 1, 1 - PercNach/100)
  percNach
  Price * KolVo * PercNach/100
.endFields
.begin
  Npp := Npp + 1;
  totSum := totSum + Price * KolVo * if (wGetTune('Fin.MBP.OstStoim') = 0, 1, 1 - PercNach/100);
  totSumIzn := totSumIzn + Price * KolVo * PercNach/100;
end.
│&&&│@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@│@@@@@@@@@@│&#&&&&&&&.&&│&#'&&&&&&&&&.&&│&#'&&&&&&&&&&.&&│&&&&.&&│&#'&&&&&&&&&&.&&│              │
.}
└───┴────────────┴───────────────────────────┴───────────┴──────────┴────────────┴───────────────┴────────────────┴───────┴────────────────┴──────────────┘
.Fields
  totSum
  totSumIzn
  sDirect
  sBuh
.endFields

 Б  Итого :                                                                                         &#'&&&&&&&&&&.&&         &#'&&&&&&&&&&.&& Б Ш


 Р  Начальник отдела  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д     Главный бухгалтер  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д Р
.}
.endform
