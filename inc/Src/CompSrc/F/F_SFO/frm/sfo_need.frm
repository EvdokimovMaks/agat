//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Спецодежда
// Ведомость потребности в СФО
//------------------------------------------------------------------------------

#doc
Печать отчета "Анализ потребности в СФО".<br>
#end
.Set Name = 'SFO_NeedVed'
.Hide
.Fields
 NameVed : string          // название отчета
 DateVed : date            // дата отчета
 GroupName : string        // группировка
 LevelCount : word         // кол-во уровней в группировке
 OrgName : string          // наименование собственной организации
 OrgOKPO : string          // ОКПО собственной организации
 cSigners : comp           // подписанты
.EndFields
Наименование ведомости: ^
Дата: ^
Группировка: ^
Кол-во уровней: ^
Организация: ^
ОКПО: ^
Подписанты: ^
Установленные фильтры:
.{Filtres CheckEnter
.Fields
!установленные фильтры
  fltName : String : 'T:-'
.EndFields
  ^
.}
.{
.{ReqSFO_LevelName CheckEnter
.Fields
 Level_Name  : string :'T:R'  // наименование уровня
 Level_Ed    : string         // единица измерения если СФО иначе ''
 Level_EdKod : string         // код единицы измерения если СФО иначе ''
 Level_Kol   : double         // требуемое количество (для уровней, вложенных в СФО)
 Level_Num   : word           // номер уровня (1 - верхний, 2 - вложенный ...)
.EndFields
Наименование уровня: ^
Ед.изм. (для СФО): ^ ^
Количество: ^
Номер уровня: ^
.}
.{ReqSFO_SpecKol CheckEnter
.Fields
 Spec_Shift : string :'T:R'  //отступ для спецификации
 Spec_Kol   : double         //затребованное количество для спецификации
!Данные по ЛК, ЛС, Сотр
 Spec_CardNRec : comp        //PersCard.NRec
 Spec_CardNum  : string      //Номер ЛК
 Spec_LSTabN   : string      //Таб.номер из ЛСч
 Spec_FIO      : string      //ФИО сотрудника
!Данные по нормам
!    по категории
 Spec_NormSfoNRec : comp     //NormSFo.NRec
 Spec_NormSfoCode : string   //код общей нормы СФО для категории сотрудников
 Spec_NormSfoName : string   //название общей нормы СФО для категории сотрудников
 Spec_NormSfoNacop : string  //"+" - накопительная норма, "-" - не накопительная
 Spec_NormSfoInd   : string  //"+" - единовременная норма, "-" - периодическая
!    по персональной спецификации
 Spec_PersNormNrec : comp    //PersSpec.Nrec
 Spec_GroupSfoName  : string //название Группы СФО - строки спецификации нормы
 Spec_GroupSfoKod   : string //код Группы СФО - строки спецификации нормы
 Spec_PersNormFrDat : date   //персональная норма с
 Spec_PersNormExDat : date   //отсчет периодов с
 Spec_PersNormToDat : date   //персональная норма по
 Spec_PersNormF_Kol : double //кол-во при первой выдаче по персональной норме
 Spec_PersNormN_Kol : double //кол-во на срок по персональной норме
 Spec_PersNormSrok  : double //срок по перс норме
 Spec_PersNormStat  : string //статус персональной нормы (Закрытая или Действующая)
 Spec_PersNormFlag  : string //признак персональной нормы для форменной (Ф) или спецодежды (С)
 Spec_PersNormMain  : string //состояние - основная норма (О) или дополнительная (Д)
 Spec_PersNormEd    : string //единица измерения предметов из группы СФО
 Spec_PersNormEdKod : string //код единицы измерения предметов из группы СФО
 Spec_PersNormReqDt : date   //наступление срока выдачи
.EndFields
        Отступ: "^"       Затребованное кол-во: ^
        --- данные по сотруднику -----
        PersCard.NRec: ^  Номер ЛК: ^   Таб.N: ^   ФИО: ^

        ------ данные по норме СФО для категории, к которой принадлежит сотрудник
        NormSFO.NREC: ^  Код нормы: ^  Название: ^   Накопительная: ^  Единовременная: ^

        ------ данные по персональной спецификации
        PersSpec.NRec: ^  Группа СФО: ^  Код группы: ^  Назначена с: ^ (^) по ^ Первоначально: ^ ед, затем ^ ед. на срок ^ мес.
        Норма: ^  Признак: ^  Состояние: ^  Единица измерения: ^ ^ Наступление срока выдачи: ^
.}
.{ReqSFO_BottLine CheckEnter
-----------------------------------------------------------------------
.}
.}
.EndForm

//-------------------------------------------------------------------------

.LinkForm SFO_NeedVed01 prototype is SFO_NeedVed
.NameInList 'Ведомость потребности в СФО'
.Group 'Краткая'
.P 70
.LM 15
.declare
#include FeeSigners.vih
.endDeclare
.Var
 iFeeSigners : FeeSigners;
 i, lSign : longint;
 FIOs, Post : array [1..2] of string;
.EndVar
.begin
 lSign := 0;

 for (i := 1; i <= Count(FIOs); i++)
 {
   FIOs[i] := '';
   Post[i] := '';
 }

 iFeeSigners.InitFeeSigners(cgReport_MBP);
 iFeeSigners.FindFeeSignersByNRec(cSigners);

 if (iFeeSigners.FeeSignersIsValid)
   if (iFeeSigners.GetSignerFirst)
     do
     {
       lSign++;
       FIOs[lSign] := iFeeSigners.GetSignerFIO(1);
       Post[lSign] := if (iFeeSigners.GetSignerRole <> '', iFeeSigners.GetSignerRole, iFeeSigners.GetSignerPosition(1));
     }
     while (iFeeSigners.GetSignerNext);
end.
.Fields
 CommonFormHeader
 NameVed
 DateVed
 GroupName
.EndFields
 И ^ И
 Й@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Й
 И                                по состоянию на  Б@@@@@@@@@@ Й

 ИГруппировка:  В^ В
Установленные фильтры: И
.{Filtres CheckEnter
.Fields
  fltName : 'T:-'
  fltName : 'T:-'
.EndFields
.{?Internal;(Substr(fltName,1,2)='По');
 К   Б^ Л
.}
.{?Internal;(Substr(fltName,1,2)<>'По');
 К  ^ К
.}
.}
 И────────────────────────────────────────────────────────────────┬───────────┬─────────── И
 И                         Наименование                           │  Единица  │ Требуемое И
 И                                                                │ измерения │ количество И
 И────────────────────────────────────────────────────────────────┼───────────┼─────────── И
 И                               1                                │     2     │    3 И
 И────────────────────────────────────────────────────────────────┴───────────┴─────────── И
.{
.[H SKIP
 И                                                                              Лист @np@ И
 И────────────────────────────────────────────────────────────────┬───────────┬─────────── И
 И                               1                                │     2     │    3 И
 И────────────────────────────────────────────────────────────────┴───────────┴─────────── И
.]H
.{ReqSFO_LevelName CheckEnter
.Fields
 Level_Name : 'T:R'  Level_Ed
 if (Level_Kol > 0, String(Level_Kol), '')
.EndFields
 Й@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @~@@@@@@@@@ &&&&&&&.&&& Й
.}
.{ReqSFO_SpecKol CheckEnter
.}
.{ReqSFO_BottLine CheckEnter
.}
.}
 И──────────────────────────────────────────────────────────────────────────────────────── И
.begin
 i := 0;
end.
.{While (i < lSign)
.begin
  i++;
end.
.fields
  Post[i]
  FIOs[i]
.endfields
 И@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ И
.}
.EndForm

//-------------------------------------------------------------------------

.LinkForm SFO_NeedVed02 prototype is SFO_NeedVed
.NameInList 'Ведомость потребности в СФО'
.Group 'Подробная'
.P 70
.LM 15
.declare
#include FeeSigners.vih
.endDeclare
.Var
 iFeeSigners : FeeSigners;
 i, lSign : longint;
 FIOs, Post : array [1..2] of string;
.EndVar
.begin
 lSign := 0;

 for (i := 1; i <= Count(FIOs); i++)
 {
   FIOs[i] := '';
   Post[i] := '';
 }

 iFeeSigners.InitFeeSigners(cgReport_MBP);
 iFeeSigners.FindFeeSignersByNRec(cSigners);

 if (iFeeSigners.FeeSignersIsValid)
   if (iFeeSigners.GetSignerFirst)
     do
     {
       lSign++;
       FIOs[lSign] := iFeeSigners.GetSignerFIO(1);
       Post[lSign] := if (iFeeSigners.GetSignerRole <> '', iFeeSigners.GetSignerRole, iFeeSigners.GetSignerPosition(1));
     }
     while (iFeeSigners.GetSignerNext);
end.
.Fields
 CommonFormHeader
 NameVed
 DateVed
 GroupName
.EndFields
 И ^ И
 Й@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Й
 И                                по состоянию на  Б@@@@@@@@@@ Й

 ИГруппировка:  В^ В
Установленные фильтры: И
.{Filtres CheckEnter
.Fields
  fltName : 'T:-'
  fltName : 'T:-'
.EndFields
.{?Internal;(Substr(fltName,1,2)='По');
 К   Б^ Л
.}
.{?Internal;(Substr(fltName,1,2)<>'По');
 К  ^ К
.}
.}
 Ш──────────┬─────────────────────────┬─────────────────────────┬──────────┬──────────┬─────────┬────────┬─────────┬────────┬──────────┬───┬───┬───┬────
  № ЛК    │        Категория        │       Группа СФО        │    с..   │    по..  │  Срок   │По норме│ Ед.изм. │Потребн.│ Выдача с │Нак│Ед.│Ф/С│Осн
──────────┼─────────────────────────┼─────────────────────────┼──────────┼──────────┼─────────┼────────┼─────────┼────────┼──────────┼───┼───┼───┼────
    1     │            2            │            3            │    4     │     5    │    6    │    7   │    8    │    9   │    10    │ 11│ 12│ 13│ 14
──────────┴─────────────────────────┴─────────────────────────┴──────────┴──────────┴─────────┴────────┴─────────┴────────┴──────────┴───┴───┴───┴──── Ш
.{
.[H SKIP
 Ш                                                                                                                                          Лист @np@
──────────┬─────────────────────────┬─────────────────────────┬──────────┬──────────┬─────────┬────────┬─────────┬────────┬──────────┬───┬───┬───┬────
    1     │            2            │            3            │    4     │     5    │    6    │    7   │    8    │    9   │    10    │ 11│ 12│ 13│ 14
──────────┴─────────────────────────┴─────────────────────────┴──────────┴──────────┴─────────┴────────┴─────────┴────────┴──────────┴───┴───┴───┴──── Ш
.]H
.{ReqSFO_LevelName CheckEnter
.Fields
 Level_Name : 'T:R'  Level_Ed
 if ( Level_Kol > 0, String( Level_Kol), '')
.EndFields
 Й@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @~@@@@@@@@@ &&&&&&&.&&& Й
.}
.{ReqSFO_SpecKol CheckEnter
.Fields
 Spec_CardNum Spec_NormSfoName Spec_GroupSfoName
 if (Spec_PersNormFrDat = Date(0,0,0), '', DateToStr(Spec_PersNormFrDat, 'DD/MM/YYYY'))
 if (Spec_PersNormToDat = Date(0,0,0), '', DateToStr(Spec_PersNormToDat, 'DD/MM/YYYY'))
 if (Spec_PersNormExDat = Date(0,0,0), '',
   if (Word(Spec_PersNormSrok) = 0, 'Разовая',
     if (Word(Spec_PersNormSrok) = 65535, 'До износа', String(Word(Spec_PersNormSrok)))))
 Spec_PersNormN_Kol  Spec_PersNormEd  Spec_Kol
 if ((Spec_PersNormReqDt = Date(0,0,0)) or (Spec_PersNormReqDt = MaxDate), 'не опред.', DateToStr(Spec_PersNormReqDt, 'DD/MM/YYYY'))
 Spec_NormSfoNacop  Spec_NormSfoInd Spec_PersNormFlag Spec_PersNormMain
.EndFields
 Ш@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@@ &&&&&&&&& &&&&&&&& @@@@@@@@@ &&&&&&&& @~@@@@@@@@ &~& &~& &~& &~&  Ш
.}
.{ReqSFO_BottLine CheckEnter
.}
.}
 И────────────────────────────────────────────────────────────────────────────────────────── И
.begin
 i := 0;
end.
.{While (i < lSign)
.begin
  i++;
end.
.fields
  Post[i]
  FIOs[i]
.endfields
 И@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ И
.}
.EndForm

//-------------------------------------------------------------------------

.LinkForm SFO_NeedVed03 prototype is SFO_NeedVed
.NameInList 'Ведомость учета выдачи СФО (форма № МБ-7)'
.Group 'Подробная'
.f "sfo_need_MB_17.OUT"
.Defo Landscape
.p 50
.declare
#include FeeSigners.vih
.endDeclare
.var
  Cnt : Double;
  PodrStr : String;
  inPodrList : Boolean;
  PrintObr : Boolean;
  iFeeSigners : FeeSigners;
  sDirect, sDirPost,
  sMol, sMolPost : String;
.endvar
.begin
  sDirect := sDirPost := sMol := sMolPost := '';

  iFeeSigners.InitFeeSigners(cgReport_MBP);
  iFeeSigners.FindFeeSignersByNRec(cSigners);

  if (iFeeSigners.FeeSignersIsValid)
  {
    if (iFeeSigners.RestrictFeeByRole(1, cgRole_Direct))
    {
      sDirect := iFeeSigners.GetSignerFIO(1);
      sDirPost := iFeeSigners.GetSignerPosition(1)
    }

    if (iFeeSigners.RestrictFeeByRole(1, cgRole_Otvetstv))
    {
      sMol := iFeeSigners.GetSignerFIO(1);
      sMolPost := iFeeSigners.GetSignerPosition(1);
    }

    iFeeSigners.ClearRestriction;
  }
end.
.fields
  CommonFormHeader
  ''  OrgName  OrgOKPO
  PodrStr  DateToStr(DateVed, 'DD/MM/YYYY')
  PodrStr
.endfields
.begin
  Cnt := 0;
  PodrStr := 'по всей организации';
  PrintObr := True;
end.
 Р ^
                                                                                                                                       Типовая межотраслевая форма № MБ-7
                                                                                                                                       Утверждена постановлением Госкомстата России
                                                                                                                                       от 30.10.97 № 71а

                                               БВЕДОМОСТЬ №  Д@@@@@@@@@@ Е
                                       Бучета выдачи спецодежды, спецобуви и Б
                                         Бпредохранительных приспособлений Б
                                                                                                                                                                      ┌────────────┐
                                                                                                                                                                      │    Коды    │
                                                                                                                                                                      ├────────────┤
                                                                                                                                                        Форма по ОКУД │  0320003   │
                                                                                                                                                                      ├────────────┤
.{Filtres CheckEnter
.begin
  if (Substr(fltName, 1, 3) = 'По ')
  {
    inPodrList := False;

    if (Substr(fltName, 1, 17) = 'По подразделениям')
    {
      PodrStr := fltName;
      inPodrList := True;
    }
  }
  else
    if (inPodrList)
    {
      if (Substr(PodrStr, 1, 17) = 'По подразделениям')
        PodrStr := '';

      PodrStr := PodrStr + if (PodrStr = '', '', ', ') + Trim(fltName);
    }
end.
.}
 Организация   Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е                      по ОКПО │@~@@@@@@@@@@│
 Структурное подразделение   Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е                              └────────────┘
                                                                                   ┌───────────────┬─────────────────┬─────────────────────────────────────┬───────────────────────┐
                                                                                   │      Дата     │     Код вида    │              Cтруктурное            │          Вид          │
                                                                                   │  составления  │     операции    │             подразделение           │     деятельности      │
                                                                                   ├───────────────┼─────────────────┼─────────────────────────────────────┼───────────────────────┤
                                                                                    Б│@~@@@@@@@@@@@@@│                 │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│                       │
                                                                                   └───────────────┴─────────────────┴─────────────────────────────────────┴───────────────────────┘ Б
.{
.[h1
 ┌───────┬─────────────────────────────────────┬─────────┬──────────────────────────────────────────────────┬────────────────────┬──────────┬────────────┬─────────┬────────────────┐
 │ Номер │                                     │Табельный│Спецодежда,спецобувь и предохранит. приспособления│ Единица измерения  │          │Дата поступ-│   Срок  │   Подпись в    │
 │  по   │       Фамилия, имя, отчество        │  номер  ├─────────────────────────────┬────────────────────┼─────┬──────────────┤Количество│ления в экс-│  службы │   получении    │
 │порядку│                                     │         │         Наименование        │Номенклатурный номер│ Код │ Наименование │          │ плуатацию  │         │    (сдаче)     │
 ├───────┼─────────────────────────────────────┼─────────┼─────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │   1   │                  2                  │    3    │              4              │          5         │  6  │      7       │     8    │      9     │    10   │       11       │
 ├───────┼─────────────────────────────────────┼─────────┼─────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
.]h
.[h2
                                                                                                                                                      Оборотная сторона формы № MБ-7
 ┌───────┬─────────────────────────────────────┬─────────┬─────────────────────────────┬────────────────────┬─────┬──────────────┬──────────┬────────────┬─────────┬────────────────┐
 │   1   │                  2                  │    3    │              4              │          5         │  6  │      7       │     8    │      9     │    10   │       11       │
 ├───────┼─────────────────────────────────────┼─────────┼─────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
.]h
.{ReqSFO_LevelName CheckEnter
.Fields
  Level_Name : 'T:R'  Level_EdKod  Level_Ed
  if (Level_Kol > 0, String(Level_Kol), '')
.EndFields
 │ Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б│                    │ Б@@@@@ Б│ Б@@@@@@@@@@@@@@ Б│ Б&'&&&&&.&& Б│            │         │                │
.}
.{ReqSFO_SpecKol CheckEnter
.begin
  Cnt := Cnt + 1
end.
.Fields
  Cnt  Spec_FIO  Spec_LSTabN  Spec_GroupSfoName  Spec_GroupSfoKod  Spec_PersNormEdKod  Spec_PersNormEd  Spec_Kol
  if (Spec_PersNormExDat = Date(0,0,0), '',
    if (Word(Spec_PersNormSrok) = 0, 'Разовая',
      if (Word(Spec_PersNormSrok) = 65535, 'До износа', String(Word(Spec_PersNormSrok)))))
.EndFields
 │&&&&&& │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@│@@@@@│@@@@@@@@@@@@@@│&'&&&&&.&&│            │&&&&&&&&&│                │
.}
.{ReqSFO_BottLine CheckEnter
.}
.[f1
.begin
  PrintObr := True;
end.
 └───────┴─────────────────────────────────────┴─────────┴─────────────────────────────┴────────────────────┴─────┴──────────────┴──────────┴────────────┴─────────┴────────────────┘

.]f
.[f2
.begin
  PrintObr := False;
end.
 └───────┴─────────────────────────────────────┴─────────┴─────────────────────────────┴────────────────────┴─────┴──────────────┴──────────┴────────────┴─────────┴────────────────┘
.fields
  sMolPost sMol
  sDirPost sDirect
.endfields

  БМатериально ответственное лицо Б   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  _____________   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                           должность               подпись                    расшифровка подписи
  БРуководитель подразделения Б       Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  _____________   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                           должность               подпись                    расшифровка подписи
    "   " ______________ 20  г.
.]f
.}
.{?INTERNAL;PrintObr
.fields
  'true' : 's'
.endFields
  ^                                                                                                                                                       Оборотная сторона формы № MБ-7
 ┌───────┬──────────────────────────────────────┬─────────┬────────────────────────────┬────────────────────┬─────┬──────────────┬──────────┬────────────┬─────────┬────────────────┐
 │   1   │                  2                   │    3    │              4             │          5         │  6  │      7       │     8    │      9     │    10   │       11       │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 ├───────┼──────────────────────────────────────┼─────────┼────────────────────────────┼────────────────────┼─────┼──────────────┼──────────┼────────────┼─────────┼────────────────┤
 │       │                                      │         │                            │                    │     │              │          │            │         │                │
 └───────┴──────────────────────────────────────┴─────────┴────────────────────────────┴────────────────────┴─────┴──────────────┴──────────┴────────────┴─────────┴────────────────┘
.fields
  sMolPost sMol
  sDirPost sDirect
.endfields

  БМатериально ответственное лицо Б   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  _____________   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                           должность               подпись                    расшифровка подписи
  БРуководитель подразделения Б       Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  _____________   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                           должность               подпись                    расшифровка подписи
    "   " ______________ 20  г.
.}
 Р А
.endform
