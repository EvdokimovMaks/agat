//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 5.80 - модуль "Платежный календарь"
// Множественные фильтры - поля настройки
//------------------------------------------------------------------------------

//******************************************************************************
// строка в ячейке фильтра
Function Filter_GetName( _numRecords: word
                         ; _sNameOne: string
                                   ): string;
var ret: string;
{
  if (_numRecords > 1)
    ret := 'Множественный выбор' + ' ('+string(_numRecords)+')';
  else
    if (_numRecords = 1)
      ret := _sNameOne;
    else // _numRecords = 0
      ret := ''; // значение не определено

  Filter_GetName := ret;
}

//******************************************************************************
// поиск значения в фильтре
Function Filter_Find( _cRecTable: comp
                        ; _cTune: comp
                      ; _nFilter: word
                   ; _numRecords: word
                   ; _cRecFilter: comp
                               ): boolean;
var ret: boolean;
{
  // выбрано 0 элементов
  if _numRecords = 0
    ret := (_cRecTable = 0);
  else
    // выбран 1 элемент
    if _numRecords = 1
      ret := (_cRecTable = _cRecFilter);
    else
      // выбрано несколько элементов
      if getfirst PickRep where ((
                       UserName == PickRep.OwnName
                  and  _cTune   == PickRep.cUserDeskRep
                  and  _nFilter == PickRep.wList
                 and _cRecTable == PickRep.cRec
                                )) = tsOk
        ret := True;
      else
        ret := False;

  Filter_Find := ret;
}

// поиск нескольких значений в фильтре (одного из них)
Function Filter_FindInMarker(_markRec: longint; _cTune: comp;
                             _nFilter: word): boolean;
var
  ret: boolean;
  rec: comp;
{
  ret := False;
  while GetMarker(_markRec, 0, rec)
  {
     // выбрано несколько элементов
     if getfirst PickRep where ((
           UserName == PickRep.OwnName      and
           _cTune   == PickRep.cUserDeskRep and
           _nFilter == PickRep.wList        and
           rec      == PickRep.cRec
                               )) = tsOk
      { ret := True; break; }
     AtDeleteMarker(_markRec, 0);
  }
  Filter_FindInMarker := ret;
}
//******************************************************************************

// *** One2Many по типу заявки определяем бит в слове
function One2Many(a : word): word;
{
  One2Many := 0;
  case a of
    tREQ_RASHOD   : One2Many := 1;
    tREQ_PRIHOD   : One2Many := 2;
    tREQ_PRDRASHOD: One2Many := 4;
    tREQ_PRDPRIHOD: One2Many := 8;
    tREQ_GETCREDIT: One2Many :=16;
  end
}

// *** Many2One по биту определяем тип заявки
function Many2One(a : word): word;
{
  Many2One := 0;
  case a of
     1: Many2One := tREQ_RASHOD   ;
     2: Many2One := tREQ_PRIHOD   ;
     4: Many2One := tREQ_PRDRASHOD;
     8: Many2One := tREQ_PRDPRIHOD;
    16: Many2One := tREQ_GETCREDIT;
  end
}

// *** One2Many_ по типу заявки определяем номер бита в слове
function One2Many_(a : word): word;
{
  One2Many_ := 0;
  case a of
    tREQ_RASHOD   : One2Many_ := 1;
    tREQ_PRIHOD   : One2Many_ := 2;
    tREQ_PRDRASHOD: One2Many_ := 3;
    tREQ_PRDPRIHOD: One2Many_ := 4;
    tREQ_GETCREDIT: One2Many_ := 5;
  end
}
