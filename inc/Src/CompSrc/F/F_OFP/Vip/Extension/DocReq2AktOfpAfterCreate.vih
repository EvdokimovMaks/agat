// кропка пашырэння *** ep_OFP_DocReq_2_AktOfp_AfterCreate ***
// Выклiкаецца пасля фармавання фiнансавага абавязання па Заяўцы.
// Прызначаная для "дазапаўненнi" палёў фiнансавага абавязання пасля яго фармавання па Заяўцы

#doc
<brief> Точка расширения : "API - После формирования обязательства по Заявке" </brief>
Данная точка расширения предназначена для <i>дополнения</i> полей Обязательства
после его формирования по Заявке.<br>
<b>Параметры:</b><br>
<b><i>cDocReq</i></b> - ссылка на Заявку по которой формировалось данное обязательство<br>
<b><i>cAktOfp</i></b> - ссылка на сформированное Обязательство<br>
<b>Результат:</b><br>
Обработчик точки расширения всегда должен возвращать <b>True</b>, так как результат не важен.<br>
<b>Пример использования:</b><br>
<pre>

interface CopyInfoFromDocReqToAktOFP;
create view coffdrddpftaofpdd
from 
  AktOfp, DocReq
;

public procedure CopyInfoFromDocReqToAktOFP(cDocReq : tNRec; cAktOfp : tNRec);
{
  // Перенесли дату "по" в дату плановую дату
  update AktOFP where ((cDocReq == DocReq.NRec and cAktOfp == AktOfp.NRec))
    set AktOFP.dDoc := DocReq.DenDPlat;
}
end.

// *** cDocReq-ссылка на Документ-Источник-заявку cAktOfp-ссылка на Документ-Приёмник-ФОб
Handler with replace DocReqDendPlatToAktOFPdDOC
  on ExtensionPoint ep_OFP_DocReq_2_AktOfp_AfterCreate(cDocReq, cAktOfp : tNRec)[255]
Action
{
  var iCopyInfoFromDocReqToAktOFP: CopyInfoFromDocReqToAktOFP;
  if (LoadVipInterface(iCopyInfoFromDocReqToAktOFP))
  {
    iCopyInfoFromDocReqToAktOFP.CopyInfoFromDocReqToAktOFP(cDocReq, cAktOfp);
    FreeVipInterface(iCopyInfoFromDocReqToAktOFP);
  }
  Result := true;
}
</pre>
<b>Описание примера использования:</b><br>
Описанный выше пример осуществляет копирование поля заявки <i>по...</i> в поле 
<i>планируемая дата</i> Обязательства после формирования Обязательства по Заявке.

#end
ExtensionPoint ep_OFP_DocReq_2_AktOfp_AfterCreate(
  cDocReq : tNRec; // Ссылка на Документ-Источник - заявку
  cAktOfp : tNRec  // Ссылка на Документ-Приёмник - Финансовое обязательство
);


