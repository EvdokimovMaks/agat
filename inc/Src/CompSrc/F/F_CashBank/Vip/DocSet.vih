//------------------------------------------------------------------------------
//                                                      (c) Корпорация ГАЛАКТИКА
//------------------------------------------------------------------------------
// Галактика 8.1 - Бухгалтерский контур
// Наборы документов для раздельного учета.
//------------------------------------------------------------------------------

#ifndef _DocSet_vih_Included
#define _DocSet_vih_Included

#ifdef ComponentVersion
#Component "F_CashBank"
#end

!-------------------------------------------------------------------------------
table struct DocSet     "Наборы документов"
(
   wDocSet   : word     "Номер набора"
  ,Name      : tName    "Наименование набора"
  ,NameR     : tName    "Наименование раздела"
  ,NameROne  : tName    "Наименование раздела в ед. числе"
  ,NameShort : tName    "Наименование раздела сокращенное"
  ,Tune      : tName    "Настройка раздельного учета"
)
with index
(
   DocSet01 = wDocSet (Unique)            // Внимание, уникально должно быть !!!
);
!-------------------------------------------------------------------------------
table struct SpDocSet   "Спецификация наборов документов"
(
   wDocSet : word       "Номер набора"
  ,TiDkGal : word       "Системный тип документа"
)
with index
(
   SpDocSet01 = wDocSet + TiDkGal
  ,SpDocSet02 = TiDkGal (Unique)          // Внимание, уникально должно быть !!!
);
!-------------------------------------------------------------------------------

!-------------------------------------------------------------------------------
//  GloConst.inc
!-------------------------------------------------------------------------------
//  cgCash_Kassa        =  1; // Касса
//  cgCash_RS           =  2; // Расчетный счет
//  cgCash_ValRS        =  3; // Валютный
//  cgCash_BuhSpr       =  4; // Бух.справка
!-------------------------------------------------------------------------------
#doc
Наборы документов для раздельного учета. </brief>
Константы для наборов документов из <b>GloConst.inc</b>:
<table>
  <tr><th>Константа<th>Значение<th>Наименование набора документов
  <tr><td>cgCash_Kassa  <td>1 <td>Касса (Кассовые документы)
  <tr><td>cgCash_RS     <td>2 <td>Расчетный счет (Платежные документы)
  <tr><td>cgCash_ValRS  <td>3 <td>Валютный  расчетный счет (Валютные платежные документы)
  <tr><td>cgCash_BuhSpr <td>4 <td>Бухгалтерские справки
</table>
#end
!-------------------------------------------------------------------------------
ObjInterface ObjDocSet;
!-------------------------------------------------------------------------------
  #doc
  Пустой Init, чтобы породить загрузку интерфейса и наполнение таблицы DocSet.
  #end
  procedure Init;
  //----------------------------------------------------------------------------
  #doc
  Получить наименование набора документов по номеру.
  #end
  function GetName (wDocSet : word) : string;
  //----------------------------------------------------------------------------
  #doc
  Получить наименование раздела учета (где используются документы данного набора).
  #end
  function GetNameRazdel (wDocSet : word) : string;
  #doc
  То же, что и GetNameRazdel, но в единственном числе.
  #end
  function GetNameRazdelOne (wDocSet : word) : string;
  #doc
  То же, что и GetNameRazdel, но кратко.
  #end
  function GetNameRazdelShort (wDocSet : word) : string;
  //----------------------------------------------------------------------------
  #doc
  Включен ли раздельный учет по набору документов
  #end
  function OkManyTipDoc (wDocSet : word) : boolean;
  //----------------------------------------------------------------------------
  #doc
  Узнать номер набора по системному типу документа
  #end
  function GetDocSetByTiDkGal (TiDkGal : word) : word;
  //----------------------------------------------------------------------------
  #doc
  Можно ли редактировать таблицы настроек. (Фактически OkManyTipDoc + сообщение).
  #end
  function OkEditTables (wDocSet : word) : boolean;
  //----------------------------------------------------------------------------

  //----------------------------------------------------------------------------
  // Метод предназначен, возможно, для другого интерфейса (iCashBank например)
  #doc
  Создать записи UsersDoc с номерами галактических документов, равных
  пользовательским (TipGal = TipUsers), то есть для первой записи CashBank
  с заданным wDocSet.
  #end
  procedure UsersDocForTiDkGal (wDocSet : word; cCashBank : tNRec);
  //----------------------------------------------------------------------------

!-------------------------------------------------------------------------------
end;


!-------------------------------------------------------------------------------
#doc
Расширение #1 объектного интерфейса ObjDocSet
#end
ObjInterface ObjDocSet1;
!-------------------------------------------------------------------------------
  #doc
  Создать записи UsersDoc. Значение TipUsers определяется с учетом филиальности.
  #end
  procedure UsersDocForTiDk(awDocSet: word; acCashBank: tNRec);
  //----------------------------------------------------------------------------
  #doc
  Вычисление нового уникального в рамках филиальной базы значения UsersDoc.TipUsers
  #end
  function Unique_TipUsers: word;
  //----------------------------------------------------------------------------
end;

!-------------------------------------------------------------------------------
VipInterface DocSet implements ObjDocSet, ObjDocSet1 #Licensed_Free;
!-------------------------------------------------------------------------------
#end // _DocSet_vih_Included



