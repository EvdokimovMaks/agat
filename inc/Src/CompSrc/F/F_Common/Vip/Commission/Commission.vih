//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 7.12
// Объект для работы с комиссией и ответственными лицами
//------------------------------------------------------------------------------
#ifndef __Commission__
#define __Commission__

#include Commission.inc
/*
 * Порядок использования:
 *
 *   В vip-ах:
 *
 *    1. Объявить переменную, к примеру iCommission, типа Commission:
 *       ...
 *       var iCommission : Commission;
 *       ...
 *
 *    2. Объявить переменную, к примеру sCommission, типа string,
 *       для отображения названия выбранной комиссии:
 *       ...
 *       var sCommission : string;
 *       ...
 *
 *    3. При помощи InitCommission, в событии cmInit, инициализировать экземпляр типом TiDk.
 *       Для каждого типа документа отображается свой список комиссий,
 *       и в ДСК сохраняется ссылка на свою выбранную комиссию.
 *
 *       Затем в событии перечитать имя выбранной комиссии:
 *         set sCommission := iCommission.GetSelectionName;.
 *
 *         cmInit :
 *         {
 *         ...
 *           iCommission.InitCommission(TiDk);
 *           set sCommission := iCommission.GetSelectionName;
 *         ...
 *         }
 *
 *    4. В окне настройки параметров отчета вставить поле sCommission для выбора комиссии
 *       И в cmPick определить его обработчик, для вызова окна выбора и редактирования комиссии:
 *         iCommission.RunWindowSelection;
 *       Затем в событии перечитать имя выбранной комиссии:
 *         set sCommission := iCommission.GetSelectionName;
 *
 *         screen sc... ('',,sci13Esc);
 *         fields
 *         ...
 *           sCommission ('Комиссия') : PickButton, Protect;
 *         ...
 *
 *         <<
 *         ...
 *            Комиссия: .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 *         ...
 *         >>
 *         ...
 *
 *         cmPick:
 *         {
 *         ...
 *           case CurField of
 *         ...
 *             #sCommission :
 *             {
 *               iCommission.RunWindowSelection;
 *               set sCommission := iCommission.GetSelectionName;
 *             }
 *         ...
 *           end;
 *         ...
 *
 *         } // cmPick
 *
 *    5. Подключить макроопределение вывода комиссии в поток,
 *       при помощи подключения:
 *       ...
 *       #include Commission.fun
 *       ...
 *
 *    6. В процедуре вывода данных в поток вызвать макроопределение, для вывода комиссии в поток:
 *       ...
 *       #WriteCommission(iCommission, FrmStreamName)
 *       ...
 *       Где:
 *         iCommission   - имя переменной типа Commission, определенной выше.
 *         FrmStreamName - имя потока, в который выводится комиссия
 *
 *       При этом в поток выведутся следующие поля:
 *
 *         - Комиссия       - True, если комиссия выбрана.
 *         - cSelectionNRec - Ссылка на выбранную комиссию
 *         - Имя_комиссии   - имя выбранной комиссии
 *
 *         * Списки в формате установленном пользователем:
 *           - Информация_о_председателе - Вывод информации о председателе в формате установленном пользователем
 *           - Список_членов_комиссии    - Вывод списка членов комиссии, одной строкой, в формате установленном пользователем
 *
 *         * Списки членов комиссии, одной строкой, с жёстко установленной структурой:
 *           - ФИО_членов_комиссии             - вывод только ФИО членов комиссии
 *           - Должность_и_ФИО_членов_комиссии - вывод Должности и ФИО членов комиссии, в такой последовательности
 *           - ФИО_и_Должность_членов_комиссии - вывод ФИО и Должности членов комиссии, в такой последовательности
 *
 *         * Председатель_комиссии:
 *           - Председатель_комиссии_nRec
 *           - Председатель_комиссии_ФИО
 *           - Председатель_комиссии_Табельный_Номер
 *           - Председатель_комиссии_Должность
 *
 *         * Члены комиссии:
 *           - Количество_членов_комиссии
 *
 *         * Цикл по членам комиссии:
 *           - Член_комиссии_nRec
 *           - Член_комиссии_ФИО
 *           - Член_комиссии_Табельный_Номер
 *           - Член_комиссии_Должность
 *
 *
 *   В Формах:
 *
 *    7. Файлах форм, в самом начале подключить файл с об]явлением макроопределений
 *       ...
 *       #include Commission.frn
 *       ...
 *
 *   В прототипе формы:
 *
 *    7. Файле формы. В прототипе формы в разделе .fields перед первым циклом определить поля,
 *       при помощи макроопределения CommissionFields:
 *       ...
 *       #CommissionFields
 *       ...
 *
 *    8. В прототипе формы. В теле формы, где должен быть вывод полей,
 *       при помощи макроопределения CommissionBody:
 *       где FormName - имя прототипа
 *       ...
 *       #CommissionBody(FormName)
 *       ...
 *
 *   В присоединенной форме:
 *    9. Если присоединенная форма не вызывается из файла базовой, то также,
 *       в самом начале подключить файл с об]явлением макроопределений:
 *       ...
 *       #include Commission.frn
 *       ...
 *
 *   10. В присоединенной форме в там где должен быть раздел .var определить массивы и переменные при помощи:
 *       ...
 *       CommissionLinkVar
 *       ...
 *
 *   11. В начале присоединенной формы, после открывающей скобки главного цикла,
 *       в том месте где должно быть вывод полей комиссии, и до использования этих полей вставить:
 *       где FormName - имя прототипа
 *       ...
 *       #CommissionLinkInit(FormName)
 *       ...
 *
 *       При этом обнуляться массивы в информацией о членах комиссии,
 *       затем выведутся поля в поток и в цикле заполнятся массивы.
 *       Для использования будут доступны все нижеописанные поля,
 *       а так переменные и массивы:
 *
 *       Поля:
 *
 *         - Комиссия       - True, если комиссия выбрана.
 *         - cSelectionNRec - Ссылка на выбранную комиссию
 *         - Имя_комиссии   - имя выбранной комиссии
 *
 *         * Списки в формате установленном пользователем:
 *           - Информация_о_председателе - Вывод информации о председателе в формате установленном пользователем
 *           - Список_членов_комиссии    - Вывод списка членов комиссии, одной строкой, в формате установленном пользователем
 *
 *         * Списки членов комиссии, одной строкой, с жёстко установленной структурой:
 *           - ФИО_членов_комиссии             - вывод только ФИО членов комиссии
 *           - Должность_и_ФИО_членов_комиссии - вывод Должности и ФИО членов комиссии, в такой последовательности
 *           - ФИО_и_Должность_членов_комиссии - вывод ФИО и Должности членов комиссии, в такой последовательности
 *
 *         * Председатель_комиссии:
 *           - Председатель_комиссии_nRec
 *           - Председатель_комиссии_ФИО
 *           - Председатель_комиссии_Табельный_Номер
 *           - Председатель_комиссии_Должность
 *
 *         * Члены комиссии:
 *           - Количество_членов_комиссии
 *
 *       Следующие поля, в таком виде, в данном случае не имеют смысла,
 *       их содержимое находится с соответствующих массивах:
 *
 *         * Цикл по членам комиссии:
 *           - Член_комиссии_nRec
 *           - Член_комиссии_ФИО
 *           - Член_комиссии_Табельный_Номер
 *           - Член_комиссии_Должность
 *
 *         Массив_Член_комиссии_nRec
 *         Массив_Член_комиссии_ФИО
 *         Массив_Член_комиссии_Табельный_Номер
 *         Массив_Член_комиссии_Должность
 *
 *       Переменная:
 *
 *         Номер_члена_комиссии, к этому моменту = 0
 *
 *       Для использования можно организовать цикл:
 *
 *       .{While (Номер_члена_комиссии < Количество_членов_комиссии)
 *       .begin
 *         Номер_члена_комиссии := Номер_члена_комиссии + 1;
 *       end.
 *       .fields
 *         Массив_Член_комиссии_Должность [Номер_члена_комиссии]
 *         Массив_Член_комиссии_ФИО       [Номер_члена_комиссии]
 *       .endfields
 *
 *         Д@@@@@@@@@@@@@@@@@@@@@@@@ Д  ______________   Д@@@@@@@@@@@@@@@@@@@@@@@@@ Д
 *              должность              подпись         расшифровка подписи
 *       .}
 *
*/

#ifdef ComponentVersion
#Component "F_Common"
#end

#doc
  Объект для работы с комиссией и ответственными лицами
#end
ObjInterface ObjCommission;

//------------------------------------------------------------------------------
#doc
  Инициализация комиссии
#end
  procedure InitCommission  (In_wTiDk : word  // Тип документа - для каждого типа документа выбираются и светятся свои выборки
                                              //   0 - в этом случае выбираются и светятся все выборки...
                            );

//------------------------------------------------------------------------------
#doc
  Инициализация ответственных лиц
#end
  procedure InitResponsible (In_wTiDk : word  // Тип документа - для каждого типа документа выбираются и светятся свои выборки
                                              //   0 - в этом случае выбираются и светятся все выборки...
                            );

//------------------------------------------------------------------------------
#doc
  Запуск окна выбора и редактирования комиссии<br>
  <ul>для комиссий:
  <li>   0 - редактирование параметров
  <li>   1 - выбор комиссии
  </ul>
  <ul>для ответственных лиц
  <li>   2 - выбор ответственных лиц
  </ul>
#end
  procedure RunWindowSelection (wType : word); // RunWinCommission RunWindowSelection Responsible ??? RunWindowS

//------------------------------------------------------------------------------
#doc
  Возвращает ссылку на выбранную комиссию
  // - Selection.nRec
#end
  function GetSelection : comp; // GetSelectionCommission -> GetSelection Commission Responsible

//------------------------------------------------------------------------------
#doc
  Возвращает true если для текущего TiDk установлена комиссия
  //  - соответствует if (GetSelectionCommission <> comp(0))
#end
  function SelectionIsValid : boolean; // isValidCommission -> Commission Responsible ??? SelIsValid

//------------------------------------------------------------------------------
#doc
  Возвращает имя установленной комиссии
  // из Selection.Name,
  // для соответствующего TiDk, установленного в InitCommission,
  // или текст о том, что комиссия не установлена
  // * нужно заменить function isValid_Commission : string; на эту
#end
  function GetSelectionName : string; // GetSelNameCommission Commission Responsible

//------------------------------------------------------------------------------
#doc
  Очищает поле выбора комиссии
  // для соответствующего TiDk, установленного в InitCommission,
  // или текст о том, что комиссия не установлена
#end
  function ClearSelection : string; // ClearCommission Responsible

//------------------------------------------------------------------------------
#doc
  Возвращает установлен ли председатель комиссии
#end
  function GetCommissionChairMan : boolean; // GetCommissionChairMan

//------------------------------------------------------------------------------
#doc
  Возвращает количество составляющих, без учёта председателя...
#end
  function GetComponentsCount : word;       // GetCommissionMemberCount
                                            // membership - количество членов

//------------------------------------------------------------------------------
#doc
  Возвращает nRec председателя или члена комиссии
#end
  function GetComponentNRec (wNumber : word    //  0     - председатель комиссии,
                                               //  1,... - порядковый номер члена комиссии
                            )        : comp;

//------------------------------------------------------------------------------
#doc
  Возвращает ФИО председателя или члена комиссии
#end
  function GetComponentFIO  (wNumber : word    //  0     - председатель комиссии,
                                               //  1,... - порядковый номер члена комиссии
                            )        : string;

//------------------------------------------------------------------------------
#doc
  Возвращает табельный номер председателя или члена комиссии
#end
  function GetComponentTabN (wNumber : word    //  0     - председатель комиссии,
                                               //  1,... - порядковый номер члена комиссии
                            )        : string;

//------------------------------------------------------------------------------
#doc
  Возвращает должность председателя или члена комиссии
#end
  function GetComponentPost (wNumber : word    //  0     - председатель комиссии,
                                               //  1,... - порядковый номер члена комиссии
                            )        : string;

//------------------------------------------------------------------------------
#doc
  Возвращает председателя комиссии или список членов комиссии
#end
  function GetComponentsList (wType : word    // По установленным пользователем правилам:
                                              //   0 - Председателя комиссии, по установленным пользователем правилам;
                                              //   1 - Список членов комиссии, по установленным пользователем правилам;
                                              //
                                              // Не использую параметры пользователя:
                                              //   2 - Только ФИО, списка членов комиссии;
                                              //   3 - Должность (при наличии) и ФИО, списка членов комиссии;
                                              //   4 - ФИО и Должность (при наличии), списка членов комиссии.
                             )      : string;

//------------------------------------------------------------------------------
end;

ObjInterface ObjCommissionEx (ObjCommission);
//------------------------------------------------------------------------------

#doc
  Инициализация комиссии типом документа, и номером записи выборки.
  Текущая функция служит для того, чтобы работать с комиссией nRec которой передан в периметре In_nRec
#end
  procedure SetCommission   (In_wTiDk : word; // Тип документа - для каждого типа документа выбираются и светятся свои выборки
                                              //   0 - в этом случае выбираются и светятся все выборки...
                             In_nRec  : comp  // nRec требуемой комиссии
                            );

//------------------------------------------------------------------------------

#doc
  Инициализация ответственных лиц типом документа, и номером записи выборки.
  Текущая функция служит для того, чтобы работать с набором ответственных лиц, nRec которого передан в периметре In_nRec
#end
  procedure SetResponsible  (In_wTiDk : word; // Тип документа - для каждого типа документа выбираются и светятся свои выборки
                                              //   0 - в этом случае выбираются и светятся все выборки...
                             In_nRec  : comp  // nRec требуемой выборки ответственных лиц
                            );

//------------------------------------------------------------------------------
end;

ObjInterface ObjCommissionEx2 (ObjCommissionEx);
//------------------------------------------------------------------------------

#doc
  Возвращает ФИО в формате, заданном системной настройкой "Формат инициалов в расшифровке подписи"(Fin.SigFIO)
#end
  function GetFormatFIO (FullFIO : string    //  полное ФИО
                        )        : string;

//------------------------------------------------------------------------------
end;

ObjInterface ObjCommissionPost (ObjCommissionEx2);
//------------------------------------------------------------------------------
#doc
  Устанавливает необходимость при поиске должности председателя или члена комиссии
  анализировать значение внешнего атрибута "должность"
#end
  procedure SetPostFormat (In_NewPost : word   //  0 - не анализировать внешний атрибут
                                               //  1 - анализировать внешний атрибут
                          );

//------------------------------------------------------------------------------
end;

VipInterface Commission
  Implements ObjCommission, ObjCommissionEx, ObjCommissionEx2, ObjCommissionPost
  Licensed   (free)
;

#end // __Commission__
