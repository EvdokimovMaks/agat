.LinkForm 'AktPostupNMA' Prototype is 'OSONE'
.NameInList '[ВСМН] Акт о принятии к учету нематериального актива'
.F 'NUL'
.group 'Поступление НМА <<ВСМН>>'
.var
  iRow : word;
  iFeeSigners : FeeSigners;
  iLvar : iLvarToString;
  Массив_Член_комиссии_ФИО: ARRAY [1..2] of String;
  Массив_Член_комиссии_Должность: ARRAY [1..2] of String;
  Номер_подписанта:Word;
  iFirstRowFIO, iLastRowFIO, iFirstRowDolg, iLastRowDolg : integer;
  iLastHeadRow : integer;
.endvar
.create view n
as select
 *
from
 SpMoveOS
 MoveOS,

 KAUREFF SchetBU,
 SpKau SpKauSchet
where
((
         3000            ==  SchetBU.COTABLE //Внешнее КАУ виды продукции, работ, услуг
    and  Comp(KatOsNrec) ==  SchetBU.cRec
    and  20077           ==  SchetBU.WKAU
    and  SchetBU.CKAU    ==  SpKauSchet.nRec
))
;
.function PrnComiss(_PosName, _Dolg, _FIO :string) : boolean;
begin
  if _FIO <> ''
  {
    xlSetCellStringValue(_PosName, iRow/*Row*/, 1/*Col*/,iRow ,1);
    //---------------- должность -------------------------
    xlMergeCells(iRow, 3, iRow, 4);
    xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 3, iRow, 4);
    xlSetCellStringValue(_Dolg, iRow/*Row*/, 3/*Col*/,iRow ,3);
    if iFirstRowDolg = 0
      iFirstRowDolg := iRow;
    iLastRowDolg := iRow;
    xlMergeCells(iRow+1, 3, iRow+1, 4);
    xlSetCellStringValue('должность' , iRow+1, 3 ,iRow+1 ,3);
    xlSetFontSize(8, iRow+1, 3 ,iRow+1 ,3);
    //---------------- подпись ---------------------------
    xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 6, iRow, 6);
    xlSetCellStringValue('подпись' , iRow+1, 6 ,iRow+1 ,6);
    xlSetFontSize(8, iRow+1, 6 ,iRow+1 ,6);
    //---------------- ФИО -------------------------------
    xlMergeCells(iRow, 8, iRow, 10);
    xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 8, iRow, 10);
    xlSetCellStringValue(_FIO , iRow/*Row*/, 8/*Col*/,iRow ,8);
    if iFirstRowFIO = 0
      iFirstRowFIO := iRow;
    iLastRowFIO := iRow;
    xlMergeCells(iRow+1, 8, iRow+1, 10);
    xlSetCellStringValue('расшифровка подписи' , iRow+1, 8 ,iRow+1 ,8);
    xlSetFontSize(8, iRow+1, 8 ,iRow+1 ,8);

    xlAlignCellsEx(/*xlCentr*/-4108,-4107, iRow, 3, iRow, 8);
    xlAlignCellsEx(/*xlCentr*/-4108,-4160, iRow+1, 3, iRow+1, 8);
    PrnComiss := true;
  }
end.


.begin
  Номер_подписанта := 0;


  for (Номер_подписанта := 1; Номер_подписанта <= Count(Массив_Член_комиссии_ФИО); Номер_подписанта := Номер_подписанта + 1)
  {
    Массив_Член_комиссии_ФИО             [Номер_подписанта] := '';
    Массив_Член_комиссии_Должность       [Номер_подписанта] := '';
  }
end.
.{ // Начало формы
.begin
  xlKillExcel;
  xlDisplayAlerts(false);
  xlCreateExcelWithTemplate(TranslatePath('%ClientStartPath%')+'\XLS\F_OSOper\AktPostNMA.xlt',true);
end.
.begin
  if (xlIsExcelValid)
  {
    iFirstRowFIO := iLastRowFIO := iFirstRowDolg := iLastRowDolg := 0;
    var barcode : string;
    // barcode :=  Ean13ToEanGnivcTTF(GenerateBarCodeEx (coMoveOs, MoveOsNRec));    // Почемуто coMoveOs = 3037, а надо 3020
    barcode :=  Ean13ToEanGnivcTTF(GenerateBarCodeEx (3020, MoveOsNRec));
    xlSetCellStringValue(barcode, 2/*Row*/, 2/*Col*/,2 ,2);
    xlSetCellStringValue('     АКТ № '+Номер_документа+' от ' +DateToStr(Дата_документа, '" DD " mon YYYYг.'), 4, 3, 4 ,3);
    xlSetCellStringValue(Получатель, 8/*Row*/, 3/*Col*/,8 ,3);
    xlSetCellStringValue(Получатель, 8/*Row*/, 3/*Col*/,8 ,3);
    xlSetCellStringValue(СТРУКТУРНОЕ_ПОДРАЗДЕЛЕНИЕ_ПОЛУЧАТЕЛЯ, 10/*Row*/, 3/*Col*/,10 ,3);

    // Характеристика
    iRow := 13;

    var r : word;
    var s : string; s:='';
    r:=0;
    if iLvar.InitAllMemo(Comp(KatOSNrec), 3000, 0)
    while (iLVar.GetMemoStr(S))
    {
      r++;
      if r =1
        xlSetCellStringValue(s, iRow, 3 ,iRow ,3);
      else
      {
        iRow++;
        xlInsertRange(-4121/*xlShiftDown*/ , iRow, 1,iRow, 12);
        xlMergeCells(iRow, 1, iRow, 8);
        xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 1, iRow, 8);
        xlSetCellStringValue(s, iRow, 1 ,iRow ,1);
      }
    }

    xlSetCellStringValue(ИНВНОМЕР,13/*Row*/, 11/*Col*/,13 ,11);
  }
end.
.begin
  Номер_подписанта := 0;
end.
.{CheckEnter FEESIGNERSMEMBER_OSONE
.begin
  Номер_подписанта := Номер_подписанта + 1;
  Массив_Член_комиссии_ФИО            [Номер_подписанта] := Член_комиссии_ФИО;
  Массив_Член_комиссии_Должность      [Номер_подписанта] := Член_комиссии_Должность;
end.
.}
.{CheckEnter FEESIGNERSRESPONSIBLE_OSONE
.}
.{CheckEnter OS_OBOROT
.}
.{CheckEnter OS_LEAFS
.}
.{CheckEnter OS_SOSTAV
.}
.{CheckEnter OS_NALDRAG
.}
.{CheckEnter OS_AKTUSL
.}
.begin
  // Установить ширину заголовка таблицы
  iLastHeadRow := iRow+3;
  xlSetRowHeight(46, iRow+4/*Row*/, 12/*Col*/, iRow+4, 12);
  xlSetRowHeight(25, iRow+5/*Row*/, 12/*Col*/, iRow+5, 12);

  //спецификация
  iRow:=iRow+7;
  xlSetCellNumberValue(СТОИМОСТЬ_ЗА_ОБЩЕЕ_КОЛИЧЕСТВО, iRow, 3/*Col*/,iRow ,3);
  xlSetCellNumberValue(Срок_полезного_использования, iRow, 4/*Col*/,iRow ,4);
  xlSetCellStringValue(АМОРТИЗАЦИОННАЯ_ГРУППА_НАИМЕНОВАНИЕ, iRow, 5/*Col*/,iRow ,5);
  xlSetCellStringValue('линейный', iRow, 6/*Col*/,iRow ,6);
  xlSetCellNumberValue((1/Срок_полезного_использования)*100, iRow, 7/*Col*/,iRow ,7);

  If n.GetFirst SchetBU = tsOk
     If n.GetFirst SpKauSchet = tsOk
       xlSetCellStringValue(SpKauSchet.Name, iRow, 8/*Col*/,iRow ,8);

  xlSetCellStringValue(ИмяОС, iRow, 10/*Col*/,iRow ,10);
  xlSetCellStringValue(DateToStr(ДАТА_ПОСТ,'DD.MM.YYYY'), iRow, 11/*Col*/,iRow ,11);
  xlSetCellStringValue(ЗАВОДСКОЙ, iRow, 12/*Col*/,iRow ,12);
  xlWrapText(iRow, 1,iRow, 12);

  iRow:=iRow+9;

  var r : word;
  var s : string;  s:='';
  r:=0;

  if not iLvar.InitAllMemo(Comp(MoveOSNrec), 17, 0)
    iRow++;
  else
  {
    while (iLVar.GetMemoStr(S))
    {
      r++;
      if r =1
        xlSetCellStringValue(s, iRow, 1 ,iRow ,8);
      else
      {
        iRow++;
        xlInsertRange(-4121/*xlShiftDown*/ , iRow, 1,iRow, 12);
        xlMergeCells(iRow, 1, iRow, 8);
        xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 1, iRow, 8);
        xlSetCellStringValue(s, iRow, 1 ,iRow ,1);
      }
    }
  }
end.

.begin
// вывод подписантов
 // комиссия
  iRow := iRow+2;
  if Председатель_ФИО <> ''
  {
    PrnComiss('Председатель комиссии:', Председатель_Должность, Председатель_ФИО);
  }
  Номер_подписанта:=0;
 end.
.{while (Номер_подписанта< Количество_Членов_комиссии) OR (Номер_подписанта< 2)
.begin
  Номер_подписанта:= Номер_подписанта+ 1;
  iRow := iRow + 3;
  PrnComiss(if (Номер_подписанта= word(1), 'Члены комиссии:', ''),
            Массив_Член_комиссии_Должность [Номер_подписанта],
            Массив_Член_комиссии_ФИО   [Номер_подписанта]);
end.
.}
.begin
  iFeeSigners.InitFeeSigners(7306/*cgReport_Os*/);
  iFeeSigners.FindFeeSignersByNRec(Comp(cFeeSignersNRec));

  // Утверждающее лицо
  if (iFeeSigners.RestrictFeeByRole(2, 'UtvLico'))
    if (iFeeSigners.FeeSignersIsValid)
    {
      xlSetCellStringValue(iFeeSigners.GetSignerPosition(1), 4/*Row*/, 10/*Col*/,4 ,10);
      xlSetCellStringValue(iFeeSigners.GetSignerFIO(13), 7/*Row*/, 11/*Col*/,7 ,11);
    }

  // Бухгалтер
  iRow:= iRow + 2;
  xlSetCellStringValue('Отметка бухгалтерии об открытии карточки учета НМА:', iRow, 1, iRow ,1);
  iRow:= iRow + 1;

  if (iFeeSigners.RestrictFeeByRole(2, 'Bux')) and (iFeeSigners.FeeSignersIsValid)
    PrnComiss('', iFeeSigners.GetSignerPosition(1), iFeeSigners.GetSignerFIO(1));
  else
    PrnComiss('', ' ', ' ');

  iFeeSigners.ClearRestriction;
end.
.begin
  var fMacros : string;
  fmacros  := GetStringParameter('Files','TmpFilesDirectory',0)+'AktPostupNMA.bas';
  DeleteFile(Fmacros);
  LogStrToFile(Fmacros, 'Sub Sub1()                         ');

  if iFirstRowFIO <> 0
  {
    LogStrToFile(Fmacros, 'Dim Rng As Excel.Range             ');
    LogStrToFile(Fmacros, 'Dim Sh As Excel.Worksheet          ');
    LogStrToFile(Fmacros, 'Set Sh = Application.ActiveSheet   ');
    LogStrToFile(Fmacros, 'Set Rng = Sh.Range("C'+iFirstRowDolg+':C'+iLastRowDolg+'")');
    LogStrToFile(Fmacros, 'RngAutoFit Rng                     ');
  }

  LogStrToFile(Fmacros, 'Rows("1:'+iLastHeadRow+'").EntireRow.AutoFit    ');
  LogStrToFile(Fmacros, 'End Sub                            ');
  if ( xlImportModule(Fmacros) )
    {
      if not xlRunMacro('Sub1')
        message('Ошибка запуска макроса AktPostupNMA');
    }
  else
    message ('Ошибка загрузки макроса AktPostupNMA.');

  DeleteFile(Fmacros);
  xlKillExcel;

  OsOne.fExit;
end.
.} // Конец формы
.endform
