.linkform 'osOne_01_Rus' prototype is 'osOne'
.NameInList 'Форма N ОС-1 << Россия >>'
.group 'Поступление ОС <<Россия>>'
.group 'Поступление ОС <<ВСМН>>'
.group 'Поступление НМА <<Россия>>'
.group 'Поступление НМА <<ВСМН>>'
.group 'Внутреннее перемещение ОС <<Россия>>'
.group 'Внутреннее перемещение ОС <<ВСМН>>'
.group 'Внутреннее перемещение НМА <<Россия>>'
.group 'Внутреннее перемещение НМА <<ВСМН>>'
.group 'Выбытие ОС <<Россия>>'
.group 'Выбытие ОС <<ВСМН>>'
.group 'Выбытие НМА <<Россия>>'
.group 'Выбытие НМА <<ВСМН>>'
.hide
.defo portrait
.p 86
.var
  Характеристика_Наименование_Объекта : array[1..10] of string;
  Характеристика_Количество_Объектов  : array[1..10] of string;
  Характеристика_NameDrag             : array[1..10] of string;
  Характеристика_NomenclaturN         : array[1..10] of string;
  Характеристика_N_Edin               : array[1..10] of string;
  Характеристика_MassaDrag            : array[1..10] of string;

  Хар1 : word; // Текущий номер в цикле состава
  Хар2 : word; // Текущий номер в цикле драгметаллов
  ХарN : word; // Количество элементов в массиве данных характеристики

  Задана_Доверенность : boolean;
.endvar
#FeeSigners_Os_LinkVar
.{
.begin
  for (Хар1 := 1; Хар1 <= Count(Характеристика_Наименование_Объекта); Хар1 := Хар1 + 1)
  {
    Характеристика_Наименование_Объекта [Хар1] := '';
    Характеристика_Количество_Объектов  [Хар1] := '';
  }
  for (Хар2 := 1; Хар2 <= Count(Характеристика_NameDrag); Хар2 := Хар2 + 1)
  {
    Характеристика_NameDrag     [Хар2] := '';
    Характеристика_NomenclaturN [Хар2] := '';
    Характеристика_N_Edin       [Хар2] := '';
    Характеристика_MassaDrag    [Хар2] := '';
  }
  Хар1 := 0;
  Хар2 := 0;
  ХарN := 0;
end.
#FeeSigners_Os_LinkInit(osOne)
.fields
  '(Метод учета: ' + Метод_Учета + ')'

  Руководитель_Поставщика_Должность
  Руководитель_Поставщика

  Руководитель_Получателя_Должность
  Руководитель_Получателя

  Получатель
  Получатель_ОКПО
  Получатель_Адрес + ' ' + Получатель_Тел
    if ( Получатель_БАНК_Счет              <> '',' Р/с №: '      + Получатель_БАНК_Счет              , '') +
    if ( Получатель_БАНК_Имя               <> '',' в '           + Получатель_БАНК_Имя               , '') +
    if ( Получатель_БАНК_Адрес             <> '',' по адресу: '  + Получатель_БАНК_Адрес             , '') +
    if ( Получатель_БАНК_МФО               <> '',' БИК: '        + Получатель_БАНК_МФО               , '') +
    if ( Получатель_БАНК_Счет_в_РКЦ        <> '',' счет в РКЦ: ' + Получатель_БАНК_Счет_в_РКЦ        , '') +
    if ( Получатель_БАНК_РКЦ               <> '',' РКЦ: '        + Получатель_БАНК_РКЦ               , '')
  Структурное_подразделение_Получателя
!
  Поставщик
  Поставщик_ОКПО
  Поставщик_Адрес + ' ' + Поставщик_Тел
    if ( Поставщик_БАНК_Счет              <> '',' Р/с №: '      + Поставщик_БАНК_Счет              , '') +
    if ( Поставщик_БАНК_Имя               <> '',' в '           + Поставщик_БАНК_Имя               , '') +
    if ( Поставщик_БАНК_Адрес             <> '',' по адресу: '  + Поставщик_БАНК_Адрес             , '') +
    if ( Поставщик_БАНК_МФО               <> '',' БИК: '        + Поставщик_БАНК_МФО               , '') +
    if ( Поставщик_БАНК_Счет_в_РКЦ        <> '',' счет в РКЦ: ' + Поставщик_БАНК_Счет_в_РКЦ        , '') +
    if ( Поставщик_БАНК_РКЦ               <> '',' РКЦ: '        + Поставщик_БАНК_РКЦ               , '')
  Структурное_подразделение_Поставщика
  Основание_для_составления_акта
  Номер_приказа
  iExcelFormat.DateToStrRep(Дата_приказа)
  iExcelFormat.DateToStrRep(Дата_пост)
  iExcelFormat.DateToStrRep(Дата_выбытия)
  СчетДеб + if( trim(СубсчетДеб)='00','',' ' + СубсчетДеб) + if(Печатать_аналитику_по_счету,' ' + КауДеб,'')
  Код_ОКОФ
  Номер_документа
  Дата_документа
  Амортизационная_группа_Код
  ИнвНомер
  Заводской
  KatStroyNoSvidReg
  iExcelFormat.DateToStrRep(KatStroyDataReg)
  НаименованиеОС
  ОбъектНаходитсяВ
  Организация_изготовитель
.endfields
 Ш@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      Унифицированная форма № ОС-1

                                                                                                                                             Утверждена
                                                                                                                                         Постановлением
                                                                                                                                     Госкомстата России
                                                                                                                                      от 21.01.2003 № 7

   УТВЕРЖДАЮ                                                                       УТВЕРЖДАЮ
   Руководитель организации-сдатчика                                               Руководитель организации-получателя

    Д@@@@@@@@@@@@@@@@@@@@@@@@ Д __________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д              Д@@@@@@@@@@@@@@@@@@@@@@@ Д __________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
          должность          подпись         расшифровка подписи                          должность         подпись         расшифровка подписи

     "____" __________________ 20____ г.                                             "____" __________________ 20____ г.

   М.П.                                                                            М.П.

                                                                                                                                          ┌────────────┐
                                                                                                                                          │    Код     │
                                                                                                                                          ├────────────┤
                                                                                                                            Форма по ОКУД │   0306001  │
                                                                                                                                          ├────────────┤
   Организация-получатель  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е по ОКПО │@~@@@@@@@@@@│
                                                        наименование                                                                      └────────────┘
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                    адрес, телефон, факс
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                    банковские реквизиты
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                         наименование структурного подразделения                                                          ┌────────────┐
   Организация-сдатчик  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е по ОКПО │@~@@@@@@@@@@│
                                                       наименование                                                                       └────────────┘
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                  адрес, телефон, факс
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                  банковские реквизиты                                                                    ┌────────────┐
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д         │            │
                                        наименование структурного подразделения                                                   ┌───────┼────────────┤
   Основание для составления акта  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д │ номер │@~@@@@@@@@@@│
                                         приказ, распоряжение, договор (с указанием его вида, основных обязательств)              ├───────┼────────────┤
                                                                                                                                  │  дата │@~@@@@@@@@@@│
   ________________________________________________________________________________________________┌──────┬───────────────────────┴───────┼────────────┤
                                                                                                   │      │принятия к бухгалтерскому учету│@~@@@@@@@@@@│
   ________________________________________________________________________________________________│ Дата ├───────────────────────────────┼────────────┤
                                                                                                   │      │списания с бухгалтерского учета│@~@@@@@@@@@@│
   ________________________________________________________________________________________________└──────┴───────────────────────────────┼────────────┤
                                                                                                   Счет, субсчет, код аналитического учета│@~@@@@@@@@@@│
                                                         ┌──────────────────────────┬──────────────────┐                                  ├────────────┤
                                                         │      Номер документа     │ Дата составления │                          по ОКОФ │@~@@@@@@@@@@│
                                                         ├──────────────────────────┼──────────────────┤ ┌───────┬────────────────────────┼────────────┤
                                                         │@~@@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@@│ │       │ амортизационной группы │@~@@@@@@@@@@│
                                  БАКТ Б                     └──────────────────────────┴──────────────────┘ │       ├────────────────────────┼────────────┤
                                                                                                         │ Номер │            инвентарный │@~@@@@@@@@@@│
             БО ПРИЕМЕ-ПЕРЕДАЧЕ ОБЪЕКТА ОСНОВНЫХ СРЕДСТВ Б                                                   │       ├────────────────────────┼────────────┤
                     Б(КРОМЕ ЗДАНИЙ, СООРУЖЕНИЙ) Б                                                           │       │              заводской │@~@@@@@@@@@@│
                                                                                                         └───────┼────────────────┬───────┼────────────┤
                                                                                                                 │Государственная │ номер │@~@@@@@@@@@@│
                                                                                                                 │регистрация прав├───────┼────────────┤
                                                                                                                 │на недвижимость │  дата │@~@@@@@@@@@@│
   Объект основных средств  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е└────────────────┴───────┴────────────┘
                                                 наименование, назначение, модель, марка

   _____________________________________________________________________________________________________________________________________________________

   Место нахождения объекта в момент приема-передачи  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д

   _____________________________________________________________________________________________________________________________________________________

   Организация-изготовитель  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                   наименование

   __________________________________________________________________________________________________________________________┌────────────┬────────────┐
                                                                                                                             │Доля в праве│            │
       Справочно: 1. Участники долевой собственности ________________________________________________________________________│общей собст-├────────────┤
                                                                                                                             │венности, % │            │
   __________________________________________________________________________________________________________________________└────────────┴────────────┘

                  2. Иностранная валюта <*> _________________________________ ___________________ __________________ ____________________________
                                                       наименование                   курс             на дату                   сумма

       --------------------------------
       <*> Заполняется в случае, когда стоимость объекта основных средств при приобретении была выражена в иностранной валюте.

                                                                                                                             2-я страница формы № ОС-1

.fields
  // Таблица 1
  iExcelFormat.DateToStrRep(Год_вып_fict, 'YYYY')   // 01
  iExcelFormat.DateToStrRep(Дата_платы)             // 02
                                                                                    // 03
  if (Новая_сумма_износа = 0 ,'', iExcelFormat.SrokIspEdIzmWord(Фактический_срок_эксплуатации) ) // 04
  if (Новая_сумма_износа = 0 ,'', iExcelFormat.SrokIspEdIzmWord(Срок_полезного_использования)  ) // 05
  iExcelFormat.DoubleToStrRep(Новая_сумма_износа)   // 06
  // [102.116978] если операция выбытия  и объект у организации-сдатчика,
  // то выводим остаточную сумму в любом случае  даже если сумма износа = 0
  if (  ((Новая_сумма_износа = 0)  and ( (Word(Тип_Операции) <> 4) or  (Word(Объект_у_Получателя) = 0) ) ),'',
    iExcelFormat.DoubleToStrrep(Полная_стоимость - Новая_сумма_износа)  ) // 07
  if (Новая_сумма_износа = 0 ,'', iExcelFormat.DoubleToStrRep(Стоимость_приобретения_договорная)      ) // 08
  // Таблица 2
  if (Не_заполнять_раздел_2_в_ОС1 or Стоимость_поступления_первоначальная = 0, '',
    iExcelFormat.DoubleToStrRep (Стоимость_поступления_первоначальная) )    // 01
  if (Не_заполнять_раздел_2_в_ОС1, '', iExcelFormat.SrokIspEdIzmWord(Срок_полезного_использования)) // 02
  if (Не_заполнять_раздел_2_в_ОС1, '', Наименование_алгоритма_начисления_амортизации ) // 03
  if (Не_заполнять_раздел_2_в_ОС1, '', string(Норма, 10, 6)                          ) // 04
.endfields
   1. Сведения о состоянии объекта основных средств на дату передачи                                   2. Сведения об объекте основных средств на дату
                                                                                                       принятия к бухгалтерскому учету
   ┌────────────────────────────┬─────────┬─────────┬───────────────┬───────────────┬───────────────╥───────────────┬─────────┬────────────────────────┐
   │            Дата            │Фактиче- │         │               │               │               ║ Первоначальная│         │   Способ начисления    │
   ├─────┬────────────┬─────────┤ский срок│  Срок   │     Сумма     │  Остаточная   │  Стоимость    ║ стоимость  на │  Срок   │      амортизации       │
   │ вы- │   ввода в  │последне-│ эксплу- │полезного│  начисленной  │  стоимость,   │ приобретения  ║дату принятия к│полезного├─────────────┬──────────┤
   │пуска│эксплуатацию│ го капи-│ атации  │ исполь- │  амортизации  │     руб.      │ (договорная   ║ бухгалтерскому│ исполь- │             │          │
   │(год)│  (первона- │тального │  (лет,  │ зования │   (износа),   │               │  стоимость),  ║    учету,     │ зования │ наименование│  норма   │
   │     │  чальная)  │ ремонта │месяцев) │         │      руб.     │               │    руб.       ║     руб.      │         │             │          │
   ├─────┼────────────┼─────────┼─────────┼─────────┼───────────────┼───────────────┼───────────────╫───────────────┼─────────┼─────────────┼──────────┤
   │  1  │      2     │    3    │    4    │    5    │       6       │       7       │       8       ║       1       │    2    │      3      │    4     │
   ├─────┼────────────┼─────────┼─────────┼─────────┼───────────────┼───────────────┼───────────────╫───────────────┼─────────┼─────────────┼──────────┤
   │@~@@@│@@@@@@@@@@@@│         │@@@@@@@@@│@@@@@@@@@│&&&&&&&&&&&&.&&│&&&&&&&&&&&&.&&│&&&&&&&&&&&&.&&║&&&&&&&&&&&&.&&│@@@@@@@@@│@@@@@@@@@@@@@│&&&.&&&&&&│
   └─────┴────────────┴─────────┴─────────┴─────────┴───────────────┴───────────────┴───────────────╨───────────────┴─────────┴─────────────┴──────────┘
.begin
  Хар1 := Хар1 + 1;
  Характеристика_Наименование_Объекта [Хар1] := string(НаименованиеОС);
  Характеристика_Количество_Объектов  [Хар1] := string(Кол_во,14,Разрядность_дробной_части);
end.
.{ CheckEnter Os_Oborot
.}
.{ CheckEnter Os_Leafs
.}
.{ CheckEnter Os_Sostav
.begin
  Хар1 := Хар1 + 1;
  Характеристика_Наименование_Объекта [Хар1] := string(Наименование_Составляющей);
  Характеристика_Количество_Объектов  [Хар1] := iExcelFormat.KolOsToStrRep(Кол_во_Составляющих);
end.
.}
.{ CheckEnter Os_NalDrag
.begin
  if (NameDrag <> '')
  {
    Хар2++;
    Характеристика_NameDrag     [Хар2] := NameDrag;
    Характеристика_NomenclaturN [Хар2] := NomenclaturN;
    Характеристика_N_Edin       [Хар2] := N_Edin;
    Характеристика_MassaDrag    [Хар2] := if(MassaDrag<>0,string(MassaDrag,18,12),'');
  }
end.
.}
.{ CheckEnter Os_AktUsl
.}

   3. Краткая индивидуальная характеристика объекта основных средств
   ┌─────────────────────────────────────────────────────────────────┬─────────────────────────────────────────────────────────────────────────────────┐
   │     Объект основных средств, приспособления, принадлежности     │           Содержание драгоценных материалов (металлов, камней и т.д.)           │
   ├──────────────────────────────────────────────────┬──────────────┼──────────────────────┬──────────────┬───────────┬────────────┬──────────────────┤
   │                    наименование                  │  количество  │    наименование      │номенклатурный│  единица  │ количество │       масса      │
   │                                                  │              │драгоценных материалов│    номер     │ измерения │            │                  │
   ├──────────────────────────────────────────────────┼──────────────┼──────────────────────┼──────────────┼───────────┼────────────┼──────────────────┤
   │                         1                        │       2      │          3           │      4       │     5     │     6      │         7        │
   ├──────────────────────────────────────────────────┼──────────────┼──────────────────────┼──────────────┼───────────┼────────────┼──────────────────┤
.begin
  if (Хар2 = 0)
    Хар2 := 1;

  ХарN := Хар1 + Хар2 - 1;
  Хар1 := 0;
end.
.{WHILE Хар1<ХарN
.begin
  Хар1++;
end.
.fields
  if(Хар1 > Хар2, Характеристика_Наименование_Объекта [Хар1 - Хар2 + 1], if(Хар1 = 1, Характеристика_Наименование_Объекта [1], ''))
  if(Хар1 > Хар2, Характеристика_Количество_Объектов  [Хар1 - Хар2 + 1], if(Хар1 = 1, Характеристика_Количество_Объектов  [1], ''))

  Характеристика_NameDrag     [Хар1]
  Характеристика_NomenclaturN [Хар1]
  Характеристика_N_Edin       [Хар1]
  Характеристика_MassaDrag    [Хар1]
.endfields
   │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&│@@@@@@@@@@@│            │&&&&&.&&&&&&&&&&&&│
.}
   └──────────────────────────────────────────────────┴──────────────┴──────────────────────┴──────────────┴───────────┴────────────┴──────────────────┘


.fields
   Характеристика
.endfields

   Другие характеристики

    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д


                                                                                                                             3-я страница формы № ОС-1

   Комиссия по приему-передаче

       Результат испытания на "____" __________________ 20____ г.

                                                       соответствует                        требуется
       Объект основных средств техническим условиям _______________________    Доработка ________________
                                                      не соответствует                     не требуется

   ________________________________________________________________________  ___________________________________________________________________________
                     указать, что не соответствует                                                  указать, что требуется

   ________________________________________________________________________  ___________________________________________________________________________

   ________________________________________________________________________  ___________________________________________________________________________

   Заключение комиссии: ________________________________________________________________________________________________________________________________

   _____________________________________________________________________________________________________________________________________________________


   Приложение. Техническая документация _____________________________

.fields
  Председатель_Должность
  Председатель_ФИО
.endfields
   Председатель комиссии  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д    __________________________      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                     должность                          подпись                                 расшифровка подписи

.{While (Номер_подписанта< Количество_Членов_комиссии) OR (Номер_подписанта< 2)
.begin
  Номер_подписанта:= Номер_подписанта+ 1;
end.
.fields
  if (Номер_подписанта= word(1), 'Члены комиссии:', '')
  if (Номер_подписанта<= Количество_Членов_комиссии, Массив_Член_комиссии_Должность [Номер_подписанта], '')
  if (Номер_подписанта<= Количество_Членов_комиссии, Массив_Член_комиссии_ФИО   [Номер_подписанта], '')
.endfields
   @@@@@@@@@@@@@@@        Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д    __________________________      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                     должность                          подпись                                 расшифровка подписи

.}
   Объект основных средств

.begin
  if (Доверенность_номер <> '') and (Longint(Дата_документа) <> 0) and (Доверенное_лицо_ФИО <> '')
    Задана_Доверенность := true;
  else
    Задана_Доверенность := false;
end.
.fields
  // Сдал
  МОЛ_Должность_Поставщика
  МОЛ_Поставщика

  // Принял
  if(Доверенное_лицо_ФИО = '', МОЛ_Должность_Получателя, Доверенное_лицо_должность)
  if(Доверенное_лицо_ФИО = '', МОЛ_Получателя, Доверенное_лицо_ФИО)

  // Сдал
  Табельный_номер_Поставщика

  // Дата под графой "Принял"
  if((Доверенное_лицо_ФИО = '') or (Longint(Накладная_дата) = 0), '', Day(Накладная_дата))
  if((Доверенное_лицо_ФИО = '') or (Longint(Накладная_дата) = 0), '', DateToStr(Накладная_дата, 'mon'))
  if((Доверенное_лицо_ФИО = '') or (Longint(Накладная_дата) = 0), '20____', Year(Накладная_дата))

  // Доверенность
  if(Задана_Доверенность, Day(Доверенность_дата_выдачи), '')
  if(Задана_Доверенность, DateToStr(Доверенность_дата_выдачи, 'mon'), '')
  if(Задана_Доверенность, DateToStr(Доверенность_дата_выдачи, 'YYYY'), '20____')
  if(Задана_Доверенность, Доверенность_номер, '')
  if(Задана_Доверенность, Получатель + ' ' + Доверенное_лицо_ФИО_дат_падеж, '')

  // Принял на ответственное хранение
  МОЛ_Должность_Получателя
  МОЛ_Получателя

  // Дата принятия на ответств. хранение
  if((Доверенное_лицо_ФИО = '') or (Longint(Дата_документа) = 0), '', Day(Дата_документа))
  if((Доверенное_лицо_ФИО = '') or (Longint(Дата_документа) = 0), '', DateToStr(Дата_документа, 'mon'))
  if((Доверенное_лицо_ФИО = '') or (Longint(Дата_документа) = 0), '20____', Year(Дата_документа))

  // Принял на ответственное хранение
  Табельный_номер_Получателя

  if (Тип_Операции <> 4, Номер_документа, '')
  if (Тип_Операции <> 4, iExcelFormat.DateToStrRep(Дата_документа), '')

  Главный_бухгалтер_Поставщика
  Главный_бухгалтер_Получателя
.endfields
   Сдал                                                                     │ Принял
    Д@@@@@@@@@@@@@@@@@@@@@ Д __________________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д│  Д@@@@@@@@@@@@@@@@@@@@ Д ____________________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
         должность            подпись             расшифровка подписи       │       должность            подпись               расшифровка подписи
                                                                            │
                                                        ┌──────────────────┐│
   "____" __________________ 20____ г.  Табельный номер │@~@@@@@@@@@@@@@@@@││ " Д@~@@ Д"  Д@~@@@@@@@@@@@@@@@@ Д @~@@@@ г.
                                                        └──────────────────┘│
                                                                            │ По доверенности от " Д@~@@ Д"  Д@~@@@@@@@@@@@@@@@@ Д @~@@@@ г.  №  Д@~@@@@@@@@@@@@@ Д,
                                                                            │
                                                                            │ выданной  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                                            │                    кем, кому (фамилия, имя, отчество)
                                                                            │
                                                                            │ Объект основных средств принял на ответственное хранение
                                                                            │
                                                                            │  Д@@@@@@@@@@@@@@@@@@@@ Д ____________________  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                                            │     должность                 подпись              расшифровка подписи
                                                                            │
                                                                            │                                                       ┌──────────────────┐
                                                                            │ " Д@~@@ Д"  Д@~@@@@@@@@@@@@@@@@ Д @~@@@@ г.   Табельный номер │@~@@@@@@@@@@@@@@@@│
                                                                            │                                                       └──────────────────┘
                                                                            │
                                                                            │                                     ┌─────────────────┬──────────────────┐
   Отметка бухгалтерии:                                                     │    Отметка бухгалтерии об открытии  │ Номер документа │ Дата составления │
   В инвентарной карточке (книге) учета объекта основных средств            │ инвентарной карточки учета объекта  ├─────────────────┼──────────────────┤
   выбытие отмечено                                                         │        основных средств или записи  │@~@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@@│
                                                                            │                в инвентарной книге  └─────────────────┴──────────────────┘
                                                                            │
   Главный бухгалтер _______________  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  │ Главный бухгалтер  ____________________  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                         подпись              расшифровка подписи           │                          подпись                     расшифровка подписи
                                                                            │


.}
.endform
