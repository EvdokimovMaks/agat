// =============================================================================
//                                                     (c)  корпорация Галактика
// Галактика 7.11 - Производственный контур
// Описание интерфейса-объекта "Общие функции поиска документов-норм"
// =============================================================================

#include oAlgInDocList.vih

#component "M_MnPlan"

//******************************************************************************
// Типы данных

//------------------------------------------------------------------------------
// Структура для задания параметров поиска документов-норм
#ifndef __Def_TFindNDParams__
#define __Def_TFindNDParams__
#doc
 Структура для задания параметров поиска документов-норм</brief> <br>
 Поля: <br>
   TypeRes          - Тип ресурса, для которого необходимо найти документ-норму: <br>
     toSpPlan    : Позиция производственного плана <br>
     toSpMnfZakaz: Позиция производственного заказа <br>
   cRes             - Ссылка на ресурс <br>
   KAUProducer      - КАУ изготовителя: <br>
     cgKau_KatOrg : Организация <br>
     cgKau_KatPodr: Подразделение <br>
   cProducer        - Ссылка на изготовителя <br>
   FlUseKPN_SrcOfDN - Флаг использования параметра КПН "Источник расчета потребностей": <br>
     0: Не использовать <br>
     1: Искать, при отсутствии - "Не рассчитывать потребность" <br>
     2: Искать, при отсутствии использовать значение поля DefaultSrcOfND <br>
   DefaultSrcOfND   - Источник расчета потребностей по умолчанию <br>
     0: Производственная спецификация <br>
     1: Маршрутная карта <br>
     2: Производственная спецификация и маршрутная карта <br>
     3: Не рассчитывать потребность <br>
     4: ПС с учетом изготовителя <br>
     5: МК с учетом изготовителя <br>
     6: ПС и МК с учетом изготовителя <br>
     7: Маршрутная карта и производственная спецификация <br>
     8: МК и ПС с учетом изготовителя <br>
   FlFindPosNDForTO - Флаг "Искать позицию документа-нормы для тех.операции" <br>
     0: Не искать <br>
     1: Искать <br>
   ClearDocList     - Очищать список результата перед поиском <br>
#end
Type TFindNDParams = record
  TypeRes         : word;    // Тип ресурса, для которого необходимо найти документ-норму:
                             //   toSpPlan     - Позиция производственного плана
                             //   toSpMnfZakaz - Позиция производственного заказа
  cRes            : comp;    // Ссылка на ресурс
  KAUProducer     : word;    // КАУ изготовителя:
                             //   cgKau_KatOrg  - Организация
                             //   cgKau_KatPodr - Подразделение
  cProducer       : comp;    // Ссылка на изготовителя
  FlUseKPN_SrcOfDN: word;    // Флаг использования параметра КПН "Источник расчета потребностей":
                             //   0 - Не использовать
                             //   1 - Искать, при отсутствии - "Не рассчитывать потребность"
                             //   2 - Искать, при отсутствии использовать значение поля DefaultSrcOfND
  DefaultSrcOfND  : word;    // Источник расчета потребностей по умолчанию
                             //   0 - Производственная спецификация
                             //   1 - Маршрутная карта
                             //   2 - Производственная спецификация и маршрутная карта
                             //   3 - Не рассчитывать потребность
                             //   4 - ПС с учетом изготовителя
                             //   5 - МК с учетом изготовителя
                             //   6 - ПС и МК с учетом изготовителя
                             //   7 - Маршрутная карта и производственная спецификация
                             //   8 - МК и ПС с учетом изготовителя
  FlFindPosNDForTO: word;    // Флаг "Искать позицию документа-нормы для тех.операции"
                             //   0 - Не искать
                             //   1 - Искать
  ClearDocList    : boolean; // Очищать список результата перед поиском
End;
#endif

//------------------------------------------------------------------------------
// Структура для хранения информации о найденном документе-норме
#ifndef __Def_TInfoFindND__
#define __Def_TInfoFindND__
#doc
 Структура для хранения информации о найденном документе-норме</brief> <br>
 Поля: <br>
   KAUNormDoc - КАУ документа-нормы: <br>
     cgKau_Hdr_PS  : Производственная спецификация <br>
     cgKau_KatMarsh: Маршрутная карта <br>
   cNormDoc   - Ссылка на документ-норму <br>
   cPosND     - Ссылка на позицию документа-нормы <br>
#end
Type TInfoFindND = record
  KAUNormDoc: word; // КАУ документа-нормы:
                    //   cgKau_Hdr_PS   - Производственная спецификация
                    //   cgKau_KatMarsh - Маршрутная карта
  cNormDoc  : comp; // Ссылка на документ-норму
  cPosND    : comp; // Ссылка на позицию документа-нормы

End;
#endif

//******************************************************************************
// ObjInterface oFindND_CF
#ifndef __Def_oFindND_CF__
#define __Def_oFindND_CF__
#doc
 Описание интерфейса-объекта "Общие функции поиска документов-норм"</brief>
#end
ObjInterface oFindND_CF;

//------------------------------------------------------------------------------
#doc
 Предварительная подготовка данных</brief> <br>
#end
Procedure Prepare;

//------------------------------------------------------------------------------
#doc
 Поиск документа-нормы</brief> <br>
 Параметры: <br>
   ardFindNDParams   - Структура для задания параметров поиска документов-норм <br>
   var ardInfoFindND - Структура для хранения информации о найденном документе-норме <br>
 Результат: <br>
   True - если успешно, иначе - False <br>
 <br>
 Краткое описание поиска документа-нормы для заданного ресурса. <br>
   Для ресурсов с типом toSpPlan, toSpMnfZakaz: <br>
     1) Ищется в аналитиках ресурса (снизу вверх, от позиции к шапке документа) аналитика с ролью "Работа". <br>
        Если найдена и КАУ = cgKau_Marsh_Sp, то возвращается соответствующая МК, иначе - переход к п.2. <br>
     2) Ищется в аналитиках ресурса (снизу вверх, от позиции к шапке документа) аналитика с ролью "Техмаршрут". <br>
        Если найдена и КАУ = cgKau_KatMarsh, то возвращается соответствующая МК, иначе - переход к п.3. <br>
     2) Ищется в аналитиках ресурса (снизу вверх, от позиции к шапке документа) аналитика с ролью "Производственная спецификация". <br>
        Если найдена и КАУ = cgKau_Hdr_PS, то возвращается соответствующая ПС, иначе - переход к п.4. <br>
     4) Определяется источник расчета потребности (в соответствии с параметрами
        ardFindNDParams.FlUseKPN_SrcOfDN и ardFindNDParams.DefaultSrcOfND),
        т.е. определяется значение для внутренней переменной "Источник расчета потребности",
        аналогичной параметру КПН "Источник расчета потребности" (в зависимости от вышеупомянутых
        в этом пункте параметров либо считывается из каталога КПН, либо используется значение по умолчанию
        из параметра ardFindNDParams.DefaultSrcOfND). <br>
        Если источник расчета потребности определяет поиск с учетом изготовителя, то: <br>
          в аналитиках ресурса (снизу вверх, от позиции к шапке документа) ищется аналитика с ролью "Изготовитель". <br>
          Если аналитика не найдена, то для изготовителя используются значения параметров
          ardFindNDParams.KAUProducer, ardFindNDParams.cProducer. <br>
          Если Изготовитель не определен, то значение переменной "Источник расчета потребности"
          считается таким, что поиск производится без учета изготовителя. <br>
     5) Поиск документа-нормы производится в параметрах документа, к которому относится
        ресурс, в зависимости от определенных в 4-м пункте переменных "Источник расчета потребности" и Изготовитель. <br>
        Если документ-норма не найден, то переход к п.6. <br>
     6) Поиск активного документа-нормы для Изготовителя (если для изготовителя не надо искать, то
        этот пункт пропускается и переход к п.7). <br>
     7) Поиск "активного" документа-нормы. <br>
 <br>
   Для ресурсов с типом toObjParam: <br>
     1) Документ-норма ищется на закладке "Способы изготовления". Если не найден, то переход к п.2. <br>
     2) Поиск "активного" документа-нормы. <br>
#end
Function GetNormDoc(ardFindNDParams: TFindNDParams; var ardInfoFindND: TInfoFindND): boolean;

//------------------------------------------------------------------------------
#doc
 Тестирование</brief>
#end
Procedure TestModule;

End; // ObjInterface oFindND_CF

//******************************************************************************
// Расширение объекта поиска источников норм
#doc
 Расширение объекта поиска источников норм</brief>
#end
ObjInterface oFindND_CF_Ext;

//------------------------------------------------------------------------------
#doc
 Возвращает кол-во в списке aNDList</brief> <br>
 Параметры: <br>
   aParams - Параметры поиска <br>
   aNDList - Список найденных источников норм <br>
 Результат: <br>
   Возвращает кол-во в списке aNDList <br>
#end
Function GetNormDocExt(aParams: TFindNDParams; aNDList: oAlgInDocList): longint;

End; // ObjInterface oFindND_CF_Ext

//******************************************************************************
// Реализации

// Реализация поиска в заказах
VipInterface iFindND_CF  implements oFindND_CF Licensed(Free);

// Реализация поиска для позиции входящих в накладных
VipInterface iFindND_SOA  implements oFindND_CF Licensed(Free);

// Реализация расширенного поиска в накладных
VipInterface iFindND_NGP implements oFindND_CF_Ext, oFindND_CF Licensed(Free);

// Реализация расширенного поиска в заказах
VipInterface iFindND_PZ implements oFindND_CF_Ext, oFindND_CF Licensed(Free);

#endif
