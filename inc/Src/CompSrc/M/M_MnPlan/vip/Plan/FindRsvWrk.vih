//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.1 - Производственный контур
// Объект для поиска записей журнала резервирования и производственных центров
//------------------------------------------------------------------------------

#ifdef ComponentVersion
#component "M_MnPlan"
#end

Type stParams = record
  TypePC  : Word;
  wStatus : Word;
  cParty  : Comp;
  dBeg    : Date;
  dEnd    : Date;
  wState  : Word;
  cPodr   : Comp;
  cSmena  : Comp;
  wMode   : Word;
  wNumPick: Word;
  cVarPl  : Comp;
  wFlag   : word;  //Если and 1 > 0, в случае работы ветки DSQL, в cCurPos будет записан SpMnPl
End;

Type stFindRsvWrk = Record
  Status : Word;
  State  : Word;
  Mode   : Word;
  Party  : Comp;
End;

// таблица ПЦ
Table Struct TmpWrk_
(
  cWrk    : Comp,
  cOwner  : comp
)
With Index
(
  TmpWrk_01 = cWrk + cOwner
);

// таблица позиций журнала
Table Struct TmpRsv_
(
  cRsv   : Comp,
  cCurPos: Comp,
  Kol    : Double
)
With Index
(
  TmpRsv_01 = cRsv,
  TmpRsv_02 = cCurPos + cRsv
);


#doc
 Описание интерфейса "Объект для поиска записей журнала резервирования и производственных центров"</brief>
#end
ObjInterface oFindRsvWrk;

//------------------------------------------------------------------------------
// Поразделение может выступать не только как ПЦ, но и как цех в оборудовании
// и цех у сотрудника
// Поиск ПЦ
// !!! вариант планирования не учитывается (пока)
// wMode and 1 - учитываем все нижестоящие подразделения
// wMode and 2 - учитываем вышестоящее подразделение
// wMode and 4 - учитываем всё оборудование, в котором есть найденные подразделения
// wMode and 8 - учитываем всех сотрудников, у которых есть найденные подразделения
Procedure FindWrkForPodr(cPodr: Comp; wMode: Word);

//------------------------------------------------------------------------------
// wDirect - предмет поиска
//         = 0 - поиск предыдущих операций на один уровень
//         = 1 - поиск следующих операций на один уровень
// wMode   - метод поиска
//         = 0 - по связям между записями журнала
//         = 1 - по маршрутной карте
//         = 2 - по маршрутной карте + партия
Function FindRsvOper(cRsvOper: Comp; wDirect, wMode: Word; wDebug: Word; strJour : stFindRsvWrk): Boolean;

//------------------------------------------------------------------------------
// чистит временную таблицу TmpRsv_
Function ClearAllFindRsvOper: Boolean;

//------------------------------------------------------------------------------
// Поиск RsvOper по различным параметрам - найденные записи сохраняются в таблицу TmpRsv_
// TypePC  - тип производственного центра (ПЦ)
// wStatus - статус записи журнала (чтобы не учитывался, передаем 10)
// cParty  - ссылка на партию журнала
// dBeg, dEnd - дата начала и окончания
// wState  - состояние записи журнала (чтобы не учитывалось, передаем 4096)
// cPodr   - ссылка на подразделение, по которому будет поиск ПЦ (если передается, то TypePC
//                                                                должен быть равен 0)
// cDoc    - ссылка на документ планирования
// cSmena  - номер смены (если 0, то не учитывается)
// wMode   - 0 - вхождение RsvOper.cRoleAn1, RsvOper.cRoleAn2 в dBeg, dBeg
//           1 - пересечение RsvOper.cRoleAn1, RsvOper.cRoleAn2 и dBeg, dBeg
Function FindRsvOperByParams(TypePC: Word; wStatus: Word; cParty: Comp; dBeg, dEnd: Date; wState: Word; cPodr: Comp; cDoc: Comp; cSmena: Comp; wMode: Word): Boolean;

// -----------------------------------------------------------------------------
// Поиск RsvOper по различным параметрам - найденные записи сохраняются в таблицу TmpRsv_
Function FindRsvOperBy4Param(cParty: Comp; cOper: Comp; cIzd: Comp; cPodr: Comp): Boolean;

//------------------------------------------------------------------------------
// Поиск контрольной операции для текущей
Function FindKontrOper(cCurMarsh_Sp: Comp): Comp;

end;

ObjInterface oFindRsvWrk1(oFindRsvWrk);

  Function FindRsvOperByParamsExt(ParamsNT : stParams):Boolean;
end;

ObjInterface oFindRsvWrk2(oFindRsvWrk1);

//------------------------------------------------------------------------------
// Процедура поиска ПЦ для подразделения cPodr
// подразделение может выступать не только как ПЦ, но и как цех в оборудовании
// и цех у сотрудника
// wMode and 1  - учитываем все нижестоящие подразделения
// wMode and 2  - учитываем вышестоящее подразделение
// wMode and 4 - учитываем всё оборудование, которое относится к найденным подразделениям (с учетом вышестоящих и нижестоящих)
// wMode and 8 - учитываем всех сотрудников, которые относится к найденным подразделениям (с учетом вышестоящих и нижестоящих)
// wMode and 16 - не очищаем таблицу TmpWrk_
// wMode and 32 - учитываем все группы оборудования, которые относятся к найденным подразделениям (с учетом вышестоящих и нижестоящих)
Procedure FindWrkForPodrExt(cPodr: Comp; cVarPlan: Comp; wMode: Word);

end;
VipInterface FindRsvWrk implements oFindRsvWrk2 #Licensed_Free;
