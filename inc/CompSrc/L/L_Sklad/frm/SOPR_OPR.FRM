! ╔═════════════════════════════════════════════════════════════════════════╗
! ║  Разработал:  Александр Крахотко                                        ║
! ║  Назначение:  Не оприходованые мц из накладной                          ║
! ╚═════════════════════════════════════════════════════════════════════════╝
#doc
Не оприходованые МЦ из накладной
#end
.Set Name = 'Sopr_opr'
.hide
.fields
  номер_н дата_н
!.{
  название
  баркод
  отпуск_ед
  партия_н
  кол_н : double
!.if sopr_opr_yes
!.{
    вид_о номер_о дата_о склад_о мол_о партия_о кол_о : double
!.}
  итого_кол_в_ордерах : double
!.else
!.end
  еще_не_распределенно : double
!.if sopr_opr_error
  ошибка
!.end
!.if sopr_opr_error_end
  общая_ошибка
!.end
!.}
  дата_оприходования
.endfields
  номер_н ^
  дата_н  ^
.{
  название       ^
  баркод         ^
  отпуск_ед      ^
  партия_н       ^
  кол_н          ^
.if sopr_opr_yes
.{
    вид_о     ^
    номер_о   ^
    дата_о    ^
    склад_о   ^
    мол_о     ^
    партия_о  ^
    кол_о     ^
.}
  итого_кол_в_ордерах   ^
.else
.end
  еще_не_распределенно  ^
.if sopr_opr_error
  ошибка                ^
.end
.}
.if sopr_opr_error_end
  общая_ошибка          ^
.end
дата_оприходования      ^
.endform

//------------------------------------------------------------------------------
#doc
Реестр МЦ по накладной МЦ
#end
.linkform 'sopr_opr_1' prototype is 'sopr_opr'
.nameInList 'Реестр МЦ по накладной МЦ'
#INCLUDE kolznak.frn
.fields
  CommonFormHeader
  номер_н дата_н
!.{
  название баркод отпуск_ед партия_н
  kolznakst(кол_н, 14)

!.if sopr_opr_yes
!.{
    номер_о дата_о
  kolznakst(кол_о, 14)
      if (length( склад_о) = 0,'','Склад:"' +SubStr(trim( склад_о),1,15)+'" ')+
      if (length(   мол_о) = 0,'','МОЛ:"'   +SubStr(trim(   мол_о),1,15)+'" ')+
      if (length(партия_о) = 0,'','Партия:"'+SubStr(trim(партия_о),1,15)+'" ')
!.}
  kolznakst(итого_кол_в_ордерах, 14)
!.else
!.end
  kolznakst(еще_не_распределенно, 14)
!.if sopr_opr_error
  ошибка
!.end
!.}
!.if sopr_opr_error_end
  общая_ошибка
!.end
   дата_оприходования
.endfields
^
  Накладная №  ^ от ^
────────────────────┬──────────────┬─────────┬────────────────────┬─────────────
 наименование МЦ    │бар код       │  ЕдИзм  │партия в накладной  │количество
────────────────────┴──────────────┴─────────┴────────────────────┴─────────────
.{

@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@ @@@@@@@@@ @@@@@@@@@@@@@@@@@@@@ ^
.if sopr_opr_yes
  ───────────┬──────────┬─────────────┬─────────────────────────────────────────
   Ном.Орд.  │ Дата     │ Количество  │ Разрез хранения (Склад/МОЛ/Партия)
  ───────────┴──────────┴─────────────┴─────────────────────────────────────────
.{
   @@@@@@@@@@ @@@@@@@@@@ ^ ^
.}
  кол-во в ордерах ^
.else
  НЕТ в ордерах    ^
.end
.if sopr_opr_error
  ***** ^
.end

.}
.if sopr_opr_error_end
------------------------------------------------------------------------------
  ^
.end

  Дата оприходования(списания) ^
.endform

//------------------------------------------------------------------------------
#doc
Краткий Реестр МЦ по накладной МЦ
#end
.linkform 'sopr_opr_2' prototype is 'sopr_opr'
.nameInList 'Краткий Реестр МЦ по накладной МЦ'
#INCLUDE kolznak.frn
.fields
  номер_н дата_н
!.{
!.if sopr_opr_yes
!.{
!.}
  название баркод
  kolznakst(кол_н, 14)
  kolznakst(итого_кол_в_ордерах, 14)
  kolznakst(еще_не_распределенно, 14)
!.else
  название баркод
  kolznakst(кол_н, 14)
  kolznakst(еще_не_распределенно, 14)
!.end

!.if sopr_opr_error
  ошибка
!.end
!.}
!.if sopr_opr_error_end
  общая_ошибка
!.end
  дата_оприходования
.endfields
  Накладная №  ^ от ^
────────────────────┬──────────────┬──────────────┬──────────────┬──────────────
 наименование МЦ    │бар код       │кол. в наклад.│кол. в ордерах│остаток
────────────────────┴──────────────┴──────────────┴──────────────┴──────────────
.{
.if sopr_opr_yes
.{
.}
@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@ ^ ^ ^
.else
@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@ ^                ^
.end
.if sopr_opr_error
  ***** ^
.end
.}
.if sopr_opr_error_end
------------------------------------------------------------------------------
  ^
.end

  Дата оприходования(списания) ^
.endform
