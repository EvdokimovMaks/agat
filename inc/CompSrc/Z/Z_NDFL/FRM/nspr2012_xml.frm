//-----------------------------------------------------------------------------
//                                                     (c) корпорация ГАЛАКТИКА
// Галактика 8.10 - заработная плата
// НДФЛ-2
//-----------------------------------------------------------------------------
#doc
НДФЛ-2
#end
.Linkform 'nspr2012_01_XML' prototype 'nspr2005'
.nameinlist 'Файл обмена в формате XML c 2012 года'
.F 'nsprX2005.XML'
.group '2012'
.var
 RДолг5_9:                double;
 RВзыск5_10:              double;
 Rгражданство:            string;
 RMes31:                  string;
 RMes32:                  string;
 RБуква_я:                string;
 RPrizSt, RPrizStConst:   string;
 RPrizImu:                string;
 Rдата_документа:         string;
 RТелефон_работодателя:   string;
 RНалог5_5:               double;
 boPrint, boPrint1,
 VichPrint:               boolean;
 wPar_PlPor:              word;
.endvar
.function TestCode(_code: string): boolean;
  begin
    Result := false;
     case _code of
     '103', '104', '105', '108', '109', '110', '111', '112', '113',//стандартные
     '311', '312', '318',                                          //имущественные
     '114', '115', '118', '119', '122', '123', '116', '117', '120', '121', '124','125', //детские
     '319': result := true;                                        //социальные
    end;
  end.
.begin
  RБуква_я := Буква_я
  Rдата_документа := datetostr(дата_документа,'DD.MM.YYYY')
  RТелефон_работодателя := string(Телефон_работодателя, 20);
  if ( not ReadMyDsk(wPar_PlPor, 'NDFL_wPar_PlPor', false) )
    wPar_PlPor := 1;
end.
.fields
ANSI2OEM(имя_файла)
.endFields
<?xml version="1.0" encoding="windows-1251"?>
<Файл ИдФайл="^" ВерсПрог="Галактика 8.10" ВерсФорм="5.02">
.fields
  ОКАТО_предприятия
  год_за_который_отчет
  Признак
  ИНН_работодателя
  КПП_работодателя
  ИНН_работодателя   //Для физического лица
.endfields
  <СвРекв ОКАТО="^" ОтчетГод="^" ПризнакФ = "^">
.{?INTERNAL; (nrec_ip = 0)
    <СвЮЛ ИННЮЛ="^" КПП="^"></СвЮЛ>
.}
.{?INTERNAL; (nrec_ip <> 0)
    <СвФЛ ИННФЛ="^"></СвФЛ>
.}
  </СвРекв>
.{ nspr2005_Work CheckEnter
.begin
//Хитрое условие определения гражданства. Скатал с бумажной версии
  Rгражданство :=  Гражданство;
  if (Rгражданство = '') and (Статус = '1')
    Rгражданство := '643'
end.
.fields
  Rдата_документа
  год_за_который_отчет
  Номер
  Признак
  код_ГНИ
  ОКАТО_предприятия
  RТелефон_работодателя
  Replace(название_работодателя, '"', '&quot;')
  ИНН_работодателя
  КПП_работодателя
  ИНН_работодателя
.endfields
  <Документ КНД="1151078" ДатаДок="^" ОтчетГод="^" НомСпр="^" Признак="^" КодНО="^">
    <СвНА ОКАТО="^"
.{?INTERNAL; (Trim(Телефон_работодателя) <> '')
    Тлф="^"
.}
    >
.{?INTERNAL; (nrec_ip = 0)
      <СвНАЮЛ НаимОрг="^" ИННЮЛ="^" КПП="^"></СвНАЮЛ>
.}
.{?INTERNAL; (nrec_ip <> 0)
      <СвНАФЛ ИННФЛ="^">
.fields
 Фамилия_ИП
 Имя_ИП
 Отчество_ИП
.endFields
        <ФИО Фамилия="^" Имя="^"
.{?INTERNAL; (Trim(Отчество_ИП) <> '')
        Отчество="^"
.}
        ></ФИО>
      </СвНАФЛ>
.}
    </СвНА>
.fields
  ИНН_налогоплательщика
  статус
  дата_рождения
  RГражданство
.endfields
    <ПолучДох
.{?internal; (Trim(ИНН_налогоплательщика) <> '')
      ИННФЛ="^"
.}
      Статус="^" ДатаРожд="^" Гражд="^">
.fields
  Фамилия_налогоплательщика
  Имя_налогоплательщика
  Отчество_налогоплательщика
  код_документа_удостоверяющего_личность
  СерияНомДок
  Индекс_РФ
  код_региона_РФ
  район_РФ
  город_РФ
  населенный_пункт_РФ
  улица_РФ
  дом_РФ
  корпус_РФ
  квартира_РФ
  Код_страны_прописки
  Адрес_прописки
.endFields
      <ФИО Фамилия="^" Имя="^"
.{?INTERNAL; (Trim(Отчество_налогоплательщика) <> '')
      Отчество="^"
.}
      ></ФИО>
      <УдЛичнФЛ КодУдЛичн="^" СерНомДок="^"></УдЛичнФЛ>
.{?INTERNAL; (trim(Индекс_РФ) <> '') or (trim(код_региона_РФ) <> '')
      <АдрМЖРФ
.{?INTERNAL; (trim(Индекс_РФ)<>'')
        Индекс="^"
.}
        КодРегион="^"
.{?INTERNAL; (trim(район_РФ)<>'')
        Район="^"
.}
.{?INTERNAL; (trim(город_РФ)<>'')
        Город="^"
.}
.{?INTERNAL; (trim(населенный_пункт_РФ)<>'')
        НаселПункт="^"
.}
.{?INTERNAL; (trim(улица_РФ)<>'')
        Улица="^"
.}
.{?INTERNAL; (trim(дом_РФ)<>'')
        Дом="^"
.}
.{?INTERNAL; (trim(корпус_РФ)<>'')
        Корпус="^"
.}
.{?INTERNAL; (trim(квартира_РФ)<>'')
        Кварт="^"
.}
        ></АдрМЖРФ>
.}
.{?INTERNAL; (trim(Индекс_РФ) = '') or (trim(код_региона_РФ) = '')
      <АдрИНО КодСтр="^" АдрТекст="^"></АдрИНО>
.}
    </ПолучДох>
.{
.fields
  Stavka
  RMes31    Доход31_код    Доход31_сум   Вычет31_код   Вычет31_сум
  RMes32    Доход32_код    Доход32_сум   Вычет32_код   Вычет32_сум
.endfields
   <СведДох  Ставка="^">
      <ДохВыч>
.{  nspr2005_Cycle13and30 CheckEnter
.begin
case Mes31 of
10, 11, 12:
{
  RMes31 := string(Mes31, 2);
}
else
{
  RMes31 := '0'+string(Mes31, 1);
}
end;
case Mes32 of
10, 11, 12:
{
  RMes32 := string(Mes32, 2);
}
else
{
  RMes32 := '0'+string(Mes32, 1);
}
end;
end.
.{?INTERNAL; (Double(Доход31_сум) <> 0)
        <СвСумДох Месяц="^" КодДоход="^" СумДоход="^">
.{?INTERNAL; (Double(Вычет31_сум) <> 0)
          <СвСумВыч КодВычет="^" СумВычет="^"/>
.}
        </СвСумДох>
.}
.{?INTERNAL; (Double(Доход32_сум) <> 0)
        <СвСумДох Месяц="^" КодДоход="^" СумДоход="^">
.{?INTERNAL; (Double(Вычет32_сум) <> 0)
          <СвСумВыч КодВычет="^" СумВычет="^"/>
.}
        </СвСумДох>
.}
.}
      </ДохВыч>
.fields
  Код_стандарт_вычета1  Сумма_стандарт_вычета1
  Код_стандарт_вычета2  Сумма_стандарт_вычета2
  Код_стандарт_вычета3  Сумма_стандарт_вычета3
  Код_стандарт_вычета4  Сумма_стандарт_вычета4
.endfields
.{ nspr2005_Stand CheckEnter
.begin
 boPrint  := false;
 boPrint1 := false;
 VichPrint:= false;
 if ((Double(Сумма_стандарт_вычета1) <> 0) and TestCode(Код_стандарт_вычета1))
   boPrint := true
 else
   if ((Double(Сумма_стандарт_вычета2) <> 0) and TestCode(Код_стандарт_вычета2))
     boPrint := true
     else
       if ((Double(Сумма_стандарт_вычета3) <> 0) and TestCode(Код_стандарт_вычета3))
         boPrint := true
           if ((Double(Сумма_стандарт_вычета4) <> 0) and TestCode(Код_стандарт_вычета4))
             boPrint := true;
end.
.{ nspr2005_SVychLoop CheckEnter
.{?INTERNAL; (boPrint = true) and (VichPrint = false);
       <НалВычССИ>
.begin
 boPrint1 := true;
 VichPrint:= true;
end.
.}
.{?INTERNAL; (Double(Сумма_стандарт_вычета1) <> 0) and TestCode(Код_стандарт_вычета1);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_стандарт_вычета2) <> 0) and TestCode(Код_стандарт_вычета2);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_стандарт_вычета3) <> 0) and TestCode(Код_стандарт_вычета3);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_стандарт_вычета4) <> 0) and TestCode(Код_стандарт_вычета4);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.}
.fields
  Номер_уведомления
  Дата_уведомления
  Выдано_уведомление
.endfields
.{?INTERNAL; (boPrint1 = true);
.{?INTERNAL; (trim(Номер_уведомления) <> '') or (trim(Дата_уведомления) <> '') or (trim(Выдано_уведомление) <> '')
        <УведИмущВыч НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведИмущВыч>
.}
      </НалВычССИ>
.}
.}
.begin
 RВзыск5_10 := 0;
 RДолг5_9   := 0;
 RНалог5_5  := 0;
 If word(Trim(год_за_который_отчет))>=2011
 { 
  // Графа 5.5 только для сумм налога с 2011 года.  
  If ( wPar_PlPor = 1 )
    RНалог5_5 := Double(НалогУпл)
  Else
    RНалог5_5 := Double(Налог5_3);   
 }

If  Trim (Не_работник)  <> '1'
{
   if  Double(Налог5_3)  > Double(Налог5_4)
     RВзыск5_10 :=  Double(Налог5_3) - Double(Налог5_4)
   else
     RДолг5_9 :=  Double(Налог5_4) - Double(Налог5_3);

!   RВзыск5_10 := Налог2620
}
else   // не работник предприятия
{
    if  Double(Налог5_3)  > Double(Налог5_4)
       RВзыск5_10 :=  Double(Налог5_3) - Double(Налог5_4)
}

end.
.fields
  Доход5_1
  Доход5_2
  Налог5_3
  Налог5_4
  RНалог5_5
  RДолг5_9
  RВзыск5_10
.endfields
      <СГДНалПер
        СумДохОбщ="^" НалБаза="^" НалИсчисл="^" НалУдерж="^"
.{?INTERNAL; word(год_за_который_отчет) >= 2011
        НалПеречисл="^"
.}
        НалУдержЛиш="^" НалНеУдерж="^"/>
    </СведДох>
.}
  </Документ>
.}
</Файл>
.endform
