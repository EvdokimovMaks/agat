//-----------------------------------------------------------------------------
//                                                     (c) корпорация ГАЛАКТИКА
// Галактика 9.10 - заработная плата
// Форма прототип для формирования справки 2-НДФЛ
//-----------------------------------------------------------------------------
#doc
НДФЛ-2
#end
.Linkform 'NDFL2_2015XML' prototype 'NDFL2_prototype'
.nameinlist 'Файл обмена в формате XML c 2015 года'
.F 'NdflX2015.XML'
.group 'Ndfl_2015'
.var 
 Долг1             : LongInt;
 Долг2             : LongInt; 
 СумНалУд          : LongInt; 
 boPrint, 
 VichPrint         : boolean;
.endvar
.fields
ANSI2OEM(Имя_файла)
.endFields
.{ Main_Cycle_2NDFL CheckEnter
<?xml version="1.0" encoding="windows-1251"?>
#ifdef Gal9_1
<Файл ИдФайл="^" ВерсПрог="Галактика 9.10" ВерсФорм="5.04">
#else
<Файл ИдФайл="^" ВерсПрог="Галактика 8.10" ВерсФорм="5.04">
#endif
.fields
 АббрОКТМОилиОКАТО
 ОКАТО
 Год
 Признак
.endfields
  <СвРекв ^="^" ОтчетГод="^" ПризнакФ = "^">
.{?INTERNAL; (ТипОрг = 0)
.fields
 ИНН
 КПП 
.endfields
    <СвЮЛ ИННЮЛ="^" КПП="^"></СвЮЛ>
.}
.{?INTERNAL; (ТипОрг <> 0)
.fields
 ИНН
.endfields
    <СвФЛ ИННФЛ="^"></СвФЛ>
.}
  </СвРекв>
.{ Person_Cycle_2NDFL CheckEnter
.fields  
  Дата
  Номер
  Год    
  Признак
  НомерКоррект
  ИФНС
  НалАгент
  ФамСост
  ИмяСост
  ОтчСост
  ДоверенностьСоставителя   
  Replace(ОргУпПредст, '"', '&quot;')
  ОКАТО  
  Телефон  
.endfields
  <Документ КНД="1151078" ДатаДок="^" НомСпр="^" ОтчетГод="^" Признак="^" НомерКорр="^" КодНО="^">
    <Подписант ПрПодп="^"
.{?INTERNAL; (НалАгент = 2 or ИНН <> '')
      <ФИО Фамилия="^" Имя="^" Отчество="^"></ФИО>
.}
.{?INTERNAL; (НалАгент = 2)
      <СвПред НаимДок="^" 
.{?INTERNAL; (ОргУпПредст <> '')
              НаимОрг="^"
.}
      </СвПред>
.}
    </Подписант>
    <СвНА ОКТМО="^"
.{?INTERNAL; (Trim(Телефон) <> '')
    Тлф="^"
.}
    >
.{?INTERNAL; (ТипОрг = 0)
.fields
  Replace(Организация, '"', '&quot;')
  ИНН
  КПП
.endfields
      <СвНАЮЛ НаимОрг="^" ИННЮЛ="^" КПП="^"></СвНАЮЛ>
.}
.{?INTERNAL; (ТипОрг <> 0)
.fields
  ИНН
  ФамРук
  ИмяРук
  ОтчРук
.endfields 
      <СвНАФЛ ИННФЛ="^">
	<ФИО Фамилия="^" Имя="^" Отчество="^"></ФИО>
      </СвНАФЛ>
.}
    </СвНА>
.fields  
  ИНН_налогоплательщика
  ИНН2_налогоплательщика
  Статус
  Дата_рождения
  Гражданство
.endfields
    <ПолучДох
.{?internal; (Trim(ИНН_налогоплательщика) <> '')
      ИННФЛ="^"
.}
.{?internal; (Trim(ИНН2_налогоплательщика) <> '')
      ИННИно="^"
.}
      Статус="^" ДатаРожд="^" Гражд="^">
.fields
  Фамилия
  Имя
  Отчество
  Код_документа
  Серия_номер_документа
  ИндексРФ
  КодРегионаРФ
  РайонРФ
  ГородРФ
  НасПунктРФ
  replace(УлицаРФ,'"','')
  replace(ДомРФ,'"','')
  replace(КорпусРФ,'"','')
  replace(КвартираРФ,'"','')
  replace(КодСтраныПроживания,'"','')
  replace(ПолныйАдрес,'"','')
.endFields
      <ФИО Фамилия="^" Имя="^"
.{?INTERNAL; (Trim(Отчество) <> '')
      Отчество="^"
.}
      ></ФИО>
      <УдЛичнФЛ КодУдЛичн="^" СерНомДок="^"></УдЛичнФЛ>
.{?INTERNAL; (trim(ИндексРФ) <> '') or (trim(КодРегионаРФ) <> '')
      <АдрМЖРФ
.{?INTERNAL; (trim(ИндексРФ)<>'')
        Индекс="^"
.}
        КодРегион="^"
.{?INTERNAL; (trim(РайонРФ)<>'')
        Район="^"
.}
.{?INTERNAL; (trim(ГородРФ)<>'')
        Город="^"
.}
.{?INTERNAL; (trim(НасПунктРФ)<>'')
        НаселПункт="^"
.}
.{?INTERNAL; (trim(УлицаРФ)<>'')
        Улица="^"
.}
.{?INTERNAL; (trim(ДомРФ)<>'')
        Дом="^"
.}
.{?INTERNAL; (trim(КорпусРФ)<>'')
        Корпус="^"
.}
.{?INTERNAL; (trim(КвартираРФ)<>'')
        Кварт="^"
.}
        ></АдрМЖРФ>
.}
.{?INTERNAL; (trim(ПолныйАдрес) <> '')
      <АдрИНО КодСтр="^" АдрТекст="^"></АдрИНО>
.}
    </ПолучДох>
.fields
 Ставка_Налога
 Месяц_Ст1 Код_дохода_Ст1 Сумма_дохода_Ст1 Код_Вычета_Ст1 Сумма_вычета_Ст1
 Месяц_Ст2 Код_дохода_Ст2 Сумма_дохода_Ст2 Код_Вычета_Ст2 Сумма_вычета_Ст2
.endfields
.{ Percent_Cycle_2NDFL CheckEnter
   <СведДох  Ставка="^">
      <ДохВыч>
.{ Incom_Cycle_2NDFL CheckEnter
.begin
case Месяц_Ст1 of
10, 11, 12:
{
  Месяц_Ст1 := string(Месяц_Ст1, 2);
}
else
{
  Месяц_Ст1 := '0'+string(Месяц_Ст1, 1);
}
end;
case Месяц_Ст2 of
10, 11, 12:
{
  Месяц_Ст2 := string(Месяц_Ст2, 2);
}
else
{
  Месяц_Ст2 := '0'+string(Месяц_Ст2, 1);
}
end;
end.
.{?INTERNAL; (Double(Сумма_дохода_Ст1) <> 0)
        <СвСумДох Месяц="^" КодДоход="^" СумДоход="^">
.{?INTERNAL; (Double(Сумма_вычета_Ст1) <> 0)
          <СвСумВыч КодВычет="^" СумВычет="^"/>
.}
        </СвСумДох>
.}
.{?INTERNAL; (Double(Сумма_дохода_Ст2) <> 0)
        <СвСумДох Месяц="^" КодДоход="^" СумДоход="^">
.{?INTERNAL; (Double(Сумма_вычета_Ст2) <> 0)
          <СвСумВыч КодВычет="^" СумВычет="^"/>
.}
        </СвСумДох>
.}
.}//Incom_Cycle_2NDFL CheckEnter
      </ДохВыч>
.fields
 Код_Вычета_1     Сумма_вычета_1
 Код_Вычета_2     Сумма_вычета_2
 Код_Вычета_3     Сумма_вычета_3
 Код_Вычета_4     Сумма_вычета_4
.endfields
.begin
 boPrint := false;
 VichPrint := false;
end.
.{ Discount_Cycle_2NDFL CheckEnter
.begin
 boPrint  := false;  
 if (Double(Сумма_вычета_1) <> 0)
   boPrint := true
 else
   if (Double(Сумма_вычета_2) <> 0) 
     boPrint := true
     else
       if (Double(Сумма_вычета_3) <> 0)
         boPrint := true
           else
             if (Double(Сумма_вычета_4) <> 0) 
               boPrint := true;
end.
.{?INTERNAL; (boPrint = true) and (VichPrint = false);
       <НалВычССИ>
.begin 
 VichPrint:= true;
end.
.}
.{?INTERNAL; (Double(Сумма_вычета_1) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_вычета_2) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_вычета_3) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.{?INTERNAL; (Double(Сумма_вычета_4) <> 0);
        <ПредВычССИ КодВычет="^" СумВычет="^"></ПредВычССИ>
.}
.}//Discount_Cycle_2NDFL CheckEnter
.fields
  НомерУведСоцВыч
  ДатаВыдачиСоцУвед
  КемВыданоСоцУвед
.endfields
.{ Info_DiscountSoc_Cycle_2NDFL CheckEnter
.{?INTERNAL; (trim(НомерУведСоцВыч) <> '') or (trim(ДатаВыдачиСоцУвед) <> '') or (trim(КемВыданоСоцУвед) <> '')
        <УведСоцВыч НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведСоцВыч>
.}
.}
.fields
  НомерУведИмВыч
  ДатаВыдачиУвед
  КемВыданоУвед
.endfields
.{ Info_Discount_Cycle_2NDFL CheckEnter
.{?INTERNAL; (trim(НомерУведИмВыч) <> '') or (trim(ДатаВыдачиУвед) <> '') or (trim(КемВыданоУвед) <> '')
        <УведИмущВыч НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведИмущВыч>
.}       
.}//Info_Discount_Cycle_2NDFL CheckEnter
.{?INTERNAL; (VichPrint = true);
       </НалВычССИ>
.}
.begin
 Долг1 := 0;
 Долг2 := 0;
 if ( Ставка_Налога = 13 )
   СумНалУд := СуммаНалогаУд-СуммаАвПлат
 else
   СумНалУд := СуммаНалогаУд;
 if ( СуммаНалогаИсч > СуммаНалогаУд )
   Долг2 := СуммаНалогаИсч - СуммаНалогаУд
 else
   if ( СуммаНалогаИсч < СуммаНалогаУд )
     Долг1 := СуммаНалогаУд  - СуммаНалогаИсч
end.
.fields
 СуммаДохода     
 НалБаза         
 СуммаНалогаИсч  
 СуммаАвПлат
 СумНалУд   
 СуммаНалогаПереч
 Долг1
 Долг2
.endfields
      <СГДНалПер
        СумДохОбщ="^" НалБаза="^" НалИсчисл="^" АвансПлатФикс="^" НалУдерж="^"
        НалПеречисл="^"
        НалУдержЛиш="^" НалНеУдерж="^"/>
    </СведДох>
.fields
  НомерУведАвПлатВыч   
  ДатаВыдачиУведАвПлат   
  КемВыданоУведАвПлат     
.endfields
.{ Info_DiscountAvPlat_Cycle_2NDFL CheckEnter
.{?INTERNAL; (trim(НомерУведАвПлатВыч) <> '') or (trim(ДатаВыдачиУведАвПлат) <> '') or (trim(КемВыданоУведАвПлат) <> '')
        <УведФиксПлат НомерУвед="^" ДатаУвед="^"  ИФНСУвед="^"></УведФиксПлат>
.}       
.}
.}
  </Документ>
.}
</Файл>
.}
.endform
