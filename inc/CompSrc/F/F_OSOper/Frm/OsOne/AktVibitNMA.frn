.linkform 'AktVibitNMA' prototype is 'OSONE'
.NameInList '[ВСМН] Акт о списании нематериальных активов'
.F 'NUL'
.group 'Выбытие НМА <<ВСМН>>'
.var
  KontactLico_Dolg : string;
  iCol, iRow, NPP : word;
  KontactLico_Dov : string;
  Direct_Name : string;
  Direct_Dolg : string;
  Direct_Dov  : string;
  iFeeSigners : FeeSigners;
  Массив_Член_комиссии_ФИО: ARRAY [1..2] of String;
  Массив_Член_комиссии_Должность: ARRAY [1..2] of String;
  Номер_подписанта: Word;
  iFirstRowFIO, iLastRowFIO, iFirstRowDolg, iLastRowDolg : integer;
  RegInfo : string;
  iLvar : iLvarToString;
  iLastHeadRow : integer;
.endvar
.function PrnComiss(_PosName, _Dolg, _FIO :string) : boolean;
begin

  if _FIO <> ''
  {
 //   xlInsertRange(-4121/*xlShiftDown*/ , iRow, 1,iRow+3, 12);
    xlSetCellStringValue(_PosName, iRow/*Row*/, 1/*Col*/,iRow ,1);

    //---------------- должность -------------------------
    xlMergeCells(iRow, 3, iRow, 4);
    xlAlignCells(-4152,iRow+1, 3 ,iRow+1 ,3);
    xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 3, iRow, 4);
    xlSetCellStringValue(_Dolg, iRow/*Row*/, 3/*Col*/,iRow ,3);
    if iFirstRowDolg = 0
      iFirstRowDolg := iRow;
    iLastRowDolg := iRow;
    xlMergeCells(iRow+1, 3, iRow+1, 4);
    xlSetCellStringValue('должность' , iRow+1, 3 ,iRow+1 ,3);
    xlSetFontSize(8, iRow+1, 3 ,iRow+1 ,3);
     //---------------- подпись ---------------------------
    xlMergeCells(iRow+1, 6, iRow+1, 7);
    xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 6, iRow, 7);
    xlSetCellStringValue('подпись' , iRow+1, 6 ,iRow+1 ,6);

    //---------------- ФИО -------------------------------
    xlMergeCells(iRow, 9, iRow, 10);
    xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 9, iRow, 10);
    xlSetCellStringValue(_FIO , iRow/*Row*/, 9/*Col*/,iRow ,10);
    if iFirstRowFIO = 0
      iFirstRowFIO := iRow;
    iLastRowFIO := iRow;
    xlMergeCells(iRow+1, 9, iRow+1, 10);
    xlSetCellStringValue('расшифровка подписи' , iRow+1, 9 ,iRow+1 ,10);
    XlSetFontSize(10, iRow, 3, iRow, 10);
    xlSetFontSize(8, iRow+1, 3 ,iRow+1 ,10);
    xlAlignCellsEx(/*xlCentr*/-4108,-4107, iRow, 3, iRow, 10);
    xlAlignCellsEx(/*xlCentr*/-4108,-4160, iRow+1, 3, iRow+1, 10);
    PrnComiss := true;
  }
end.
.{
.begin
  xlKillExcel;
  xlDisplayAlerts(false);
  xlCreateExcelWithTemplate(TranslatePath('%ClientStartPath%')+'\XLS\F_OSOper\AktVibitNMA.xlt',true);
end.


.begin
if (xlIsExcelValid)
{
  iRow := 13;

  var r : word;
  var s : string;
  r:=0;

  if iLvar.InitAllMemo(Comp(KatOSNrec), 3000, 0)
    while (iLVar.GetMemoStr(S))
    {
      r++;
      if r = 1
        xlSetCellStringValue(s, iRow, 3 ,iRow ,3);
      else
      {
        iRow++;
        xlInsertRange(-4121/*xlShiftDown*/ , iRow, 1,iRow, 12);
        xlMergeCells(iRow, 1, iRow, 6);
        xlFrameCells(xlBorderB, xlThin, 1, 1, iRow, 1, iRow,6);
        xlSetCellStringValue(s, iRow, 1 ,iRow ,1);
      }
    }

  iRow:=iRow+3;
  var barcode : string;
  barcode :=  Ean13ToEanGnivcTTF(GenerateBarCodeEx (coMoveOs, MoveOsNRec));
  xlSetCellStringValue(barcode, 2/*Row*/, 2/*Col*/,2 ,2);
  xlSetCellStringValue('АКТ № '+НОМЕР_ДОКУМЕНТА, 5, 3/*Col*/,5 ,3);
  xlSetCellStringValue(Поставщик,9,2,9,2);
  xlSetCellStringValue(Структурное_подразделение_Поставщика,11,3,11,3);
  xlSetCellStringValue(Структурное_подразделение_Поставщика,11,3,11,3);
  xlSetCellStringValue('назначенная приказом от '+ if (Day(Дата_приказа)<>0,String(Дата_приказа)+' г. ','')+'№ '+Номер_приказа,iRow,1,iRow,1);
  xlSetCellStringValue(String(ДАТА_ДОКУМЕНТА),12,10,12,10);
  xlSetCellStringValue(Поставщик_ОКПО,12,9,12,9);
  xlSetCellStringValue(ПРИЧИНА_СПИСАНИЯ,iRow+2,2,iRow+2,2);

  // Установить ширину заголовка таблицы
  iLastHeadRow := iRow+3;
  xlSetRowHeight(59, iRow+4/*Row*/, 10/*Col*/, iRow+4, 10);

  //спецификация
  iRow:=iRow+6;
  xlSetCellStringValue(''''+ИНВНОМЕР, iRow, 1/*Col*/,iRow ,1);
  xlSetCellStringValue(НАИМЕНОВАНИЕОС, iRow, 2/*Col*/,iRow ,2);
  xlSetCellStringValue(String(ДАТА_ПОСТ),iRow, 3/*Col*/,iRow ,3);
  xlMergeCells(iRow,4,iRow,6);
  xlSetCellNumberValue(СРОК_ПОЛЕЗНОГО_ИСПОЛЬЗОВАНИЯ,iRow, 4/*Col*/,iRow ,4);
  xlMergeCells(iRow,7,iRow,8);
  xlSetCellNumberValue (Полная_стоимость,iRow, 7/*Col*/,iRow ,7);
  xlSetCellNumberValue (СУММА_НАЧИСЛЕННОЙ_АМОРТИЗАЦИИ,iRow, 9/*Col*/,iRow ,9);
  xlSetCellNumberValue (Полная_стоимость-Новая_сумма_износа,iRow, 10/*Col*/,iRow ,10);

  xlAlignCells(/*xlRight*/-4152, iRow, 4, iRow, 12);
  xlAlignCellsEx(-4108,-4108,iRow,1,iRow,12);
  XlWrapText(iRow, 1,iRow, 12);
  XlSetFontSize(10,iRow, 1,iRow, 12);

  xlFrameCells(63,2,1,1, iRow, 1, iRow,10)
}
end.
.begin
  Номер_подписанта := 0;
end.
.{ CheckEnter FEESIGNERSMEMBER_OSONE
.begin
  Номер_подписанта := Номер_подписанта + 1;
  Массив_Член_комиссии_ФИО            [Номер_подписанта] := Член_комиссии_ФИО;
  Массив_Член_комиссии_Должность      [Номер_подписанта] := Член_комиссии_Должность;
  end.
.}


.{ CheckEnter FeeSignersResponsible_OSONE
.}


.{CheckEnter OS_OBOROT
.}

.{CheckEnter OS_LEAFS
.}


.{CheckEnter OS_SOSTAV
.}


.{CheckEnter OS_NALDRAG
.}


.{CheckEnter OS_AKTUSL
.}
.begin
// вывод подписантов
 // комиссия
  iRow := iRow+2;
  if Председатель_ФИО <> ''
  {
    PrnComiss('Председатель комиссии:', Председатель_Должность, Председатель_ФИО);
  }
  Номер_подписанта:=0;
 end.

.{while (Номер_подписанта< Количество_Членов_комиссии) OR (Номер_подписанта< 2)
.begin
  Номер_подписанта:= Номер_подписанта+ 1;
  iRow := iRow + 3;
  PrnComiss(if (Номер_подписанта= word(1), 'Члены комиссии:', ''),
            Массив_Член_комиссии_Должность [Номер_подписанта],
            Массив_Член_комиссии_ФИО   [Номер_подписанта]);
end.
.}
.begin
  iFeeSigners.InitFeeSigners(7306/*cgReport_Os*/);
  iFeeSigners.FindFeeSignersByNRec(Comp(cFeeSignersNRec));

  // Утверждающее лицо
  if (iFeeSigners.RestrictFeeByRole(2, 'UtvLico'))
    if (iFeeSigners.FeeSignersIsValid)
    {
      xlSetCellStringValue(iFeeSigners.GetSignerPosition(1), 4/*Row*/, 9/*Col*/,4 ,9);
      xlSetCellStringValue(iFeeSigners.GetSignerFIO(13), 7/*Row*/, 10/*Col*/,7 ,10);
    }

  // Бухгалтер
  iRow:= iRow + 3;
  xlSetCellStringValue('Отметка бухгалтерии:', iRow, 1, iRow ,1);
  iRow:= iRow + 1;
  xlSetCellStringValue('В карточке учета нематериального актива выбытие', iRow, 1, iRow ,1);
  iRow:= iRow + 1;
  xlSetCellStringValue('отмечено', iRow, 1, iRow ,1);
  iRow:= iRow + 2;

  if (iFeeSigners.RestrictFeeByRole(2, 'Bux')) and (iFeeSigners.FeeSignersIsValid)
    PrnComiss('', iFeeSigners.GetSignerPosition(1), iFeeSigners.GetSignerFIO(1));
  else
    PrnComiss('', ' ', ' ');

  iFeeSigners.ClearRestriction;
end.
.begin
  var fMacros : string;
  fmacros  := GetStringParameter('Files','TmpFilesDirectory',0)+'AktVibitNMA.bas';
  DeleteFile(Fmacros);
  LogStrToFile(Fmacros, 'Sub Sub1()                         ');

  if iFirstRowFIO <> 0
  {
    LogStrToFile(Fmacros, 'Dim Rng As Excel.Range             ');
    LogStrToFile(Fmacros, 'Dim Sh As Excel.Worksheet          ');
    LogStrToFile(Fmacros, 'Set Sh = Application.ActiveSheet   ');
    LogStrToFile(Fmacros, 'Set Rng = Sh.Range("C'+iFirstRowDolg+':C'+iLastRowDolg+'")');
    LogStrToFile(Fmacros, 'RngAutoFit Rng                     ');
  }

  LogStrToFile(Fmacros, 'Rows("1:'+iLastHeadRow+'").EntireRow.AutoFit    ');
  LogStrToFile(Fmacros, 'End Sub                            ');
  if ( xlImportModule(Fmacros) )
    {
      if not xlRunMacro('Sub1') 
        message('Ошибка запуска макроса AktVibitNMA');
    }
  else
    message ('Ошибка загрузки макроса AktVibitNMA.');

  DeleteFile(Fmacros);
  xlKillExcel;
end.

.} // Конец формы
.endform
