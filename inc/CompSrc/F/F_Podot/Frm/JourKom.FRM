//-----------------------------------------------------------------------------------
//                                                           (c) корпорация Галактика
// Галактика 5.80 - Касса
// Журнал учета работников, выбывающих в командировки
//-----------------------------------------------------------------------------------

#doc
Журнал учета работников, выбывающих в командировки.
#end
.set name='JourKom'
.hide
.fields
 НаимОрг
! цикл по сотрудникам
 PrikazNrec : comp
 НомерПП
 ФИОКоманд
 FIO_VP
 Должность
 ДатаПрик
 НомерПрик
 ДатаКомУдост
 НомерКомУдост
 ФИОДолжПриказодат
 ДатаВыбыт
 ДатаПрибыт
 ОргНазн
 МестоНазн
! цикл по этапам
 НомерППЭтап
 ОргНазнЭтап
 МестоНазнЭтап
.endfields

^
.{
^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
.{ CheckEnter JourKomEtap
^ ^ ^
.}
.}
.endform

!=============================================
.linkform 'JourKom01' prototype is 'JourKom'
!=============================================
.nameinlist 'Журнал учета работников, выбывающих в командировки РБ'
.Group 'Журнал учета работников'
.defo landscape
.fields
 CommonFormHeader
 НаимОрг:'P:C'
 НомерПП
 ФИОКоманд
 Должность
 ДатаПрик
 НомерПрик
 ФИОДолжПриказодат
 ДатаВыбыт
 ДатаПрибыт
.endfields
 И^
.{?Internal;(wGetTune('Country')=ccBel);
                                                                    Приложение 3
                                                                    к Инструкции о порядке и размерах возмещения расходов
                                                                    при служебных командировках
                                                                    в пределах Республики Беларусь
                                                                    от 4.02.2013 №4
.} И

                                                Б ЖУРНАЛ
                            учета работников, выбывающих в служебные командировки Б

 И                            из  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                ────────────────────────────────────────────────────────────────
                                                  (наименование организации) И

.{
.[h
 И ───┬───────────────────────────┬────────────┬───────────────────┬───────────────────────────┬─────────────────────────┬───────────────
    │          Фамилия,         │            │      Приказ       │      Фамилия, инициалы,   │          Дата           │    Расписка
    │ собственное имя, отчество │            │  (распоряжение)   │     должность работника,  │                         │командированного
  № │  (если таковое имеется)   │ Занимаемая │ о командировании  │        подписавшего       ├────────────┬────────────┤  работника в
 п/п│     командированного      │ должность  ├─────────┬─────────┤       командировочное     │            │            │   получении
    │         работника         │            │  дата   │  номер  │        удостоверение      │фактического│фактического│командировочного
    │                           │            │         │         │                           │  выбытия   │  прибытия  │ удостоверения
 ───┴───────────────────────────┴────────────┴─────────┴─────────┴───────────────────────────┴────────────┴────────────┴─────────────── И
.]h
 И@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@ @@@@@@@@@@@@ И
.{ CheckEnter JourKomEtap
.}
.}
.[f
 И ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────── И
.]f
.endform

!=============================================
.linkform 'JourKom02' prototype is 'JourKom'
!=============================================
.nameinlist 'Журнал учета работников, выбывающих в командировки'
.Group 'Журнал учета работников'
.defo landscape
.fields
 CommonFormHeader
 НаимОрг:'P:C'
!спецификация
 НомерПП
 ФИОКоманд
 '№ '+НомерКомУдост+' от '+ ДатаКомУдост
 ОргНазн
 МестоНазн
! этапы
 НомерПП+'.'+НомерППЭтап
 ОргНазнЭтап
 МестоНазнЭтап
.endfields
 И^
.{?Internal;(wGetTune('Country')=ccRus);
                                                                      Приложение № 2
                                                                      к Приказу
                                                                      Минздравсоцразвития России
                                                                      от 11 сентября 2009 г. № 739н
.} И

                                                  Б ЖУРНАЛ
                            учета работников, выбывающих в служебные командировки
                                       из командирующей организации Б

 И                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                 ────────────────────────────────────────────────────────────────
                                                   (наименование организации) И

.{
.[h
 И ──────┬────────────────────────────────┬─────────────────────────┬───────────────────────────────────────────────────────────────
       │     Фамилия, имя, отчество     │      Дата и номер       │                       Место командирования
   №   │        командированного        │    командировочного     ├────────────────────────────────┬──────────────────────────────
  п/п  │           работника            │      удостоверения      │    Наименование организации    │       Пункт назначения
 ──────┼────────────────────────────────┼─────────────────────────┼────────────────────────────────┼────────────────────────────── И
.]h
 И  @@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ И
.{  CheckEnter JourKomEtap
 И  @@@@@│                                │                         │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ И
.}
.}
.[f
 И ──────┴────────────────────────────────┴─────────────────────────┴────────────────────────────────┴────────────────────────────── И
.]f
.endform
