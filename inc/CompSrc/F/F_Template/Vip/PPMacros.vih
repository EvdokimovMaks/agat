//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 5.80 - модуль "Макросы назначения"
// Базовая функциональность Macros API
//------------------------------------------------------------------------------

#ifndef __PPMacros_VIH__
#define __PPMacros_VIH__

#ifdef ComponentVersion
  #Component "F_Template"
#end

// Префикс интерфейсов, выполняющих обработку макроопределений назначений платежа у платежных поручений.
#define PPMPrefix 'F_Template::PPM_'

#doc
 Объект реализующий шаблоны для формирования назначения платежа в ПП
#end

const
  // Значение фильтра по HozOper.Tidk, где хранятся описания шаблонов. Различные группы шаблонов (например, шаблоны
  // заполнения назначений платежа у платежных поручений, шаблоны заполнения примечания в ДО) разделяются по полю
  // HozOper.SysOper. Соответствующие значения для этого поля описаны нижеследующими константами ccTemplateGroup_*
  ccTemplateRecordId    = 776; // В качестве этого значения выбрано свободное (нигде более не используемое) значение HozOper.Tidk.

  ccTemplateGroup_PlPor = 0; //Группа шаблонов заполнения назначений платежа у платежных поручений
  ccTemplateGroup_DO    = 1; //Группа шаблонов заполнения примечания в ДО
end;

ObjInterface ObjPPTemplate;

  #doc
  Вызвать окно интерфейса выбора шаблона для назначения платежа ПП
  #end
  Function PicTempl(TiDkGal : word; var cTempl: comp): boolean;

  #doc
   CulcTempl - запустить алгоритм расчета и построить выгрузку
      cTempl - ссылка на номер шаблона в таблице HozOper
  #end
  Function CulcTempl(TiDkGal, TiDkUser: word; cSoprDoc, cTempl: comp; isSyntaxCheck: boolean): boolean;


  #doc
   возвращает результаты работы шаблона, num -- номер строки шаблона (начиная с 1)
  #end

  Function GetTemplResult(num: word): string;
  #doc
   возвращает кол-во параметров макроса
  #end

  Function ParamCount: word;
  #doc
   наименование параметра с номером Num
  #end

  Function ParamName(Num: word): string;
  #doc
   значение параметра с номером Num
  #end

  Function ParamValue(Num: word): string;

  #doc
   задает кол-во полей
  #end

  Procedure SetFieldCol(col: word);
  #doc
   задает длину поля под номером num
  #end

  Procedure SetFieldLen (num: word; len: word);
  #doc
   возвращает длину поля под номером num
  #end

  Function  GetFieldLen (num: word): word;
  #doc
   возвращает наименование шаблона
  #end

  Function  GetTemplName(cTempl: comp): string;
  #doc
   добавляет шаблон "Шаблон для обеспечения преемственности"
  #end

  Function  InitDefaultTempl(var cTempl: comp; var TemplName: string): boolean;
End;


#doc
  Абстрактный объект макроса шаблона
#end

ObjInterface ObjPPMacros;

  #doc
   GetInfo - получить описание алгоритма расчета, используется в интерфейсе выбора идентификаторов макросов
  #end
  Function GetInfo: string;

  #doc
   ParamMaster - вызвать мастер и получить список дополнительных параметров [Param1:"Value1"][Param2:"Value2"]
  #end
  Function ParamMaster: string;

  #doc
   Расчет макроса
  #end
  Function Culc(TiDkGal, TiDkUser: word; cSoprDoc: comp; PpmServer: ObjPPTemplate;
                isSyntaxCheck: boolean; var ErrDescr: string): string;
End;

#doc
  Дополнительные методы обработки
#end
ObjInterface ObjPPMacrosExt;

  #doc
   SetFilMethod - метод заполнения полей документа : 0 - заполняем строку в любом случае, 1 - только пустую
   (если метод не задается, то используется настройка для работы с различными типами документов : заполнять
    весь шаблон/только пустые строки) (вызывается после CulcTempl)
  #end
  Procedure SetFilMethod(wMethod: word);

  #doc
  FillWithResult - заполнить строку wFillStr значением из шаблона строкой номер wCountStr
  #end
  Procedure FillWithResult(wCountStr : word; var sFillStr : string);

end;

#doc
Расширение интерфейса ввиду добавления новых методов.
#end
ObjInterface ObjPPMacrosExt1(ObjPPMacrosExt);
  #doc
  Установить текущую группу шаблонов. В качестве группы используются константы ccTemplateGroup_*.
  #end
  Procedure SetTemplateGroup(_templateGroup: word);

  #doc
  Получить текущую группу шаблонов. Группы шаблонов описаны константами ccTemplateGroup_*.
  #end
  Function GetTemplateGroup: word;

  #doc
  Установить значения префикса интерфейсов, выполняющих обработку макроопределений.</brief>
  Например, для обработки макросов назначений платежа у платежных поручений используются интерфейсы,
  наименование которых должно начинаться с "F_Template::PPM_"
  #end
  Procedure SetMarcoIfcPrefix(_marcoIfcPrefix: string);

  #doc
  Получить текущее значение префикса интерфейсов, выполняющих обработку макроопределений.
  #end
  Function GetMarcoIfcPrefix: string;
end;

VipInterface PPT_GetTemplate implements ObjPPTemplate, ObjPPMacrosExt1
#ifdef ATL51
  Licensed (Free)
#end
;

#endif
