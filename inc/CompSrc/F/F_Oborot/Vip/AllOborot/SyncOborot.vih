// ----------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.1 - Бухгалтерский контур
// Объект синхронизации встроенных View и Edit оборотов
// ----------------------------------------------------------------------------

#ifndef SyncOborot_vih
#define SyncOborot_vih

#ifdef ComponentVersion
 #Component "F_Oborot"
#end

#include TDocument.vih  // Свойства документов и их спецификаций
#include PSManager.vih  // Работа с регистрами учета и планами счетов
#include CheckFocus.vih // Проверка фокуса ввода в контейнере (ObjCheckFocusInContainer)

#doc
Объект синхронизации встроенных View и Edit оборотов<br>
<i>Данный интерфейс надо использовать, только если необходимо разделить
просмотр и редактирование проводок по документу</i><br>
</brief><br>
Типовое использование данного интерфейса такое:
<pre class="example">
##include SyncOborot.vih

Interface ...

##include SyncOborot.var
!-------------------------------------------------------------------------------
! Описание формата для редактирования

Embedded embEditOborot Interface iSyncOborot.wnEdit;
 Show at(, 22);
End;

!-------------------------------------------------------------------------------
! Описание формата для просмотра (в примере - закладка)

Embedded embViewOborot 'Проводки' Interface iSyncOborot.wnView;
End;

cmInit : // Окна, в котором встроено редактирование
         // (SetParams заново НЕ нужен !!!)
{
  ...
  iSyncOborot.InitEdit;
  // дополнительный Synс не нужен !!!
}

!-------------------------------------------------------------------------------
! Везде в коде, где надо отрисовать встроенный Oborot

  ...
  iSyncOborot.Sync

cmInit : // Главного окна, в котором встроен просмотр
{
  ...
  iSyncOborot.SetParams(TDocument(Self), iPSManager, cgiOborot_DocOborot, cgiOborot_DocSpOborot);
  iSyncOborot.InitView;
}

!-------------------------------------------------------------------------------
! При окончании работы Обязательно освобождать обратные ссылки, иначе не
! выгрузятся интерфейсы

cmDone :
{
  ...
  iSyncOborot.FreeLinks;
}
!-------------------------------------------------------------------------------
</pre>
#end
ObjInterface ObjSyncOborot;

  #doc
  Установить основные параметры
  #end
  procedure SetParams (_Document : TDocument; _PS: PSManager; wViewBoundMode, vEditBoundMode : word);

  #doc
  Инициализация (отображение) панели просмотра оборотов
  #end
  Procedure InitView;

  #doc
  Инициализация (отображение) панели редактирования оборотов
  #end
  Procedure InitEdit;

  #doc
  Синхронизация и перерисовка панелей
  #end
  Procedure Sync;

  #doc
  Освободить обратные ссылки
  #end
  procedure FreeLinks;

  #doc
  Проверка прав доступа на редактирование документа
  #end
  Event Function EventCanEditDoc(awShowMessage: word; var awDocProtect: word): boolean;

  #doc
  Событие, генерируемое при передаче фокуса ввода контейнеру, если проводки редактировались
  #end
  Event Procedure EventAfterEdit;

End;

VipInterface SyncOborot implements ObjSyncOborot, ObjCheckFocusInContainer #Licensed_Free;

#end // SyncOborot_vih
