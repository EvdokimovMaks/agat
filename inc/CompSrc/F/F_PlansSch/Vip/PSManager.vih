//------------------------------------------------------------------------------
//                                                      (c) Корпорация ГАЛАКТИКА
// Галактика 8.1 - Бухгалтерский контур
// Работа с регистрами учета и планами счетов
//------------------------------------------------------------------------------

#ifndef __PSManager__vih__
#define __PSManager__vih__

#ifdef ComponentVersion
#Component "F_PlansSch"
#end

#doc
Работа с регистрами учета и планами счетов<br>
<pre>
-------------------------------------------------------------------------------
 Типовое использование данного интерфейса таково:

 ##include PSManager.vih

 Interface ...

 ##include PSManager.var
 var
   cPlansSch : tNRec;

 В случае, если интерфейс подписывается на событие EventChangePlansSch,
 он должен использовать НЕ-разделяемый экземпляр PSManager, для этого достаточно
 использовать другой var-файл:

 ##include PSManagerNew.var

-------------------------------------------------------------------------------

 Обработка переключения плана счетов в простом интерфейсе:
 ...
 [подготовительные операции, например UpdateTable]
 if (iPSManager.PickPlansSch(cPlansSch))
 {
   [отрисовка обновленных значений]
 }
 ...
 cmInit :
 {
   if (not iPSManager.GetTunePlansSch(cPlansSch))
   {
     Abort;
     Exit;
   }
   [далее по тексту]
 }

-------------------------------------------------------------------------------

 Обработка переключения плана счетов в случае необходимости одновременного
 переключения как в контейнере, так и во встроенных в него интерфейсах:
 ...
 cmPlansNo:
 {
   [подготовительные операции, например UpdateTable]
   iPSManager.PickPlansSch(cPlansSch);
 }
 ...
 Procedure onChangePlansSch(_cPlansSch: comp);
 {
   myPlansNo := _cPlansSch;
   [отрисовка обновленных значений]
 }
 ...
 cmInit :
 {
   if (not iPSManager.GetTunePlansSch(myPlansNo))
   {
     Abort;
     Exit;
   }
   BindEvent(onChangePlansSch, iPSManager.EventChangePlansSch);
   [далее по тексту]
 }
</pre>
#end
ObjInterface ObjPSManager;
  #doc
  Получить имя плана счетов
  #end
  function GetName (cPlansSch : tNRec) : string;

  #doc
  Получить код плана счетов
  #end
  function GetKod (cPlansSch : tNRec) : string;

  #doc
  Получить код регистра
  #end
  function GetKodReg (cPlansSch : tNRec) : word;

  #doc
  Запись настройки "Текущий план счетов"
  #end
  procedure SetTunePlansSch (cPlansSch : tNRec);

  #doc
  Получение настройки "Текущий план счетов"
  #end
  function GetTunePlansSch (var cPlansSch : tNRec) : boolean;

  #doc
  Выбор плана счетов, вернет True и новый план счетов
  #end
  function PickPlansSch (var cPlansSch : tNRec) : boolean;

  #doc
  Запись настройки "Текущий регистра учета"
  #end
  procedure SetTuneRegUch (cPlansSch : tNRec);

  #doc
  Получение настройки "Текущий регистра учета"
  #end
  function GetTuneRegUch (var cPlansSch : tNRec) : boolean;

  #doc
  Выбор регистра учета, вернет True и новый регистр - cPlansSch
  #end
  function PickRegUch   (var cPlansSch : tNRec) : boolean;

  //----------------------------------------------------------------------------
  // Пока не делаю, KatReg не умеет принимать код регистра пока
  //
  // Выбор регистра учета, вернет True и новый регистр - KodReg
  // function PickRegUch_KodReg (var KodReg : word) : boolean;

  #doc
  Послать оповещение "ВСЕМ" о смене "Текущего плана счетов"
  (используется при смене плана счетов для визуальной части)
  #end
  Procedure Run_Events (cPlansSch : tNRec);

  #doc
  Оповещение о смене регистра учета
  #end
  Event Procedure EventChangePlansSch(_cPlansSch: comp);

  #doc
  <pre>
  Отложенная подписка на событие EventChangePlansSch.
  Пояснение: Если сделать BindEvent для события EventChangePlansSch
  в обработчике этого же события но в другом интерфейсе, то Атлантис начнет плющить
  и он отвалится по рантайму. Для решения этой проблемы реализована "отложенная подписка",
  т.е. вслед за событием EventChangePlansSch посылается EventSubscribe, чтобы в последнем
  можно было подписаться на первое.
  Проявление (как было до использования EventSubscribe): В любом интерфейсе со встроенными
  оборотами переключиться на финпроводки, выйти из интерфейса, загрузить его снова
  и попытаться пееключиться на бухпроводки. В этот момент в обработчике EventChangePlansSch
  интерфейса AllViewOborot бует загружен ViewOborot и в нем выполнена подписка на EventChangePlansSch.
  После чего - рантайм.
  </pre>
  #end
  Event Procedure EventSubscribe;
end;

VipInterface PSManager implements ObjPSManager #Licensed_Free;

#end // __PSManager__vih__
