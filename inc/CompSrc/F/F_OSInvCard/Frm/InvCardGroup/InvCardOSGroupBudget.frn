.linkform 'InvCardOSGroupBudget' prototype is 'FormOS6a' // FormGroupOS6a
.NameInList 'Инвентарная карточка группового учета ОС (для бюджетных организаций) << Россия >>'
.group 'Бюджетная организация'
.hide
.defo landscape
.p 62
.var
  boFormOS6Amortization            : boolean;

  wOSSrokIspEdIzm                     : word // Равно значению настройки wGetTune('OS.SrokIspEdIzm'). Для того чтобы не читать каждый раз её из базы...

  // ***** Объявление вспомогательных переменных *****
  НомерИК                             : word; // Текущий номер элемента в цикле
  НомерИК_Max                         : word; // Количество ИК

  i, NN                               : word;
  КолНомер_в_ИК                       : word;

  // ***** Переменные общих полей *****
  Flag                                : boolean;
  Flag_Структурное_подразделение      : boolean;
  Flag_Наименование_объекта_полное    : boolean;
  Flag_Код_ОКОФ                       : boolean;
  Flag_Материально_ответственное_лицо : boolean;
  Flag_Табельный_номер                : boolean;
  Flag_Номер_амортизационной_группы   : boolean;
  Flag_Срок_полезного_использования   : boolean;
  Flag_Счет                           : boolean;

  // Наименование объекта (полное)
  // При печати простых инвентарных карточек по текущей сложной
  // Наименование берется из вышестоящего сложного
  Наименование_объекта_полное         : string;

  // ***** Объявление массивов *****
  МИК_Инвентарный_номер         : array[1..2] of string;
  МИК_Заводской_номер           : array[1..2] of string;
  МИК_Наименование              : array[1..2] of string;

  МИК_Количество                : array[1..2] of double; // Количество в конкретной ИК
  МИК_Поступления_дата          : array[1..2] of date;
  МИК_Поступления_сумма         : array[1..2] of double;
  МИК_КоличествоПереоценок      : array[1..2] of word;
  МИК_КоличествоАмортизаций     : array[1..2] of word;
  МИК_Выбытия_Выбыло            : array[1..2] of boolean;
  МИК_Выбытия_дата              : array[1..2] of date;

  МИК_Первоначальная_стоимость  : array[1..2] of double; // 1-2 Первоначальная стоимость на дату принятия к бухгалтерскому учету
  МИК_Стоимость                 : array[1..2] of double;
  МИК_Остаточная_стоимость      : array[1..2] of double;
  МИК_Цена_Единицу              : array[1..2] of double;

  МИК_Дата_Выпуска              : array[1..2] of date;
  МИК_Акт_Ввода                 : array[1..2] of string;  // Дата номер акта ввода в эксплуатацию
  МИК_Принятие_дата             : array[1..2] of date;
  МИК_Снятие_дата               : array[1..2] of date;
  МИК_СПИ                       : array[1..2] of string;

  МИК_Аморт_ДатаСтоп         : array[1..2] of date;
  МИК_Аморт_НормаМесяц       : array[1..2] of double;
  МИК_Аморт_НормаГод         : array[1..2] of double;
  МИК_Аморт_Сумма            : array[1..2] of double;
  МИК_Аморт_Метод            : array[1..2] of string;
  МИК_Аморт_ДатаНач          : array[1..2] of date;
  МИК_Аморт_ДатаКон          : array[1..2] of date;

  МИК_Поступление_дата       : array[1..2] of date;
  МИК_Поступление_Кол        : array[1..2] of double;
  МИК_Поступление_Сумма      : array[1..2] of double;

  МИК_Выбытие_дата           : array[1..2] of date;
  МИК_Выбытие_Кол            : array[1..2] of double;
  МИК_Выбытие_Сумма          : array[1..2] of double;

  МИК_Остаток_Кол            : array[1..2] of double;
  МИК_Остаток_Сумма          : array[1..2] of double;

  // ***** Цикл по операциям *****
  НомерОпер : word;
  МИК_КоличествоОпер   : array[1..2] of word;
  МО_Причина           : array[1..2] of string;
  МО_Номер             : array[1..2] of string;
  МО_Дата              : array[1..2] of date;
  МО_Сумма             : array[1..2] of double;

  Sum_Stoim, Sum_OstStoim, Sum_Amort : double;

.endvar
.begin
   // ***** Обнуление переменных *****
  НомерИК          := 0;
  НомерИК_Max      := 0;
  НомерОпер        := 0;
  КолНомер_в_ИК    := 0;

  wOSSrokIspEdIzm := wGetTune('OS.SrokIspEdIzm'); // в месяцах = 0, в годах = 1

  // ***** Обнуление переменных проверки отличий общих полей *****
  Flag                                := true;
  Flag_Структурное_подразделение      := true;
  Flag_Наименование_объекта_полное    := true;
  Flag_Код_ОКОФ                       := true;
  Flag_Материально_ответственное_лицо := true;
  Flag_Табельный_номер                := true;
  Flag_Номер_амортизационной_группы   := true;
  Flag_Срок_полезного_использования   := true;
  Flag_Счет                           := true;

  Наименование_объекта_полное         := '';

  // ***** Обнуление массивов по таблице *****
  for (i := 1; i <= Count(МИК_Количество); i++)
  {
    МИК_Инвентарный_номер         [i] := '';
    МИК_Заводской_номер           [i] := ''
    МИК_Дата_выпуска              [i] := date (0,0,0);
    МИК_Акт_Ввода                 [i] := '';

    МИК_Количество                [i] := 0;
    МИК_Наименование              [i] := '';
    МИК_Поступления_дата          [i] := date (0,0,0);
    МИК_Поступления_сумма         [i] := 0;
    МИК_Первоначальная_стоимость  [i] := 0;
    МИК_Выбытия_Выбыло            [i] := false;
    МИК_Выбытия_дата              [i] := date (0,0,0);
    МИК_Стоимость                 [i] := 0;
    МИК_Остаточная_стоимость      [i] := 0;
    МИК_СПИ                       [i] := '';

    МИК_Аморт_ДатаСтоп            [i] := date (0,0,0);
    МИК_Аморт_НормаМесяц          [i] := 0;
    МИК_Аморт_НормаГод            [i] := 0;
    МИК_Аморт_Сумма               [i] := 0;
    МИК_Аморт_метод               [i] := '';
    МИК_Аморт_ДатаНач             [i] := date (0,0,0);
    МИК_Аморт_ДатаКон             [i] := date (0,0,0);

    МИК_КоличествоОпер            [i] := 0;
  }

  for (i := 1; i <= Count(МО_Номер); i++)
  {
    МО_Причина[i]  := '';
    МО_Номер  [i]  := '';
    МО_Дата   [i]  := date(0,0,0)
    МО_Сумма  [i]  := 0;
  }

  Sum_Stoim := Sum_OstStoim := Sum_Amort :=0;
end.
.{
! ***** Заполнение массивов *****
.{ CheckEnter CycleByIK
.begin
  НомерИК++;
  НомерИК_Max++;

  // ***** Проверка отличий общих полей *****
  // Структурное подразделение
  if (Общее_Структурное_подразделение <> Структурное_подразделение)
  {
    Flag := false;
    Flag_Структурное_подразделение := false;
  }

  // Наименование объекта (полное)
  if (wTipPrint = 1)
    Наименование_объекта_полное := Наименование_вышестоящего_сложного;
  else
  {
    if (Наименование_объекта_полное = '')
      Наименование_объекта_полное := Наименование;
    else
    {
      if (Наименование_объекта_полное <> Наименование)
      {
        Flag_Наименование_объекта_полное := false;
        Наименование_объекта_полное := Наименование_объекта_полное + ', ' + Наименование;
      }
    }
  }

  // по ОКОФ
  if (Общий_Код_ОКОФ <> Код_ОКОФ)
  {
    Flag := false;
    Flag_Код_ОКОФ := false;
  }

  if (Общее_Материально_ответственное_лицо <> Материально_ответственное_лицо)
  {
    Flag := false;
    Flag_Материально_ответственное_лицо := false;
  }

  if (Общий_Табельный_номер <> Табельный_номер)
  {
    Flag := false;
    Flag_Табельный_номер := false;
  }

  // if (Общий_Номер_амортизационной_группы <> Номер_амортизационной_группы)
  //{
  //  Flag := false;
  //  Flag_Номер_амортизационной_группы := false;
  //}

  //if (Общий_Срок_полезного_использования <> Срок_полезного_использования)
  //{
  //  Flag := false;
  //  Flag_Срок_полезного_использования := false;
  //}

  if (Общий_Счет <> Счет)
  {
    Flag := false;
    Flag_Счет := false;
  }

  // ***** Заполнение массивов *****
  МИК_Инвентарный_номер        [НомерИК] := Инвентарный_номер;
  МИК_Заводской_номер          [НомерИК] := Заводской_номер;
  МИК_Наименование             [НомерИК] := Наименование;

  МИК_Дата_Выпуска             [НомерИК] := Дата_Выпуска;
  МИК_Акт_Ввода                [НомерИК] := iExcelFormat.DateToStrRep(Поступления_дата) + ' ' + Поступления_номер;
  МИК_Поступления_дата         [НомерИК] := Поступления_дата;
  МИК_Выбытия_Выбыло           [НомерИК] := Выбыло;
  МИК_Выбытия_дата             [НомерИК] := Выбытия_дата;

  МИК_Количество               [НомерИК] := Количество;
  МИК_Стоимость                [НомерИК] := Стоимость_ИК_за_единицу;
  МИК_Остаточная_стоимость     [НомерИК] := Остаточная_стоимость;

  МИК_Первоначальная_стоимость [НомерИК] := Первоначальная_стоимость;

  МИК_СПИ                       [НомерИК] := iExcelFormat.SrokIspEdIzmWord(Срок_полезного_использования);

  МИК_Аморт_ДатаСтоп            [НомерИК] := Дата_окончания_начисления;
  МИК_Аморт_НормаМесяц          [НомерИК] := Срок_полезного_использования / if(wOSSrokIspEdIzm = 0, 12, 1) / 12;
  МИК_Аморт_НормаГод            [НомерИК] := Срок_полезного_использования / if(wOSSrokIspEdIzm = 0, 12, 1);
  МИК_Аморт_Сумма               [НомерИК] := Сумма_Амортизации;
  МИК_Аморт_метод               [НомерИК] := Алгоритм_начисления_амортизации;
  МИК_Аморт_ДатаНач             [НомерИК] := Дата_Начало_Амортизации;
  МИК_Аморт_ДатаКон             [НомерИК] := Дата_Конец_Амортизации;

  Sum_Stoim += МИК_Стоимость [НомерИК] * МИК_Количество [НомерИК];
  Sum_Amort += МИК_Аморт_Сумма [НомерИК];
  Sum_OstStoim  += МИК_Остаточная_стоимость [НомерИК]
end.
.{ CheckEnter CycleByOverAppreciated
.}
.{ CheckEnter CycleByAmortization
.}
.{ CheckEnter CycleByOperation
.begin
   if (Операции_Тип = 3) OR (Операции_Тип = 6)
   {
     НомерОпер++;
     МИК_КоличествоОпер [НомерИК]++;

     МО_Причина[НомерОпер]  := '';
     МО_Номер  [НомерОпер]  := Операции_Основание + ' ' + Операции_Номер;
     МО_Дата   [НомерОпер]  := Операции_Дата;
     МО_Сумма  [НомерОпер]  := Стоимость_за_общее_количество;
   }
end.
.}
.}
! ***** Вывод сообщения о отличии общих полей *****
.begin
  if (not Flag_Наименование_объекта_полное and wTipPrint <> 1)
    Message ('   В отчет выводятся карточки, у которых отличаются наименования!!!'#13 +
             'В заголовке формы наименования будут перечислены через запятую.' , Information);

  if (not Flag)
    Message ('   Т.к. значения общих полей у карточек отличаются,'#13 +
             'нижеследующие общие поля не будут заполнены:'#13 +
             if (Flag_Код_ОКОФ                       , '', ' - Код ОКОФ'#13                                        ) +
             if (Flag_Структурное_подразделение      , '', ' - Структурное подразделение'#13                       ) +
             if (Flag_Материально_ответственное_лицо , '', ' - Материально ответственное лицо'#13                  ) +
             if (Flag_Табельный_номер                , '', ' - Табельный номер материально ответственного лица'#13 ) +
             if (Flag_Счет                           , '', ' - Счет'#13                                            ) +
             if (Flag_Срок_полезного_использования   , '', ' - Срок полезного использования'#13                    )
             , Information);
end.
! ***** Вывод в форму *****
.fields
  // ***** Общая информация *****

  '(Метод учета: ' + Метод_Учета + ')'

   iExcelFormat.DateToStrRep(Дата_составления)   // Дата открытия
   iExcelFormat.DateToStrRep(Дата_Выбытия)       // Дата закрытия

   Наименование_организации  // Учреждение
   ОКПО                      // по ОКПО

   if (Flag_Структурное_подразделение, Общее_Структурное_подразделение, '')  // Структурное подразделение
   ''

  // Наименование объекта (полное)
  //   Заполняется следующим образом:
  //     Если печатаем карточки по пометке, то если у всех карточек наименование одинаковое, то выводим его один раз.
  //     Если у карточек оно отличается, то сообщаем об этом и выводим все наименования через запятую...
  //     Если печатаем все простые по текущей сложной, то выводим наименование текущей сложной...
  Наименование_объекта_полное
  Общий_Код_ОКОФ   // по ОКОФ

  Вид_Оборудования   // Назначение объекта
  Код_Оборудования   // код

  Наименование_Изготовителя     // Организация-изготовитель
  ''                            // ВИД ОБЪЕКТА
  Общее_Структурное_подразделение
  Общее_Структурное_подразделение_Код

  if (Flag_Материально_ответственное_лицо, Общее_Материально_ответственное_лицо, '')  // Материально ответственное лицо
  if (Flag_Табельный_номер, Общий_Табельный_номер, '')


  // Срок_полезного_использования
  // if (Flag_Срок_полезного_использования, iExcelFormat.SrokIspEdIzmWord (Общий_Срок_полезного_использования), '')

  // Номер счета
  if (Flag_Счет, Общий_Счет, '')
.endfields
 Ш^
                                                                                                                                                                       Утверждена приказом Минфина России от 30.03.2015 N 52н
                ЩИНВЕНТАРНАЯ КАРТОЧКА ГРУППОВОГО УЧЕТА НЕФИНАНСОВЫХ АКТИВОВ № Щ

                                                                                                                                                                                                  ┌───────────────────────────┐
                                                                                                                                                                                                  │            КОДЫ           │
                                                                                                                                                                                                  ├───────────────────────────┤
                                                                                                                                                                                    Форма по ОКУД │          0504032          │
                                                                                                                                                                                                  ├───────────────────────────┤
                                                                                                                                                                                    Дата открытия │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
                                                                                                                                                                                    Дата закрытия │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Учреждение (бланкосодержатель) @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   по ОКПО │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Структурное подразделение @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Наименование объекта (полное) @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   по ОКОФ │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Назначение объекта @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Организация-изготовитель @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   по ОКПО │                           │
                                                                                                                                                                                                  ├───────────────────────────┤
   Вид объекта @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           │                           │
                                                                                                                                                                                                  ├───────────────────────────┤
   Местонахождение объекта (адрес) @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  Номер *  │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Материально ответственное лицо (при наличии) @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           │@~@@@@@@@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                  ├───────────────────────────┤
   Единица измерения: руб. (0,00)                                                                                                                                                        по ОКЕИ  │            383            │
                                                                                                                                                                                                  └───────────────────────────┘
                                                           ┌───────────────────────────────┐                                                                        Дата формирования         ┌───────────────────────────────┐
                                               Номер счета │@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│                                                                        карточки (по требованию)  │                               │
                                                           └───────────────────────────────┘                                                                                                  └───────────────────────────────┘
  Б1. Сведения об объектах Б
   ──────────────────────────────────────────────────────────────────┬───────────────────┬──────────────────────────────────┬─────────────────────┬──────────────┬─────────────────────────────────────────────────────────────
                       Номер объекта учета                           │                   │         Дата и номер акта        │                     │              │                           Дата
   ──────────────────────┬─────────────────────┬─────────────────────┤  Датв выпуска,    │              ввода в             │     Цена за         │ Количество   │───────────────────────────┬─────────────────────────────────
       инвентарный       │ реестровый номер    │   заводской         │  изготовления     │           эксплуатацию***        │     единицу         │  объектов    │    принятия к учету       │       снятия с учета
                         │   имущества**       │    (иной)           │                   │                                  │                     │              │                           │
   ──────────────────────┼─────────────────────┼─────────────────────┼───────────────────┼──────────────────────────────────┼─────────────────────┼──────────────┼───────────────────────────┼─────────────────────────────────
            1            │         2           │         3           │        4          │               5                  │          6          │      7       │            8              │               9
   ──────────────────────┼─────────────────────┼─────────────────────┼───────────────────┼──────────────────────────────────┼─────────────────────┼──────────────┼───────────────────────────┼─────────────────────────────────
.begin
  НомерИК := 1
end.
.{While (НомерИК <= НомерИК_Max)
.begin
  NN := 1;
end.
.fields
  // Таблица 1
  МИК_Инвентарный_номер[НомерИК]                          // 01
  МИК_Заводской_номер  [НомерИК]                          // 03
  iExcelFormat.DateToStrRep(МИК_Дата_Выпуска [НомерИК])   // 04
  МИК_Акт_Ввода        [НомерИК]                          // 05

  iExcelFormat.DoubleToStrRep(МИК_Стоимость [НомерИК])    // 06
  iExcelFormat.KolOsToStrRep(МИК_Количество [НомерИК])   // 07

  iExcelFormat.DateToStrRep(МИК_Поступления_дата [НомерИК]) // 08
  iExcelFormat.DateToStrRep(МИК_Выбытия_дата [НомерИК])     // 09
 .endfields
!  ──────────────────────┬─────────────────────┬─────────────────────┬───────────────────┬──────────────────────────────────┬─────────────────────┬──────────────┬───────────────────────────┬─────────────────────────────────
   @@@@@@@@@@@@@@@@@@@@@@│                     │@@@@@@@@@@@@@@@@@@@@@│    @@@@@@@@@@     │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&│        @@@@@@@@@@         │             @@@@@@@@@@
.begin
   НомерИК++;
end.
.}  // .{While (НомерИК <= НомерИК_Max)
   ──────────────────────┴─────────────────────┴─────────────────────┴───────────────────┴──────────────────────────────────┴─────────────────────┴──────────────┴───────────────────────────┴─────────────────────────────────

.fields
   DoubleToStr (Сумма_Амортизации_Объектов_Поступ, '\2p[|-]3666`666`666`666`667.88')
.endfields
  Б2. Стоимость объектов, изменение балансовой стоимости,  начисление амортизации Б
   ────────────────┬────────────┬────────────────────────────────────────────┬─────────────┬─────────┬───────────────────────────────────────────────────────────────────────────────────────┬───────────────────┬─────────────
                   │ Первона-   │   Изменение стоимости объекта              │Восстанови-  │         │            Амортизация                                     На дату принятия к учету   │&&&&&&&&&&&&&&&&&&&│
   Инвентарный     │  чальная   │───────┬────────────────────────┬───────────┤ тельная     │  Срок   │──────────┬────────────────┬───────────────┬───────────────────────────────────────────┴───────────────────┤ Остаточная
     номер         │ стоимость  │       │       документ         │           │(балансовая) │полезного│   дата   │     норма      │   сумма       │                   метод начисления                            │  стоимость
                   │  группы    │причина│─────────────┬──────────┤  сумма    │  стоимость  │использо-│   (год)  ├────────┬───────┤  начисленной  ├─────────────────────────────────────────┬──────────┬──────────┤
                   │  обектов   │       │Наименование,│  Дата    │           │             │ вания   │окончания │месячная│годовая│  амортизации  │             наименование                │  Начало  │окончание │
                   │            │       │  номер      │          │           │             │         │          │        │       │               │                                         │начисления│начисления│
   ────────────────┼────────────┼───────┼─────────────┼──────────┼───────────┼─────────────┼─────────┼──────────┼────────┼───────┼───────────────┼─────────────────────────────────────────┼──────────┼──────────┼─────────────
         1         │    2       │   3   │     4       │    5     │     6     │      7      │   8     │    9     │   10   │  11   │      12       │                 13                      │    14    │   15     │     16
   ────────────────┼────────────┼───────┼─────────────┼──────────┼───────────┼─────────────┼─────────┼──────────┼────────┼───────┼───────────────┼─────────────────────────────────────────┼──────────┼──────────┼─────────────
.begin
  НомерИК := 1
  НомерОпер := 0;
end.
.{While (НомерИК <= НомерИК_Max)
.begin
  NN := 1;
end.
.fields
  // * Таблица 2
  МИК_Инвентарный_номер [НомерИК]                                         // 01 Инвентарный номер
  iExcelFormat.DoubleToStrRep ( МИК_Первоначальная_стоимость [НомерИК] )  // 02

  if (NN <= МИК_КоличествоОпер [НомерИК],   МО_Номер [НомерОпер + NN], '')                               // 04
  if (NN <= МИК_КоличествоОпер [НомерИК],   iExcelFormat.DateToStrRep(МО_Дата  [НомерОпер + NN]), '')    // 05
  if (NN <= МИК_КоличествоОпер [НомерИК],   iExcelFormat.DoubleToStrRep(МО_Сумма [НомерОпер + NN]), '')  // 06

  iExcelFormat.DoubleToStrRep(МИК_Стоимость [НомерИК] * МИК_Количество [НомерИК])    // 07
  МИК_СПИ [НомерИК]                                                                  // 08

// * Амортизация (последняя дата начисления)
  iExcelFormat.DateToStrRep(МИК_Аморт_ДатаСтоп     [НомерИК])     // 09   (дата окончания начисления)
  iExcelFormat.DoubleToStrRep(МИК_Аморт_НормаМесяц [НомерИК], '6667.899999')     // 10
  iExcelFormat.DoubleToStrRep(МИК_Аморт_НормаГод   [НомерИК], '6667.899999')     // 11
  iExcelFormat.DoubleToStrRep(МИК_Аморт_Сумма      [НомерИК])       // 12
  МИК_Аморт_метод      [НомерИК]                                    // 13
  iExcelFormat.DateToStrRep(МИК_Аморт_ДатаНач     [НомерИК])        // 14
  iExcelFormat.DateToStrRep(МИК_Аморт_ДатаКон     [НомерИК])        // 15
  iExcelFormat.DoubleToStrRep(МИК_Остаточная_стоимость [НомерИК])   // 16
//  МИК_КоличествоОпер [НомерИК]
.endfields
!  ────────────────┼────────────┼───────┼─────────────┼──────────┼───────────┼─────────────┼─────────┼──────────┼────────┼───────┼───────────────┼─────────────────────────────────────────┼──────────┼──────────┼─────────────
   @@@@@@@@@@@@@@@@│&&&&&&&&&&&&│       │@@@@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&│&&&&&&&&&&&&&│&&&&&&&&&│@@@@@@@@@@│&&&&&&&&│&&&&&&&│&&&&&&&&&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&&&
! ***** Цикл по операциям *****
.{While ( NN < МИК_КоличествоОпер [НомерИК]) )
.begin
 NN++;
end.
.fields
   МО_Номер [НомерОпер + NN]                               // 04
   iExcelFormat.DateToStrRep(МО_Дата  [НомерОпер + NN])    // 05
   iExcelFormat.DoubleToStrRep(МО_Сумма [НомерОпер + NN])  // 06
.endfields
                   │            │       │@@@@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&│             │         │          │        │       │               │                                         │          │          │
.}   // {While ( NN < МИК_КоличествоОпер [НомерИК]) )
.begin
  НомерОпер  := НомерОпер + МИК_КоличествоОпер  [НомерИК];
  НомерИК++;
end.
.}   // .{While (НомерИК <= НомерИК_Max)
.fields
  iExcelFormat.DoubleToStrRep(Sum_Stoim)
  iExcelFormat.DoubleToStrRep(Sum_Amort)
  iExcelFormat.DoubleToStrRep(Sum_OstStoim)
.endfields
   ────────────────┴────────────┴───────┴─────────────┴──────────┴───────────┼─────────────┼─────────┼──────────┼────────┼───────┼───────────────┼─────────────────────────────────────────┼──────────┼──────────┼─────────────
                                                                       Итого │&&&&&&&&&&&&&│   X     │    X     │   X    │   X   │&&&&&&&&&&&&&&&│                   X                     │     X    │     X    │&&&&&&&&&&&&&
                                                                             └─────────────┴─────────┴──────────┴────────┴───────┴───────────────┴─────────────────────────────────────────┴──────────┴──────────┴─────────────

* При необходимости может быть указан кадастровый номер земельного участка, на котором  расположен объект основных средств (недвижимость).
**  По соответствующим уровням бюджетов (федеральный бюджет (РНФИ),  бюджет субъекта  Российской Федерации, муниципального образования).
***  По животным, многолетним насаждениям, по недвижимому и движимому имуществу, земельным участкам указывается соответственно  дата рождения, дата закладки, дата регистрации.

  Б 3. Движение объектов Б
   ──────────────────────────────────────────────┬───────────────────────────────────────────────────────────────╥───────────────────────────────────────────────────────────────╥─────────────────────────────────────────────
                     Инвентарный                 │                           Поступило                           ║                       Выбыло (передано)                       ║                   Остаток
                        номер                    ├────────────┬──────────────────────┬───────────────────────────╫────────────┬──────────────────────┬───────────────────────────╫────────────────┬────────────────────────────
                                                 │   дата     │            коли-     │           сумма,          ║   дата     │            коли-     │           сумма,          ║      коли-     │           сумма,
                                                 │            │           чество     │            руб            ║            │           чество     │            руб            ║     чество     │            руб
   ──────────────────────────────────────────────┼────────────┼──────────────────────┼───────────────────────────╫────────────┼──────────────────────┼───────────────────────────╫────────────────┼────────────────────────────
                       1                         │     2      │            3         │             4             ║     5      │         6            │             7             ║        8       │              9
   ──────────────────────────────────────────────┼────────────┼──────────────────────┼───────────────────────────╫────────────┼──────────────────────┼───────────────────────────╫────────────────┼────────────────────────────
.begin
  НомерИК := 1
end.
.{While (НомерИК <= НомерИК_Max)
.begin
  NN := 1;
end.
.fields
  // * Таблица 3
  МИК_Инвентарный_номер [НомерИК]      // 01 Инвентарный номер

  // * Поступило
  iExcelFormat.DateToStrRep(МИК_Поступления_дата [НомерИК])          // 02 - дата
  iExcelFormat.KolOsToStrRep(МИК_Количество [НомерИК])   // 03 - количество
  iExcelFormat.DoubleToStrRep(МИК_Первоначальная_стоимость [НомерИК] * МИК_Количество [НомерИК])  // 04 - сумма, руб

  // * Выбыло (передано)
  iExcelFormat.DateToStrRep(МИК_Выбытия_дата [НомерИК])                                              // 05 - дата
  if (МИК_Выбытия_Выбыло [НомерИК],
    iExcelFormat.KolOsToStrRep( МИК_Количество [НомерИК]), '')                                       // 06 - количество выбыло
  if (МИК_Выбытия_Выбыло [НомерИК],
    iExcelFormat.DoubleToStrRep(МИК_Остаточная_стоимость [НомерИК] * МИК_Количество [НомерИК]), '')  // 07 - сумма  выбыло

  // * Остаток
  if (МИК_Выбытия_Выбыло [НомерИК], '',
    iExcelFormat.KolOsToStrRep(МИК_Количество [НомерИК]) )                                          // 08 - количество
  if (МИК_Выбытия_Выбыло [НомерИК], '',
    iExcelFormat.DoubleToStrRep(МИК_Остаточная_стоимость [НомерИК] * МИК_Количество [НомерИК]) )    // 09 - сумма, руб
.endfields
    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@│&&&&&&&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&&&&&&&&&&&&║@~@@@@@@@@@@│&&&&&&&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&&&&&&&&&&&&║&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&&&&&&&&&&&&&
.begin
  НомерИК++;
end.
.}   // .{While (НомерИК <= НомерИК_Max)
   ──────────────────────────────────────────────┴────────────┴──────────────────────┴───────────────────────────╨────────────┴──────────────────────┴───────────────────────────╨────────────────┴────────────────────────────

  Б4. Дополнительные сведения об объектах Б
   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

.fields
  ЛОзаВК_Должность
  ЛОзаВК
.endfields
   Приложение. Документация на объекты основных средств (паспорт, свидетельство, чертеж, модель, тип, марка и др.) на  ___л. в ___ экз.

   Карточку заполнил @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    ______________________     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                              (должность)                           (подпись)                        (расшифровка подписи) Ш
 И
 И
.} // Конец формы
.endform
