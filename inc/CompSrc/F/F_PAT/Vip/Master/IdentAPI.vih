//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Бухгалтерский контур
// API идентификаторов
//------------------------------------------------------------------------------

#ifndef __IdentApi_vih__
#define __IdentApi_vih__

#include TxoApi.vih

#ifdef ComponentVersion
#Component "F_Pat"
#end

table struct IdentApiKeyList
(
  Key       : s80,    // идентификатор алгоритма, желательно русскими буквами
  KeyLength : word,   // длина идентификатора
  PluginIfc : s80,    // реализация алгоритма - интерфейс плагина
  PluginWnd : s80,    // реализация алгоритма - окно мастера параметров
  Caption   : s120    // описание идентификатора
)
with index
(
  IdentApiKeyList01 = Key,
  IdentApiKeyList02 = KeyLength(Desc)
);

Set Directory For Pascal "INCVIPLO"; // каталог для Паскаль - заготовок
create pascal "IdentApi.inc" for IdentApiKeyList;

#doc
API идентификаторов. Построитель списка ключей.
#end
objinterface ObjIdentApiBuildKeyList;
  #doc
  Построить список ключей в таблице IdentApiKeyList
  #end
  procedure BuildKeyList;
end;

vipinterface IdentApiBuildKeyList
  implements ObjIdentApiBuildKeyList
  #ifdef Atl51
  Licensed (Free)
  #end
;

#doc
API идентификаторов. Объект-регистратор ключей.
#end
objinterface ObjIdentApiPluginRegistrator;
  #doc
  Событие происходит кода регистратор добавляет ключ в список
  #end
  event procedure AddIdent( Key, Caption, PluginIfc, PluginWnd  : string );

  #doc
  Регистрация ключей
  #end
  procedure DoRegisterIdents;
end;

// to do TIdentApiContextInfo -  Context ContextP1 ContextP2 TiDkGal SysOper
// to do TIdentApiDocumentInfo -  cSopHoz cSoprDoc  переделать на функции GetInfoXXXXXbyHandle

#doc
API идентификаторов. Объектный интерфейс который должны реализовывать алгоритмы расчета ключей.
#end
objinterface ObjIdentApiPlugin;
  #doc
  Взаимодействие с окном мастера параметров <br>
  Событие приходит при изменении параметров ключа
  #end
  event procedure OnPluginWndParamChanged;

  #doc
  Взаимодействие с окном мастера параметров <br>
  Событие приходит при нажатии клавиши "ввод"
  #end
  event procedure OnPluginWndPressEnter;

  #doc
  Инициализировать окно парметров ключа
  #end
  function InitPluginWnd( TiDkGal, SysOper : word; Key : string ) : boolean;

  #doc
  генерация строки параметров [Param1:"Value1"][Param2:"Value2"]
  #end
  function ParamGen : string;

  #doc
  разбор массива параметров [Param1:"Value1"][Param2:"Value2"]
  (параметры уже предварительно разобраны калькулятором и переложены в массив )
  #end
  function ParamParser( hTxo : longint; TiDkGal, SysOper : word ) : boolean;

  #doc
  инициализация расчета -  срабатывает один раз при первом обращении к плагину
  во время разноске одного документа, закрытии одного счета, формирования одного FR отчета и т.п.
  #end
  procedure InitCalculation (hTxo : longint; buf : TTxoApiInfoDoc);

  #doc
  конкретный расчет конкретных параметров
  #end
  procedure MakeCalculation (hTxo : longint; buf : TTxoApiInfoDoc);

  #doc
  завершение расчета -  срабатывает один раз после завершения всех расчетов
  при разноске одного документа, закрытии одного счета, формирования одного FR отчета и т.п.
  #end
  procedure DoneCalculation (hTxo : longint; buf : TTxoApiInfoDoc);
end;

#endif // __IdentApi_vih__
