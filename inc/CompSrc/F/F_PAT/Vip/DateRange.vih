//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 5.82 - Бухгалтерский контур
// Обработка для параметра кодировки диапазона дат
//------------------------------------------------------------------------------

#ifndef __DATERANGE_VIH__
#define __DATERANGE_VIH__

#ifdef ComponentVersion
#Component "F_Pat"
#end

#include DateRange.vpp  // Тип для периода

#doc
Обработка для параметра кодировки диапазона дат.
</brief>
<table>
  <tr><th rowspan=2 valign=center>Тип диапазона<th colspan=6>Способ кодировки диапазона
  <tr><th>Текущий<th>Начало<br>текущего<th>Предыдущий<th>Cледующий<th>Конкретный<th>Внутри периода xx
  <tr><td>День     <td> <td>  <td>    <td>
      <td>DD.MM.YYYY<br>НачТек<br>КонТек<br>НачПер<br>КонПер<br>XозДок<br>Дnn
      <td>Дnn.Mxx.Kxx.Пxx.Гxx
  <tr><td>Месяц    <td>M<td>  <td>MПnn<td>MСnn<td>Mnn<td>Mnn.Kxx.Пxx.Гxx
  <tr><td>Квартал  <td>К<td>КН<td>КПnn<td>КСnn<td>Кnn<td>Knn.Пxx.Гxx
  <tr><td>Полугодие<td>П<td>ПН<td>ППnn<td>ПСnn<td>Пnn<td>Пnn.Гxx
  <tr><td>Год      <td>Г<td>ГН<td>ГПnn<td>ГСnn<td>Гnn<td>
  <tr><td colspan=7>Интервалы из любых типов диапазонов: A_B
</table>
Например:
<ul>
  <li>[Период:M] - текущий месяц
  <li>[Период:К1] - первый квартал текущего года
  <li>[Период:МП] - предыдущий месяц
  <li>[Период:МП2] - пред-предыдущий месяц
</ul>
#end
ObjInterface ObjDateRangeParam;

  #doc
  убрать все дополнительные пункты в полях со списком в мастере
  #end
  procedure ReSetList;

  #doc
  добавить дополнительный пункт (CodeValue, NameInList) в поле со списком SignValue в мастере (см.BuhConst.inc)
  #end
  procedure AddInList(SignValue : word; CodeValue : word; NameValue : string; NameInList : string);

  #doc
  запустить мастер для формрования Кода Диапазона
  #end
  function Pick(var RangeCode : string) : boolean;

  #doc
  значение буфера по умолчанию
  #end
  procedure GetDefaultPeriod(var MyPeriod : type$Period);

  #doc
  сфомировать Код Диапазона автоматически без вызова мастера
  #end
  function AutoCreate(MyIntervalSwitch : word; MyLeftPeriod, MyRightPeriod : type$Period; var RangeCode : string) : boolean;

  #doc
  сбросить все дополнительные значения параметров
  #end
  procedure ReSetValueParam;

  #doc
  передать границы дополнительного значения CodeValue одного из параметров SignValue (см.BuhConst.inc)
  #end
  procedure SetValueParam(SignValue : word; CodeValue : word; NameValue : string; BeginDate, EndDate : date);

  #doc
  проверка, является ли заданный период "относительным" (текущим, предыдущим, следующим)
  #end
  function IsRelativePeriod(RangeCode : string) : boolean;

  #doc
  запустить разбор Кода Диапазона, получить конкретные границы этого диапазона
  #end
  function Parse(RangeCode : string; Basis :date; var dBeg, dEnd : date) : boolean;

  #doc
  возвращает строку ошибки, если Parse возвратила false
  #end
  function GetErrorInfo : string;
end;

VipInterface DateRangeParam
  Implements ObjDateRangeParam
  #ifdef Atl51
  Licensed (Free)
  #end
;

#endif //__DATERANGE_VIH__
