/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Основные средства                                         ║
 ║ Версия        : 6.00                                                      ║
 ║ Назначение    : Каталог наличия драгметаллов по местам размещения         ║
 ║ Ответственный : Мурашко Сергей Владимирович                               ║
 ║ Параметры     : есть                                                      ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/
Interface NalDrag 'Драгметаллы', EscClose, Cyan, DoAccept;
  show at (10,2,70,19);
Create view Nal_Drag

var
  pNrec    : comp;
  myTiDk   : word;
  tmyTiDk  : byte;
  temp     : word;
(
  VidDrg,
  KatName,
  KatVid
)
as select
  if (NalDrag.VidDrag = 0, 'Паспорт', if (NalDrag.VidDrag = 1, 'Комиссионно', 'Справочник')),
  if (NalDrag.TiDk = 1, Katos.InNum+'/'+Katos.NameOs,
    if (NalDrag.TiDk = 2, KatMBP.NNumber+'/'+KatMBP.Name, KatMC.Name)),
  if (NalDrag.TiDk = 1, 'ОС', if (NalDrag.TiDk = 2, 'МБП', 'МЦ')),
  NalDrag.*, KatDrag.*, Katos.*, KatMBP.*, KatMC.*
from
  NalDrag,
  KatDrag,
  Katos,
  KatMBP,
  KatMC
where
((
  KatDrag.NRec     == Naldrag.cKatDrag and
  NalDrag.cKatos   == Katos.NRec       and
  NalDrag.cKatos   == KatMBP.NRec      and
  NalDrag.cKatos   == KatMC.NRec
))
order by KatDrag.Name
condition cnTiDk = if (myTiDk <> 0, ( myTiDk = NalDrag.TiDk ), True)
;

Parameters myTiDk;

Procedure WinTitle;
{ case myTiDk of
    0 : SetWindowTitle(wnMainWindow, 'Драгметаллы');
    1 : SetWindowTitle(wnMainWindow, 'Драгметаллы в ОС');
    2 : SetWindowTitle(wnMainWindow, 'Драгметаллы в cпецоснастке');
    3 : SetWindowTitle(wnMainWindow, 'Драгметаллы в МЦ');
  end; // case
} // procedure WinTitle

Window wConWin 'Установка ограничений' EscClose, Gray;
Show at (17,4,63,16);
Panel pConWin
Screen scrConWin
fields
  tmyTiDk ('',,sci1Esc) : noprotect;
buttons
  cmOk,default,,'Ввод ограничений',,sci1Esc;
  cmCancel,,,'Выход',,sci1Esc;
<<

   ┌── Показывать наличие драгметаллов ──┐
   │ (.) во всех объектах учета         `│
   │ (.) в основных средствах           `│
   │ (.) в cпецоснастке                 `│
   │ (.) в материальных ценностях       `│
   └─────────────────────────────────────┘

    <.~П~родолжить.>        <.~О~тмена.>

 >>
end; // screen tmyTiDk
end; // panel pConWin
HandleEvent
cmInit:
  { temp := myTiDk;
    tmyTiDk := byte(myTiDk);
    SelectField(#tmyTiDk);
  }
cmOk:
  { myTiDk := word(tmyTiDk);
    if (GetFirst KatDrag = tsOk)
      { CloseWindow(CurWindow);
        ReScanPanel(#NalDrag);
        ReScanPanel(#KatDrag);
        WinTitle;
      }
    else
      { Message(''#3'Нет записей удовлетворяющих заданному ограничению!',Information);
        myTiDk := temp;
        WinTitle;
      }
  }
end; // handleevent window wConWin
end; // window ConWin

Panel p1
show at (,,,6)
table KatDrag;
Browse vopbr
fields
  KatDrag.Kod  'Код'('Код драгметалла',hcDragEdit,sci1Esc):[3], protect;
  KatDrag.Name 'Наименование драгметалла'('Наименование драгметалла',hcDragEdit,sci1Esc):[51], protect;
end;
HandleEvent
cmPositionChanged:
  ReScanPanel(#NalDrag);
end;
end;

Panel p2
show at (,7,,)
Browse brKat table NalDrag
fields
  KatName ' Наименование '('Реквизиты объекта учета',,sci1Esc):[20], protect;
  NalDrag.Kol  'Вес драгметалла'('Вес драгметалла',,sci1Esc): [19.12], protect;
  VidDrg       'Вид'('Вид',,sci1Esc):[11],protect;
  KatVid 'Где'('Вид объекта учета',,sci1Esc):[3], protect;
end; // browse brKat
end; // panel p2

HandleEvent
cmInit:
  { WinTitle;
    PushCondition(tccnTiDk);
  }
cmFilterSave:
  RunWindow(wConWin);
end; // handleevent
End.
