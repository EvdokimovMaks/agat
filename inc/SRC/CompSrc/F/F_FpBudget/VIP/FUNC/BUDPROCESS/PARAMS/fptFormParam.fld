//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Управление бюджетом"
// Типовые формы
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
#include FpModeParam.Inc   // Константы идентификаторов режимов параметров
#include FpTFormParam.Inc  // Константы идентификаторов параметров типа "Типовые формы"
#include FpITune.Mac       // Макросы для чтения настроек интерфейсов
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
function GetTblTForm(_cTune : comp): boolean;
{
  GetTblTForm := False;

  delete all tblTForm;
  if (not pObjITune.GetTblBoxRec(_cTune, Tbl_tblTForm) )
  {Exit;}
  while GetFirst FastFirstRow BoxRec = tsOk
  {
    do
    {
      if (BoxRec.cRec = 0)  { Break; }
      var cSavedRec : comp; cSavedRec := BoxRec.cRec;
      ClearBuffer(#tblTForm);

      #GetTblFld(Comp,Tbl_tblTForm,cSavedRec,TblVar__tblTForm_cRec,tblTForm.cRec)
      #GetTblFld(LongInt,Tbl_tblTForm,cSavedRec,TblVar__tblTForm_fOrder,tblTForm.fOrder)

      var tsRes : word;  tsRes := insert current tblTForm;
      if (tsRes <> tsOk)
      { FpLog('GetTblTForm : (insert current tblTForm <> tsOk)'+string(tsRes)); }

    }
    while False;

    var cBoxRec : comp; cBoxRec := BoxRec.cRec;
    delete all BoxRec where ((cBoxRec == BoxRec.cRec));
  } //while GetFirst FastFirstRow BoxRec

  GetTblTForm := True;
}

//------------------------------------------------------------------------------
function SetTblTForm(_cTune : comp): boolean;
{
  SetTblTForm := False;
  PushPos(#tblTForm);
  pObjITune.DelTbl(_cTune, Tbl_tblTForm);
  _Loop tblTForm
  {
    var cSavedRec : comp; cSavedRec := tblTForm.cRec;
    #SetTblFld(Comp,Tbl_tblTForm,cSavedRec,TblVar__tblTForm_cRec, tblTForm.cRec)
    #SetTblFld(LongInt,Tbl_tblTForm,cSavedRec,TblVar__tblTForm_fOrder,tblTForm.fOrder)
  }
  PopPos(#tblTForm);
  SetTblTForm := True;
}

//------------------------------------------------------------------------------
function Read_ITune(_cTune : comp; _wEntity : word) : boolean;
{
  Read_ITune := False;

  pObjITune.InitEnt(_wEntity, _cTune);

  GetTblTForm(_cTune);
  #GetFld(Comp,   Var_cCurTForm , cCurTForm )
  #GetFld(word,Var_w1Mode, w1Mode )
  #GetFld(word,Var_wNMode, wNMode )
  #GetFld(word,Var_wParent1, wParent1 )
  #GetFld(word,Var_wParentN, wParentN )
  SetMode1TForm(w1Mode);
  SetModeNTForm(wNMode);
  SetParent1TForm(wParent1);
  SetParentNTForm(wParentN);
  Set1TForm(cCurTForm);
  CheckFieldState;

  pObjITune.DoneEnt(_wEntity, _cTune);

  Read_ITune := True;
}

//------------------------------------------------------------------------------
function Save_ITune(_cTune : comp; _wEntity : word) : boolean;
{
  Save_ITune := False;

  pObjITune.InitEnt(_wEntity, _cTune);

  #SetFld(Comp, Var_cCurTForm, Get1TForm )
  #SetFld(word, Var_w1Mode, GetMode1TForm)
  #SetFld(word, Var_wNMode, GetModeNTForm)
  #SetFld(word, Var_wParent1, GetParent1TForm)
  #SetFld(word, Var_wParentN, GetParentNTForm)


  SetTblTForm(_cTune);

  pObjITune.DoneEnt(_wEntity, _cTune);

  Save_ITune := True;
}

//------------------------------------------------------------------------------
function Del_ITune(_cTune : comp; _wEntity : word) : boolean;
{ Del_ITune := pObjITune.DelEnt(_wEntity, _cTune);
}
