//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Управление бюджетом"
// Визуальный интерфейс : "Копирование бюджетов"
//------------------------------------------------------------------------------


Const
  Var_cIndToApply     = 'cInd';
  Var_cTFormToApply   = 'cTF' ;
  Var_wPRelMode       = 'PRlM';

  Var_wFixMode        = 'FixM';
  Var_wTFormMode      = 'TFM' ;
  Var_wFormulaFixMode = 'TFFM';

  Tbl_ListBudget  = 'TBud';

  TblVar__ListBudget_fOrder          = 'LBOrd';
  TblVar__ListBudget_cBudget         = 'LBB';
  TblVar__ListBudget_cBudVar         = 'LBBV';
  TblVar__ListBudget_wBitReg         = 'LBRg';
  TblVar__ListBudget_cCO             = 'LBCO';
  TblVar__ListBudget_cPeriod         = 'LBPrd';
  TblVar__ListBudget_cBudVariant     = 'LBVar';
  TblVar__ListBudget_sBudStage       = 'LBStg';

  TblVar__ListBudget_cSlaveBudget    = 'LBSB';
  TblVar__ListBudget_cSlaveBudVar    = 'LBSBV';
  TblVar__ListBudget_wSlaveBitReg    = 'LBSRg';
  TblVar__ListBudget_wSlaveDelMode   = 'LBSDl';
  TblVar__ListBudget_cSlaveCO        = 'LBSCO';
  TblVar__ListBudget_cSlavePeriod    = 'LBSPr';
  TblVar__ListBudget_cSlaveBudVariant= 'LBSVr';
  TblVar__ListBudget_sSlaveBudStage  = 'LBSBS';

end;

//------------------------------------------------------------------------------
#include FpListParam.Inc   // Константы идентификаторов параметров типа "Список бюджетов"
#include FpITune.Mac       // Макросы для чтения настроек интерфейсов
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// GetTBL
//------------------------------------------------------------------------------
function MyGetTblListBudget(_cTune : comp) : boolean;
{
  MyGetTblListBudget := False;

  delete all ListBudget where ((fListBudgetID == ListBudget.ID));
  if (not pObjITune.GetTblBoxRec(_cTune, Tbl_ListBudget) )
  {Exit;}

  while GetFirst FastFirstRow BoxRec = tsOk
  {
    do
    {
      if (BoxRec.cRec = 0)  { Break; }
      var cSavedRec : comp;  cSavedRec := BoxRec.cRec;
      ClearBuffer(#ListBudget);
      ListBudget.ID   := GetListBudgetID;

      #GetTblFld(LongInt,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_fOrder, ListBudget.fOrder)

      //////////////////////////////////////////////////////////////////////////
      // БЮДЖЕТ ИСТОЧНИК

      #GetTblFld(Word,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_wBitReg,ListBudget.wBitReg)

      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cCO,ListBudget.cCO)
      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cPeriod,ListBudget.cPeriod)
      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cBudVariant,ListBudget.cBudVariant)
      #GetTblFld(string,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_sBudStage,ListBudget.sBudStage)

      // для того чтобы определить что это уже новые настройки
      var cTmpBudget : comp;  cTmpBudget := 0;
      var cTmpBudVar : comp;  cTmpBudVar := 0;
      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cBudget,cTmpBudget)
      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cBudVar,cTmpBudVar)
      var isCurNewParam : boolean;  isCurNewParam := (cTmpBudget = 0) and (cTmpBudVar = 0);

      // определение бюджета
      if (not isCurNewParam)
      { // возможно это первый запуск после использования старого
        ListBudget.cBudget := cTmpBudget;
        if (ListBudget.cBudget <> 0 )
        {
          ListBudget.cCO     := pExtFpBudget.GetCO    (ListBudget.cBudget);
          ListBudget.cPeriod := pExtFpBudget.GetPeriod(ListBudget.cBudget);
        }
      }
      else if (ListBudget.cCO <> 0) and (ListBudget.cPeriod <> 0)
      { // определим бюджет
        ListBudget.cBudget := pExtFpBudget.FindBudget(ListBudget.cCO, ListBudget.cPeriod, cgiNoMessage);
      }

      // определение копии
      if (ListBudget.cBudget <> 0)
      {
        if (not isCurNewParam)
        { // возможно это первый запуск после использования старого
          ListBudget.cBudVar := cTmpBudVar;
          if (ListBudget.cBudVar <> 0 )
          {
            ListBudget.cBudVariant := pExtFpBudVar.GetVariant(ListBudget.cBudVar);
            ListBudget.sBudStage   := pExtFpBudVar.GetStage  (ListBudget.cBudVar);
          }
        }
        else if (ListBudget.cBudVariant <> 0)
        { // определим копию
          ListBudget.cBudVar := pExtFpBudVar.FindBudVar(ListBudget.cBudget, ListBudget.cBudVariant, ListBudget.sBudStage, cgiNoMessage);
        }
      }

      // наименование бюджета
      ListBudget.Name := pExtFpBudget.GetName(word(4), ListBudget.cBudget);

      // БЮДЖЕТ ИСТОЧНИК
      //////////////////////////////////////////////////////////////////////////


      //////////////////////////////////////////////////////////////////////////
      // БЮДЖЕТ ПРИЕМНИК
      #GetTblFld(Word,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_wSlaveBitReg, ListBudget.wSlaveBitReg)
      #GetTblFld(Word,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_wSlaveDelMode,ListBudget.wSlaveDelMode)

      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveCO,ListBudget.cSlaveCO)
      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlavePeriod,ListBudget.cSlavePeriod)
      #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveBudVariant,ListBudget.cSlaveBudVariant)
      #GetTblFld(String,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_sSlaveBudStage,ListBudget.sSlaveBudStage)

      // режимы обработки данных бюджета приемника
      if (not isCurNewParam)
      { // возможно это первый запуск после использования старого
        var wOldDelMode : word;  wOldDelMode := ListBudget.wSlaveDelMode;
        if (wOldDelMode = 0)
        { // было "замещать"
          ListBudget.wSlaveDelMode := 1;
        }
        if (wOldDelMode = 1)
        { // было "добавлять"
          ListBudget.wSlaveDelMode := 0;
        }
      }

      // определение бюджета
      if (not isCurNewParam)
      { // возможно это первый запуск после использования старого
        #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveBudget, ListBudget.cSlaveBudget)
        if (ListBudget.cSlaveBudget <> 0 )
        {
          ListBudget.cSlaveCO     := pExtFpBudget.GetCO    (ListBudget.cSlaveBudget);
          ListBudget.cSlavePeriod := pExtFpBudget.GetPeriod(ListBudget.cSlaveBudget);
        }
      }
      else if (ListBudget.cSlaveCO <> 0) and (ListBudget.cSlavePeriod <> 0)
      { // определим бюджет
        ListBudget.cSlaveBudget := pExtFpBudget.FindBudget(ListBudget.cSlaveCO, ListBudget.cSlavePeriod, cgiNoMessage);
      }

      // определение копии
      if (ListBudget.cSlaveBudget <> 0)
      {
        if (ListBudget.cSlaveBudVariant = 0)
        { // возможно это первый запуск после использования старого
          #GetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveBudVar, ListBudget.cSlaveBudVar)
          if (ListBudget.cSlaveBudVar <> 0 )
          {
            ListBudget.cSlaveBudVariant := pExtFpBudVar.GetVariant(ListBudget.cSlaveBudVar);
            ListBudget.sSlaveBudStage   := pExtFpBudVar.GetStage  (ListBudget.cSlaveBudVar);
          }
        }
        else if (ListBudget.cBudVariant <> 0)
        { // определим копию
          ListBudget.cSlaveBudVar := pExtFpBudVar.FindBudVar(ListBudget.cSlaveBudget, ListBudget.cSlaveBudVariant, ListBudget.sSlaveBudStage, cgiNoMessage);
        }
      }

      // наименование бюджета
      ListBudget.SlaveName := pExtFpBudget.GetName(word(4), ListBudget.cSlaveBudget);

      // БЮДЖЕТ ПРИЕМНИК
      //////////////////////////////////////////////////////////////////////////

      // вставка в список бюджетов
      var tsRes : word;  tsRes := insert current ListBudget;
      if (tsRes <> tsOk)
      { FpLog('MyGetTblListBudget : (insert current ListBudget <> tsOk)'+string(tsRes)); }
    }
    while False;

    var cBoxRec : comp;  cBoxRec := BoxRec.cRec;
    delete all BoxRec where ((cBoxRec == BoxRec.cRec));
  }
  pEditListBudget.ReCalcOrder;
  if GetFirst KatH <> tsOk
  { Exit; }
  fSynchPosition(KatH.NRec);

  MyGetTblListBudget := True;
}
//------------------------------------------------------------------------------
// SetTBL
//------------------------------------------------------------------------------
function MySetTBLListBudget(_cTune : comp) : boolean;
{
  MySetTBLListBudget := False;
  PushPos(#ListBudget);
  pObjITune.DelTbl(_cTune, Tbl_ListBudget);
  _Loop ListBudget where ((fListBudgetID == ListBudget.ID))
  {
      var cSavedRec : comp;  cSavedRec := ListBudget.NRec;
      #SetTblFld(LongInt,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_fOrder, ListBudget.fOrder)
      #SetTblFld(Word,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_wBitReg,ListBudget.wBitReg)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cCO,ListBudget.cCO)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cPeriod,ListBudget.cPeriod)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cBudVariant,ListBudget.cBudVariant)
      #SetTblFld(String,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_sBudStage,ListBudget.sBudStage)

      #SetTblFld(Word,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_wSlaveBitReg, ListBudget.wSlaveBitReg)
      #SetTblFld(Word,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_wSlaveDelMode,ListBudget.wSlaveDelMode)

      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveCO,ListBudget.cSlaveCO)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlavePeriod,ListBudget.cSlavePeriod)
      #SetTblFld(comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveBudVariant,ListBudget.cSlaveBudVariant)
      #SetTblFld(String,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_sSlaveBudStage,ListBudget.sSlaveBudStage)

      // для того чтобы определить что это уже новые настройки, в бюджет запишем 0
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cBudget,0)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cBudVar,0)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveBudget,0)
      #SetTblFld(Comp,Tbl_ListBudget,cSavedRec,TblVar__ListBudget_cSlaveBudVar,0)
  }
  PopPos(#ListBudget);
  MySetTBLListBudget := True;
}

//------------------------------------------------------------------------------
function MyRead_ITune(_Entity : word; _cTune : comp) : boolean;
{
  MyRead_ITune := False;

  // признак "загружены настройки из пакета заданий"
  if ( not isLoadBatchITune )
  {
    if _cTune = 0
    {
      if pObjFpProfileManager.UpdateZerroIDToOneID
        Set CurProfileID := 1;
      _Entity := CurIntEntity;
    }
  }

  pObjITune.InitEnt(_Entity, _cTune);
  do
  {
    // загрузка полей интерфейса
    #GetFld(Comp,   Var_cIndToApply     , cIndToApply    )
    #GetFld(Comp,   Var_cTFormToApply   , cTFormToApply  )
    #GetFld(Word,   Var_wPRelMode       , wPRelMode      )

    #GetFld(Word,   Var_wFixMode        , wFixMode       )

    if (cTFormToApply <> 0)
    {
    #GetFld(Word,   Var_wTFormMode      , wTFormMode     )
    #GetFld(Word,   Var_wFormulaFixMode , wFormulaFixMode)
    }

    // загрузка временных таблиц интерфейса
    MyGetTblListBudget(_cTune);
  }
  while False;
  pObjITune.DoneEnt(_Entity, _cTune);

  ReScanPanel(#KatH);
  ReReadRecord(#EmptyTable);
  CheckFieldsState;
  ShowMyHeader;

  MyRead_ITune := True;
}

//------------------------------------------------------------------------------
function Read_ITune(_cTune : comp) : boolean;
{
  isLoadBatchITune := True;
  Read_ITune := MyRead_ITune(CurIntEntity, _cTune);
}

//------------------------------------------------------------------------------
function MySave_ITune(_Entity : word; _cTune : comp) : boolean;
{
  MySave_ITune := False;

  // были загружены настройки из пакета заданий
  if ( not isLoadBatchITune )
  {
  }

  pObjITune.InitEnt(_Entity, _cTune);

  // сохранение временных таблиц интерфейса
  MySetTBLListBudget(_cTune);

  // сохранение полей интерфейса
  #SetFld(Comp,   Var_cIndToApply     , cIndToApply    )
  #SetFld(Comp,   Var_cTFormToApply   , cTFormToApply  )

  #SetFld(Word,   Var_wPRelMode       , wPRelMode      )

  #SetFld(Word,   Var_wFixMode        , wFixMode       )
  #SetFld(Word,   Var_wTFormMode      , wTFormMode     )
  #SetFld(Word,   Var_wFormulaFixMode , wFormulaFixMode)

  pObjITune.DoneEnt(_Entity, _cTune);

  MySave_ITune := True;
}

//------------------------------------------------------------------------------
function Save_ITune(_cTune : comp) : boolean;
{ isLoadBatchITune := True;
  Save_ITune := MySave_ITune(CurIntEntity, _cTune)
}

//------------------------------------------------------------------------------
function MyDel_ITune(_Entity : word; _cTune : comp) : boolean;
{ MyDel_ITune := pObjITune.DelEnt(_Entity, _cTune);
}

//------------------------------------------------------------------------------
function Del_ITune(_cTune : comp) : boolean;
{ Del_ITune := MyDel_ITune(CurIntEntity, _cTune);
}

//------------------------------------------------------------------------------
// Загрузка списка бюджетов для менеджера
//------------------------------------------------------------------------------
function MyGetTblList(_cTune : comp): boolean;
{
  MyGetTblList := False;
  var _Nrec         : comp    =  0;
  var _cRecCO       : comp    =  0;
  var _cSlaveRecCO  : comp    =  0;
  var _cRecPer      : comp    =  0;
  var _cSlaveRecPer : comp    =  0;
  var _cRecVar      : comp    =  0;
  var _cSlaveRecVar : comp    =  0;
  var _sStage       : string  = '';
  var _sSlaveStage  : string  = '';
  var _cBudget      : comp    =  0;
  var _cSlaveBudget : comp    =  0;
  var _cBudVar      : comp    =  0;
  var _cSlaveBudVar : comp    =  0;
  var _wBitReg      : word    =  0;
  var _wSlaveBitReg : word    =  0;
  var _wDelMode     : word    =  0;
  var _fOrder       : LongInt =  0;
  if (not pObjITune.GetTblBoxRec(_cTune, Tbl_tblList) )
  {Exit;}

  Delete All KatH;


  while GetFirst FastFirstRow BoxRec = tsOk
  {
    do
    {
      if (BoxRec.cRec = 0)  { Break; }
      var cSavedRec : comp; cSavedRec := BoxRec.cRec;

      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_NRec, _NRec)
      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_cRecCO, _cRecCO)
      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_cSlaveRecCO, _cSlaveRecCO)
      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_cRecPer, _cRecPer)
      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_cSlaveRecPer, _cSlaveRecPer)
      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_cRecVar, _cRecVar)
      #GetTblFld(Comp,Tbl_tblList,cSavedRec,TblVar__tblList_cSlaveRecVar, _cSlaveRecVar)
      #GetTblFld(String,Tbl_tblList,cSavedRec,TblVar__tblList_sStage, _sStage)
      #GetTblFld(String,Tbl_tblList,cSavedRec,TblVar__tblList_sSlaveStage, _sSlaveStage)
      #GetTblFld(Word,Tbl_tblList,cSavedRec,TblVar__tblList_wBitReg, _wBitReg)
      #GetTblFld(Word,Tbl_tblList,cSavedRec,TblVar__tblList_wSlaveBitReg, _wSlaveBitReg)
      #GetTblFld(Word,Tbl_tblList,cSavedRec,TblVar__tblList_wDelMode, _wDelMode)
      #GetTblFld(LongInt,Tbl_tblList,cSavedRec,TblVar__tblList_fOrder,_fOrder)
    }
    while False;


    // заполняем бюджеты
    _cBudget := pExtFpBudget.FindBudget(_cRecCO,_cRecPer, cgiNoMessage);
    if _cBudget = 0
    {
      //Exit;
    }
    _cBudVar := pExtFpBudVar.FindBudVar(_cBudget, _cRecVar, _sStage, cgiNoMessage);
    if ( _cBudVar = 0 )
    {
      //_cBudVar := pExtFpBudget.FindAnyAvBudVar(_cBudget, True);
    }

    _cSlaveBudget := pExtFpBudget.FindBudget(_cSlaveRecCO,_cSlaveRecPer, cgiNoMessage);
    if _cSlaveBudget = 0
    {
      //Exit;
    }
    _cSlaveBudVar := pExtFpBudVar.FindBudVar(_cSlaveBudget, _cSlaveRecVar, _sSlaveStage, cgiNoMessage);
    if ( _cSlaveBudVar = 0 )
    {
      //_cSlaveBudVar := pExtFpBudget.FindAnyAvBudVar(_cSlaveBudget, True);
    }

    ClearBuffer(#KatH);
    KatH.ID      := GetListBudgetID;
    KatH.fOrder  := _fOrder;
    KatH.cBudget := _cBudget;
    KatH.Name    := pExtFpBudget.GetName(word(0 or 4), _cBudget);
    KatH.cBudget          := _cBudget;
    KatH.cBudVar          := _cBudVar;
    KatH.cCO              := _cRecCO;
    KatH.cPeriod          := _cRecPer;
    KatH.cBudVariant      := _cRecVar;
    KatH.sBudStage        := _sStage;
    KatH.wBitReg          := _wBitReg;

    KatH.cSlaveBudget     := _cSlaveBudget;
    KatH.SlaveName        := pExtFpBudget.GetName(word(0 or 4), _cSlaveBudget);
    KatH.cSlaveBudget     := _cSlaveBudget;
    KatH.cSlaveBudVar     := _cSlaveBudVar;
    KatH.cSlaveCO         := _cSlaveRecCO;
    KatH.cSlavePeriod     := _cSlaveRecPer;
    KatH.cSlaveBudVariant := _cSlaveRecVar;
    KatH.sSlaveBudStage   := _sSlaveStage;
    KatH.wSlaveBitReg     := _wSlaveBitReg;

    KatH.wSlaveDelMode    := _wDelMode;

    if ( Insert Current KatH <> tsOk )
    { Fplog('MyGetTblList: Insert Current KatH <> tsOk'); }

    var cBoxRec : comp; cBoxRec := BoxRec.cRec;
    delete all BoxRec where ((cBoxRec == BoxRec.cRec));
  } //while GetFirst FastFirstRow BoxRec

  if GetFirst KatH = tsOk
    fSynchPosition(KatH.NRec);

  MyGetTblList := True;
}

//------------------------------------------------------------------------------
// Загрузка из менеджера бюджетов источников
//------------------------------------------------------------------------------
function Read_ITuneNMainBudSrc(_cTune : comp; _wEntity : word) : boolean;
{
  Read_ITuneNMainBudSrc := False;

  pObjITune.InitEnt(_wEntity, _cTune);
  MyGetTblList(_cTune);
  pObjITune.DoneEnt(_wEntity, _cTune);

  Read_ITuneNMainBudSrc := True;
}
//------------------------------------------------------------------------------
// Загрузка из менеджера бюджетов приемников
//------------------------------------------------------------------------------
function Read_ITuneNSlaveBudSrc(_cTune : comp; _wEntity : word) : boolean;
{
  Read_ITuneNSlaveBudSrc := False;
  // глушим параметры для 810
  comp(_cTune);
  word(_wEntity);

  Read_ITuneNSlaveBudSrc := True;
}
