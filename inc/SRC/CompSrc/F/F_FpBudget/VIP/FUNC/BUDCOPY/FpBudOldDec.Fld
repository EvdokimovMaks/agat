//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Управление бюджетом"
// Декомпозиция по периодам
//------------------------------------------------------------------------------

Const
  Var_cMainBudget              = 'MBudg';
  Var_cMainBudVar              = 'MBudV';

  Tbl_TreeBudget               = 'TTBt';

  TblVar__TreeBudget_NRec      = 'TBNR';
  TblVar__TreeBudget_cNode     = 'TBNd';
  TblVar__TreeBudget_cBudget   = 'TBBd';
  TblVar__TreeBudget_cBudVar   = 'TBBV';
  TblVar__TreeBudget_cCO       = 'TBCO';
  TblVar__TreeBudget_cPeriod   = 'TBPd';
  TblVar__TreeBudget_wExist    = 'TBEt';
  TblVar__TreeBudget_wRasp     = 'TBRp';
  TblVar__TreeBudget_LevelCode = 'TBLC';
end;


//------------------------------------------------------------------------------
function MyGetOldTblListBudget(_cTune : comp): boolean;
{
  MyGetOldTblListBudget := False;

  delete all ListBudget where ((fListBudgetID == ListBudget.ID));
  if (not pObjITune.GetTblBoxRec(_cTune, Tbl_TreeBudget) )
  {Exit;}
  while GetFirst FastFirstRow BoxRec = tsOk
  {
    do
    {
      if (BoxRec.cRec = 0)  { Break; }
      var cSavedRec : comp;  cSavedRec := BoxRec.cRec;
      ClearBuffer(#ListBudget);
      ListBudget.ID   := GetListBudgetID;

      var cTmpNode : comp = 0;
      #GetTblFld(Comp,Tbl_TreeBudget,cSavedRec,TblVar__TreeBudget_cNode, cTmpNode)

      var wRaspMode : word = 0;
      #GetTblFld(Word,Tbl_TreeBudget,cSavedRec,TblVar__TreeBudget_wRasp,wRaspMode)
      if (wRaspMode = 0) and (cTmpNode <> 0)
      { Continue; }

      #GetTblFld(Comp,Tbl_TreeBudget,cSavedRec,TblVar__TreeBudget_cBudget, ListBudget.cBudget)
      #GetTblFld(Comp,Tbl_TreeBudget,cSavedRec,TblVar__TreeBudget_cBudVar, ListBudget.cBudVar)

      // определение бюджета
      if (ListBudget.cBudget <> 0 )
      {
        ListBudget.cCO     := pExtFpBudget.GetCO    (ListBudget.cBudget);
        ListBudget.cPeriod := pExtFpBudget.GetPeriod(ListBudget.cBudget);
      }

      // определение копии
      if (ListBudget.cBudget <> 0)
      {
        if (ListBudget.cBudVar <> 0 )
        {
          ListBudget.cBudVariant := pExtFpBudVar.GetVariant(ListBudget.cBudVar);
          ListBudget.sBudStage   := pExtFpBudVar.GetStage  (ListBudget.cBudVar);
        }
      }

      // наименование бюджета
      if (ListBudget.cBudget <> 0)
      {
        ListBudget.Name := pExtFpBudget.GetName(word(4), ListBudget.cBudget);
      }

      if (cTmpNode = 0)
      { // главный бюджет
        Set AgrcBudget  := ListBudget.cBudget;
        Set AgrcBudVar  := ListBudget.cBudVar;
        Set AgrCO       := ListBudget.cCO    ;
        Set AgrPeriod   := ListBudget.cPeriod;
        Set AgrVariant  := ListBudget.cBudVariant;
        Set AgrBudStage := ListBudget.sBudStage  ;
      }
      else
      { // подчиненный бюджет

        // вставка в список бюджетов
        var tsRes : word;  tsRes := insert current ListBudget;
        if (tsRes <> tsOk)
        { FpLog('MyGetOldTblListBudget : (insert current ListBudget <> tsOk)'+string(tsRes)); }
      }

    }
    while False;

    var cBoxRec : comp;  cBoxRec := BoxRec.cRec;
    delete all BoxRec where ((cBoxRec == BoxRec.cRec));
  } //while GetFirst FastFirstRow BoxRec

  MyGetOldTblListBudget := True;
}
