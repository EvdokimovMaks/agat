//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Управление бюджетом"
// Визуальный интерфейс : "Распределение по ЦО"
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// Блокировка данных бюджета
//------------------------------------------------------------------------------
//------------------------------------------------------------------------------
function UnLockValuesData(
  _SessionID  : LongInt
) : boolean;
{
  UnLockValuesData := False;
  var Err : boolean;  Err := False;
  do
  {
    if (not pLockValues.Session_TryUnLock(_SessionID) )
    { Err := True; }
    if (not pLockValues.Session_End(_SessionID) )
    { Err := True; }
  }
  while False;
  if (Err)
    pLockValues.ShowMessageLastError;
  UnLockValuesData := (not Err);
}
//------------------------------------------------------------------------------
function LockValuesData(
  var _SessionID  : LongInt
) : boolean;
{
  LockValuesData := False;
  var Err : boolean;  Err := False;
  StartNewVisual(vtDownVisual, vfTimer+vfBreak+vfConfirm, 'Блокировка данных...', 1);
  do
  {
    _SessionID := pLockValues.Session_Begin;
    if (_SessionID = 0)
    { Err := True; Break; }

    PushPos(#MRTune);
    PushPos(#SpMR);
    PushPos(#fpRasp);
    PushPos(#fpRaspR);
    var LockParam  : TFpLockLaluesParam;
    _Loop ListUsedBudVar where
     ((fListBudgetID == ListUsedBudVar.ID))
    {
      var isEditBud : boolean;  isEditBud := (ListUsedBudVar.EditMode=1);

      ClearAdvRecord(LockParam);
      LockParam.wSegBit  := word(fpcgBudVarLock or fpcgKodRegLock or fpcgStBudLock);
      LockParam.wSemMode := if(isEditBud, fpcgActiveSem, 0);
      LockParam.cBudVar  := ListUsedBudVar.cBudVar;
      LockParam.wKodReg  := ListUsedBudVar.KodReg;

      if GetFirst MRTune <> tsOk {}
      _Loop SpMR
      {
        if GetFirst fpRasp <> tsOk
        { Continue; }

        if (not isEditBud)
        { // источник
      LockParam.cStBud  := fpRasp.cStBud;
      pLockValues.Session_AddPLock(_SessionID, LockParam);
        }
        else
        { // приемник
          _Loop fpRaspR
          {
      LockParam.cStBud  := fpRaspR.cStBase;
      pLockValues.Session_AddALock(_SessionID, LockParam);
          }
        }
      } //_Loop fpRasp
    } //_Loop ListUsedBudVar
    PopPos(#MRTune);
    PopPos(#SpMR);
    PopPos(#fpRasp);
    PopPos(#fpRaspR);

    if (Err)  { Break; }
    if (not pLockValues.Session_TryLock(_SessionID) )
    { Err := True; Break; }

  }
  while False;
  StopVisual('', 0);

  if (Err)
  {
    pLockValues.ShowMessageLastError;
    UnLockValuesData(_SessionID);
  }
  LockValuesData := (not Err);
}

