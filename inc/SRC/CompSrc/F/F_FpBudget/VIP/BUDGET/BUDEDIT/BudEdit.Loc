//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Управление бюджетом"
// Редактирование бюджета
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// Блокировка данных бюджета
//------------------------------------------------------------------------------
//------------------------------------------------------------------------------
function UnLockValuesData(
  _SessionID  : LongInt
) : boolean;
{
  UnLockValuesData := False;
  var Err : boolean;  Err := False;
  do
  {
    if (not pLockValues.Session_TryUnLock(_SessionID) )
    { Err := True; }
    if (not pLockValues.Session_End(_SessionID) )
    { Err := True; }
  }
  while False;
  if (Err)
    pLockValues.ShowMessageLastError;
  UnLockValuesData := (not Err);
}
//------------------------------------------------------------------------------
function LockValuesData(
  var _SessionID  : LongInt
) : boolean;
{
  LockValuesData := False;
  _SessionID := 0;
  var Err : boolean;  Err := False;
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm+vfScreenBottom, 'Блокировка данных...', 1);
  do
  {
    _SessionID := pLockValues.Session_Begin;
    if (_SessionID = 0)
    { Err := True; Break; }

    var LockParam  : TFpLockLaluesParam;
    _Loop ListBudget where ((GetListBudgetID == ListBudget.ID))
    {
      // все статьи типовой формы для источников
      _Loop FpMeanTF where
        ((cCommonTForm  == FpMeanTF.cMain
      and cgKau_FpStBud == FpMeanTF.KodGrKau))
      {
        ClearAdvRecord(LockParam);
        LockParam.wSegBit  := word(fpcgBudVarLock or fpcgKodRegLock or fpcgStBudLock);
        LockParam.cBudVar  := ListBudget.cBudVar;
        LockParam.wKodReg  := wMainReg;
        LockParam.cStBud   := FpMeanTF.cMean;
        pLockValues.Session_AddPLock(_SessionID, LockParam);
      } // _Loop FpMeanTF
    } // _Loop ListBudget

    if (not pLockValues.Session_TryLock(_SessionID) )
    { Err := True; Break; }

  }
  while False;
  StopVisual('', 0);
  if (Err)
  {
    pLockValues.ShowMessageLastError;
    UnLockValuesData(_SessionID);
  }

  LockValuesData := (not Err);
}

//------------------------------------------------------------------------------
function GetIsBudVarLock( _cBudVar: comp ) : boolean;
{
  GetIsBudVarLock := False;
  var SessionID : LongInt;
  SessionID := 0;
  var Err : boolean;  Err := False;
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm+vfScreenBottom, 'Блокировка данных...', 1);
  do
  {
    SessionID := pLockValues.Session_Begin;
    if (SessionID = 0)
    { Err := True; Break; }

    var LockParam  : TFpLockLaluesParam;

     // все статьи типовой формы для приемника
    _Loop FpMeanTF where
      ((cCommonTForm  == FpMeanTF.cMain
    and cgKau_FpStBud == FpMeanTF.KodGrKau))
    {
      ClearAdvRecord(LockParam);
      LockParam.wSegBit  := word(fpcgBudVarLock or fpcgKodRegLock or fpcgStBudLock);
      LockParam.cBudVar  := _cBudVar;
      LockParam.wKodReg  := wMainReg;
      LockParam.cStBud   := FpMeanTF.cMean;
      pLockValues.Session_AddALock(SessionID, LockParam);
    } // _Loop FpMeanTF

    if (not pLockValues.Session_TryLock(SessionID) )
    { Err := True; Break; }

  }
  while False;
  StopVisual('', 0);
  if (Err)
  {
    pLockValues.ShowMessageLastError;
  }
   UnLockValuesData(SessionID);

  GetIsBudVarLock := Err;
}

