//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 9.10 - модуль "Управление бюджетом"
// Визуальный интерфейс : "Редактирование агрегатов аналитик"
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
function UnLockValuesData(
  _SessionID  : LongInt
) : boolean;
{
  UnLockValuesData := False;
  var Err : boolean;  Err := False;
  do
  {
    if (not pLockValues.Session_TryUnLock(_SessionID) )
    { Err := True; }
    if (not pLockValues.Session_End(_SessionID) )
    { Err := True; }
  }
  while False;
  if (Err)
    pLockValues.ShowMessageLastError;
  UnLockValuesData := (not Err);
}
//------------------------------------------------------------------------------
function LockValuesData(
  var _SessionID  : LongInt;
      _Buff       : TFpValues
) : boolean;
{
  LockValuesData := False;
  _SessionID := 0;
  var Err : boolean;  Err := False;
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm+vfScreenBottom, 'Блокировка данных...', 1);
  do
  {
    _SessionID := pLockValues.Session_Begin;
    if (_SessionID = 0)
    { Err := True; Break; }

    var LockParam  : TFpLockLaluesParam;
    ClearAdvRecord(LockParam);
    LockParam.wSegBit  := word(fpcgBudVarLock or fpcgKodRegLock or fpcgStBudLock or fpcgPeriodLock);
    LockParam.cBudVar  := _Buff.cBudVar;
    LockParam.wKodReg  := _Buff.KodReg;
    LockParam.cStBud   := _Buff.cStBud;
    LockParam.cPeriod  := _Buff.cPeriod;
    pLockValues.Session_AddALock(_SessionID, LockParam);

    if (not pLockValues.Session_TryLock(_SessionID) )
    { Err := True; Break; }
  }
  while False;
  StopVisual('', 0);
  if (Err)
  {
    pLockValues.ShowMessageLastError;
    UnLockValuesData(_SessionID);
  }

  LockValuesData := (not Err);
}

//------------------------------------------------------------------------------
function LockAllPeriodValuesData(
  var _SessionID  : LongInt;
      _Buff       : TFpValues
) : boolean;
{
  LockAllPeriodValuesData := False;
  _SessionID := 0;
  var Err : boolean;  Err := False;
  StartNewVisual(vtRotateVisual, vfTimer+vfBreak+vfConfirm+vfScreenBottom, 'Блокировка данных...', 1);
  do
  {
    _SessionID := pLockValues.Session_Begin;
    if (_SessionID = 0)
    { Err := True; Break; }

    var MID : TPtr;  MID := InitMarker('',8,200,100,False);
    pExtFpPeriod.MakeMarkerLeafPeriod(MID, _Buff.cPeriod);
    var i : LongInt;
    var cPeriod : comp;
    for (i := 0; i < GetMarkerCount(MID); inc(i))
    {
      if (not GetMarker(MID, i, cPeriod) )
      { Continue; }

      var LockParam  : TFpLockLaluesParam;
      ClearAdvRecord(LockParam);
      LockParam.wSegBit  := word(fpcgBudVarLock or fpcgKodRegLock or fpcgStBudLock or fpcgPeriodLock);
      LockParam.cBudVar  := _Buff.cBudVar;
      LockParam.wKodReg  := _Buff.KodReg;
      LockParam.cStBud   := _Buff.cStBud;
      LockParam.cPeriod  := cPeriod;
      pLockValues.Session_AddALock(_SessionID, LockParam);

    }
    DoneMarker(MID, '');

    if (not pLockValues.Session_TryLock(_SessionID) )
    { Err := True; Break; }
  }
  while False;
  StopVisual('', 0);
  if (Err)
  {
    pLockValues.ShowMessageLastError;
    UnLockValuesData(_SessionID);
  }

  LockAllPeriodValuesData := (not Err);
}
