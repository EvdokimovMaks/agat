//-----------------------------------------------------------------------------------------------------------
//                                                                                   (c) корпорация Галактика
// Галактика 9.1 - модуль "Электронная отчетность"
// Алгоритмы расчета : "Сведения о просроченной задолженности по заработной плате (3-Ф)"
//-----------------------------------------------------------------------------------------------------------


//===========================================================================================================
//#region СЕРВИСНЫЕ МЕТОДЫ
//-----------------------------------------------------------------------------------------------------------
@Script SetFldVal(fld:String; val:variant);
@begin
  XMLMAP.SetVariantAttrValueByName(fld,val);
@end.

//-----------------------------------------------------------------------------------------------------------
@Script GetStrVal(fld:String):String;
@begin
  Result := Trim(String(XMLMAP.GetAttrValueByName(fld)));
@end.

//-----------------------------------------------------------------------------------------------------------
@Script GetDblVal(fld:String):double;
@begin
  Result := Double(XMLMAP.GetAttrValueByName(fld));
@end.

//-----------------------------------------------------------------------------------------------------------
@Script MesErrStop(mesMesAdrXML, mesPole, mesMes: String): boolean;
@begin
  MesError.InsertMesError(mesMesAdrXML, Comp(0), '', mesPole, mesMes, 0, 0);
  Result := True;
@end.

//------------------------------------------------------------------------------------------------------------
@Script NullStr(fld:string):boolean;
@begin
  Result := XMLMAP.GetIsNullAttrValueByName(fld);
@end.
//#endregion СЕРВИСНЫЕ МЕТОДЫ
//***********************************************************************************************************


//===========================================================================================================
//#region On_ExportXML ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ЭКСПОРТЕ XML
//-----------------------------------------------------------------------------------------------------------
@Script On_ExportXML : boolean;
@begin
  if (not XMLMap.ExpToXml(_XmlFileName_)) {
    Message('On_ExportXML: '+ XMLMap.GetStLastError, cancelButton+Warning);
    Exit;
  }

  Message('Выгружен в файл ' + _XmlFileName_);
@end.
//#endregion On_ExportXML ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ЭКСПОРТЕ В XML
//***********************************************************************************************************


//===========================================================================================================
//#region On_Check ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ ПОЛЕЙ
//-----------------------------------------------------------------------------------------------------------
@Script On_Check : boolean;
@begin
  // ОБЪЯВЛЕНИЕ ПЕРЕМЕННЫХ
  var fl : boolean;
  var d01_3, d02_3, d03_3, d03_4, d04_3, d04_4, d05_3, d05_4, d06_3, d06_4, d07_3, d08_3, d09_3, d10_3, d11_3 : Double;
  var a01_3, a02_3, a03_3, a03_4, a04_3, a04_4, a05_3, a05_4, a06_3, a06_4, a07_3, a08_3, a09_3, a10_3, a11_3 : String;


  // #region ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ
  a01_3 := 'Документ/Разд1/ЗадПоЗП/Всего'                   ;   d01_3 := GetDblVal(a01_3);
  a02_3 := 'Документ/Разд1/ЗадПоЗП/ПослЗП/Всего'            ;   d02_3 := GetDblVal(a02_3);
  a03_3 := 'Документ/Разд1/ИзСтр01_Бюд/Всего'               ;   d03_3 := GetDblVal(a03_3);
  a04_3 := 'Документ/Разд1/ИзСтр01_Бюд/ФедБюд/Всего'        ;   d04_3 := GetDblVal(a04_3);
  a05_3 := 'Документ/Разд1/ИзСтр01_Бюд/БудСубРФ/Всего'      ;   d05_3 := GetDblVal(a05_3);
  a06_3 := 'Документ/Разд1/ИзСтр01_Бюд/МестБюд/Всего'       ;   d06_3 := GetDblVal(a06_3);
  a07_3 := 'Документ/Разд1/ФондПослЗП/Всего'                ;   d07_3 := GetDblVal(a07_3);
  a08_3 := 'Документ/Разд1/ЧислРаб/Всего'                   ;   d08_3 := GetDblVal(a08_3);
  a09_3 := 'Документ/Разд1/ИзСтр01_Зад/ЗаМес2016/Всего'     ;   d09_3 := GetDblVal(a09_3);
  a10_3 := 'Документ/Разд1/ИзСтр01_Зад/ЗаМес2015Ран/Всего'  ;   d10_3 := GetDblVal(a10_3);
  a11_3 := 'Документ/Разд1/ИзСтр01_Увол/Всего'              ;   d11_3 := GetDblVal(a11_3);

  a03_4 := 'Документ/Разд1/ИзСтр01_Бюд/ЗаВыпГосЗак'         ;   d03_4 := GetDblVal(a03_4);
  a04_4 := 'Документ/Разд1/ИзСтр01_Бюд/ФедБюд/ЗаВыпГосЗак'  ;   d04_4 := GetDblVal(a04_4);
  a05_4 := 'Документ/Разд1/ИзСтр01_Бюд/БудСубРФ/ЗаВыпГосЗак';   d05_4 := GetDblVal(a05_4);
  a06_4 := 'Документ/Разд1/ИзСтр01_Бюд/МестБюд/ЗаВыпГосЗак' ;   d06_4 := GetDblVal(a06_4);
  // #endregion ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ


  //---------------------------------------------------------------------------------------------------------
  if NullStr('Документ/НаимОрг') MesErrStop('Документ/НаимОрг', '"Наименование организации"', 'Поле обязательно к заполнению');
  if NullStr('Документ/Адрес'  ) MesErrStop('Документ/Адрес'  , '"Адрес организации"'       , 'Поле обязательно к заполнению');
  if NullStr('Документ/ОКПО'   ) MesErrStop('Документ/ОКПО'   , '"Код ОКПО"'                , 'Поле обязательно к заполнению');

  //---------------------------------------------------------------------------------------------------------
  fl := not NullStr(a01_3);
  if (fl or not NullStr(a02_3)) if not (d02_3 <= d01_3) MesErrStop(a02_3, 'Стр. 02, гр. 3', 'К01: Значение стр. 02, гр. 3 должно быть меньше либо равно значению стр. 01, гр. 3');
  if (fl or not NullStr(a03_3)) if not (d03_3 <= d01_3) MesErrStop(a03_3, 'Стр. 03, гр. 3', 'К02: Значение стр. 03, гр. 3 должно быть меньше либо равно значению стр. 01, гр. 3');
  if (fl or not NullStr(a09_3)) if not (d09_3 <= d01_3) MesErrStop(a09_3, 'Стр. 09, гр. 3', 'К09: Значение стр. 09, гр. 3 должно быть меньше либо равно значению стр. 01, гр. 3');
  if (fl or not NullStr(a10_3)) if not (d10_3 <= d01_3) MesErrStop(a10_3, 'Стр. 10, гр. 3', 'К09: Значение стр. 10, гр. 3 должно быть меньше либо равно значению стр. 01, гр. 3');
  if (fl or not NullStr(a11_3)) if not (d11_3 <= d01_3) MesErrStop(a11_3, 'Стр. 11, гр. 3', 'К09: Значение стр. 11, гр. 3 должно быть меньше либо равно значению стр. 01, гр. 3');

  //---------------------------------------------------------------------------------------------------------
  if (not NullStr(a03_3) or not NullStr(a04_3) or not NullStr(a05_3) or not NullStr(a06_3))
    if not (d03_3 = d04_3 + d05_3 + d06_3) MesErrStop(a03_3, 'Стр. 03, гр. 3', 'К03: Значение стр. 03, гр. 3 должно быть равно сумме строк 04, 05, 06, гр. 3');

  //---------------------------------------------------------------------------------------------------------
  if (not NullStr(a03_4) or not NullStr(a04_4) or not NullStr(a05_4) or not NullStr(a06_4))
    if not (d03_4 = d04_4 + d05_4 + d06_4) MesErrStop(a03_4, 'Стр. 03, гр. 4', 'К04: Значение стр. 03, гр. 4 должно быть равно сумме строк 04, 05, 06, гр. 4');

  //---------------------------------------------------------------------------------------------------------
  if (not NullStr(a03_4) or not NullStr(a03_3)) if not (d03_4 <= d03_3) MesErrStop(a03_4, 'Стр. 03, гр. 4', 'К05: Значение стр. 03, гр. 4 должно быть меньше либо равно значению стр. 03, гр. 3');
  if (not NullStr(a04_4) or not NullStr(a04_3)) if not (d04_4 <= d04_3) MesErrStop(a04_4, 'Стр. 04, гр. 4', 'К05: Значение стр. 04, гр. 4 должно быть меньше либо равно значению стр. 04, гр. 3');
  if (not NullStr(a05_4) or not NullStr(a05_3)) if not (d05_4 <= d05_3) MesErrStop(a05_4, 'Стр. 05, гр. 4', 'К05: Значение стр. 05, гр. 4 должно быть меньше либо равно значению стр. 05, гр. 3');
  if (not NullStr(a06_4) or not NullStr(a06_3)) if not (d06_4 <= d06_3) MesErrStop(a06_4, 'Стр. 06, гр. 4', 'К05: Значение стр. 06, гр. 4 должно быть меньше либо равно значению стр. 06, гр. 3');

  //---------------------------------------------------------------------------------------------------------
  if not NullStr(a07_3) if not (d07_3 > 0) MesErrStop(a07_3, 'Стр. 07, гр. 3', 'К06: Значение стр. 07, гр. 3 должно быть больше 0 (нуля)'                          );

  //---------------------------------------------------------------------------------------------------------
  if (not NullStr(a07_3) or not NullStr(a02_3))
    if not (d07_3 >= d02_3)
      MesErrStop(a07_3, 'Стр. 07, гр. 3', 'К07: Значение стр. 07, гр. 3 должно быть больше либо равно значению стр. 02, гр. 3');

  //---------------------------------------------------------------------------------------------------------
  if not NullStr(a01_3)
    if (d01_3 > 0) and not ((d07_3 > 0) and (d08_3 > 0))
    then MesErrStop(a01_3, 'Стр. 01, гр. 3', 'К08: Если значение стр. 01, гр. 3 больше 0 (нуля), то строки 07 и 08, гр. 3 должны быть больше 0 (нуля)');

  //---------------------------------------------------------------------------------------------------------
  fl := (not NullStr(a09_3) or not NullStr(a10_3));

  if (fl or not NullStr(a11_3)) if not (d11_3 <= d09_3 + d10_3) MesErrStop(a11_3, 'Стр. 11, гр. 3', 'К10: Значение стр. 11, гр. 3 должно быть меньше либо равно сумме строк 09 и 10, гр. 3');
  if (fl or not NullStr(a01_3)) if not (d09_3 + d10_3 <= d01_3) MesErrStop(a01_3, 'Стр. 01, гр. 3', 'К11: Сумма строк 09 и 10, гр. 3 должно быть меньше либо равно значению стр. 01, гр. 3');

  //---------------------------------------------------------------------------------------------------------
  if NullStr('Документ/Тел'                ) MesErrStop('Документ/Тел'                , '"Телефон"'  , 'Поле обязательно к заполнению');
  if NullStr('Документ/Подписант/Должность') MesErrStop('Документ/Подписант/Должность', '"Должность"', 'Поле обязательно к заполнению');
  if NullStr('Документ/Подписант/ФИО'      ) MesErrStop('Документ/Подписант/ФИО'      , '"ФИО"'      , 'Поле обязательно к заполнению');

  //---------------------------------------------------------------------------------------------------------
  if (NullStr('Документ/ДатаДок') or GetStrVal('Документ/ДатаДок') = 'ДД мес ГГГГ')
    MesErrStop('Документ/ДатаДок', '"Дата документа"', 'Поле обязательно к заполнению');
@end.
//#endregion On_Check ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ ПОЛЕЙ
//***********************************************************************************************************


//===========================================================================================================
//#region On_Recalc ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ РАСЧЁТЕ ПОЛЕЙ
//-----------------------------------------------------------------------------------------------------------
@Script On_Recalc : boolean;
  //=========================================================================================================
  //#region СЕРВИС On_Recalc
  //---------------------------------------------------------------------------------------------------------
  #declare _Log(_strAdr, _strMes, _strVal)
    MesError.InsertRasch('', #_strAdr, #_strMes, #_strVal, 0);
  #end

  #declare GDS0(_val)
    IF(#_val < 0, '('+DoubleTostr(Round(#_val), '[|-]36666666666666666666666666')+')', DoubleTostr(Round(#_val), '[|-]36666666666666666666666666'))
  #end

  #declare _SetFldGV(_fld, _val)
    SetFldVal(#_fld, #_val);
    #_val := GetDblVal(#_fld);
  #end
  //#endregion СЕРВИС On_Recalc
  //*********************************************************************************************************
@begin
  // ЗНАЧЕНИЯ ПО УМОЛЧАНИЮ
  if (NullStr('Документ/ДатаДок') or (GetStrVal('Документ/ДатаДок') = 'ДД мес ГГГГ'))
    SetFldVal('Документ/ДатаДок', DateTostr(Cur_Date, 'DD mon YYYY'));

  // ОБЪЯВЛЕНИЕ ПЕРЕМЕННЫХ
  var d01_3, d02_3, d03_3, d03_4, d04_3, d04_4, d05_3, d05_4, d06_3, d06_4 : Double;
  var a03_3, a03_4, a04_3, a04_4, a05_3, a05_4, a06_3, a06_4, Mes03_3, Mes03_4, Val03_3, Val03_4 : String;


  // #region ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ
  a04_3 := 'Документ/Разд1/ИзСтр01_Бюд/ФедБюд/Всего'        ;   d04_3 := GetDblVal(a04_3);
  a05_3 := 'Документ/Разд1/ИзСтр01_Бюд/БудСубРФ/Всего'      ;   d05_3 := GetDblVal(a05_3);
  a06_3 := 'Документ/Разд1/ИзСтр01_Бюд/МестБюд/Всего'       ;   d06_3 := GetDblVal(a06_3);

  a04_4 := 'Документ/Разд1/ИзСтр01_Бюд/ФедБюд/ЗаВыпГосЗак'  ;   d04_4 := GetDblVal(a04_4);
  a05_4 := 'Документ/Разд1/ИзСтр01_Бюд/БудСубРФ/ЗаВыпГосЗак';   d05_4 := GetDblVal(a05_4);
  a06_4 := 'Документ/Разд1/ИзСтр01_Бюд/МестБюд/ЗаВыпГосЗак' ;   d06_4 := GetDblVal(a06_4);

  a03_3 := 'Документ/Разд1/ИзСтр01_Бюд/Всего'      ;
  a03_4 := 'Документ/Разд1/ИзСтр01_Бюд/ЗаВыпГосЗак';
  // #endregion ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ

  // #region ИНФОРМАЦИОННЫЙ РАЗДЕЛ
  d03_3 := d04_3 + d05_3 + d06_3;   #_SetFldGV(a03_3, d03_3);
  d03_4 := d04_4 + d05_4 + d06_4;   #_SetFldGV(a03_4, d03_4);

  Mes03_3 := 'Стр. 03 (гр. 3) = стр. 04 (гр. 3) + стр. 05 (гр. 3) + стр. 06 (гр. 3)';
  Mes03_4 := 'Стр. 03 (гр. 4) = стр. 04 (гр. 4) + стр. 05 (гр. 4) + стр. 06 (гр. 4)';

  Val03_3 := #GDS0(d03_3)+' = '+#GDS0(d04_3)+' + '+#GDS0(d05_3)+' + '+#GDS0(d06_3);
  Val03_4 := #GDS0(d03_4)+' = '+#GDS0(d04_4)+' + '+#GDS0(d05_4)+' + '+#GDS0(d06_4);

  #_Log(a03_3, Mes03_3, Val03_3);
  #_Log(a03_4, Mes03_4, Val03_4);
  // #endregion ИНФОРМАЦИОННЫЙ РАЗДЕЛ
@end.
//#endregion On_Recalc ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ РАСЧЁТЕ ПОЛЕЙ
//***********************************************************************************************************
