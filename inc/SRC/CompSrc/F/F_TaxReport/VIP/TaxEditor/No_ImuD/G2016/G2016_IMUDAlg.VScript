//------------------------------------------------------------------------------------------------------------
//                                                                                    (c) корпорация Галактика
// Галактика 9.1 - модуль "Электронная отчетность"
// Алгоритмы расчета : "Декларация по налогу на имущество организаций"
//------------------------------------------------------------------------------------------------------------

//============================================================================================================
//#region СЕРВИСНЫЕ МЕТОДЫ
//------------------------------------------------------------------------------------------------------------
@Script GetStrVal(fld:string):string;
@begin
  Result := Trim(XMLMAP.GetAttrValueByName(fld));
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetDblVal(fld:string):double;
@begin
  Result := Double(XMLMAP.GetAttrValueByName(fld));
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetFldVis(fld:string):boolean;
@begin
  Result := XMLMAP.GetAttrVisByName(fld);
@end.

//------------------------------------------------------------------------------------------------------------
@Script SetFldVal(fld:string;val:string);
@begin
  XMLMAP.SetAttrValueByName(fld,val,2);
@end.

//------------------------------------------------------------------------------------------------------------
@Script SetFldVis(fld:string;vis:boolean);
@begin
  XMLMAP.SetAttrVisByName(fld,vis);
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetCountFld(fld:string):LongInt;
@begin
  Result := XMLMap.GetNodeCountByName(fld);
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetIter(iter:LongInt):string;
@begin
  Result := '';
  if (iter > 0)
    Result := '[' + String(iter) + ']';
@end.

//------------------------------------------------------------------------------------------------------------
@Script MesError_Alg(mesMesAdrXML, mesPole, mesMes: string; mesCountIter: integer = 0): boolean;
@begin
  MesError.InsertMesError(mesMesAdrXML, Comp(0), '', mesPole, mesMes, mesCountIter);
  Result := True;
@end.

//------------------------------------------------------------------------------------------------------------
@Script NullStr(fld:string):boolean;
@begin
  Result := XMLMAP.GetIsNullAttrValueByName(fld);
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetValPrev(inpStr, XMLAdr_1, XMLAdr_2 : string) : double;
@begin
  Result := pFH.Func('GetPrevVal', String(pFH.Func(inpStr)), XMLAdr_1, XMLAdr_2);
@end.
//#endregion СЕРВИСНЫЕ МЕТОДЫ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ УСТАНОВКЕ ВИДИМОСТИ ПОЛЕЙ
//------------------------------------------------------------------------------------------------------------
// Установить видимость полей всей декларации
@Script On_Visable : boolean;
  //==========================================================================================================
  //#region СЕРВИС
  //----------------------------------------------------------------------------------------------------------
  procedure IfVis(adrIfVis : string);
  { SetFldVis(adrIfVis, not NullStr(adrIfVis)); }

  procedure IfVisDbl(adrIfVis : string);
  { SetFldVis(adrIfVis, (not NullStr(adrIfVis) and (GetDblVal(adrIfVis) <> 0))); }

  procedure SetVisF(adrIfVis : string);
  { SetFldVis(adrIfVis, False); }

  procedure SetVisT(adrIfVis : string);
  { SetFldVis(adrIfVis, True); }
  //#endregion СЕРВИС
  //**********************************************************************************************************
@begin
  Result := False;

  var i, j, Count_I, Count_J :LongInt;
  var XMLAdr_I, XMLAdr_J : string;
  var tBool : boolean;

  // ТИТУЛЬНЫЙ ЛИСТ
  IfVis('Файл/Документ/СвНП/Тлф'                );
  IfVis('Файл/Документ/Подписант/ФИО/Отчество'  );
  IfVis('Файл/Документ/Подписант/СвПред/НаимОрг');

  tBool := not NullStr('Файл/Документ/СвНП/НПЮЛ/КПП'                );  SetFldVis('Файл/Документ/СвНП/НПЮЛ'                , tBool);
                                                                        SetFldVis('Файл/Документ/СвНП/НПЮЛ/КПП'            , tBool);
  tBool := not NullStr('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ФормРеорг');  SetFldVis('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ'      , tBool);
                                                                        SetFldVis('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ИННЮЛ', tBool);
                                                                        SetFldVis('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/КПП'  , tBool);

  tBool := (GetStrVal('Файл/Документ/Подписант/ПрПодп') = '2');
  SetFldVis('Файл/Документ/Подписант/СвПред', tBool);

  // РАЗДЕЛ 1
  Count_I := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ');
  for (i := 0; i < Count_I; i++)
  {
    XMLAdr_I := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i);

    if (i > 0)
    {
      tBool := (not NullStr(XMLAdr_I + '/КБК') and not NullStr(XMLAdr_I + '/ОКТМО'));
      SetFldVis(XMLAdr_I, tBool);
    }

    // РАЗДЕЛ 2
    Count_J := GetCountFld(XMLAdr_I + '/РасОбДеятРФ/РасОб');
    SetFldVis(XMLAdr_I + '/РасОбДеятРФ', (Count_J <> 0));

    for (j := 0; j < Count_J; j++)
    {
      XMLAdr_J := XMLAdr_I + '/РасОбДеятРФ/РасОб' + getIter(j);

      tBool :=     (GetDblVal(XMLAdr_J + '/ВидИмущ') <> 0 )
               and (GetStrVal(XMLAdr_I + '/ОКТМО'  ) <> '');

      SetFldVis(XMLAdr_I + '/РасОбДеятРФ', tBool);
      SetFldVis(XMLAdr_J                 , tBool);

      IfVis(XMLAdr_J + '/ДанРас/КодНалЛьг');
      IfVis(XMLAdr_J + '/ДанРас/ДолСт'    );
      IfVis(XMLAdr_J + '/ДанРас/КодЛгПНС' );
      IfVis(XMLAdr_J + '/НалПер/КодЛгУмен');
      IfVis(XMLAdr_J + '/НалПер/СумЛгУмен');

      IfVisDbl(XMLAdr_J + '/ДанРас/СтИмущНеобл'                   );
      IfVisDbl(XMLAdr_J + '/НалПер/СумАвИсчисл'                   );
      IfVisDbl(XMLAdr_J + '/НалПер/СумНалПред'                    );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0101/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0101/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0102/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0102/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0103/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0103/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0104/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0104/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0105/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0105/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0106/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0106/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0107/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0107/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0108/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0108/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0109/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0109/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0110/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0110/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0111/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0111/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0112/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом0112/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом3112/СтОстОН'  );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ОстСтом3112/СтЛьгИмущ');
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ВтчНедИм/СтОстОН'     );
      IfVisDbl(XMLAdr_J + '/ДанРас/СтоимМес/ВтчНедИм/СтЛьгИмущ'   );
    }

    // РАЗДЕЛ 3
    Count_J := GetCountFld(XMLAdr_I + '/РасОБНедИО/РасОб');
    SetFldVis(XMLAdr_I + '/РасОБНедИО', (Count_J <> 0));

    for (j := 0; j < Count_J; j++)
    {
      XMLAdr_J := XMLAdr_I + '/РасОБНедИО/РасОб' + getIter(j);

      tBool := not NullStr(XMLAdr_J + '/ДанРас/НомКадЗдан');
      SetFldVis(XMLAdr_I + '/РасОБНедИО', tBool);
      SetFldVis(XMLAdr_J                , tBool);

      if not NullStr(XMLAdr_J + '/ДанРас/СведСтКад/СтИмущК')
      {
        SetVisT(XMLAdr_J + '/ДанРас/СведСтКад'             )
        IfVis  (XMLAdr_J + '/ДанРас/СведСтКад/СтИмущНеоблК')
      }
      else SetVisF(XMLAdr_J + '/ДанРас/СведСтКад')

      if not NullStr(XMLAdr_J + '/ДанРас/СведСтИнв/СтИмущИ')
      {
        SetVisT(XMLAdr_J + '/ДанРас/СведСтИнв'             )
        IfVis  (XMLAdr_J + '/ДанРас/СведСтИнв/СтИмущНеоблИ')
      }
      else SetVisF(XMLAdr_J + '/ДанРас/СведСтИнв');

      IfVis(XMLAdr_J + '/ДанРас/КодНалЛьг');
      IfVis(XMLAdr_J + '/ДанРас/НомКадПом');
      IfVis(XMLAdr_J + '/ДанРас/ДолСт'    );
      IfVis(XMLAdr_J + '/ДанРас/КодЛгПНС' );
      IfVis(XMLAdr_J + '/ДанРас/КоэфК'    );

      tBool := not NullStr(XMLAdr_J + '/НалПер/КодЛгУмен');
      SetFldVis(XMLAdr_J + '/НалПер/КодЛгУмен', tBool);
      SetFldVis(XMLAdr_J + '/НалПер/СумЛгУмен', tBool);

      IfVisDbl(XMLAdr_J + '/НалПер/СумАвИсчисл');
    }

    if (Count_I = 1) or (GetFldVis(XMLAdr_I + '/РасОБНедИО') or GetFldVis(XMLAdr_I + '/РасОбДеятРФ'))
    then SetVisT(XMLAdr_I);
    else SetVisF(XMLAdr_I);
  }

  Result := True;
@end. // On_Visable
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ УСТАНОВКЕ ВИДИМОСТИ ПОЛЕЙ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ
//------------------------------------------------------------------------------------------------------------
// Проверить всю декларацию
@Script On_Check : boolean;
  //==========================================================================================================
  //#region СЕРВИС
  //----------------------------------------------------------------------------------------------------------
  #include AllTaxObj.Vih

  function IsValidKodNalLg(strIn: string; var strGetMes: string) : boolean;
  begin
    //Код налоговой льготы
    //КодНалЛьг
    strGetMes := 'Возможные значения: 2012000/XXXXXXXXXXXX или [ЛЛЛЛЛЛЛ]. Формат: 7 или 20 символов';
    Result := False;
    Result :=    CheckError.isRegExpr('^(.......)$', strIn)
              or CheckError.isRegExpr('^((2012000)/(............))$', strIn);
  end;

  function IsValidKodLgUmen(strIn: string; var strGetMes: string) : boolean;
  begin
    //Код налоговой льготы (в виде уменьшения суммы налога, подлежащей уплате в бюджет)
    //КодЛгУмен
    strGetMes := 'Возможные значения: 2012500/XXXXXXXXXXXX. Формат поля: 20 символов';
    Result := False;
    Result := CheckError.isRegExpr('^(2012500/(............))$', strIn);
  end;

  function IsValidKodLgPNS(strIn: string; var strGetMes: string) : boolean;
  begin
    //Код налоговой льготы (установленный  в виде понижения налоговой ставки)
    //КодЛгПНС
    strGetMes := 'Возможные значения: 2012400/XXXXXXXXXXXX. Формат поля: 20 символов';
    Result := False;
    Result := CheckError.isRegExpr('^(2012400/(............))$', strIn);
  end;

  function IsValidKodVid(strIn: string; var strGetMes: string) : boolean;
  begin
    //Код вида имущества
    //ВидИмущ
    strGetMes := 'Возможные значения: 1, 2, 3, 4, 5, 6, 7 или 8';
    Result := False;
    Result := CheckError.isRegExpr('^(1|2|3|4|5|6|7|8)$', strIn);
  end;

  function IsValidIDNomTS(strIn: string; var strGetMes: string) : boolean;
  begin
    //Идентификационный номер транспортного средства
    //ИдНомТС
    strGetMes := 'Формат поля: от 1 до 25 символов, либо пусто';
    Result := False;
    Result :=    Length(strIn) =  0
              or Length(strIn) <= 25;
  end;

  function IsValidKadas(strIn: string; var strGetMes: string) : boolean;
  begin
    strGetMes := 'Формат поля: от 1 до 100 символов, либо пусто';
    Result := False;
    Result :=    Length(strIn) =  0
              or Length(strIn) <= 100;
  end;

  function IsValidStroki(strIn: string; var strGetMes: string) : boolean;
  begin
    strGetMes := 'Формат поля: (от 1 до 10 знаков)/(от 1 до 10 знаков) ';
    Result := False;
    Result := CheckError.isRegExpr('^\-?[0-9]{1,15}$', strIn);
  end;

  function IsValidProstDrob21(strIn: string; var strGetMes: string) : boolean;
  begin
    //Простая дробь в формате: (от 1 до 10 знаков)/(от 1 до 10 знаков), где ведущие нули в числителе и знаменателе недопустимы
    //ПростДроб21
    strGetMes := 'Формат поля: XXXXXXXXXX/XXXXXXXXXX. Формат: 21 символов';

    Result := False;
    if (strIn = '') or (Pos('/', strIn) = 0) Exit;

    var buf_1 : string; buf_1 := SubStr(strIn,                   1,                 Pos('/', strIn) - 1);
    var buf_2 : string; buf_2 := SubStr(strIn, Pos('/', strIn) + 1, Length(strIn) - Pos('/', strIn)    );

    Result := CheckError.isRegExpr('^([1-9]{1}|[1-9]{1}[0-9]{1}|[1-9]{1}[0-9]{0,2}|[1-9]{1}[0-9]{0,3}|[1-9]{1}[0-9]{0,4}|[1-9]{1}[0-9]{0,5}|[1-9]{1}[0-9]{0,6}|[1-9]{1}[0-9]{0,7}|[1-9]{1}[0-9]{0,8}|[1-9]{1}[0-9]{0,9})$', buf_1)
          and CheckError.isRegExpr('^([1-9]{1}|[1-9]{1}[0-9]{1}|[1-9]{1}[0-9]{0,2}|[1-9]{1}[0-9]{0,3}|[1-9]{1}[0-9]{0,4}|[1-9]{1}[0-9]{0,5}|[1-9]{1}[0-9]{0,6}|[1-9]{1}[0-9]{0,7}|[1-9]{1}[0-9]{0,8}|[1-9]{1}[0-9]{0,9})$', buf_2);
  end;

  function IsValidKoef(strIn: string; var strGetMes: string) : boolean;
  begin
    //Коэффициент K
    strGetMes := 'Формат поля: XX/XX';
    Result := False;
    Result := CheckError.isRegExpr('^([0-9]{1}|[1-9]{1}[0-9]{1})/([1-9]{1}|[1-9]{1}[0-9]{1})$', strIn);
  end;

  function IsValidNalStav(strIn: string; var strGetMes: string) : boolean;
  begin
    //Налоговая ставка
    //НалСтавка
    strGetMes := 'Формат поля: X.XX';
    Result := False;
    Result := CheckError.isRegExpr('^[0-9]{1}$', strIn)
              or CheckError.isRegExpr('^[0-9]{1}\.[0-9]{1}[1-9]{0,1}|[1-9]{1}[0-9]{0,1}$', strIn);
  end;

  #declare _InsMes(_AdrXML, _Pole, _StrMes)
    MesError_Alg
    (
      #_AdrXML,
      #_Pole,
      #_StrMes
    );
  #end

  #declare _MesNoKor(_AdrXML, _Pole)
    MesError_Alg
    (
      #_AdrXML,
      #_Pole,
      'Поле не соответствует регулярному выражению. ' + getMes
    );
  #end

  #declare _NoKor(_AdrXML, _Pole)
    MesError_Alg
    (
      #_AdrXML,
      #_Pole,
      'Поле не соответствует формату. ' + getMes
    );
  #end

  #declare _MesObyz(_AdrXML, _Pole)
    MesError_Alg
    (
      #_AdrXML,
      #_Pole,
      'Строка обязательна к заполнению'
    );
  #end
  //#endregion СЕРВИС
  //**********************************************************************************************************
@begin
  Result := False;

  var getMes   ,
  sNaimOrg     , sINNJUL        , sKPP      , sFormReorg, sINNJUL_2  , sKPP_2     , sKBK       , sOKTMO       ,
  sIdFajl      , sVersProg      , sVersForm , sKodNO    , sNomKorr   , sPoMestu   , sKND       , sDataDok     , sPeriod ,
  sSumLgYmen   , sNalPU         , sFormRasch, sKodLgYmen, sIdNomTS   , sSTIm      , sSumAvIsch , sStOstVs     ,
  sOtchetGod   , sPrPodp        , sFamilija , sImja     , sOtchestvo , sNaimDok   , sNaimOrg_2 , sOKVJED      , sTlf       ,
  sKodVid      , sSumNalPred    , sKodNalL  , sStImNeob , sDolST     , sNalBaz    , sKodLgPNS  , sNalStav     ,sSumNalIsch ,
  sKojefKl     , sSumIschisl_Upl, sKodOsvNal, sSumOsvNal, sKodUmenSum, sSumUmenSum, sProcUm    , sKodSnizhStav,
  sSumSnizhStav, sPonizhStav    , sDoljaTS  , sKojefKp  , sOKATO     , sKNalLg    , sKLgPNS    , sKLgYmen     ,
  sNomKad      , sStImK         , sStImI    , sDolCT    , sNalBaza   , sNalSt     , sKoefK     , sSumNalIs    ,
  aNomKad      , aStImK         , aStImI    , aDolCT    , aNalBaza   , aNalSt     , aKoefK     , aSumNalIs    ,
  aNaimOrg     , aINNJUL        , aKPP      , aFormReorg, aINNJUL_2  , aKPP_2     , aKBK       , aOKTMO       , aNalIschisl, aOKATO,
  aIdFajl      , aVersProg      , aVersForm , aKodNO    , aNomKorr   , aPoMestu   , aKND       , aDataDok     , aPeriod ,
  aAvPUKv2     , aSumNalPred    , aNalPU    , aFormRasch, aKodVidTS  , aIdNomTS   , aMarkaTS   , aRegZnakTS   , aSumNalIsch,
  aOtchetGod   , aPrPodp        , aFamilija , aImja     , aOtchestvo , aNaimDok   , aNaimOrg_2 , aOKVJED      , aTlf       ,
  aOKEINalBaza , aJEkologKl     , aVipuskTS , aVladenTS , aKojefKv   , aNalStavka , aSumIschisl, aLgotMesTS   ,
  aSumAvIsch   , aKodLgYmen     , aSumLgYmen, aStOstVs  , aDolST     , aNalBaz    , aKodLgPNS  , aNalStav     ,
  aKodVid      , aSTIm          , aKodNalL  , aStImNeob , aKNalLg    , aKLgPNS    , aKLgYmen   : string = '';

  if not IsValidDate(GetStrVal('Файл/Документ/ДатаДок'), getMes)
    MesError_Alg('Файл/Документ/ДатаДок', '"Дата"', 'Заполните поле или произведите перерасчет для установки текущей даты');
  if not IsValidOKVED(GetStrVal('Файл/Документ/СвНП/ОКВЭД'), getMes) #_MesNoKor('Файл/Документ/СвНП/ОКВЭД', '"ОКВЭД"');

  aKodNO     := 'Файл/Документ/КодНО'                        ;   sKodNO     := GetStrVal(aKodNO    );
  aNomKorr   := 'Файл/Документ/НомКорр'                      ;   sNomKorr   := GetStrVal(aNomKorr  );
  aPoMestu   := 'Файл/Документ/ПоМесту'                      ;   sPoMestu   := GetStrVal(aPoMestu  );
  aKND       := 'Файл/Документ/КНД'                          ;   sKND       := GetStrVal(aKND      );
  aDataDok   := 'Файл/Документ/ДатаДок'                      ;   sDataDok   := GetStrVal(aDataDok  );
  aPeriod    := 'Файл/Документ/Период'                       ;   sPeriod    := GetStrVal(aPeriod   );
  aOtchetGod := 'Файл/Документ/ОтчетГод'                     ;   sOtchetGod := GetStrVal(aOtchetGod);
  aPrPodp    := 'Файл/Документ/Подписант/ПрПодп'             ;   sPrPodp    := GetStrVal(aPrPodp   );
  aFamilija  := 'Файл/Документ/Подписант/ФИО/Фамилия'        ;   sFamilija  := GetStrVal(aFamilija );
  aImja      := 'Файл/Документ/Подписант/ФИО/Имя'            ;   sImja      := GetStrVal(aImja     );
  aOtchestvo := 'Файл/Документ/Подписант/ФИО/Отчество'       ;   sOtchestvo := GetStrVal(aOtchestvo);
  aNaimDok   := 'Файл/Документ/Подписант/СвПред/НаимДок'     ;   sNaimDok   := GetStrVal(aNaimDok  );
  aNaimOrg_2 := 'Файл/Документ/Подписант/СвПред/НаимОрг'     ;   sNaimOrg_2 := GetStrVal(aNaimOrg_2);
  aOKVJED    := 'Файл/Документ/СвНП/ОКВЭД'                   ;   sOKVJED    := GetStrVal(aOKVJED   );
  aTlf       := 'Файл/Документ/СвНП/Тлф'                     ;   sTlf       := GetStrVal(aTlf      );
  aNaimOrg   := 'Файл/Документ/СвНП/НПЮЛ/НаимОрг'            ;   sNaimOrg   := GetStrVal(aNaimOrg  );
  aINNJUL    := 'Файл/Документ/СвНП/НПЮЛ/ИННЮЛ'              ;   sINNJUL    := GetStrVal(aINNJUL   );
  aKPP       := 'Файл/Документ/СвНП/НПЮЛ/КПП'                ;   sKPP       := GetStrVal(aKPP      );
  aFormReorg := 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ФормРеорг';   sFormReorg := GetStrVal(aFormReorg);
  aINNJUL_2  := 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ИННЮЛ'    ;   sINNJUL_2  := GetStrVal(aINNJUL_2 );
  aKPP_2     := 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/КПП'      ;   sKPP_2     := GetStrVal(aKPP_2    );

  if (sFormReorg <> '')
  {
    if not IsValidFormReorg(sFormReorg, getMes) #_InsMes(aFormReorg, '"Форма реорганизации"', getMes);
    else
    {
      if sFormReorg <> '0'
      {
        if not CheckError.Prov_INN(sINNJUL_2, getMes) #_InsMes(aINNJUL_2, '"ИНН реорганизованной организации"', getMes);
        if not IsValidKPP(sKPP_2, getMes) #_InsMes(aKPP_2, '"КПП реорганизованной организации"', getMes);
      }
      else
      {
        getMes := 'Строка не должна быть заполнена при текущем значении поля "Форма реорганизации"';
        if (Length(sINNJUL_2) > 0) #_InsMes(aINNJUL_2, '"ИНН реорг. орг."', getMes);
        if (Length(sKPP_2   ) > 0) #_InsMes(aINNJUL_2, '"ИНН реорг. орг."', getMes);
      }
    }
  }
  else
  {
    getMes := 'Строка не должна быть заполнена при текущем значении поля "Форма реорганизации"';
    if (Length(sINNJUL_2) > 0) #_InsMes(aINNJUL_2, '"ИНН реорг. орг."', getMes);
    if (Length(sKPP_2)    > 0) #_InsMes(aKPP_2   , '"КПП реорг. орг."', getMes);
  }

  if not IsValidSONO(sKodNO, getMes) #_MesNoKor(aKodNO, 'Код налогового органа');
  if not CheckError.Prov_INN(sINNJUL, getMes) #_InsMes(aINNJUL, '"ИНН"', getMes);
  if not IsValidKPP(sKPP, getMes) #_MesNoKor(aKPP, '"КПП"');

  var i, j: LongInt;

  if (sNaimOrg = '') MesError_Alg(aNaimOrg, '"Налогоплательщик"', 'Поле обязательно для заполнения');
  else if not IsValidNaimOrg(sNaimOrg, getMes) #_MesNoKor(aNaimOrg, '"Налогоплательщик"');
  if (sTlf <> '')
    if not IsValidTlf(sTlf, getMes) #_MesNoKor(aTlf, '"Номер контактного телефона"');
  if (sNaimDok <> '')
    if not IsValidNaimDok(sNaimDok, getMes) #_MesNoKor(aNaimDok, '"Форма реорганизации"');

  if not IsValidFIO(sFamilija, getMes) #_MesNoKor(aFamilija, '"Фамилия"');
  if not IsValidFIO(sImja    , getMes) #_MesNoKor(aImja    , '"Имя"'    );
  if sOtchestvo <> ''
    if not IsValidFIO(sOtchestvo, getMes) #_MesNoKor(aOtchestvo, '"Отчество"');

  if not IsValidPrPodp(sPrPodp, getMes) #_InsMes(aPrPodp, '"Признак подписанта"', getMes)
  else
  {
    if (sPrPodp = '2')
    {
      if not IsValidNaimDok(sNaimDok, getMes) #_InsMes(aNaimDok, '"Наим. док., подтв. полномочия предст."', 'Поле обязательно для заполнения при значении поля "Признак подписанта" равного 2. ' + getMes);
        if sNaimOrg_2 <> ''
          if not IsValidNaimOrg(sNaimOrg_2, getMes) #_InsMes(aNaimOrg_2, '"Наименование организации"', getMes);
    }
    else
    {
      getMes := 'Поле заполняется только при значении поля "Признак подписанта" равного 2';
      if (Length(sNaimDok  ) > 0) MesError_Alg(aNaimDok  , '"Наименование документа"'  , getMes);
      if (Length(sNaimOrg_2) > 0) MesError_Alg(aNaimOrg_2, '"Наименование организации"', getMes);
    }
  }
  //***********************************************************************************************


  //==========================================================================================================
  //#region РАЗДЕЛ 1
  //----------------------------------------------------------------------------------------------------------
  var Count_I : LongInt;  Count_I := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ');
  var tB: boolean; tB:=False;
  for (i := 0; i < Count_I; i++)
    if  GetFldVis('Файл/Документ/ИмущНД/СумНалПУ'+getIter(i))
       tB:=True;
  if (not tB)
  {
    #_InsMes('Файл/Документ/ИмущНД/СумНалПУ', 'ПРЕДУПРЕЖДЕНИЕ', 'Необходимо заполнить хотя бы один Раздел 1 и один из его подразделов.');
    Exit;
  }

  //----------------------------------------------------------------------------------------------------------
  for (i := 0; i < Count_I; i++)
  {
    if  (Count_I = 1)
    and (not GetFldVis('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/РасОбДеятРФ'))
    and (not GetFldVis('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/РасОБНедИО' ))
    {
      #_InsMes('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/РасОбДеятРФ/РасОб', 'ПРЕДУПРЕЖДЕНИЕ', 'Необходимо заполнить хотя бы один из подразделов.')
      Continue;
    }

    if GetFldVis('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i))
    {
      aOKTMO := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/ОКТМО';   sOKTMO := GetStrVal(aOKTMO );
      aKBK   := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/КБК'  ;   sKBK   := GetStrVal(aKBK   );
      aNalPU := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/НалПУ';   sNalPU := GetStrVal(aNalPU );

      if not IsValidOKTMO(sOKTMO, getMes) #_MesNoKor(aOKTMO, 'Стр. 010')
      if not IsValidKBK  (sKBK  , getMes) #_MesNoKor(aKBK  , 'Стр. 020')

      if NullStr(aNalPU) #_MesObyz(aNalPU, 'Стр. 030/040')
    }


    //========================================================================================================
    //#region РАЗДЕЛ 2
    //--------------------------------------------------------------------------------------------------------
    var Count_J : LongInt;  Count_J := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/РасОбДеятРФ/РасОб');
    for (j := 0; j < Count_J; j++)
    {
      // адрес текущего раздела строки
      var sRazdelAddr : string;  sRazdelAddr := 'Файл/Документ/ИмущНД/СумНалПУ'+getIter(i)+'/РасОбДеятРФ/РасОб'+getIter(j);
      if (not GetFldVis(sRazdelAddr) )
      { Continue; }

      aKodVid     := sRazdelAddr +'/ВидИмущ'                         ; sKodVid     := GetStrVal(aKodVid    );
      aSTIm       := sRazdelAddr +'/ДанРас/СтИмущ'                   ; sSTIm       := GetStrVal(aSTIm      );
      aKodNalL    := sRazdelAddr +'/ДанРас/КодНалЛьг'                ; sKodNalL    := GetStrVal(aKodNalL   );
      aStImNeob   := sRazdelAddr +'/ДанРас/СтИмущНеобл'              ; sStImNeob   := GetStrVal(aStImNeob  );
      aDolST      := sRazdelAddr +'/ДанРас/ДолСт'                    ; sDolST      := GetStrVal(aDolST     );
      aKodLgPNS   := sRazdelAddr +'/ДанРас/КодЛгПНС'                 ; sKodLgPNS   := GetStrVal(aKodLgPNS  );
      aNalStav    := sRazdelAddr +'/ДанРас/НалСтав'                  ; sNalStav    := GetStrVal(aNalStav   );
      aStOstVs    := sRazdelAddr +'/ДанРас/СтоимМес/СтОстВс/Значение'; sStOstVs    := GetStrVal(aStOstVs   );
      aNalBaz     := sRazdelAddr +'/НалПер/НалБаза'                  ; sNalBaz     := GetStrVal(aNalBaz    );
      aSumNalIsch := sRazdelAddr +'/НалПер/СумНалИсчисл'             ; sSumNalIsch := GetStrVal(aSumNalIsch);
      aSumAvIsch  := sRazdelAddr +'/НалПер/СумАвИсчисл'              ; sSumAvIsch  := GetStrVal(aSumAvIsch );
      aKodLgYmen  := sRazdelAddr +'/НалПер/КодЛгУмен'                ; sKodLgYmen  := GetStrVal(aKodLgYmen );
      aSumLgYmen  := sRazdelAddr +'/НалПер/СумЛгУмен'                ; sSumLgYmen  := GetStrVal(aSumLgYmen );
      aSumNalPred := sRazdelAddr +'/НалПер/СумНалПред'               ; sSumNalPred := GetStrVal(aSumNalPred);

      if NullStr(aKodVid    ) #_MesObyz(aKodVid    , 'Стр. 001')
      if NullStr(aSTIm      ) #_MesObyz(aSTIm      , 'Стр. 150')
      if NullStr(aNalBaz    ) #_MesObyz(aNalBaz    , 'Стр. 190')
      if NullStr(aNalStav   ) #_MesObyz(aNalStav   , 'Стр. 210')
      if NullStr(aSumNalIsch) #_MesObyz(aSumNalIsch, 'Стр. 220')
      if NullStr(aStOstVs   ) #_MesObyz(aStOstVs   , 'Стр. 270')

      // Код вида имущества
      if not IsValidKodVid(sKodVid, getMes)
      { // если "Код вида имущества" - НЕ корректен
        #_MesNoKor(aKodVid, 'Стр. 001');
      }
      else
      { // если "Код вида имущества" - корректен
        if (sKodVid='1' and  sKBK<>'18210602020021000110' )
        {
          #_InsMes(aKodVid, 'Стр. 001', 'В разд.1 должен быть КБК 18210602020021000110, т.к. в выбран кода имущества "1" (имущество, входящее в ЕСГ), или необходимо пересмотреть код имущества.')
        }
        if ((sKodVid='2' or sKodVid='3' or sKodVid='5') and  sKBK<>'18210602010021000110' )
        {
         #_InsMes(aKodVid, 'Стр. 001', 'В разд.1 должен быть КБК 18210602010021000110, т.к. в выбран кода имущества "'+sKodVid+'", или необходимо пересмотреть код имущества.')
        }
      } // если "Код вида имущества" - корректен

      if not (sSTIm  = '') {
        if not IsValidStroki(sSTIm, getMes)
          #_MesNoKor(aSTIm, 'Стр. 150');
      }

      // Доля налогоплательщика
      if (sKodVid = '2')
      { // если "Код вида имущества" = 2
        if (not IsValidProstDrob21(sDolST, getMes) )
          #_NoKor(aDolST, 'Стр. 180');
      } // если "Код вида имущества" = 2
      else
      { // если "Код вида имущества" != 2
        if (sDolST != '')
        { // строка с кодом 180 заполняется только в "Разделах 2" с отметкой 2 по строке "код вида имущества"
          getMes := '"доля налогоплательщика" заполняется только если "код вида имущества"=2';
          #_InsMes(aDolST, 'Стр. 180', getMes)
        }
      } // если "Код вида имущества" != 2

      if not (sNalBaz     = '') if not IsValidStroki (sNalBaz    , getMes) #_MesNoKor(aNalBaz    , 'Стр. 190');
      if not (sNalStav    = '') if not IsValidNalStav(sNalStav   , getMes) #_MesNoKor(aNalStav   , 'Стр. 210');
      if not (sSumNalIsch = '') if not IsValidStroki (sSumNalIsch, getMes) #_MesNoKor(aSumNalIsch, 'Стр. 220');

      if (sKodNalL   <> '') if not IsValidKodNalLg(sKodNalL , getMes) #_MesNoKor(aKodNalL , 'Стр. 160');
      if (sKodLgPNS  <> '') if not IsValidKodLgPNS(sKodLgPNS, getMes) #_MesNoKor(aKodLgPNS, 'Стр. 200');

      if (sKodLgYmen <> '')
      {
        if not IsValidKodLgUmen    (sKodLgYmen , getMes) #_MesNoKor(aKodLgYmen, 'Стр. 240');
        if (sSumLgYmen  = '') #_InsMes(aSumLgYmen, 'Стр. 250', 'Строка обязательна к заполнению. Заполните ее и произведите перерасчет')
      }
    } // for
    //#endregion РАЗДЕЛ 2
    //********************************************************************************************************


    //========================================================================================================
    //#region РАЗДЕЛ 3
    //--------------------------------------------------------------------------------------------------------
    Count_J := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) +'/РасОБНедИО/РасОб');
    for (j := 0; j < Count_J; j++)
    {
      // адрес текущего раздела строки
      var sRazdelAddr : string;  sRazdelAddr := 'Файл/Документ/ИмущНД/СумНалПУ'+getIter(i)+'/РасОБНедИО/РасОб'+getIter(j);
      if (not GetFldVis(sRazdelAddr) )
      { Continue; }

      aNomKad     := sRazdelAddr +'/ДанРас/НомКадЗдан'         ; sNomKad    := GetStrVal(aNomKad   );
      aStImK      := sRazdelAddr +'/ДанРас/СведСтКад/СтИмущК'  ; sStImK     := GetStrVal(aStImK    );
      aStImI      := sRazdelAddr +'/ДанРас/СведСтИнв/СтИмущИ'  ; sStImI     := GetStrVal(aStImI    );
      aDolCT      := sRazdelAddr +'/ДанРас/ДолСт'              ; sDolCT     := GetStrVal(aDolCT    );
      aNalBaza    := sRazdelAddr +'/НалПер/НалБаза'            ; sNalBaza   := GetStrVal(aNalBaza  );
      aNalSt      := sRazdelAddr +'/ДанРас/НалСтав'            ; sNalSt     := GetStrVal(aNalSt    );
      aKoefK      := sRazdelAddr +'/ДанРас/КоэфК'              ; sKoefK     := GetStrVal(aKoefK    );
      aSumNalIs   := sRazdelAddr +'/НалПер/СумНалИсчисл'       ; sSumNalIs  := GetStrVal(aSumNalIs );
      aKNalLg     := sRazdelAddr +'/ДанРас/КодНалЛьг'          ; sKNalLg    := GetStrVal(aKNalLg   );
      aKLgPNS     := sRazdelAddr +'/ДанРас/КодЛгПНС'           ; sKLgPNS    := GetStrVal(aKLgPNS   );
      aKLgYmen    := sRazdelAddr +'/НалПер/КодЛгУмен'          ; sKLgYmen   := GetStrVal(aKLgYmen  );
      aSumLgYmen  := sRazdelAddr +'/НалПер/СумЛгУмен'          ; sSumLgYmen := GetStrVal(aSumLgYmen);

      if NullStr(aNomKad  ) #_MesObyz(aNomKad  , 'Стр. 014')
      if NullStr(aNalBaza ) #_MesObyz(aNalBaza , 'Стр. 060')
      if NullStr(aNalSt   ) #_MesObyz(aNalSt   , 'Стр. 080')
      if NullStr(aSumNalIs) #_MesObyz(aSumNalIs, 'Стр. 100')

      if NullStr(aStImK) and NullStr(aStImI) #_InsMes(aStImK, 'Стр. 020, 030', 'Должна быть заполнена или стр. 020, или стр. 030')

      if not (sNomKad   = '') if not IsValidKadas  (sNomKad  , getMes) #_MesNoKor(aNomKad  , 'Стр. 014')
      if not (sNalBaza  = '') if not IsValidStroki (sNalBaza , getMes) #_MesNoKor(aNalBaza , 'Стр. 060')
      if not (sNalSt    = '') if not IsValidNalStav(sNalSt   , getMes) #_MesNoKor(aNalSt   , 'Стр. 080')
      if not (sSumNalIs = '') if not IsValidStroki (sSumNalIs, getMes) #_MesNoKor(aSumNalIs, 'Стр. 100')

      if ((sStImI = '') and not (sStImK = '')) if not IsValidStroki(sStImK, getMes) #_MesNoKor(aStImK, 'Стр. 020');
      if ((sStImK = '') and not (sStImI = '')) if not IsValidStroki(sStImI, getMes) #_MesNoKor(aStImI, 'Стр. 030');

      if  (sStImK  <> '') and (sStImI <> '')
        #_InsMes(aStImI, 'Стр. 020, 030', 'Если строка 020 заполнена, то строки 030 не заполняется');
      if (sDolCT <> '')
        if not IsValidProstDrob21(sDolCT, getMes)
          #_NoKor(aDolCT, 'Стр. 050');
      if (sKoefK  <> '')
        if not IsValidKoef (sKoefK, getMes) #_MesNoKor(aKoefK, 'Стр. 090');
      if (sKLgPNS <> '') or (sKLgYmen <> '')
        if (sKNalLg <> '') #_InsMes(aKNalLg, 'Стр. 040', 'Строка не должна быть заполнена');
      else if (sKNalLg <> '')
        if not IsValidKodNalLg(sKNalLg, getMes)
          #_MesNoKor(aKNalLg, 'Стр. 040');
      if (sKLgPNS  <> '')
        if not IsValidKodLgPNS(sKLgPNS, getMes) #_MesNoKor(aKLgPNS, 'Стр. 070');
      if (sKLgYmen <> '')
      {
        if not IsValidKodLgUmen(sKLgYmen, getMes) #_MesNoKor(aKLgYmen, 'Стр. 120');
        if (sSumLgYmen  = '') #_InsMes(aSumLgYmen, 'Стр. 130', 'Строка обязательна к заполнению. Заполните ее и произведите перерасчет')
      }
    } // for
    //#endregion РАЗДЕЛ 3
    //********************************************************************************************************

  } // for
  //#endregion РАЗДЕЛ 1
  //**********************************************************************************************************

  Result := True;
@end. // On_Check
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ РАСЧЕТЕ
//------------------------------------------------------------------------------------------------------------
// Рассчитать поля всей декларации
@Script On_Recalc : boolean;
  //==========================================================================================================
  //#region СЕРВИС
  //----------------------------------------------------------------------------------------------------------
  #declare _Log(_strAdr, _strMes, _strVal)
    MesError.InsertRasch('', #_strAdr, #_strMes, #_strVal, 0);
  #end

  function GD0(_strVal : double) : string;
  {
    Result := DoubleToStr(Round(_strVal), '[|-]36666666666666666666');
  }

  //----------------------------------------------------------------------------------------------------------
  // Получить значение дробного показателя
  function myGetDrobValue(
      _sAddr  : string;  // адрес показателя
  var _sValue : string;  // строковое значение
  var _dValue : double   // числовое значение
  ) : boolean;
  {
    Result := False;
    _sValue := '';
    _dValue :=  0;

    var sValue : string;  sValue := GetStrVal(_sAddr);
    var dValue : double;  dValue := 0;
    if (sValue = '')
    { Exit; }
    var p : byte;  p := Pos('/', sValue);
    if (p = 0)
    { Exit; }

    var sSumChis : string;  sSumChis := SubStr(sValue, 1, p - 1);
    var sSumZnam : string;  sSumZnam := SubStr(sValue, p + 1, Length(sValue) - p);
    if (sSumChis = '')
    or (sSumZnam = '')
    { Exit; }

    var wSumChis : word;  wSumChis := word(sSumChis);
    var wSumZnam : word;  wSumZnam := word(sSumZnam);
    if (wSumChis = 0)
    or (wSumZnam = 0)
    { Exit; }

    dValue := Round(wSumChis/wSumZnam, 4);

    _sValue := sValue;
    _dValue := dValue;
    Result := True;
  } // function myGetDrobValue
  //#endregion СЕРВИС
  //**********************************************************************************************************
@begin
  Result := False;

  var tBool:boolean;
  var posStr, LenStr:LongInt;
  var i, j: LongInt;
  var sMSTUm, aNalBaza, sMNalPU02, buf : string;
  var Sum, Sum1, Sum2, SumS, Sum3, Sum4, Sum5, NalStav, SumNal, sSTImuK, sSTImuNK, bufSum230, bufSum110, buf3mes, buf6mes, buf9mes : double;

  if (XMLMAP.GetIsNullAttrValueByName('Файл/Документ/ДатаДок') )
    XMLMAP.SetDateAttrValueByName('Файл/Документ/ДатаДок', Cur_Date);


  //==========================================================================================================
  //#region РАЗДЕЛ 1
  //----------------------------------------------------------------------------------------------------------
  {
  var R01RecCount : LongInt;  R01RecCount := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ');
  for (i := 0; i < R01RecCount; i++)
  { // побежали по "РАЗДЕЛ 1"

    var sR01Addr : string;  sR01Addr := 'Файл/Документ/ИмущНД/СумНалПУ'+getIter(i);

    //========================================================================================================
    //#region РАСЧЕТ ДАННЫХ ПО ПОДЧИНЕННЫМ РАЗДЕЛАМ
    //========================================================================================================
    //#region РАЗДЕЛ 2
    //--------------------------------------------------------------------------------------------------------
    var R02RecCount : LongInt;  R02RecCount := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ'+getIter(i)+'/РасОбДеятРФ/РасОб');
    for (j := 0; j < R02RecCount; j++)
    { // побежали по "РАЗДЕЛ 2"

      // адрес текущего раздела строки
      var sRazdelAddr : string;  sRazdelAddr := sR01Addr+'/РасОбДеятРФ/РасОб'+getIter(j);

      //======================================================================================================
      //#region НАЛОГОВАЯ БАЗА
      //------------------------------------------------------------------------------------------------------
      Sum1 := GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0101/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0102/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0103/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0104/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0105/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0106/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0107/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0108/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0109/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0110/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0111/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0112/СтОстОН');
      Sum1 := Sum1 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом3112/СтОстОН');
      SumS := Sum1;
      Sum1 := Round(Sum1/13);
      SetFldVal(sRazdelAddr+'/ДанРас/СтИмущ',  GD0(Sum1));
      var sMSTUm : string;  sMSTUm := 'Стр. 150 = Сумма гр.3(Стр. 020-140) / 13 (результат округлен до целых)';
      #_Log(sRazdelAddr+'/ДанРас/СтИмущ', sMSTUm, GD0(Sum1)+' = '+String(SumS)+' / 13');
      Sum2 := GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0101/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0102/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0103/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0104/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0105/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0106/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0107/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0108/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0109/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0110/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0111/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом0112/СтЛьгИмущ');
      Sum2 := Sum2 + GetDblVal(sRazdelAddr+'/ДанРас/СтоимМес/ОстСтом3112/СтЛьгИмущ');
      SumS := Sum2;
      Sum2 := Round(Sum2/13);
      SetFldVal(sRazdelAddr+'/ДанРас/СтИмущНеобл',  GD0(Sum2));
      var sMSTUmNeb : string;  sMSTUmNeb := 'Стр. 170 = Сумма гр.4(Стр. 020-140) / 13 (результат округлен до целых)'   ;
      #_Log(sRazdelAddr+'/ДанРас/СтИмущНеобл', sMSTUmNeb, GD0(Sum2)+' = '+String(SumS)+' / 13');

      // Доля балансовой стоимости объекта недвижимого имущества
      var sDoljaAddr  : string;  sDoljaAddr := sRazdelAddr+'/ДанРас/ДолСт';
      var sDoljaValue : string;  sDoljaValue := '';
      var dDoljaValue : double;  dDoljaValue :=  0;
      myGetDrobValue(sDoljaAddr, sDoljaValue, dDoljaValue);

      var sMNalBaza : string;  sMNalBaza := '';
      if (GetStrVal(sRazdelAddr+'/ВидИмущ') != '2')
      { // если "код вида имущества" НЕ = 2
        Sum := Round(Sum1-Sum2);
        sMNalBaza := 'Стр. 190 = Стр. 150 - Стр. 170';
        #_Log(sRazdelAddr+'/НалПер/НалБаза', sMNalBaza, GD0(Sum)+' = '+String(Sum1)+' - '+String(Sum2));
      }
      else
      { // если "код вида имущества" = 2
        // Объекты недвижимого имущества, имеющего место фактического нахождения на территориях
        // разных субъектов Российской Федерации либо на территории субъекта Российской Федерации
        // и в территориальном море Российской Федерации (на континентальном шельфе Российской
        // Федерации или в исключительной экономической зоне Российской Федерации)
        Sum := Round((Sum1-Sum2)*dDoljaValue);
        sMNalBaza := 'Стр. 190 = Стр. 150 - Стр. 170 * Стр. 180 (результат округлен до целых)';
        #_Log(sRazdelAddr+'/НалПер/НалБаза', sMNalBaza, GD0(Sum)+' = ('+String(Sum1)+' - '+String(Sum2)+ ') * '+sDoljaValue);
      }
      SetFldVal(sRazdelAddr+'/НалПер/НалБаза',  GD0(Sum));
      //#endregion НАЛОГОВАЯ БАЗА
      //******************************************************************************************************


      //======================================================================================================
      //#region СУММА НАЛОГА
      //------------------------------------------------------------------------------------------------------
      SumS := Sum;
      NalStav := GetDblVal(sRazdelAddr+'/ДанРас/НалСтав');
      Sum := Round(Sum*NalStav/100);
      SetFldVal(sRazdelAddr+'/НалПер/СумНалИсчисл',  GD0(Sum));
      var sMSumNalIsch : string;  sMSumNalIsch := 'Стр. 220 = Стр. 190 * Стр. 210 / 100'                                     ;
      #_Log(sRazdelAddr+'/НалПер/СумНалИсчисл', sMSumNalIsch, GD0(Sum)+' = '+String(SumS)+' * '+String(NalStav)+ ' / 100');
      //#endregion СУММА НАЛОГА
      //******************************************************************************************************


      //======================================================================================================
      //#region РАСЧЕТ ПОКАЗАТЕЛЯ СТРОКИ 230 РАЗДЕЛА 2 С УЧЕТОМ ДАННЫХ АВАНСОВЫХ ПЛАТЕЖЕЙ В НАЛОГОВОМ ПЕРИОДЕ
      //------------------------------------------------------------------------------------------------------
      if     (String(pFH.Func('pathSelectionImuD_3mes')) <> '')
         and (String(pFH.Func('pathSelectionImuD_6mes')) <> '')
         and (String(pFH.Func('pathSelectionImuD_9mes')) <> '')
      {
        buf3mes := GetValPrev('pathSelectionImuD_3mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОбДеятРФ/РасОб' + getIter(j));
        buf6mes := GetValPrev('pathSelectionImuD_6mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОбДеятРФ/РасОб' + getIter(j));
        buf9mes := GetValPrev('pathSelectionImuD_9mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОбДеятРФ/РасОб' + getIter(j));

        bufSum230 := buf3mes + buf6mes + buf9mes;

        SetFldVal(sRazdelAddr+'/НалПер/СумАвИсчисл', GD0(bufSum230));
        #_Log    (sRazdelAddr+'/НалПер/СумАвИсчисл', 'Стр. 230 = Сумме стр. 180 Раздела 2 нал. расч. по аванс. плат. за 1й квартал, полгодие и 9 месяцев', FormatStr_4('%S = %S + %S + %S', GD0(bufSum230), GD0(buf3mes), GD0(buf6mes), GD0(buf9mes)));
      }
      //#endregion РАССЧЕТ ПОКАЗАТЕЛЯ СТРОКИ 230 РАЗДЕЛА 2 С УЧЕТОМ ДАННЫХ АВАНСОВЫХ ПЛАТЕЖЕЙ В НАЛОГОВОМ ПЕРИОДЕ
      //******************************************************************************************************


      //======================================================================================================
      //#region СУММА НАЛОГОВОЙ ЛЬГОТЫ, УМЕНЬШАЮЩЕЙ СУММУ НАЛОГА, ПОДЛЕЖАЩУЮ УПЛАТЕ В БЮДЖЕТ
      //------------------------------------------------------------------------------------------------------
      do
      { // По строке с кодом 250 указывается сумма налоговой льготы, уменьшающей сумму налога, подлежащую уплате в бюджет.
        // Например, если законом субъекта Российской Федерации установлена льгота для данной категории налогоплательщиков
        // в виде уплаты в бюджет 80% суммы исчисленного налога, то значение по строке с кодом 250 должно быть
        // подсчитано как (значение строки с кодом 220 - значение строки с кодом 230) х (100 - 80) : 100;
        var sAddr : string;  sAddr := sRazdelAddr+'/НалПер';
        if (GetStrVal(sAddr+'/КодЛгУмен') != '')
        { // если есть льгота уменьшающая сумму налога
          if (GetDblVal(sAddr+'/СумЛгУмен') = 0)
          { // если сумма налоговой льготы НЕ задана
            var prc250 : double;  prc250 := GetDblVal(sAddr+'/LowSLgProc');
            if (prc250 != 0)
            { // если задана %-т уплаты в бюджет суммы исчисленного налога
              var sum220 : double;  sum220 := GetDblVal(sAddr+'/СумНалИсчисл');
              var sum230 : double;  sum230 := GetDblVal(sAddr+'/СумАвИсчисл');
              var sum250 : double;  sum250 := (sum220-sum230)*(100-prc250)/(100);
              SetFldVal(sAddr+'/СумЛгУмен', GD0(sum250) );

              // протокол вычислений
              var sFormula : string;  sFormula := FormatStr_1(
                'Стр. 250 = (Стр. 220 - Стр. 230) * (100 - %S) / 100'
              , GD0(prc250) );
              var sRashifr : string;  sRashifr := FormatStr_4(
                '%S = (%S - %S) * (100 - %S) / 100'
              , GD0(sum250), GD0(sum220), GD0(sum230), GD0(prc250) );
              #_Log(sAddr+'/СумЛгУмен', sFormula, sRashifr);
            } // если задана %-т уплаты в бюджет суммы исчисленного налога
          } // если сумма налоговой льготы НЕ задана
        } // если есть льгота уменьшающая сумму налога
      }
      while False;
      //#endregion СУММА НАЛОГОВОЙ ЛЬГОТЫ, УМЕНЬШАЮЩЕЙ СУММУ НАЛОГА, ПОДЛЕЖАЩУЮ УПЛАТЕ В БЮДЖЕТ
      //******************************************************************************************************

    } // побежали по "РАЗДЕЛ 2"
    //#endregion РАЗДЕЛ 2
    //********************************************************************************************************


    //========================================================================================================
    //#region РАЗДЕЛ 3
    //--------------------------------------------------------------------------------------------------------
    var R03RecCount : LongInt;  R03RecCount := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ'+getIter(i)+'/РасОБНедИО/РасОб');
    Sum3 := 0;
    for (j := 0; j < R03RecCount; j++)
    { // побежали по "РАЗДЕЛ 3"

      // адрес текущего раздела строки
      var sRazdelAddr : string;  sRazdelAddr := sR01Addr+'/РасОБНедИО/РасОб'+getIter(j);

      //======================================================================================================
      //#region НАЛОГОВАЯ БАЗА
      //------------------------------------------------------------------------------------------------------
      sSTImuK  := GetDblVal(sRazdelAddr+'/ДанРас/СведСтКад/СтИмущК');
      sSTImuNK := GetDblVal(sRazdelAddr+'/ДанРас/СведСтКад/СтИмущНеоблК');

      // Доля балансовой стоимости объекта недвижимого имущества
      var sDoljaAddr  : string;  sDoljaAddr := sRazdelAddr+'/ДанРас/ДолСт';
      var sDoljaValue : string;  sDoljaValue := '';
      var dDoljaValue : double;  dDoljaValue :=  0;
      myGetDrobValue(sDoljaAddr, sDoljaValue, dDoljaValue);

      var sMNalBaza : string;  sMNalBaza := '';
      Sum1 := sSTImuK-sSTImuNK;
      if (Sum1 <= 0)
      { // если стоимость имущества отрицательная
        Sum := 0
        sMNalBaza := 'Стр. 060 = 0';
        #_Log(sRazdelAddr+'/НалПер/НалБаза', sMNalBaza, GD0(Sum)+' = 0');
      }
      else
      { // если стоимость имущества положительная
        if (dDoljaValue = 0)
        { // если используется все имущество
          Sum := Round(Sum1, 0);
          sMNalBaza := 'Стр. 060 = (Стр. 020 - стр.025)(результат округлен до целых)';
          #_Log(sRazdelAddr+'/НалПер/НалБаза', sMNalBaza, GD0(Sum)+' = ('+sSTImuK + ' - ' + sSTImuNK+')');
        } // если используется все имущество
        else
        { // если используется только часть имущества
          Sum := Round(Sum1*dDoljaValue, 0);
          sMNalBaza := 'Стр. 060 = (Стр. 020 - стр.025) * Стр. 050 (результат округлен до целых)';
          #_Log(sRazdelAddr+'/НалПер/НалБаза', sMNalBaza, GD0(Sum)+' = ('+sSTImuK + ' - ' + sSTImuNK+') * '+sDoljaValue);
        } // если используется только часть имущества
      } // если стоимость имущества положительная
      SetFldVal(sRazdelAddr+'/НалПер/НалБаза',  GD0(Sum));
      //#endregion НАЛОГОВАЯ БАЗА
      //******************************************************************************************************

      //======================================================================================================
      //#region РАСЧЕТ ПОКАЗАТЕЛЯ СТРОКИ 110 РАЗДЕЛА 3 С УЧЕТОМ ДАННЫХ АВАНСОВЫХ ПЛАТЕЖЕЙ В НАЛОГОВОМ ПЕРИОДЕ
      //------------------------------------------------------------------------------------------------------
      if     (String(pFH.Func('pathSelectionImuD_3mes')) <> '')
         and (String(pFH.Func('pathSelectionImuD_6mes')) <> '')
         and (String(pFH.Func('pathSelectionImuD_9mes')) <> '')
      {
        buf3mes := GetValPrev('pathSelectionImuD_3mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОБНедИО/РасОб' + getIter(j));
        buf6mes := GetValPrev('pathSelectionImuD_6mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОБНедИО/РасОб' + getIter(j));
        buf9mes := GetValPrev('pathSelectionImuD_9mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОБНедИО/РасОб' + getIter(j));

        bufSum110 := buf3mes + buf6mes + buf9mes;

        SetFldVal(sRazdelAddr+'/НалПер/СумАвИсчисл', GD0(bufSum110));
        #_Log    (sRazdelAddr+'/НалПер/СумАвИсчисл', 'Стр. 110 = Сумме стр. 090 Раздела 3 нал. расч. по аванс. плат. за 1й квартал, полгодие и 9 месяцев', FormatStr_4('%S = %S + %S + %S', GD0(bufSum110), GD0(buf3mes), GD0(buf6mes), GD0(buf9mes)));
      }
      //#endregion РАССЧЕТ ПОКАЗАТЕЛЯ СТРОКИ 110 РАЗДЕЛА 3 С УЧЕТОМ ДАННЫХ АВАНСОВЫХ ПЛАТЕЖЕЙ В НАЛОГОВОМ ПЕРИОДЕ
      //******************************************************************************************************

      //======================================================================================================
      //#region СУММА НАЛОГА
      //------------------------------------------------------------------------------------------------------
      // Коэффициент К
      var sKoefKAddr  : string;  sKoefKAddr := sRazdelAddr+'/ДанРас/КоэфК';
      var sKoefKValue : string;  sKoefKValue := '';
      var dKoefKValue : double;  dKoefKValue :=  0;
      myGetDrobValue(sKoefKAddr, sKoefKValue, dKoefKValue);

      // Сумма налога
      NalStav := GetDblVal(sRazdelAddr+'/ДанРас/НалСтав');
      {
        var sFormula : string;  sFormula := 'Стр. 100 = Стр. 060 * Стр. 080';
        var sRashifr : string;  sRashifr := FormatStr_2('%S * %S', GD0(Sum), GD0(NalStav) );
        SumNal := Sum * NalStav;
        if (dKoefKValue != 0)
        { // если имущество использовалось НЕ весь период
          sFormula += ' * Стр. 090';
          sRashifr += FormatStr_1(' * %S', sKoefKValue);
          SumNal := SumNal * dKoefKValue;
        } // если имущество использовалось НЕ весь период
        sFormula += ' / 100';
        sRashifr += ' / 100';
        SumNal   := SumNal / 100;
        sRashifr := FormatStr_1('%S = ', GD0(SumNal) ) + sRashifr;

        SetFldVal(sRazdelAddr+'/НалПер/СумНалИсчисл',  GD0(SumNal));
        #_Log(sRazdelAddr+'/НалПер/СумНалИсчисл', sFormula, sRashifr);
      }
      //#endregion СУММА НАЛОГА
      //******************************************************************************************************

      //======================================================================================================
      //#region СУММА НАЛОГОВОЙ ЛЬГОТЫ, УМЕНЬШАЮЩЕЙ СУММУ НАЛОГА, ПОДЛЕЖАЩУЮ УПЛАТЕ В БЮДЖЕТ
      //------------------------------------------------------------------------------------------------------
      do
      { // По строке с кодом 130 указывается сумма налоговой льготы, уменьшающей сумму налога, подлежащую уплате в бюджет.
        // sum130=(sum100-sum110)*(100-prc130)/100
        var sAddr : string;
        sAddr := sRazdelAddr+'/НалПер';
        if (GetStrVal(sAddr+'/КодЛгУмен') != '')
        { // если есть льгота уменьшающая сумму налога
          if (GetDblVal(sAddr+'/СумЛгУмен') = 0)
          { // если сумма налоговой льготы НЕ задана
            var prc130 : double;  prc130 := GetDblVal(sAddr+'/LowSLgProc');
            if (prc130 != 0)
            { // если задана %-т уплаты в бюджет суммы исчисленного налога
              var sum100 : double;  sum100 := GetDblVal(sAddr+'/СумНалИсчисл');
              var sum110 : double;  sum110 := GetDblVal(sAddr+'/СумАвИсчисл');
              var sum130 : double;  sum130 := (sum100-sum110)*(100-prc130)/(100);
              SetFldVal(sAddr+'/СумЛгУмен', GD0(sum130) );

              // протокол вычислений
              var sFormula : string;  sFormula := FormatStr_1(
                'Стр. 130 = (Стр. 100 - Стр. 110) * (100 - %S) / 100'
              , GD0(prc130) );
              var sRashifr : string;  sRashifr := FormatStr_4(
                '%S = (%S - %S) * (100 - %S) / 100'
              , GD0(sum130), GD0(sum100), GD0(sum110), GD0(prc130) );
              #_Log(sAddr+'/СумЛгУмен', sFormula, sRashifr);
            } // если задана %-т уплаты в бюджет суммы исчисленного налога
          } // если сумма налоговой льготы НЕ задана
        } // если есть льгота уменьшающая сумму налога
      }
      while False;
      //#endregion СУММА НАЛОГОВОЙ ЛЬГОТЫ, УМЕНЬШАЮЩЕЙ СУММУ НАЛОГА, ПОДЛЕЖАЩУЮ УПЛАТЕ В БЮДЖЕТ
      //******************************************************************************************************

    } // побежали по "РАЗДЕЛ 3"
    //#endregion РАЗДЕЛ 3
    //********************************************************************************************************
    //#endregion РАСЧЕТ ДАННЫХ ПО ПОДЧИНЕННЫМ РАЗДЕЛАМ
    //********************************************************************************************************


    //========================================================================================================
    //#region ВЫЧИСЛЕНИЕ ИТОГО ПО РАЗДЕЛУ 1
    //========================================================================================================
    //#region РАЗДЕЛ 2
    //--------------------------------------------------------------------------------------------------------
    Sum1 := 0;
    Sum3 := 0;
    Sum4 := 0;
    for (j := 0; j < R02RecCount; j++)
    { // побежали по "РАЗДЕЛ 2"

      // адрес текущего раздела строки
      var sRazdelAddr : string;  sRazdelAddr := sR01Addr+'/РасОбДеятРФ/РасОб'+getIter(j);

      if (GetStrVal(sRazdelAddr +'/ВидИмущ')<>'4')
      { // если "код вида имущества" НЕ = 4
        Sum1 := Sum1 + GetDblVal(sRazdelAddr +'/НалПер/СумНалИсчисл');
        Sum1 := Sum1 - GetDblVal(sRazdelAddr +'/НалПер/СумАвИсчисл');
        Sum1 := Sum1 - GetDblVal(sRazdelAddr +'/НалПер/СумЛгУмен');
        Sum2:= GetDblVal(sRazdelAddr +'/НалПер/СумНалИсчисл');
        Sum3:= GetDblVal(sRazdelAddr +'/НалПер/СумАвИсчисл');
        Sum4:= GetDblVal(sRazdelAddr +'/НалПер/СумЛгУмен');
        Sum:=Sum2-Sum3-Sum4;
        sMNalPU02 := '03.'+String(i+1)+'.'+String(j+1)+' Раздел 2(Стр. 220 - (Стр. 230 + Стр. 250))';
        #_Log(sR01Addr, sMNalPU02, GD0(Sum)+' = '+String(Sum2)+' - ('+String(Sum3)+' + '+String(Sum4)+')');
      } // если "код вида имущества" НЕ = 4
      else
      { // если "код вида имущества" = 4
        // Имущество, принадлежащее российской организации и расположенное на территории другого государства,
        // суммы налога по которому уплачены в соответствии с законодательством другого государства
        Sum2 := GetDblVal(sRazdelAddr +'/НалПер/СумНалИсчисл');
        Sum3 := GetDblVal(sRazdelAddr +'/НалПер/СумЛгУмен');
        Sum4 := GetDblVal(sRazdelAddr +'/НалПер/СумНалПред');
        if ((Sum2-Sum3) < Sum4)
        {
          Sum1:=Sum1-(Sum2-Sum3)
          sMNalPU02:= '03.'+String(i+1)+'.'+String(j+1)+' Раздел 2(Стр. 220 - Cтр. 250)';
          Sum4:=0-(Sum2-Sum3);
          #_Log(sR01Addr, sMNalPU02, GD0(Sum4)+' =-( '+String(Sum2)+' - '+String(Sum3)+')');
        }
        else
        {
          Sum1:=Sum1-Sum4;
          Sum4:=0-Sum4;
          sMNalPU02 := '03.'+String(i+1)+'.'+String(j+1)+' Раздел 2(Стр. 260)';
          #_Log(sR01Addr, sMNalPU02, GD0(Sum4)+' = '+String(Sum4));
        }
      } // если "код вида имущества" = 4
    } // побежали по "РАЗДЕЛ 2"
    //#endregion РАЗДЕЛ 2
    //********************************************************************************************************


    //========================================================================================================
    //#region РАЗДЕЛ 3
    //--------------------------------------------------------------------------------------------------------
    Sum3 := 0;
    for (j := 0; j < R03RecCount; j++)
    { // побежали по "РАЗДЕЛ 3"

      // адрес текущего раздела строки
      var sRazdelAddr : string;  sRazdelAddr := sR01Addr+'/РасОБНедИО/РасОб'+getIter(j);

      Sum3 := Sum3 + GetDblVal(sRazdelAddr+'/НалПер/СумНалИсчисл');
      Sum3 := Sum3 - GetDblVal(sRazdelAddr+'/НалПер/СумАвИсчисл');
      Sum3 := Sum3 - GetDblVal(sRazdelAddr+'/НалПер/СумЛгУмен');
      Sum5 := GetDblVal(sRazdelAddr+'/НалПер/СумНалИсчисл');
      Sum2 := GetDblVal(sRazdelAddr+'/НалПер/СумАвИсчисл');
      Sum4 := GetDblVal(sRazdelAddr+'/НалПер/СумЛгУмен');
      Sum := Sum5 - Sum2 - Sum4;
      sMNalPU02 := '04.'+String(i+1)+'.'+String(j+1)+' Раздел 3(Стр. 100 - (Стр. 110 + Стр. 130))';
      #_Log(sR01Addr, sMNalPU02, GD0(Sum)+' = '+String(Sum5)+' - ('+String(Sum2)+' + '+String(Sum4)+')');
    } // побежали по "РАЗДЕЛ 3"
    //#endregion РАЗДЕЛ 3
    //********************************************************************************************************

    Sum := Round(Sum1+Sum3);
    SetFldVal('Файл/Документ/ИмущНД/СумНалПУ'+getIter(i)+'/НалПУ',  GD0(Sum));
    var sMNalPU : string;  sMNalPU := 'Стр. 030 = Cумма(Разделов 2) + Сумма( Разделов 3)'                        ;
    #_Log('Файл/Документ/ИмущНД/СумНалПУ'+getIter(i)+'/НалПУ', sMNalPU, GD0(Sum)+' = '+String(Sum1)+' + '+String(Sum3));
    //#endregion ВЫЧИСЛЕНИЕ ИТОГО ПО РАЗДЕЛУ 1
    //********************************************************************************************************
  } // побежали по "РАЗДЕЛ 1"
  }
  //#endregion РАЗДЕЛ 1
  //**********************************************************************************************************

  Result := True;
@end. // On_Recalc
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ РАСЧЕТЕ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ КОНТРОЛЬНЫХ СООТНОШЕНИЙ
//------------------------------------------------------------------------------------------------------------
@Script On_CheckKS : boolean;
  //==========================================================================================================
  //#region СЕРВИС
  //----------------------------------------------------------------------------------------------------------
  // Для работы с текущим отчетом
  #undef  GD // Вернуть значение поля типа Double
  #define GD(_XMLAdr) Double(XMLMap.GetAttrValueByName(#_XMLAdr))

  #undef  GS // Вернуть значение поля типа String
  #define GS(_XMLAdr) GetStrVal(#_XMLAdr)

  #undef  Dl // Вернуть значение поля типа Доля
  #define Dl(_XMLAdr) IsValidDrob(#_XMLAdr)

  // Сравнить
  #undef   KS
  #declare KS(_bRes, _XMLAdr, _mesPole, _mesMes)
    if (not (#_bRes))
    { MesError.InsertMesError(#_XMLAdr, Comp(0), '', #_mesPole, #_mesMes, 0); }
  #end

  // КС
  #undef  _KS
  #define _KS(_XMLAdr, _mesPole, _mesMes) MesError.InsertMesError(#_XMLAdr, Comp(0), '', #_mesPole, #_mesMes, 0);

  function IsValidR3S040(strIn: string) : boolean;
  begin
    //Код налоговой льготы
    Result := False;
    Result :=   (CheckError.isRegExpr('^(.......)$', strIn) and not CheckError.isRegExpr('^2012000$', strIn))
              or CheckError.isRegExpr('^((2012000)/(............))$', strIn);
  end;

  function IsValidR3S070_120(strIn: string; strMask: string) : boolean;
  begin
    //Код налоговой льготы
    Result := False;
    Result := CheckError.isRegExpr('^((' + strMask + ')/(............))$', strIn);
  end;

  function IsValidKadastr(strIn: string) : boolean;
  begin
    //Кадастровый номер
    Result := False;

    if    CheckError.isRegExpr('^(..):(..):(......):(.{1,})$' , strIn)
       or CheckError.isRegExpr('^(..):(..):(.......):(.{1,})$', strIn)
    then Result :=     (Pos('.' , strIn) = 0)
                   and (Pos(',' , strIn) = 0)
                   and (Pos(';' , strIn) = 0)
                   and (Pos('"' , strIn) = 0)
                   and (Pos('/' , strIn) = 0)
                   and (Pos('!' , strIn) = 0)
                   and (Pos('@' , strIn) = 0)
                   and (Pos('#' , strIn) = 0)
                   and (Pos('$' , strIn) = 0)
                   and (Pos('%' , strIn) = 0)
                   and (Pos('&' , strIn) = 0)
                   and (Pos('*' , strIn) = 0)
                   and (Pos('(' , strIn) = 0)
                   and (Pos(')' , strIn) = 0)
                   and (Pos('_' , strIn) = 0)
                   and (Pos('+' , strIn) = 0)
                   and (Pos('|' , strIn) = 0)
                   and (Pos('\' , strIn) = 0)
                   and (Pos('-' , strIn) = 0)
                   and (Pos('''', strIn) = 0)
                   and (Pos(']' , strIn) = 0)
                   and (Pos('[' , strIn) = 0)
                   and (Pos('}' , strIn) = 0)
                   and (Pos('{' , strIn) = 0)
                   and (Pos('`' , strIn) = 0)
                   and (Pos('~' , strIn) = 0)
                   and (Pos('N' , strIn) = 0)
                   and (Pos('?' , strIn) = 0)
                   and (Pos('<' , strIn) = 0)
                   and (Pos('>' , strIn) = 0)
                   and (Pos('О' , strIn) = 0)
                   and (Pos('З' , strIn) = 0)
                   and (Length(strIn) >= 1 and Length(strIn) <= 100);
  end;

  function IsValidDrob(_XMLAdr: string) : double;
  begin
    result := 0;

    var buf : string; buf := '';
    if (not NullStr(_XMLAdr))
    {
      buf := GetStrVal(_XMLAdr);
      if (buf <> '') and (Pos('/', buf) <> 0)
      then result := Round(Double(SubStr(buf, 1, Pos('/', buf) - 1)) / Double(SubStr(buf, Pos('/', buf) + 1, Length(buf) - Pos('/', buf))), 4);
    }
  end;
  //#endregion СЕРВИС
  //**********************************************************************************************************
@begin
  Result := False;

  var _CountI, _CountJ, i, j : integer = 0;

//----------------------------------------------------------------------------------------------------------
//#region ПРОВЕРКА СООТНОШЕНИЙ
//----------------------------------------------------------------------------------------------------------
  var XMLAdr
    , a1d030_040, a1sOKTMO, a1sKBK
    , a2sVIm
    , a2g3d020, a2g3d030, a2g3d040, a2g3d050, a2g3d060, a2g3d070, a2g3d080, a2g3d090, a2g3d100, a2g3d110, a2g3d120, a2g3d130, a2g3d140
    , a2g4d020, a2g4d030, a2g4d040, a2g4d050, a2g4d060, a2g4d070, a2g4d080, a2g4d090, a2g4d100, a2g4d110, a2g4d120, a2g4d130, a2g4d140
    , a2d150, a2s160, a2d170, a2d180, a2d190, a2d210, a2d220, a2d230, a2d250, a2d260
    , a3s014, a3s015, a3d020, a3d025, a3s040, a3d050, a3d060, a3s070, a3d080, a3d090, a3d100, a3d110, a3s120, a3d130
    , r2sVIm, r2s160
    , r3s014, r3s015, r3s040, r3s070, r3s120 : string = '';

  var bufR2_KS1_01, bufR2_KS1_02, bufR3_KS1_01, bufR3_KS1_11
    , r1d030_040
    , r2g3d020, r2g3d030, r2g3d040, r2g3d050, r2g3d060, r2g3d070, r2g3d080, r2g3d090, r2g3d100, r2g3d110, r2g3d120, r2g3d130, r2g3d140
    , r2g4d020, r2g4d030, r2g4d040, r2g4d050, r2g4d060, r2g4d070, r2g4d080, r2g4d090, r2g4d100, r2g4d110, r2g4d120, r2g4d130, r2g4d140
    , r2d150, r2d170, r2d180, r2d190, r2d210, r2d220, r2d230, r2d250, r2d260
    , r3d020, r3d025, r3d050, r3d060, r3d080, r3d090, r3d100, r3d110, r3d130 : double = 0.0;

  var flKS_KodVIm4 : boolean = false;

  // РАЗДЕЛ 1
  _CountI := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ');
  for (i := 0; i < _CountI; i++)
  {
    bufR2_KS1_01 := bufR2_KS1_02 := bufR3_KS1_01 := bufR3_KS1_11 := 0;
    flKS_KodVIm4 := false;

    // РАЗДЕЛ 2
    _CountJ := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) + '/РасОбДеятРФ/РасОб');

    for (j := 0; j < _CountJ; j++)
    {
      XMLAdr := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) + '/РасОбДеятРФ/РасОб' + getIter(j);

      if not GetFldVis(XMLAdr) continue;

      a2sVIm   := XMLAdr + '/ВидИмущ'                            ;   r2sVIm   := #GS(a2sVIm  );
      a2g3d020 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0101/СтОстОН';   r2g3d020 := #GD(a2g3d020);    a2g4d020 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0101/СтЛьгИмущ';   r2g4d020 := #GD(a2g4d020);
      a2g3d030 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0102/СтОстОН';   r2g3d030 := #GD(a2g3d030);    a2g4d030 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0102/СтЛьгИмущ';   r2g4d030 := #GD(a2g4d030);
      a2g3d040 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0103/СтОстОН';   r2g3d040 := #GD(a2g3d040);    a2g4d040 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0103/СтЛьгИмущ';   r2g4d040 := #GD(a2g4d040);
      a2g3d050 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0104/СтОстОН';   r2g3d050 := #GD(a2g3d050);    a2g4d050 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0104/СтЛьгИмущ';   r2g4d050 := #GD(a2g4d050);
      a2g3d060 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0105/СтОстОН';   r2g3d060 := #GD(a2g3d060);    a2g4d060 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0105/СтЛьгИмущ';   r2g4d060 := #GD(a2g4d060);
      a2g3d070 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0106/СтОстОН';   r2g3d070 := #GD(a2g3d070);    a2g4d070 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0106/СтЛьгИмущ';   r2g4d070 := #GD(a2g4d070);
      a2g3d080 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0107/СтОстОН';   r2g3d080 := #GD(a2g3d080);    a2g4d080 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0107/СтЛьгИмущ';   r2g4d080 := #GD(a2g4d080);
      a2g3d090 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0108/СтОстОН';   r2g3d090 := #GD(a2g3d090);    a2g4d090 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0108/СтЛьгИмущ';   r2g4d090 := #GD(a2g4d090);
      a2g3d100 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0109/СтОстОН';   r2g3d100 := #GD(a2g3d100);    a2g4d100 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0109/СтЛьгИмущ';   r2g4d100 := #GD(a2g4d100);
      a2g3d110 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0110/СтОстОН';   r2g3d110 := #GD(a2g3d110);    a2g4d110 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0110/СтЛьгИмущ';   r2g4d110 := #GD(a2g4d110);
      a2g3d120 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0111/СтОстОН';   r2g3d120 := #GD(a2g3d120);    a2g4d120 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0111/СтЛьгИмущ';   r2g4d120 := #GD(a2g4d120);
      a2g3d130 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0112/СтОстОН';   r2g3d130 := #GD(a2g3d130);    a2g4d130 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом0112/СтЛьгИмущ';   r2g4d130 := #GD(a2g4d130);
      a2g3d140 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом3112/СтОстОН';   r2g3d140 := #GD(a2g3d140);    a2g4d140 := XMLAdr + '/ДанРас/СтоимМес/ОстСтом3112/СтЛьгИмущ';   r2g4d140 := #GD(a2g4d140);
      a2d150   := XMLAdr + '/ДанРас/СтИмущ'                      ;   r2d150   := #GD(a2d150  );
      a2s160   := XMLAdr + '/ДанРас/КодНалЛьг'                   ;   r2s160   := #GS(a2s160  );
      a2d170   := XMLAdr + '/ДанРас/СтИмущНеобл'                 ;   r2d170   := #GD(a2d170  );
      a2d180   := XMLAdr + '/ДанРас/ДолСт'                       ;   r2d180   := #Dl(a2d180  );
      a2d190   := XMLAdr + '/НалПер/НалБаза'                     ;   r2d190   := #GD(a2d190  );
      a2d210   := XMLAdr + '/ДанРас/НалСтав'                     ;   r2d210   := #GD(a2d210  );
      a2d220   := XMLAdr + '/НалПер/СумНалИсчисл'                ;   r2d220   := #GD(a2d220  );
      a2d230   := XMLAdr + '/НалПер/СумАвИсчисл'                 ;   r2d230   := #GD(a2d230  );
      a2d250   := XMLAdr + '/НалПер/СумЛгУмен'                   ;   r2d250   := #GD(a2d250  );
      a2d260   := XMLAdr + '/НалПер/СумНалПред'                  ;   r2d260   := #GD(a2d260  );

      if (r2sVIm = '4')
      {
        if (not flKS_KodVIm4) flKS_KodVIm4 := true;

        // КС :: 1.02, 1.04 - получение данных
        bufR2_KS1_02 += if ((r2d220 - (r2d230 + r2d250)) < r2d260, r2d220 - (r2d230 + r2d250), r2d260);
      }
      else
      {
        // КС :: 1.01, 1.03 - получение данных
        bufR2_KS1_01 += r2d220 - (r2d230 + r2d250);
      }

      // КС :: 1.05
      if not (r2d150 = Round((r2g3d020 + r2g3d030 + r2g3d040 + r2g3d050 + r2g3d060 + r2g3d070 + r2g3d080 + r2g3d090 + r2g3d100 + r2g3d110 + r2g3d120 + r2g3d130 + r2g3d140) / 13))
      then #_KS(a2d150, 'Стр. 150', 'КС 1.5 :: Значение р2с150 должно равняться (р2г3с020 + р2г3с030 + р2г3с040 + р2г3с050 + р2г3с060 + р2г3с070 + р2г3с080 + р2г3с090 + р2г3с100 + р2г3с110 + р2г3с120 + р2г3с130 + р2г3с140) / 13')

      // КС :: 1.06
      if not (r2d170 = Round((r2g4d020 + r2g4d030 + r2g4d040 + r2g4d050 + r2g4d060 + r2g4d070 + r2g4d080 + r2g4d090 + r2g4d100 + r2g4d110 + r2g4d120 + r2g4d130 + r2g4d140) / 13))
      then #_KS(a2d170, 'Стр. 170', 'КС 1.6 :: Значение р2с170 должно равняться (р2г4с020 + р2г4с030 + р2г4с040 + р2г4с050 + р2г4с060 + р2г4с070 + р2г4с080 + р2г4с090 + р2г4с100 + р2г4с110 + р2г4с120 + р2г4с130 + р2г4с140) / 13')

      // КС :: 1.07
      if ((#GS(a2d170) <> '0') and (#GS(a2d170) <> ''))
        if (r2s160 = '')
        then #_KS(a2s160, 'Стр. 160', 'КС 1.7 :: Если р2с160 /= 0, то р2с170 /= 0; если р2с160 = 0, то р2с170 = 0')

      // КС :: 1.08
      if (r2sVIm = '2')
        if not (r2d190 = Round((r2d150 - r2d170) * r2d180))
        then #_KS(a2d190, 'Стр. 190', 'КС 1.8 :: Если "Код вида имущества" = 2, то р2с190 = (р2с150 - р2с170) * р2с180')

      // КС :: 1.09
      case r2sVIm of
        '1', '3', '4', '5', '6':
        {
          if not (r2d190 = Round(r2d150 - r2d170))
          then #_KS(a2d190, 'Стр. 190', 'КС 1.9 :: Если "Код вида имущества" = 1 или 3 или 4 или 5 или 6, то р2с190 = р2с150 - р2с170')
        }
      end;

      // КС :: 1.10
      if not (r2d220 = Round((r2d190 * r2d210) / 100))
      then #_KS(a2d220, 'Стр. 220', 'КС 1.10 :: Значение р2с220 должно равняться (р2с190 * р2с210) / 100')

      // КС :: 1.11
      bufR3_KS1_11 := GetValPrev('pathSelectionImuD_3mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОбДеятРФ/РасОб' + getIter(j))
                    + GetValPrev('pathSelectionImuD_6mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОбДеятРФ/РасОб' + getIter(j))
                    + GetValPrev('pathSelectionImuD_9mes', 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i), 'РасОбДеятРФ/РасОб' + getIter(j));

      if not (r2d230 = Round(bufR3_KS1_11))
      then #_KS(a2d230, 'Стр. 230', 'КС 1.11 :: Р2с230 = Сумма Р2с180 нал. расч. по аванс. плат. за 1й квартал, полугодие и 9 месяцев тек. нал. пер. (рассчит. значение: '+DoubleToStr(Round(bufR3_KS1_11), '[|-]36666666666666666666')+')')
    }

    // РАЗДЕЛ 3
    _CountJ := GetCountFld('Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) + '/РасОБНедИО/РасОб');
    for (j := 0; j < _CountJ; j++)
    {
      XMLAdr := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i) + '/РасОБНедИО/РасОб' + getIter(j);

      if not GetFldVis(XMLAdr) continue;

      a3s014 := XMLAdr + '/ДанРас/НомКадЗдан'            ;   r3s014 := #GS(a3s014);
      a3s015 := XMLAdr + '/ДанРас/НомКадПом'             ;   r3s015 := #GS(a3s015);
      a3d020 := XMLAdr + '/ДанРас/СведСтКад/СтИмущК'     ;   r3d020 := #GD(a3d020);
      a3d025 := XMLAdr + '/ДанРас/СведСтКад/СтИмущНеоблК';   r3d025 := #GD(a3d025);
      a3s040 := XMLAdr + '/ДанРас/КодНалЛьг'             ;   r3s040 := #GS(a3s040);
      a3d050 := XMLAdr + '/ДанРас/ДолСт'                 ;   r3d050 := #Dl(a3d050);
      a3d060 := XMLAdr + '/НалПер/НалБаза'               ;   r3d060 := #GD(a3d060);
      a3s070 := XMLAdr + '/ДанРас/КодЛгПНС'              ;   r3s070 := #GS(a3s070);
      a3d080 := XMLAdr + '/ДанРас/НалСтав'               ;   r3d080 := #GD(a3d080);
      a3d090 := XMLAdr + '/ДанРас/КоэфК'                 ;   r3d090 := #Dl(a3d090);
      a3d100 := XMLAdr + '/НалПер/СумНалИсчисл'          ;   r3d100 := #GD(a3d100);
      a3d110 := XMLAdr + '/НалПер/СумАвИсчисл'           ;   r3d110 := #GD(a3d110);
      a3s120 := XMLAdr + '/НалПер/КодЛгУмен'             ;   r3s120 := #GS(a3s120);
      a3d130 := XMLAdr + '/НалПер/СумЛгУмен'             ;   r3d130 := #GD(a3d130);

      // КС :: 1.01, 1.03 - получение данных
      bufR3_KS1_01 += r3d100 - (r3d110 + r3d130);

      // КС :: 1.20
      if not (r3d060 = Round((r3d020 - r3d025) * r3d050))
      then #_KS(a3d060, 'Стр. 060', 'КС 1.20 :: В случае представления налоговой декларации за налоговый период 2014 года и последующие налоговые периоды, р3с60 = (р3с020 - р3с025) * р3с50')

      if (#GS(a3d090) <> '')
      {
        // КС :: 1.21
        if not (r3d100 = Round(r3d060 * r3d080 * r3d090 / 100))
        then #_KS(a3d090, 'Стр. 100', 'КС 1.21 :: Если р3с090 /= "-", то р3с100 = (р3с060 * р3с080 * р3с090) / 100')
      }
      else
      {
        // КС :: 1.22
        if not (r3d100 = Round(r3d060 * r3d080 / 100))
        then #_KS(a3d090, 'Стр. 100', 'КС 1.22 :: Если р3с090 = "-", то р3с100 = (р3с060 * р3с080) / 100')
      }

      // КС :: 1.24
      if (not IsValidKadastr(r3s014))
      then #_KS(a3s014, 'Стр. 014', 'КС 1.24 :: Если р3с014 содержит символы . , ; " / ! @ # $ % /\ & * () _ + | \ - '''' ] [ } { '''' ~ N ? < >, буквы О, З и (или) не соотв. структуре "2 знака:2 знака:6 или 7 знаков:любое кол. знаков", то...')

      // КС :: 1.25
      if (r3s015 <> '')
        if (not IsValidKadastr(r3s015))
        then #_KS(a3s015, 'Стр. 015', 'КС 1.25 :: Если р3с015 содержит символы . , ; " / ! @ # $ % /\ & * () _ + | \ - '''' ] [ } { '''' ~ N ? < >, буквы О, З и (или) не соотв. структуре "2 знака:2 знака:6 или 7 знаков:любое кол. знаков", то...')

      // КС :: 1.26
      if (r3s040 <> '') if (not IsValidR3S040(r3s040))
      then #_KS(a3s040, 'Стр. 040', 'КС 1.26 :: Если р3с040 /= 0 и заполнена вторая часть показателя р3с040, то первые 7 знаков р3с040 = "2012000". Необходимый формат значения: XXXXXXX или XXXXXXX/XXXXXXXXXXXX')

      // КС :: 1.27, 1.28
      if (r3s070 <> '') if (not IsValidR3S070_120(r3s070, '2012400')) #_KS(a3s070, 'Стр. 070', 'КС 1.27 :: Если р3с070 /= 0, то р3с070 первые 7 знаков = "2012400". Необходимый формат значения: XXXXXXX/XXXXXXXXXXXX')
      if (r3s120 <> '') if (not IsValidR3S070_120(r3s120, '2012500')) #_KS(a3s120, 'Стр. 120', 'КС 1.28 :: Если р3с120 /= 0, то р3с120 первые 7 знаков = "2012500". Необходимый формат значения: XXXXXXX/XXXXXXXXXXXX')
    }

    // РАЗДЕЛ 1
    XMLAdr  := 'Файл/Документ/ИмущНД/СумНалПУ' + getIter(i);

    a1d030_040 := XMLAdr + '/НалПУ';   r1d030_040 := #GD(a1d030_040);

    if flKS_KodVIm4
    {
      // КС :: 1.02, 1.04
      if (Round(bufR2_KS1_01 - bufR2_KS1_02) <> 0)
        if not (r1d030_040 = Round(bufR2_KS1_01 - bufR2_KS1_02))
        then #_KS(a1d030_040, 'Стр. 030/040', 'КС 1.2, 1.4 :: Если присутствует Раздел 2 с "Вид имущ." = 4 то  р1с030 = раздел 2 ("Вид имущ." = 1|2|3|5|6 (с220 - (р2с230 + р2с250)) - "Вид имущ." = 4 (МИНИМУМ(с220 - (р2с230 + р2с250), с260)))')
    }
    else
    {
      // КС :: 1.01, 1.03
      if (Round(bufR2_KS1_01 + bufR3_KS1_01) <> 0)
        if not (r1d030_040 = Round(bufR2_KS1_01 + bufR3_KS1_01))
        then #_KS(a1d030_040, 'Стр. 030/040', 'КС 1.1, 1.3 :: Если отсутствует Раздел 2 с "Код вида имущества" = 4 то р1с030 = р2с220 - (р2с230 + р2с250) + р3с100 - (р3с110 + р3с130)')
    }
  }
//----------------------------------------------------------------------------------------------------------
//#endregion ПРОВЕРКА СООТНОШЕНИЙ
//----------------------------------------------------------------------------------------------------------
  Result := True;
@end. // On_CheckKS
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ КОНТРОЛЬНЫХ СООТНОШЕНИЙ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ЭКСПОРТЕ В XML
//------------------------------------------------------------------------------------------------------------
@Script On_ExportXML : boolean;
@begin
  SetFldVal('Файл/ИдФайл', Replace(_IdFail_, '.xml', ''));

  if (not XMLMap.ExpToXml(_XmlFileName_)) {
    Message('On_ExportXML: '+ XMLMap.GetStLastError, cancelButton+Warning);
    Exit;
  }

  message('Выгружен в файл ' + _XmlFileName_);
@end. // On_ExportXML
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ЭКСПОРТЕ В XML
//************************************************************************************************************
