//------------------------------------------------------------------------------------------------------------
//                                                                                    (c) корпорация Галактика
// Галактика 9.1 - модуль "Электронная отчетность"
// Алгоритмы расчета : "Сведения о неполной занятости и движении работников(П-4(НЗ))"
//------------------------------------------------------------------------------------------------------------


//============================================================================================================
//#region ДОБЫЧА ДАННЫХ ИЗ ГАЛАКТИКИ
//------------------------------------------------------------------------------------------------------------
// заголовочные поля
&p_naimorg = &собсорг[рез:наим]
&p_Adr     = &СобсОрг[Рез:Адр]
&p_Tel     = &СобсОрг[Рез:Тел]
&p_OKPO    = &СобсОрг[Рез:ОКПО]

&p_Dolgn   = 'Руководитель организации'
&p_FIO     = &РукПред[Рез:Фам] + ' ' +  &РукПред[Рез:Имя] + ' ' +  &РукПред[Рез:Отч]

//#endregion ДОБЫЧА ДАННЫХ ИЗ ГАЛАКТИКИ
//************************************************************************************************************


@Script GetStrVal(fld:string):string;
@begin
  Result := Trim(XMLMAP.GetAttrValueByName(fld));
@end.

@Script GetDblVal(fld:string):double;
@begin
  Result := Round(Double(XMLMAP.GetAttrValueByName(fld)), 2);
@end.

@Script SetFldVal(fld:string;val:string);
@begin
  XMLMAP.SetAttrValueByName(fld,val);
@end.


//***************************************************************************************************************************************************

@Script MessageErrorStop(mesMesAdrXML: string; mesPri_t: comp; mesStr, mesPole, mesMes: string): boolean;
@begin
  MesError.InsertMesError(mesMesAdrXML, mesPri_t, mesStr, mesPole, mesMes);
  Result := True;
@end.

//***************************************************************************************************************************************************

@Script On_Check : boolean;
  #declare _InsMes(_AdrXML, _Pole)
    MessageErrorStop
    (
      #_AdrXML,
      Prior,
      NameStr,
      #_Pole,
      _Mes
    );
  #end

  #declare _InsMesDop(_AdrXML, _Pole, _Mes)
    MessageErrorStop
    (
      #_AdrXML,
      Prior,
      NameStr,
      #_Pole,
      #_Mes
    );
  #end
@begin
  var Prior: Comp;
  var  NameStr, _Mes: string;
  var
    a01, a02, a03, a05, a06, a07, a08, a09, a10, a12, a13, a14, a15, a16, a17 :string;
  var
    d01, d02, d03, d05, d06, d07, d08, d09, d10, d12, d13, d14, d15, d16, d17, sum :double;

  a01 := 'Документ/НеполЗанят/ЧРИнцРаб'            ;  d01 := GetDblVal(a01);
  a02 := 'Документ/НеполЗанят/ЧРСоглаш'            ;  d02 := GetDblVal(a02);
  a03 := 'Документ/НеполЗанят/ЧРвПростое'          ;  d03 := GetDblVal(a03);
  a05 := 'Документ/НеполЗанят/ЧРбезСохЗП'          ;  d05 := GetDblVal(a05);
  a06 := 'Документ/НеполЗанят/ЧРПСписСост/Всего'   ;  d06 := GetDblVal(a06);
  a07 := 'Документ/НеполЗанят/ЧРПСписСост/Дополн'  ;  d07 := GetDblVal(a07);
  a08 := 'Документ/НеполЗанят/ЧРВСписСост/Всего'   ;  d08 := GetDblVal(a08);
  a09 := 'Документ/НеполЗанят/ЧРВСписСост/СоглСтор';  d09 := GetDblVal(a09);
  a10 := 'Документ/НеполЗанят/ЧРВСписСост/СокрЧР'  ;  d10 := GetDblVal(a10);
  a12 := 'Документ/НеполЗанят/ЧРВСписСост/СобстЖел';  d12 := GetDblVal(a12);
  a13 := 'Документ/НеполЗанят/ЧРКонКв'             ;  d13 := GetDblVal(a13);
  a14 := 'Документ/НеполЗанят/ЧРРабМКонКВ'         ;  d14 := GetDblVal(a14);
  a15 := 'Документ/НеполЗанят/ЧРНаемСлКв'          ;  d15 := GetDblVal(a15);
  a16 := 'Документ/НеполЗанят/ЧРЖенУхЗаРеб'        ;  d16 := GetDblVal(a16);
  a17 := 'Документ/НеполЗанят/ЧРдо3Лет'            ;  d17 := GetDblVal(a17);

  sum := d08 + d13;

  Prior := Comp(1); NameStr := '02 Раздел 1';
  _mes := 'Значение поля должно быть меньше либо равно';
  if (d01 > sum )  #_InsMesDop(a01, 'Стр. 01', _mes + ' сумме строк 13, 08');
  if (d02 > sum )  #_InsMesDop(a02, 'Стр. 02', _mes + ' сумме строк 13, 08');
  if (d03 > sum )  #_InsMesDop(a03, 'Стр. 03', _mes + ' сумме строк 13, 08');
  if (d05 > sum )  #_InsMesDop(a05, 'Стр. 05', _mes + ' сумме строк 13, 08');
  if (d01 + d02 + d03 + d05 > sum )  #_InsMesDop(a08, 'Стр. 08', 'Сумма строк 13, 08 должна быть больше либо равна сумме строк 01, 02 ,03 ,05');
  if ((d07 <> 0) and (d06 = 0))  #_InsMesDop(a06, 'Стр. 06', 'При наличии данных по строке 07 должна быть заполнена строка 06 ');
  _mes := 'Значение поля должно быть больше либо равно';
  if (d07 > d06)  #_InsMesDop(a06, 'Стр. 06', _mes + ' Стр. 07');
  if (d09 + d10 + d12 > d08) #_InsMesDop(a08, 'Стр. 08', _mes + ' сумме строк 09, 10, 12');
  if (d14 > d13)  #_InsMesDop(a13, 'Стр. 13', _mes + ' Стр. 14');
  if (d15 > d13)  #_InsMesDop(a13, 'Стр. 13', _mes + ' Стр. 15');
  if (d16 > d13)  #_InsMesDop(a13, 'Стр. 13', _mes + ' Стр. 16');
  if (d17 > d13)  #_InsMesDop(a13, 'Стр. 13', _mes + ' Стр. 17');
  //*************************************************************************
  Prior := Comp(2); NameStr := '01 Титульный лист'; _Mes := 'Поле обязательно к заполнению';
  if (GetStrVal('Документ/НаимОрг') = '') #_InsMes('Документ/НаимОрг', '"Наименование организации"');
  if (GetStrVal('Документ/Адрес'  ) = '') #_InsMes('Документ/Адрес'    , '"Адрес организации"'     );
  if (GetStrVal('Документ/ОКПО'   ) = '') #_InsMes('Файл/Документ/ТитЛист/ОКПО'   , '"Код ОКПО"'   );

  Prior := Comp(3); NameStr := '02 Раздел 1';
  if (GetStrVal('Документ/Тел'                ) = '') #_InsMes('Файл/Документ/ТитЛист/Подписант/Тел'  , '"Телефон"'  );
  if (GetStrVal('Документ/Подписант/Должность') = '') #_InsMes('Документ/Подписант/Должность', '"Должность"');
  if (GetStrVal('Документ/Подписант/ФИО'      ) = '') #_InsMes('Документ/Подписант/ФИО'  , '"ФИО"'      );

  if (GetStrVal('Документ/ДатаДок') = '' or GetStrVal('Документ/ДатаДок') = 'ДД мес ГГГГ')
    MessageErrorStop('Документ/ДатаДок', Prior, '17 Раздел 9.3', '"Дата документа"','Поле обязательно к заполнения');

  //*************************************************************************

  MesError.RereadTable;

@end.

@Script On_Recalc : boolean;
@begin
  //Документ
  if ((GetStrVal('Документ/ДатаДок') = '') or (GetStrVal('Документ/ДатаДок') = 'DD mon YYYY'))
     SetFldVal('Документ/ДатаДок', DateToStr(Cur_Date, 'DD mon YYYY'));
@end.

//***************************************************************************************************************************************************

@Script On_ExportXML : boolean;
@begin
  if (not XMLMap.ExpToXml(_XmlFileName_)) {
    Message('On_ExportXML: '+ XMLMap.GetStLastError, cancelButton+Warning);
    Exit;
  }

  message('Выгружен в файл ' + _XmlFileName_);
@end.
