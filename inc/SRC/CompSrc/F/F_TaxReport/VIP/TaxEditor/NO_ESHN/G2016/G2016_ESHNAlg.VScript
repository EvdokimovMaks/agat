//------------------------------------------------------------------------------------------------------------
//                                                                                    (c) корпорация Галактика
// Галактика 9.1 - модуль "Электронная отчетность"
// Алгоритмы расчета : "Декларация по ЕСХН"
//------------------------------------------------------------------------------------------------------------


//============================================================================================================
//#region СЕРВИСНЫЕ МЕТОДЫ
//------------------------------------------------------------------------------------------------------------
@Script GetStrVal(fld:string):string;
@begin
  Result := Trim(String(XMLMAP.GetAttrValueByName(fld)));
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetStrValFormat(fld:string):string;
@begin
  var bufRes: string;

  bufRes := Trim(String(XMLMAP.GetAttrValueByName(fld)));

  if (bufRes = '')
  then Result := '0'
  else if (Double(bufRes) < 0)
       then Result := '('+bufRes+')'
       else Result := bufRes;
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetDblVal(fld:string):double;
@begin
  Result := Double(XMLMAP.GetAttrValueByName(fld));
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetFldVis(fld:string):boolean;
@begin
  Result := False;
  Result := XMLMAP.GetAttrVisByName(fld);
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetFldVisInTree(fld:string):boolean;
@begin
  Result := XMLMAP.GetAttrVisByNameInTree(fld);
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetPatVal(expr:string):double;
#include AllTaxObj.Vih
@begin
  Result := Double(ПАТ(expr));
@end.

//------------------------------------------------------------------------------------------------------------
@Script SetIntVal(fld:string;val:double);
@begin
  XMLMAP.SetVariantAttrValueByName_Formula(fld,DoubleToStr(Round(val), '[|-]3666666666666666666666'),2);
@end.

//------------------------------------------------------------------------------------------------------------
@Script SetFldVal(fld:string; val:string);
@begin
  XMLMAP.SetVariantAttrValueByName_Formula(fld, string(val), 2);
@end.

//------------------------------------------------------------------------------------------------------------
@Script SetNulVal(fld:string);
@begin
  XMLMAP.SetIsNullAttrValueByName(fld, 2);
@end.

//------------------------------------------------------------------------------------------------------------
@Script SetFldVis(fld:string;vis:boolean);
@begin
  XMLMAP.SetAttrVisByName(fld,vis);
@end.

//------------------------------------------------------------------------------------------------------------
@Script MessageErrorStop_ALG(mesMesAdrXML, mesStr, mesPole, mesMes: string; mesCountIter: integer = 0): boolean;
@begin
  MesError.InsertMesError(mesMesAdrXML, Comp(0), mesStr, mesPole, mesMes, mesCountIter);
  Result := True;
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetIter(iter:longint):string;
@begin
  Result := '';

  if (iter > 0)
    Result := '[' + String(iter) + ']';
@end.

//------------------------------------------------------------------------------------------------------------
@Script DTS_I(zName: string): string;
@begin
  Result := DoubleToStr(Round(Double(zName)), '[|-]366666666666666666666');
@end.

//------------------------------------------------------------------------------------------------------------
@Script GetStrNull(fld:string):boolean;
@begin
  Result := XMLMAP.GetIsNullAttrValueByName(fld);
@end.
//#endregion СЕРВИСНЫЕ МЕТОДЫ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ УСТАНОВКЕ ВИДИМОСТИ ПОЛЕЙ
//------------------------------------------------------------------------------------------------------------
@Script On_Visable : boolean;
  #declare IfVis(adrIfVis)
    SetFldVis(#adrIfVis, not GetStrNull(#adrIfVis));
  #end

  #declare SetVisF(adrIfVis)
    SetFldVis(#adrIfVis, False);
  #end

  #declare SetVisT(adrIfVis)
    SetFldVis(#adrIfVis, True);
  #end
@begin
  #IfVis('Файл/Документ/СвНП/Тлф');

  var buf : string;
  buf := GetStrVal('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ФормРеорг');

  if (buf = '0')
  {
    #SetVisT('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ'      );
    #SetVisF('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ИННЮЛ');
    #SetVisF('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/КПП'  );
  }
  else
  {
    if    (buf = '1')
       or (buf = '2')
       or (buf = '3')
       or (buf = '5')
       or (buf = '6')
    {
      #SetVisT('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ'      );
      #SetVisT('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ИННЮЛ');
      #SetVisT('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/КПП'  );
    }
    else #SetVisF('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ');
  }

  #IfVis('Файл/Документ/СвНП/НПФЛ/ФИО/Отчество');

  if     not GetStrNull('Файл/Документ/Подписант/ФИО/Фамилия')
     and not GetStrNull('Файл/Документ/Подписант/ФИО/Имя'    )
  {
    #SetVisT('Файл/Документ/Подписант/ФИО'         );
    #IfVis  ('Файл/Документ/Подписант/ФИО/Отчество');
  }
  else #SetVisF('Файл/Документ/Подписант/ФИО');

  if    (GetStrVal('Файл/Документ/Подписант/ПрПодп') = '2')
     or not GetStrNull('Файл/Документ/Подписант/СвПред/НаимДок')
  {
    #SetVisT('Файл/Документ/Подписант/СвПред'        );
    #IfVis  ('Файл/Документ/Подписант/СвПред/НаимОрг');
  }
  else #SetVisF('Файл/Документ/Подписант/СвПред');

  #IfVis('Файл/Документ/ЕСХН/АвПУ'               );
  #IfVis('Файл/Документ/ЕСХН/ОКТМО_Пер'          );
  #IfVis('Файл/Документ/ЕСХН/РасчНал/СумДоход'   );
  #IfVis('Файл/Документ/ЕСХН/РасчНал/СумРасход'  );
  #IfVis('Файл/Документ/ЕСХН/РасчНал/СумУбытУмен');

  var CountI, I : integer;
  var XMLAdr    : string ;
  var fl        : boolean;

  //--------------------------------------------------------------------------------------------------------
  //#region Файл/Документ/ЕСХН/РасчУбытУмНБ
  //--------------------------------------------------------------------------------------------------------
  fl := false;
  CountI := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод');

  for (i := 0; i < CountI; i++)
  {
    XMLAdr := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод' + getIter(i);

    if     not GetStrNull(XMLAdr + '/ГодУбыт')
       and not GetStrNull(XMLAdr + '/СумУбыт')
    {
      #SetVisT(XMLAdr);
      fl := true;
    }
    else #SetVisF(XMLAdr);
  }

  if fl and not GetStrNull('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредВс')
  then #SetVisT('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред');
  else #SetVisF('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред');

  fl := false;
  CountI := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед/УбытСледГод');

  for (i := 0; i < CountI; i++)
  {
    XMLAdr := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед/УбытСледГод' + getIter(i);

    if     not GetStrNull(XMLAdr + '/ГодУбыт')
       and not GetStrNull(XMLAdr + '/СумУбыт')
    {
      #SetVisT(XMLAdr);
      fl := true;
    }
    else #SetVisF(XMLAdr);
  }

  if fl and not GetStrNull('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед/УбытСледВс')
  then #SetVisT('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед');
  else #SetVisF('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед');

  #IfVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/СумУбытПер');

  if    GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/СумУбытПер')
     or GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред'  )
     or GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед'  )
  then #SetVisT('Файл/Документ/ЕСХН/РасчУбытУмНБ');
  else #SetVisF('Файл/Документ/ЕСХН/РасчУбытУмНБ');
  //--------------------------------------------------------------------------------------------------------
  //#endregion Файл/Документ/ЕСХН/РасчУбытУмНБ
  //--------------------------------------------------------------------------------------------------------


  //--------------------------------------------------------------------------------------------------------
  //#region Файл/Документ/ЕСХН/ОтчетИсп
  //--------------------------------------------------------------------------------------------------------
  fl := false;
  CountI := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/ОтчетИсп/ОтчетИспКод');

  for (i := 0; i < CountI; i++)
  {
    XMLAdr := 'Файл/Документ/ЕСХН/ОтчетИсп/ОтчетИспКод' + getIter(i);

    if     not GetStrNull(XMLAdr + '/КодВидПост')
       and not GetStrNull(XMLAdr + '/ДатаПост'  )
       and not GetStrNull(XMLAdr + '/СумДенСред')
       and not GetStrNull(XMLAdr + '/СрокИсп'   )
    {
      fl := true;

      #SetVisT(XMLAdr                  );
      #IfVis  (XMLAdr + '/СумИспСрок'  );
      #IfVis  (XMLAdr + '/СумИспНеСрок');
      #IfVis  (XMLAdr + '/СумНеИспСрок');
    }
    else #SetVisF(XMLAdr);
  }

  if fl and not GetStrNull('Файл/Документ/ЕСХН/ОтчетИсп/СумДенСредИт')
  {
    #SetVisT('Файл/Документ/ЕСХН/ОтчетИсп'               );
    #IfVis  ('Файл/Документ/ЕСХН/ОтчетИсп/СумИспСрокИт'  );
    #IfVis  ('Файл/Документ/ЕСХН/ОтчетИсп/СумИспНеСрокИт');
    #IfVis  ('Файл/Документ/ЕСХН/ОтчетИсп/СумНеИспСрокИт');
  }
  else #SetVisF('Файл/Документ/ЕСХН/ОтчетИсп');
  //--------------------------------------------------------------------------------------------------------
  //#endregion Файл/Документ/ЕСХН/ОтчетИсп
  //--------------------------------------------------------------------------------------------------------
@end.
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ УСТАНОВКЕ ВИДИМОСТИ ПОЛЕЙ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ
//------------------------------------------------------------------------------------------------------------
@Script On_Check : boolean;
  //==========================================================================================================
  //#region СЕРВИС On_Check
  //----------------------------------------------------------------------------------------------------------
  #include AllTaxObj.Vih

  //----------------------------------------------------------------------------------------------------------
  function IsValidPeriod(strIn: string; var strGetMes: string) : boolean;
  begin
    //Налоговый период
    //Период
    strGetMes := 'Возможные значения: 34, 50, 95 или 96';

    Result := False;
    Result := CheckError.isRegExpr('^(34|50|95|96)$', strIn);
  end;

  //----------------------------------------------------------------------------------------------------------
  function IsValidPoMestu(strIn: string; var strGetMes: string) : boolean;
  begin
    //По месту нахождения (учета)
    //ПоМесту
    strGetMes := 'Возможные значения: 120, 213, 214, 215, 216 или 331';

    Result := False;
    Result := CheckError.isRegExpr('^(120|213|214|215|216|331)$', strIn);
  end;

  //----------------------------------------------------------------------------------------------------------
  function IsValidKodVidPost(strIn: string; var strGetMes: string) : boolean;
  begin
    //Код вида поступлений
    //КодВидПост
    strGetMes := 'Возможные значения: 010, 020, 030, 120, 130, 140, 141, 160, 170, 171, 172, 173, 180, 220, 281, 282, 321, 322, 323, 324, 340, 360, 400 или 500';

    Result := False;
    Result :=    CheckError.isRegExpr('^(010|020|030|120|130|140|141|160|170|171|172|173)$', strIn)
              or CheckError.isRegExpr('^(180|220|281|282|321|322|323|324|340|360|400|500)$', strIn);
  end;

  //----------------------------------------------------------------------------------------------------------
  #define _NoKor(_AdrXML, _Pole)         MessageErrorStop_ALG ( #_AdrXML, NameStr, #_Pole, 'Поле не соответствует формату. ' + getMes );
  #define _NoKorMes(_AdrXML, _Pole, _Mes)MessageErrorStop_ALG ( #_AdrXML, NameStr, #_Pole, #_Mes );

   //----------------------------------------------------------------------------------------------------------
  #declare _IsValidNoKor(_Valid, _AdrXML, _Pole, _Iter=0)
    if not #_Valid(GetStrVal(#_AdrXML), getMes)
      MessageErrorStop_ALG
      (
        #_AdrXML
      , NameStr
      , #_Pole
      , 'Поле не соответствует формату. ' + getMes
      , #_Iter
      );
  #end

   //----------------------------------------------------------------------------------------------------------
  #declare _IsValidNoKorVis(_Valid, _AdrXML, _Pole, _Iter=0)
    if GetFldVis(#_AdrXML)
      if not #_Valid(GetStrVal(#_AdrXML), getMes)
        MessageErrorStop_ALG
        (
          #_AdrXML
        , NameStr
        , #_Pole
        , 'Поле не соответствует формату. ' + getMes
        , #_Iter
        );
  #end

  #declare _IsValidNoKorDecimal(_StrMes, _AdrXML, _Pole, _Cel, _Drob, _flMin)
    if not IsValidDecimal(GetStrVal(#_AdrXML), #_Cel, #_Drob, getMes, #_flMin)
      MessageErrorStop_ALG
      (
        #_AdrXML,
        NameStr,
        #_Pole,
        'Поле не соответствует формату. Формат поля '+  #_StrMes + getMes
      );
  #end

  //#endregion СЕРВИС On_Check
  //**********************************************************************************************************
@begin
  var NameStr, XMLAdr, getMes
    , a030r2, a040r2
    , a130r21, aSumUb : string = '';
  var CountI, I : longInt = 0;
  var bufSumUb : double = 0.0;


  NameStr := pFH.Func('myGet_Struct');

  if not IsValidKND(GetStrVal('Файл/Документ/КНД'), '1151059', getMes)
    #_NoKor('Файл/Документ/КНД', 'Код налоговой декларации');


  //------------------------------------------------------------------------------------------------------------
  //#region ТИТУЛЬНЫЙ ЛИСТ
  //------------------------------------------------------------------------------------------------------------
  NameStr := pFH.Func('myGetP01');

  #_IsValidNoKor   (IsValidDate   , 'Файл/Документ/ДатаДок'          , 'Дата документа'             );
  #_IsValidNoKor   (IsValidOtchGod, 'Файл/Документ/ОтчетГод'         , 'Отчетный год'               );
  #_IsValidNoKor   (IsValidSONO   , 'Файл/Документ/КодНО'            , 'Код налогового органа'      );
  #_IsValidNoKor   (IsValidNomKorr, 'Файл/Документ/НомКорр'          , 'Номер корректировки'        );
  #_IsValidNoKor   (IsValidOKVED  , 'Файл/Документ/СвНП/ОКВЭД'       , 'Код ОКВЭД'                  );
  #_IsValidNoKor   (IsValidNaimOrg, 'Файл/Документ/СвНП/НПЮЛ/НаимОрг', 'Наименование организации'   );
  #_IsValidNoKor   (IsValidINNUL  , 'Файл/Документ/СвНП/НПЮЛ/ИННЮЛ'  , 'ИНН'                        );   #_IsValidNoKor(CheckError.Prov_INN, 'Файл/Документ/СвНП/НПЮЛ/ИННЮЛ', 'ИНН');
  #_IsValidNoKor   (IsValidKPP    , 'Файл/Документ/СвНП/НПЮЛ/КПП'    , 'КПП'                        );
  #_IsValidNoKor   (IsValidPrPodp , 'Файл/Документ/Подписант/ПрПодп' , 'Признак подписанта'         );
  #_IsValidNoKorVis(IsValidTlf    , 'Файл/Документ/СвНП/Тлф'         , 'Контактный телефон'         );
  #_IsValidNoKorVis(IsValidPeriod , 'Файл/Документ/Период'           , 'Налоговый период'           );
  #_IsValidNoKorVis(IsValidPoMestu, 'Файл/Документ/ПоМесту'          , 'По месту нахождения (учета)');

  if GetFldVis('Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ')
  {
    #_IsValidNoKor   (IsValidFormReorg, 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ФормРеорг', 'Форма реоргазации');
    #_IsValidNoKorVis(IsValidINNUL    , 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ИННЮЛ'    , 'ИНН'              );   #_IsValidNoKorVis(CheckError.Prov_INN, 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/ИННЮЛ', 'ИНН');
    #_IsValidNoKorVis(IsValidKPP      , 'Файл/Документ/СвНП/НПЮЛ/СвРеоргЮЛ/КПП'      , 'КПП'              );
  }

  #_IsValidNoKor   (IsValidFIO, 'Файл/Документ/Подписант/ФИО/Фамилия' , 'Фамилия' );
  #_IsValidNoKor   (IsValidFIO, 'Файл/Документ/Подписант/ФИО/Имя'     , 'Имя'     );
  #_IsValidNoKorVis(IsValidFIO, 'Файл/Документ/Подписант/ФИО/Отчество', 'Отчество');

  if GetFldVis('Файл/Документ/Подписант/СвПред')
  {
    #_IsValidNoKor   (IsValidNaimDok, 'Файл/Документ/Подписант/СвПред/НаимДок', 'Наименование документа');
    #_IsValidNoKorVis(IsValidNaimOrg, 'Файл/Документ/Подписант/СвПред/НаимОрг', 'Наименование организации');
  }
  //------------------------------------------------------------------------------------------------------------
  //#endregion ТИТУЛЬНЫЙ ЛИСТ
  //------------------------------------------------------------------------------------------------------------


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 1
  //------------------------------------------------------------------------------------------------------------
  NameStr := pFH.Func('myGetP02');

                                               #_IsValidNoKor       (IsValidOKTMO  , 'Файл/Документ/ЕСХН/ОКТМО'    , 'Стр. 001'                  );
  if GetFldVis('Файл/Документ/ЕСХН/АвПУ'     ) #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/АвПУ'     , 'Стр. 002'    , 12, 0, False);
  if GetFldVis('Файл/Документ/ЕСХН/ОКТМО_Пер') #_IsValidNoKor       (IsValidOKTMO  , 'Файл/Документ/ЕСХН/ОКТМО_Пер', 'Стр. 003'                  );
                                               #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/НалПУ'    , 'Стр. 004/005', 12, 0,  True);
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 1
  //------------------------------------------------------------------------------------------------------------


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 2
  //------------------------------------------------------------------------------------------------------------
  NameStr := pFH.Func('myGetP02');

  a030r2 := 'Файл/Документ/ЕСХН/РасчНал/НалБаза'    ;
  a040r2 := 'Файл/Документ/ЕСХН/РасчНал/СумУбытУмен';

  if not(GetDblVal(a040r2) <= GetDblVal(a030r2))
  then #_NoKorMes(a040r2, 'Стр. 040', 'Значение стр. 040 должно быть меньше либо равно значению стр. 030');

  if GetFldVis('Файл/Документ/ЕСХН/РасчНал/СумДоход'    ) #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчНал/СумДоход'    , 'Стр. 010', 12, 0, False);
  if GetFldVis('Файл/Документ/ЕСХН/РасчНал/СумРасход'   ) #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчНал/СумРасход'   , 'Стр. 020', 12, 0, False);
                                                          #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчНал/НалБаза'     , 'Стр. 030', 12, 0, False);
  if GetFldVis('Файл/Документ/ЕСХН/РасчНал/СумУбытУмен' ) #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчНал/СумУбытУмен' , 'Стр. 040', 12, 0, False);
                                                          #_IsValidNoKorDecimal('X.X'         , 'Файл/Документ/ЕСХН/РасчНал/Ставка'      , 'Стр. 045',  1, 1, False);
                                                          #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчНал/СумНалИсчисл', 'Стр. 050', 12, 0, False);
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 2
  //------------------------------------------------------------------------------------------------------------


  var fl : boolean;


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 2.1
  //------------------------------------------------------------------------------------------------------------
  if GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ')
  {
    NameStr := pFH.Func('myGetP03');

    if GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/СумУбытПер') #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчУбытУмНБ/СумУбытПер', 'Стр. 120', 12, 0, False);

    if GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред')
    {
      #_IsValidNoKorDecimal('XXXXXXXXXXXX', 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредВс', 'Стр. 010', 12, 0, False);

      CountI := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод');
      fl := false;

      for (i := 0; i < CountI; i++)
      {
        XMLAdr := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод' + getIter(i);

        if not GetFldVis(XMLAdr) continue;

        #_IsValidNoKor       (IsValidOtchGod, XMLAdr + '/ГодУбыт', 'Стр. '+ LPadCh(String(i+2), '0', 2) +'0: год убытка'                , -1);
        #_IsValidNoKorDecimal('XXXXXXXXXXXX', XMLAdr + '/СумУбыт', 'Стр. '+ LPadCh(String(i+2), '0', 2) +'0: сумма убытка', 12, 0, False, -1);

        fl := true;
      }

      if not fl
      then #_NoKorMes('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредВс', 'Стр. 010', 'Должна быть заполнена хотя бы одна из строк 020 - 110');
    }

    if GetFldVis('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед')
    {
      a130r21 := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед/УбытСледВс';

      #_IsValidNoKorDecimal('XXXXXXXXXXXX', a130r21, 'Стр. 010', 12, 0, False);

      CountI := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод');
      fl := false;

      for (i := 0; i < CountI; i++)
      {
        XMLAdr := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед/УбытСледГод' + getIter(i);

        if not GetFldVis(XMLAdr) continue;

        aSumUb := XMLAdr + '/СумУбыт';   bufSumUb += GetDblVal(aSumUb);

        #_IsValidNoKor       (IsValidOtchGod, XMLAdr + '/ГодУбыт', 'Стр. '+ LPadCh(String(i+14), '0', 2) +'0: год убытка'                , -1);
        #_IsValidNoKorDecimal('XXXXXXXXXXXX', XMLAdr + '/СумУбыт', 'Стр. '+ LPadCh(String(i+14), '0', 2) +'0: сумма убытка', 12, 0, False, -1);

        fl := true;
      }

      if not fl
      then #_NoKorMes(a130r21, 'Стр. 130', 'Должна быть заполнена хотя бы одна из строк 140 - 230');

      if GetFldVis(a130r21)
      {
        if not(GetDblVal(a130r21) = bufSumUb)
        then #_NoKorMes(a130r21, 'Стр. 130', 'Значение стр. 130 должно быть равно сумме строк 140 - 230');
      }
    }
  }
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 2.1
  //------------------------------------------------------------------------------------------------------------


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 3
  //------------------------------------------------------------------------------------------------------------
  if GetFldVis('Файл/Документ/ЕСХН/ОтчетИсп')
  {
    NameStr := pFH.Func('myGetP04');

    CountI  := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/ОтчетИсп/ОтчетИспКод');
    fl := false;

    for (i := 0; i < CountI; i++)
    {
      XMLAdr := 'Файл/Документ/ЕСХН/ОтчетИсп/ОтчетИспКод' + getIter(i);

      if not GetFldVis(XMLAdr) continue;

                                             #_IsValidNoKor       (IsValidKodVidPost, XMLAdr + '/КодВидПост'  , 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 1'              , -1);
                                             #_IsValidNoKor       (IsValidDate      , XMLAdr + '/ДатаПост'    , 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 2'              , -1);
                                             #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , XMLAdr + '/СумДенСред'  , 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 3', 12, 0, False, -1);
      if GetFldVis(XMLAdr + '/СумИспСрок'  ) #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , XMLAdr + '/СумИспСрок'  , 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 4', 12, 0, False, -1);
                                             #_IsValidNoKor       (IsValidDate      , XMLAdr + '/СрокИсп'     , 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 5'              , -1);
      if GetFldVis(XMLAdr + '/СумИспНеСрок') #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , XMLAdr + '/СумИспНеСрок', 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 6', 12, 0, False, -1);
      if GetFldVis(XMLAdr + '/СумНеИспСрок') #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , XMLAdr + '/СумНеИспСрок', 'Стр. '+ LPadCh(String(i+1), '0', 2) +', гр. 7', 12, 0, False, -1);

      fl := true;
    }

    if not fl
    then #_NoKorMes('Файл/Документ/ЕСХН/ОтчетИсп/СумДенСредИт', 'Итого по отчету, гр. 3', 'Должна быть заполнена хотя бы одна строка с информацией');

                          #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , 'Файл/Документ/ЕСХН/ОтчетИсп/СумДенСредИт'  , 'Итого по отчету, гр. 3', 12, 0, False);
    if GetFldVis(a130r21) #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , 'Файл/Документ/ЕСХН/ОтчетИсп/СумИспСрокИт'  , 'Итого по отчету, гр. 4', 12, 0, False);
    if GetFldVis(a130r21) #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , 'Файл/Документ/ЕСХН/ОтчетИсп/СумИспНеСрокИт', 'Итого по отчету, гр. 6', 12, 0, False);
    if GetFldVis(a130r21) #_IsValidNoKorDecimal('XXXXXXXXXXXX'   , 'Файл/Документ/ЕСХН/ОтчетИсп/СумНеИспСрокИт', 'Итого по отчету, гр. 7', 12, 0, False);
  }
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 3
  //------------------------------------------------------------------------------------------------------------
@end.
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ПРОВЕРКЕ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ РАСЧЕТЕ
//------------------------------------------------------------------------------------------------------------
@Script On_Recalc : boolean;
  //==========================================================================================================
  //#region СЕРВИС On_Recalc
  //----------------------------------------------------------------------------------------------------------
  #declare _Log(_strAdr, _strMes, _strVal)
    MesError.InsertRasch(NameStr, #_strAdr, #_strMes, #_strVal);
  #end

  #declare _GV(_strAdr)
    GetStrValFormat(#_strAdr)
  #end
  //#endregion СЕРВИС On_Recalc
  //**********************************************************************************************************
@begin
  On_Visable;
  //-----------------------------------------------------

  if ((GetStrVal('Файл/Документ/ДатаДок') = '') or (GetStrVal('Файл/Документ/ДатаДок') = 'ДД.ММ.ГГГГ'))
    SetFldVal('Файл/Документ/ДатаДок', DateToStr(Cur_Date, XMLMap.GetAttrVFormatByName('Файл/Документ/ДатаДок')));

  var XMLAdr, NameStr : string ;
  var CountI, I       : integer;
  var fl              : boolean;

  var a002r1, a004005r1
    , a010r2, a020r2, a030r2, a040r2, a045r2, a050r2
    , a010r21, a120r21, a130r21, aSumUb
    , v004005r1
    , v030r2, v050r2
    , vSumUb, v120r21, v130r21 : string = '';
  var bufSumUb : double = 0.0;

  NameStr := pFH.Func('myGetP02');

  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 2
  //------------------------------------------------------------------------------------------------------------
  a010r2 := 'Файл/Документ/ЕСХН/РасчНал/СумДоход'    ;
  a020r2 := 'Файл/Документ/ЕСХН/РасчНал/СумРасход'   ;
  a030r2 := 'Файл/Документ/ЕСХН/РасчНал/НалБаза'     ;
  a040r2 := 'Файл/Документ/ЕСХН/РасчНал/СумУбытУмен' ;
  a045r2 := 'Файл/Документ/ЕСХН/РасчНал/Ставка'      ;
  a050r2 := 'Файл/Документ/ЕСХН/РасчНал/СумНалИсчисл';

  if (GetDblVal(a010r2) > GetDblVal(a020r2))
  {
    SetIntVal(a030r2, GetDblVal(a010r2) - GetDblVal(a020r2));
    v030r2 := #_GV(a030r2)+' = '+#_GV(a010r2)+' - '+#_GV(a020r2);
  }
  else
  {
    SetIntVal(a030r2, 0);
    v030r2 := 'Поле равно 0, т.к. значение стр. 010 НЕ больше значения стр. 020';
  }

  SetIntVal(a050r2, (GetDblVal(a030r2) - GetDblVal(a040r2)) * GetDblVal(a045r2) / 100);
  v050r2 := #_GV(a050r2)+' = ('+#_GV(a030r2)+' - '+#_GV(a040r2)+') * '+#_GV(a045r2)+' / 100';

  #_Log(a030r2, 'Стр. 030 = стр. 010 - стр. 020'                   , v030r2);
  #_Log(a050r2, 'Стр. 050 = (стр. 030 - стр. 040) * стр. 045 / 100', v050r2);
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 2
  //------------------------------------------------------------------------------------------------------------


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 1
  //------------------------------------------------------------------------------------------------------------
  a002r1    := 'Файл/Документ/ЕСХН/АвПУ';
  a004005r1 := 'Файл/Документ/ЕСХН/НалПУ';

  SetIntVal(a004005r1, GetDblVal(a050r2) - GetDblVal(a002r1));
  v004005r1 := #_GV(a004005r1)+' = '+#_GV(a050r2)+' - '+#_GV(a002r1);

  #_Log(a004005r1, 'Стр. 004/005 = Раздел 2 (стр. 050) - стр. 002', v004005r1);
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 1
  //------------------------------------------------------------------------------------------------------------


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 2.1
  //------------------------------------------------------------------------------------------------------------
  NameStr := pFH.Func('myGetP03');
  CountI  := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод');

  a010r21 := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредВс';
  a120r21 := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/СумУбытПер'         ;
  a130r21 := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытСлед/УбытСледВс';

  for (i := 0; i < CountI; i++)
  {
    XMLAdr := 'Файл/Документ/ЕСХН/РасчУбытУмНБ/УбытПред/УбытПредГод' + getIter(i);

    if not GetFldVis(XMLAdr) continue;

    aSumUb := XMLAdr + '/СумУбыт';   bufSumUb += GetDblVal(aSumUb);   vSumUb := vSumUb+' + '+#_GV(aSumUb);
  }

  SetIntVal(a010r21, bufSumUb);

  if     (GetFldVisInTree(a010r2) or GetFldVisInTree(a020r2))
     and (GetDblVal      (a010r2)  < GetDblVal      (a020r2))
  {
    SetIntVal(a120r21, GetDblVal(a020r2) - GetDblVal(a010r2));
    v120r21 := #_GV(a120r21)+' = '+#_GV(a020r2)+' - '+#_GV(a010r2);
  }
  else
  {
    SetNulVal(a120r21);
    v120r21 := 'Поле очищено, т.к. оно не заполняется если значение стр. 010 Раздела 2 НЕ меньше значения стр. 020 Раздела 2';
  }

  if (GetFldVisInTree(a040r2))
  {
    SetIntVal(a130r21, GetDblVal(a010r21) - GetDblVal(a040r2) + GetDblVal(a120r21));
    v130r21 := #_GV(a130r21)+' = '+#_GV(a010r21)+' - '+#_GV(a040r2)+' + '+#_GV(a120r21);
  }
  else
  {
    SetIntVal(a130r21, GetDblVal(a010r21) + GetDblVal(a120r21));
    v130r21 := #_GV(a130r21)+' = '+#_GV(a010r21)+' - 0 + '+#_GV(a120r21);
  }

  vSumUb := Trim(SubStr(vSumUb, 4, Length(vSumUb) - 3));  vSumUb := if (vSumUb = '', '0 = 0', #_GV(a130r21)+' = '+vSumUb);

  #_Log(a010r21, 'Стр. 010 = сумма строк 020 - 110'                    , vSumUb );
  #_Log(a120r21, 'Стр. 120 = Раздел 2 (cтр. 020 - стр. 010)'           , v120r21);
  #_Log(a130r21, 'Стр. 130 = cтр. 010 - Раздел 2 (стр. 040) + стр. 120', v130r21);
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 2.1
  //------------------------------------------------------------------------------------------------------------


  //------------------------------------------------------------------------------------------------------------
  //#region РАЗДЕЛ 3
  //------------------------------------------------------------------------------------------------------------
  NameStr := pFH.Func('myGetP04');
  CountI  := XMLMap.GetNodeCountByName('Файл/Документ/ЕСХН/ОтчетИсп/ОтчетИспКод');

  var bufGr3, bufGr4, bufGr6, bufGr7 : double = 0.0;
  var vGr3, vGr4, vGr6, vGr7
    , aGr3, aGr4, aGr6, aGr7 : string = '';

  for (i := 0; i < CountI; i++)
  {
    XMLAdr := 'Файл/Документ/ЕСХН/ОтчетИсп/ОтчетИспКод' + getIter(i);

    if not GetFldVis(XMLAdr) continue;

    aGr3 := XMLAdr + '/СумДенСред'  ;                        bufGr3 += GetDblVal(aGr3); vGr3 := vGr3+' + '+#_GV(aGr3);
    aGr4 := XMLAdr + '/СумИспСрок'  ;   if GetFldVis(aGr4) { bufGr4 += GetDblVal(aGr4); vGr4 := vGr4+' + '+#_GV(aGr4); }
    aGr6 := XMLAdr + '/СумИспНеСрок';   if GetFldVis(aGr6) { bufGr6 += GetDblVal(aGr6); vGr6 := vGr6+' + '+#_GV(aGr6); }
    aGr7 := XMLAdr + '/СумНеИспСрок';   if GetFldVis(aGr7) { bufGr7 += GetDblVal(aGr7); vGr7 := vGr7+' + '+#_GV(aGr7); }
  }

  aGr3 := 'Файл/Документ/ЕСХН/ОтчетИсп/СумДенСредИт'  ;
  aGr4 := 'Файл/Документ/ЕСХН/ОтчетИсп/СумИспСрокИт'  ;
  aGr6 := 'Файл/Документ/ЕСХН/ОтчетИсп/СумИспНеСрокИт';
  aGr7 := 'Файл/Документ/ЕСХН/ОтчетИсп/СумНеИспСрокИт';

  SetIntVal(aGr3, bufGr3);
  SetIntVal(aGr4, bufGr4);
  SetIntVal(aGr6, bufGr6);
  SetIntVal(aGr7, bufGr7);

  vGr3 := Trim(SubStr(vGr3, 4, Length(vGr3) - 3));  vGr3 := if (vGr3 = '', '0 = 0', #_GV(aGr3)+' = '+vGr3);
  vGr4 := Trim(SubStr(vGr4, 4, Length(vGr4) - 3));  vGr4 := if (vGr4 = '', '0 = 0', #_GV(aGr4)+' = '+vGr4);
  vGr6 := Trim(SubStr(vGr6, 4, Length(vGr6) - 3));  vGr6 := if (vGr6 = '', '0 = 0', #_GV(aGr6)+' = '+vGr6);
  vGr7 := Trim(SubStr(vGr7, 4, Length(vGr7) - 3));  vGr7 := if (vGr7 = '', '0 = 0', #_GV(aGr7)+' = '+vGr7);

  #_Log(aGr3, 'Итого по отчету (по гр. 3) = сумме строк по гр. 3', vGr3);
  #_Log(aGr4, 'Итого по отчету (по гр. 4) = сумме строк по гр. 4', vGr4);
  #_Log(aGr6, 'Итого по отчету (по гр. 5) = сумме строк по гр. 5', vGr6);
  #_Log(aGr7, 'Итого по отчету (по гр. 7) = сумме строк по гр. 7', vGr7);
  //------------------------------------------------------------------------------------------------------------
  //#endregion РАЗДЕЛ 3
  //------------------------------------------------------------------------------------------------------------
@end.
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ РАСЧЕТЕ
//************************************************************************************************************


//============================================================================================================
//#region ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ЭКСПОРТЕ В XML
//------------------------------------------------------------------------------------------------------------
@Script On_ExportXML : boolean;
@begin
  SetFldVal('Файл/ИдФайл', Replace(_IdFail_, '.xml', ''));

  if (not XMLMap.ExpToXml(_XmlFileName_)) {
    Message('On_ExportXML: '+ XMLMap.GetStLastError, cancelButton+Warning);
    Exit;
  }

  message('Выгружен в файл ' + _XmlFileName_);
@end.
//#endregion ДЕЙСТВИЯ ВЫПОЛНЯЕМЫЕ ПРИ ЭКСПОРТЕ В XML
//************************************************************************************************************
