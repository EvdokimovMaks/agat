//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.10 - Спецоснастка
// Печать отчета о наличии спецоснастки по срокам запасов в формате FastReport
//------------------------------------------------------------------------------

//-------------------------

procedure InitFilters;
{
  fltNum := 0;
  delete all Filters;

  InitFltByType(if (pCommand > 0, Word(53), Word(23)), fltNom, nmNom);
  InitFltByType(Word(11), fltPod, nmPod);
  InitFltByType(Word(8),  fltMol, nmMol);
  InitFltByType(Word(6),  fltOrg, nmOrg);
  InitFltByType(if (pCommand > 0, Word(44), Word(2)),  fltGrp, nmGrp);
  InitFltByType(Word(45), fltCrd, nmCrd);
  InitFltByType(Word(56), fltMet, nmMet);
  InitFltClassFR(fltClass, nmClass);
}

//-------------------------

DataStream dtsRNalOMBP
(
  [CommonFormHeader] CommonFormHeader;
  [TypeRep]          short;
  [MBPKind]          pCommand;
  [dFor]             dFor;
  [Place]            GetExplStr;
  [GroupingName]     if (isValid(#Grouping), Grouping.Name, '');
  [MyOrgName]        iOrgInfo.GetMyOrgNameT(dFor);
  [NDE_ShName]       sGetTune('NDE.ShortRub');

  Table Filters
  (
    [FiltName] Filters.FiltName;
  );

  Table tblOperList
  (
    [isLeaf]   tblOperList.isLeaf;
    [OpName]   tblOperList.OpName;
    [OpDate]   tblOperList.OpDate;
    [Price]    tblOperList.Price;
    [Kol]      tblOperList.Kol;
    [Perc]     tblOperList.Perc;
    [Expl]     tblOperList.Expl;
  );

  DataSet AgeList
  (
    [dBeg]   if (pStepFR = 0, 'Общая сумма', if (pStepFR = NStep + 1, 'до', 'c '+ DateToStr(Add_Day(SubSteps(pStepFR), 1), 'DD/MM/YYYY')));
    [dEnd]   if (pStepFR = 0, 'на дату', if (pStepFR = NStep + 1, DateToStr(SubSteps(NStep), 'DD/MM/YYYY'), 'по '+ DateToStr(SubSteps(pStepFR - 1), 'DD/MM/YYYY')));

    [Stoim]  tblStoimList.Stoim;
    [Iznos]  tblStoimList.Iznos;
  );

  DataSet Signers
  (
    [NRec]  iFeeSigners.GetSignerNRec;
    [FIO]   iFeeSigners.GetSignerFIO(1);
    [TabN]  iFeeSigners.GetSignerTabN;
    [Post]  iFeeSigners.GetSignerPosition(1);
    [Role]  iFeeSigners.GetSignerRole;
  );
)

HandleEvent DataSet Signers
//-------------------------
  cmPreProcess:
  {
    lSign := 0;

    if (not iFeeSigners.FindFeeSignersByNRec(cSigners))
      AbortProcess;
  }
//-------------------------
  cmOnProcess:
  {
    if (not NextVisual)
      BreakProcess;

    lSign++;

    if (lSign = 1)
    {
      if (iFeeSigners.GetSignerFirst)
        ContinueDataSet;
    }
    else
    {
      if (iFeeSigners.GetSignerNext)
        ContinueDataSet;
    }
  }
//-------------------------
End; // HandleEvent DataSet Signers

HandleEvent DataSet AgeList
//-------------------------
  cmPreProcess:
    pStepFR := MaxWord;
//-------------------------
  cmOnProcess:
  {
    if (not NextVisual)
      BreakProcess;

    if (pStepFR = MaxWord)
    {
      set pStepFR := 0;
      ContinueDataset;
    }
    else
      if (pStepFR < NStep + 1)
      {
        set pStepFR := pStepFR + 1;
        ContinueDataset;
      }
  }
//-------------------------
End; // HandleEvent DataSet AgeList

HandleEvent Table tblOperList
//-------------------------
  cmOnProcess:
  {
    if (not NextVisual)
      BreakProcess;
  }
//-------------------------
End; // HandleEvent Table tblOperList

HandleEvent Table Filters
//-----------------------
  cmPreProcess:
    InitFilters;
//-----------------------
  cmOnProcess:
  {
    if (not NextVisual)
      BreakProcess;
  }
//-------------------------
End; // HandleEvent DataSet Filters

HandleEvent // HandleEvent DataStream dtsRIznMBP
//---------
  cmPreProcess:
    StartNewVisual(vtRotateVisual, vfTimer + vfBreak + vfConfirm, 'Формирование ведомости наличия в FastReport', 0);
//---------
  cmPostProcess:
    StopVisual('', 0);
//---------
end; // HandleEvent DataStream dtsRNalOMBP
end; // DataStream dtsRNalOMBP
