//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 8.10 - Спецоснастка
// Печать реестров документов "Спецоснастки" в формате FastReport
//------------------------------------------------------------------------------

procedure PrintInOperFR;
{
  if ((dType = mbpInExDoc) and (fltSrok > 0))
    if (not IsCalcSpSrok)
      Exit;

  ClearBuffer(#tblSpecList);
  tblSpecList.Npp     := tblDocList.Npp;
  tblSpecList.MBPNNum := KatMBP.NNumber;
  tblSpecList.MBPName := KatMBP.Name;
  tblSpecList.EdName  := KatEd.Name;
  tblSpecList.Price   := MBPIn.Price;
  tblSpecList.Kol     := MBPIn.KolDoc;

  case (dType) of
  //-------------
    mbpPostDoc,
    mbpOverDoc,
    mbpInExDoc:
      tblSpecList.PercIzn := MBPIn.PercNach;
  //-------------
  else
    tblSpecList.PercIzn := MBPIn.PercDoc;
  //-------------
  end;

  if (dType = mbpNewPDoc)
    tblSpecList.Stoim := if (wGetTune('Fin.MBP.OstStoim') > 0,
                           (MBPIn.Summa - MBPOut.Summa) - (MBPIn.SummaIsnP - MBPOut.SummaIsn), MBPIn.Summa - MBPOut.Summa);
  else
    tblSpecList.Stoim := if (wGetTune('Fin.MBP.OstStoim') > 0,
                           MBPIn.Summa - (MBPIn.SummaIsnP + MBPIn.SummaIsnN), MBPIn.Summa);

  if (GetDocGroup = mbpDGFTPrs)
    if (GetFirst PersCard where (( MBPIn.cPers == PersCard.nRec )) = tsOk)
      tblSpecList.DopInfo := Persons.FIO + ' (' + iPersInfo.GetTabNum(PersCard.nRec) + ')';

  Insert Current tblSpecList;

  if (dType = mbpSortDoc)
  {
    ClearBuffer(#tblSpecList);
    tblSpecList.Npp     := tblDocList.Npp;
    tblSpecList.MBPNNum := rKatMBP.NNumber;
    tblSpecList.MBPName := rKatMBP.Name;
    tblSpecList.EdName  := rKatEd.Name;
    tblSpecList.Price   := rMBPIn.Price;
    tblSpecList.Kol     := -MBPOut.Kol;
    tblSpecList.PercIzn := MBPOut.tPercent;
    tblSpecList.Stoim   := -if (wGetTune('Fin.MBP.OstStoim') = 0, MBPOut.Summa, MBPOut.Summa - MBPOut.SummaIsn);
    Insert Current tblSpecList;
  }
}

procedure PrintOutOperFR;
{
  if ((dType = mbpSpisDoc) and (fltPrSpis <> -1))
    if (not IsCalcSpPrSpis)
      Exit;

  ClearBuffer(#tblSpecList);
  tblSpecList.Npp     := tblDocList.Npp;
  tblSpecList.MBPNNum := KatMBP.NNumber;
  tblSpecList.MBPName := KatMBP.Name;
  tblSpecList.EdName  := KatEd.Name;
  tblSpecList.Price   := MBPIn.Price;

  if (dType = mbpIznoDoc)
  {
    tblSpecList.Kol     := MBPOut.tKol;
    tblSpecList.PercIzn := MBPOut.Percent;
    tblSpecList.Stoim   := MBPOut.SummaIsn;
  }
  else
  {
    tblSpecList.Kol     := MBPOut.Kol;
    tblSpecList.PercIzn := MBPOut.tPercent;
    tblSpecList.Stoim   := if (wGetTune('Fin.MBP.OstStoim') = 0, MBPOut.Summa, MBPOut.Summa - MBPOut.SummaIsn);
 }

  if (dType = mbpSpisDoc)
    tblSpecList.DopInfo := MBPSpis.Name;

  Insert Current tblSpecList;
}

procedure PrintSpecFR;
{
  if (isInDoc)
  {
    _LOOP MBPIn
      PrintInOperFR;
  }
  else
  {
    _LOOP MBPOut
      PrintOutOperFR;
  }
}

//------------------------------------------------------------------------------

procedure PrintMBPDocFR;
{
  Npp++;
  ClearBuffer(#tblDocList);
  tblDocList.Npp := Npp;
  tblDocList.DocDate := MBPMove.dMove;
  tblDocList.DocNom  := MBPMove.Nom;
  tblDocList.BarCode := GetDocBarCode(GetDocTiDkGal(MBPMove.Status), MBPMove.nRec);
  tblDocList.DesGr := MBPMove.DesGr;
  tblDocList.Descr := MBPMove.Descr;

  // Разрез "Куда"
  if (isValid(#KatPodr))
    tblDocList.ToPodr := GetFullPodrName(KatPodr.Name, KatPodr.cPodr);

  tblDocList.ToMol := KatMol.Name;

  // Работник
  if (GetDocGroup = mbpDGFTPrs)
    if (GetFirst PersCard where (( MBPMove.cRecs[2] == PersCard.nRec )) = tsOk)
      tblDocList.Pers := Persons.FIO + ' (' + iPersInfo.GetTabNum(PersCard.nRec) + ')';

  // Разрез "Откуда"
  if (GetDocGroup <> mbpDGWhere)
  {
    if (isValid(#KatPodrF))
      tblDocList.FrPodr := GetFullPodrName(KatPodrF.Name, KatPodrF.cPodr);

    tblDocList.FrMol := KatMolF.Name;
  }

  // Сумма по документу
  if (dType = mbpIznoDoc)
    tblDocList.Summa := _SummaIsn;
  else
    tblDocList.Summa := if (wGetTune('Fin.MBP.OstStoim') = 0, _Summa, _Summa - _SummaIsn);

  Insert Current tblDocList;

  // Печать спецификации документа
  if (wParam = 1)
    PrintSpecFR;
}

//------------------------------------------------------------------------------

DataStream dtsRReeMBP
(
  [CommonFormHeader] CommonFormHeader;
  [ReeTypeName]      GetReeTypeName;
  [ReeGroup]         GetDocGroup;
  [Detail]           wParam;
  [FrDate]           if (Longint(frDate) > 0, DateToStr(frDate, 'DD/MM/YYYY'), '');
  [ToDate]           if (Longint(toDate) > 0, DateToStr(toDate, 'DD/MM/YYYY'), '');
  [FilterName]       GetFilterName;
  [MyOrgName]        iOrgInfo.GetMyOrgNameT(toDate);
  [NDE_ShName]       sGetTune('NDE.ShortRub');
  [IsExistArchive]   (oArchiveFuncs.IsExistArchive and oArchiveFuncs.GetExistTiDkGal(GetDocTiDkGal(dType)));

  Table tblDocList
  (
    [DocDate] if (Longint(tblDocList.DocDate) > 0, DateToStr(tblDocList.DocDate, 'DD/MM/YYYY'), '');
    [DocNom]  tblDocList.DocNom;
    [BarCode] tblDocList.BarCode;
    [DesGr]   tblDocList.DesGr;
    [Descr]   tblDocList.Descr;
    [Podr]    tblDocList.ToPodr;
    [Mol]     tblDocList.ToMol;
    [Pers]    tblDocList.Pers;
    [FrPodr]  tblDocList.FrPodr;
    [FrMol]   tblDocList.FrMol;
    [Summa]   tblDocList.Summa;

    Table tblSpecList
    (
      [MBPKod]  tblSpecList.MBPNNum;
      [MBPName] tblSpecList.MBPName;
      [EdName]  tblSpecList.EdName;
      [Price]   tblSpecList.Price;
      [Kol]     tblSpecList.Kol;
      [PercIzn] tblSpecList.PercIzn;
      [Stoim]   tblSpecList.Stoim;
      [DopInfo] tblSpecList.DopInfo;
    );
  );

  DataSet tblFeeSigners
  (
    [NRec]  iFeeSigners.GetSignerNRec;
    [FIO]   iFeeSigners.GetSignerFIO(1);
    [TabN]  iFeeSigners.GetSignerTabN;
    [Post]  iFeeSigners.GetSignerPosition(1);
    [Role]  iFeeSigners.GetSignerRole;
  );
)

HandleEvent DataSet tblFeeSigners
//-------------------------
  cmPreProcess:
  {
    lSign := 0;

    if (not iFeeSigners.FindFeeSignersByNRec(cSigners))
      AbortProcess;
  }
//-------------------------
  cmOnProcess:
  {
    if (not NextVisual)
      BreakProcess;

    lSign++;

    if (lSign = 1)
    {
      if (iFeeSigners.GetSignerFirst)
        ContinueDataSet;
    }
    else
    {
      if (iFeeSigners.GetSignerNext)
        ContinueDataSet;
    }
  }
//-------------------------
End; // HandleEvent DataSet tblFeeSigners

HandleEvent Table tblSpecList
//---------------------------
  cmOnProcess:
    if (not NextVisual)
      BreakProcess;
//---------------------------
End; // HandleEvent Table tblSpecList

HandleEvent Table tblDocList
//--------------------------
  cmOnProcess:
    if (not NextVisual)
      BreakProcess;
//--------------------------
End; // HandleEvent Table tblDocList

HandleEvent // HandleEvent DataStream dtsRReeMBP
//---------
  cmPreProcess:
    StartNewVisual(vtRotateVisual, vfTimer + vfBreak + vfConfirm, 'Идет формирование реестра документов...', 0);
//---------
  cmPostProcess:
    StopVisual('', 0);
//---------
end; // HandleEvent DataStream dtsRReeMBP
end; // DataStream dtsRReeMBP
