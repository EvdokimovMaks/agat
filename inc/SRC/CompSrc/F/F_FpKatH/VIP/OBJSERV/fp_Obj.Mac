//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 5.80 - модуль "Управление бюджетом"
// Макросы
//------------------------------------------------------------------------------

#ifndef _Fp_Obj_Mac_
#define _Fp_Obj_Mac_

//------------------------------------------------------------------------------
// GETBUFF
//------------------------------------------------------------------------------
#undef mObj_GetBuff
#declare mObj_GetBuff(_BufType)
Function GetBuff(_cRec : comp; var _Buff : #_BufType) : boolean;
{ GetBuff := pMainTBl.GetBuff(_cRec, _Buff);
}
#end// mObj_GetBuff


//------------------------------------------------------------------------------
// ALL Init/Done AddFld
//------------------------------------------------------------------------------
#undef mObj_FunAllMyInit
#declare mObj_FunAllMyInit(_IDEntity)
//------------------------------------------------------------------------------
Function MyInitEntity(_cRec : comp) : boolean;
{
  MyInitEntity := False;
  var CurEntity : rcFpEntity;
  CurEntity.w := #_IDEntity;
  CurEntity.c := _cRec;
  pObjAddFld.InitEnt(CurEntity);
  cCurInitObj := _cRec;
  FixRelations(cCurInitObj);
  MyInitEntity := True;
}
//------------------------------------------------------------------------------
Function MyDoneEntity(_cRec : comp) : boolean;
{
  MyDoneEntity := False;
  if (cCurInitObj <> 0)
  {
    var CurEntity : rcFpEntity;
    CurEntity.w := #_IDEntity;
    CurEntity.c := _cRec;
    pObjAddFld.DoneEnt(CurEntity);
    cCurInitObj := 0;
  }
  MyDoneEntity := True;
}
//------------------------------------------------------------------------------
Function MyReReadAddFld(_cRec : comp) : boolean;
{ if (cCurInitObj <> 0)
    MyDoneEntity(cCurInitObj);
  MyReReadAddFld := MyInitEntity(_cRec);
}
//------------------------------------------------------------------------------
Function MyDeleteEntity(_cRec : comp) : boolean;
{
  MyDeleteEntity := False;
  var CurEntity : rcFpEntity;
  CurEntity.w := #_IDEntity;
  CurEntity.c := _cRec;
  pObjAddFld.DelEnt(CurEntity);
  cCurInitObj := 0;
  MyDeleteEntity := True;
}
//------------------------------------------------------------------------------
Function CheckInit(_cRec : comp) : boolean;
{
  CheckInit := True;
  if (cCurInitObj = _cRec)  Exit;
  MyReReadAddFld(_cRec);
}

//------------------------------------------------------------------------------
Function MyCopyAllAddFld(_SrcRec : comp; _DstRec : comp) : boolean;
{ 
  var SrcEntity : rcFpEntity;
  SrcEntity.w := #_IDEntity;
  SrcEntity.c := _SrcRec;
  var DstEntity : rcFpEntity;
  DstEntity.w := #_IDEntity;
  DstEntity.c := _DstRec;
  MyCopyAllAddFld := pObjAddFld.CopyEnt(SrcEntity, DstEntity);
}

#end// mObj_FunAllMyInit


//------------------------------------------------------------------------------
// GET
//------------------------------------------------------------------------------
#undef mObj_FunBufGetTblFld
#declare mObj_FunBufGetTblFld(_Fun,_Type,_BufType)
Function #_Fun(_Buff : #_BufType) : #_Type;
{ #_Fun := pMainTBl.#_Fun(_Buff);
}
#end// mObj_FunBufGetTblFld

//------------------------------------------------------------------------------
#undef mObj_FunBufGetAddFld
#declare mObj_FunBufGetAddFld(_Fun,_Var,_Def,_Type,_BufType)
Function #_Fun(_Buff : #_BufType) : #_Type;
{ #_Fun := #_Def;
  if (not CheckInit(_Buff.NRec) )  { Exit; }
  var Value : #_Type;  Value := #_Def;
  if (not pObjAddFld.#(_Type)GetFld(#_Var, Value) )  { Exit; }
  #_Fun := Value;
}
#end// mObj_FunBufGetAddFld

//------------------------------------------------------------------------------
#undef mObj_FunGetFld
#declare mObj_FunGetFld(_Fun,_Def,_Type,_BufType)
Function #_Fun(_cRec : comp) : #_Type;
{
  /*
  если делать так, то при неудаче GetBuff
  будет брать из старого буфера
  var Buff : #_BufType;
  pMainTBl.GetBuff(_cRec, Buff);
  #_Fun := Buf#_Fun(Buff);
  */
  /*
  так тоже не получается, т.к. mObj_FunGetFld
  общий для всех полей, а не только табличных
  #_Fun := pMainTBl.#_Fun(_cRec);
  */
  #_Fun := #_Def;
  var Buff : #_BufType;
  if (not pMainTBl.GetBuff(_cRec, Buff) )
  {
    /*
    15/02/2005 сделал "Многопользовательский режим работы с настройкой распределения агрегатов"
    // 1- это для AddFld "виртуальных реализаций"
    // используется в инт "Редактирование бюджета" - вирт т/ф = 1, или UserId
    if (_cRec <> 1) and (_cRec <> UserId)
      Exit;
    */

    Exit;
  }
  #_Fun := Buf#_Fun(Buff);
}
#end// mObj_FunGetFld

//------------------------------------------------------------------------------
// SET
//------------------------------------------------------------------------------
#undef mObj_FunBufSetTblFld
#declare mObj_FunBufSetTblFld(_Fun,_Type,_BufType)
Function #_Fun(var _Buff : #_BufType; _Value : #_Type) : boolean;
{ #_Fun := pMainTBl.#_Fun(_Buff, _Value);
}
#end// mObj_FunBufSetTblFld

//------------------------------------------------------------------------------
#undef mObj_FunBufSetAddFld
#declare mObj_FunBufSetAddFld(_Fun,_Var,_Type,_BufType)
Function #_Fun(var _Buff : #_BufType; _Value : #_Type) : boolean;
{ #_Fun := False;
  if (not CheckInit(_Buff.NRec) )  { Exit; }
  #_Fun := pObjAddFld.#(_Type)SetFld(#_Var, _Value);
  MyReReadAddFld(_Buff.NRec);
}
#end// mObj_FunBufSetAddFld

//------------------------------------------------------------------------------
#undef mObj_FunSetFld
#declare mObj_FunSetFld(_Fun,_Type,_BufType)
Function #_Fun(var _cRec : comp; _Value : #_Type) : boolean;
{ var Buff : #_BufType;
  pMainTBl.GetBuff(_cRec, Buff);
  #_Fun := Buf#_Fun(Buff, _Value);
}
#end// mObj_FunSetFld


#end  // _Fp_Obj_Mac_
