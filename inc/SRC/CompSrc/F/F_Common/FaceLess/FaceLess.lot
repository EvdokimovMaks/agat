!-------------------------------------------------------------------------------
! (c) 2002 Корпорация ГАЛАКТИКА
!-------------------------------------------------------------------------------
! Версия        : 5.82.06
! Назначение    : Скрипт обезличивания БД
! Ответственный : Герман Юрий Эдуардович
!-------------------------------------------------------------------------------
! 1 Контрагенты
#info 'Table: KatOrg. Fields: Name, UNN, Code, Addr'
Update KatOrg
!   Наименование
  set  KatOrg.Name := Trim('Организация #' + string(KatOrg.NRec))
!   ИНН
      ,KatOrg.UNN  := PadCh('','u',Length(KatOrg.UNN))
!   Внутренний учетный номер
      ,KatOrg.Code := PadCh('','c',Length(KatOrg.Code))
!   Адрес
      ,KatOrg.Addr := PadCh('','a',Length(KatOrg.Addr))
;
#info 'Table: KatOrgDescr. Fields: Name'
Update KatOrgDescr
  where ((KatOrgDescr.cRec == KatOrg.NRec)) and (KatOrgDescr.cRec <> 0)
!   Наименование (продублировано)
  set  KatOrgDescr.Name := KatOrg.Name
;
#info 'Table: KontrIer. Fields: Name'
Update KontrIer
!   Наименование (продублировано)
  where ((KontrIer.cRecDs == KatOrgDescr.NRec))
  set  KontrIer.Name := KatOrgDescr.Name
;
#info 'Table: KatB. Fields: Name, MFO1, MFO2, KorSchet, Schet1'
Update KatB
!   Наименование банка
  set  KatB.Name     := PadCh('','b',Length(KatB.Name))
!   БИК/МФО банка
      ,KatB.MFO1     := PadCh('','1',Length(KatB.MFO1))
      ,KatB.MFO2     := PadCh('','2',Length(KatB.MFO2))
!   Корреспондентский счет
      ,KatB.KorSchet := PadCh('','0',Length(KatB.KorSchet))
!   Счет ?!
      ,KatB.Schet1   := PadCh('','9',Length(KatB.Schet1))
;
#info 'Table: KatBank. Fields: Name, MFO1, MFO2, Schet1, Schet2, Schet3'
Update KatBank
!   Наименование банка
  set  KatBank.Name   := PadCh('','b',Length(KatBank.Name))
!   БИК/МФО банка
      ,KatBank.MFO1   := PadCh('','1',Length(KatBank.MFO1))
      ,KatBank.MFO2   := PadCh('','2',Length(KatBank.MFO2))
!   Расчетый счет
!   Корреспондентский счет
      ,KatBank.Schet1 := PadCh('','r',Length(KatBank.Schet1))
      ,KatBank.Schet2 := PadCh('','k',Length(KatBank.Schet2))
      ,KatBank.Schet3 := PadCh('','z',Length(KatBank.Schet3))
;
!-------------------------------------------------------------------------------
! 2.1 Наименования МЦ
#info 'Table: KatMC. Fields: Name'
Update KatMC
  set KatMC.Name := Trim('МЦ ' + if (Trim(KatMC.BarKod) = '',
                                     '#' + string(KatMC.NRec),
                                     KatMC.BarKod))
;
!-------------------------------------------------------------------------------
! 2.2 Номенклатура Векселей и ЦБ
#info 'Table: Veksl. Fields: SrDoc, NoDoc, NoDocEnd'
Update Veksl
  set  Veksl.SrDoc    := PadCh('','s',Length(Veksl.SrDoc))
      ,Veksl.NoDoc    := PadCh('','n',Length(Veksl.NoDoc))
      ,Veksl.NoDocEnd := PadCh('','e',Length(Veksl.NoDocEnd))
;
!-------------------------------------------------------------------------------
! 3 Наименования услуг
#info 'Table: KatUsl. Fields: Name'
Update KatUsl
! KatUsl.Name - Требуется уникальность
  set KatUsl.Name := Trim('Услуга ' + KatUsl.Kod + ' #' + string(KatUsl.NRec))
;
!-------------------------------------------------------------------------------
! 4 Наименования подразделений
#info 'Table: KatPodr. Fields: Name'
Update KatPodr
! KatPodr.Name - Требуется уникальность
  set KatPodr.Name := Trim('Подразделение ' + KatPodr.Kod +
                           ' #' + string(KatPodr.NRec))
;
!-------------------------------------------------------------------------------
! 5а ФИО cотрудников предприятия
#info 'Table: Persons. Fields: FIO'
Update Persons
  set Persons.FIO := Trim('Сотрудник ' + string(Persons.nRec) +
                          ' #' + string(Persons.NRec))
;
!-------------------------------------------------------------------------------
! 5б ФИО МОЛ
#info 'Table: KatMol. Fields: Name'
Update KatMol
  where ((KatMol.cPersons == Persons.NRec))
! KatMol.Name - Требуется уникальность по индексу сSkl + Name (U)
  set KatMol.Name := if (KatMol.cPersons = 0,
                         Trim('МОЛ ' + KatMol.Kod + ' #' + string(KatMol.NRec)),
                         Persons.FIO)
;
!-------------------------------------------------------------------------------
! 6 Наименования партий МЦ
#info 'Table: KatParty. Fields: Name, Kod'
Update KatParty
! KatParty.Name - Требуется уникальность по индексу cMС (U) + Name (U)
! KatParty.Kod  - Требуется уникальность Kod (U, Null)
  set  KatParty.Name := Trim('Партия МЦ #' + string(KatParty.NRec))
      ,KatParty.Kod  := ''
;
!-------------------------------------------------------------------------------
! 7 Наименования субсчетов
#info 'Table: BuhSchet. Fields: Name. Only SubSchets'
Update BuhSchet
  where (0 < BuhSchet.cNode)
  set BuhSchet.Name := Trim('Субсчет ' + BuhSchet.LevelCode +
                            ' счета ' + SubStr(BuhSchet.Schet,2,20))
;
!-------------------------------------------------------------------------------
! 8 Наименования пользовательской аналитики
#info 'Table: KatKau. Fields: Name'
Update KatKau
  where ((1 == KatKau.SysNum))
  set KatKau.Name := Trim('UserAn ' + string(KatKau.KodGroup))
;
!-------------------------------------------------------------------------------
! 9 Наименования и коды спецификаций пользовательской аналитики
#info 'Table: SpKau. Fields: Name, Code'
Update SpKau
  set  SpKau.Name := Trim('SpUserAn #' + string(SpKau.NRec))
      ,SpKau.Code := '#' + string(SpKau.NRec)
;
!-------------------------------------------------------------------------------
! 10 Наименования ОС и НМА
#info 'Table: KatOs. Fields: NameOs'
Update KatOs
  set KatOs.NameOs := Trim(if(KatOs.TiDk = 15,'ОС ',
                           if(KatOs.TiDk = 16,'НМА ','KatOs ')) + KatOs.InNum)
;
!-------------------------------------------------------------------------------
! 11 Назначение платежа в платежных документах
#info 'Table: PlPor. Fields: NamePl1, NamePl2, NamePl3, NamePl4, Podotchet, (Tax)'
Update PlPor
  set  PlPor.NamePl1   := PadCh('','1',Length(PlPor.NamePl1))
      ,PlPor.NamePl2   := PadCh('','2',Length(PlPor.NamePl2))
      ,PlPor.NamePl3   := PadCh('','3',Length(PlPor.NamePl3))
      ,PlPor.NamePl4   := PadCh('','4',Length(PlPor.NamePl4))
      ,PlPor.Podotchet := PadCh('','p',Length(PlPor.Podotchet))
! В случае, если Update PlPor работает очень долго, модно попробовать
! нижеследующий вариант:
!  set  PlPor.NamePl1   := ''
!      ,PlPor.NamePl2   := ''
!      ,PlPor.NamePl3   := ''
!      ,PlPor.NamePl4   := ''
!      ,PlPor.Podotchet := ''
;
!-------------------------------------------------------------------------------
! 12 Наименования ТХО
#info 'Table: HozOper. Fields: Name1, Name2, Name3'
Update HozOper
  set  HozOper.Name1 := PadCh('','1',Length(HozOper.Name1))
      ,HozOper.Name2 := PadCh('','2',Length(HozOper.Name2))
      ,HozOper.Name3 := PadCh('','3',Length(HozOper.Name3))
;
!-------------------------------------------------------------------------------
! 13 Содержание операций в настройке ТХО
#info 'Table: HozObor. Fields: SodPr'
Update HozObor
  set HozObor.SodPr := PadCh('',
                         if (Pos('%KAUNAME',UpCase(HozObor.SodPr)) <> 0, 'n',
                         if (Pos('%KAUCODE',UpCase(HozObor.SodPr)) <> 0, 'c','b')),
                         Length(HozObor.SodPr))
;
!-------------------------------------------------------------------------------
! 14 Содержание проводок в оборотах
#info 'Table: Oborot. Fields: SodPr'
Update Oborot
  set Oborot.SodPr := PadCh('','s',Length(Oborot.SodPr))
;
!-------------------------------------------------------------------------------
! 15 Номера договоров
#info 'Table: Dogovor. Fields: NoDoc'
Update Dogovor
  set Dogovor.NoDoc := PadCh('','d',Length(Dogovor.NoDoc))
;
!-------------------------------------------------------------------------------
! 16 Наборы МЦ
#info 'Table: KatCompl. Fields: Name'
Update KatCompl
! KatCompl.Name - Требуется уникальность (KatCompl.Kod тоже уникален)
  set KatCompl.Name := Trim('Набор МЦ ' + KatCompl.Kod)
;
!-------------------------------------------------------------------------------
! 17 Доверенные лица
#info 'Table: DovFIO. Fields: Name, Post, PasSer, PasNumb, WhereVid, DatVid'
Update DovFIO
  where ((DovFIO.cPersons == Persons.NRec))
! DovFIO.Name - Требуется уникальность
set  DovFIO.Name     := if (DovFIO.cPersons = 0,
                            'Доверенное лицо #' + string(DovFIO.NRec),
                            Persons.FIO)
    ,DovFIO.Post     := PadCh('','d',Length(DovFIO.Post))
    ,DovFIO.PasSer   := PadCh('','s',Length(DovFIO.PasSer))
    ,DovFIO.PasNumb  := PadCh('','n',Length(DovFIO.PasNumb))
    ,DovFIO.WhereVid := PadCh('','v',Length(DovFIO.WhereVid))
    ,DovFIO.DatVid   := Cur_Date
;
!-------------------------------------------------------------------------------
! 18 Организация в общесистемной настройке
#info 'Table: TuneVal. Fields: StrVal'
Update TuneVal
  where
  ((    TuneVal.cTune   == TuneDef.NRec
    and TuneVal.CompVal == KatOrg.NRec
  ))
  and ('MYORG' = TuneDef.Code)
  set TuneVal.StrVal := if (TuneVal.CompVal = 0,
                            PadCh('','o',Length(TuneVal.StrVal)),
                            KatOrg.Name)
;
!-------------------------------------------------------------------------------
! 19 Имена пользователей в настройках БД
#info 'Table: X$Users. Fields: XU$FullName'
Update X$Users
  set X$Users.XU$FullName := PadCh('','u',Length(X$Users.XU$FullName));
#info 'Table: RemUser. Fields: Remark'
Update RemUser
  set RemUser.Remark := PadCh('','u',Length(RemUser.Remark))
;
!-------------------------------------------------------------------------------
! 20 Наименования внешних классификаций и их значений
#info 'Table: ExClassName. Fields: Name'
Update ExClassName
  set ExClassName.Name := Trim('Внешняя классификация ' + string(ExClassName.ClassCode))
;
#info 'Table: ExClassSeg. Fields: Name'
Update ExClassSeg
  set ExClassSeg.Name := Trim('Значение внешней классификации #' + string(ExClassSeg.NRec))
;
!-------------------------------------------------------------------------------
! 21 Наименования внешних атрибутов
#info 'Table: AttrNam. Fields: Name'
Update AttrNam
  set AttrNam.Name := Trim('Внешний атрибут #' + string(AttrNam.NRec))
;
!-------------------------------------------------------------------------------
! 22 Номера и примечания к ДО
#info 'Table: BaseDoc. Fields: NoDoc, Name'
Update BaseDoc
  set  BaseDoc.NoDoc := PadCh('','n',Length(BaseDoc.NoDoc))
      ,BaseDoc.Name  := PadCh('','p',Length(BaseDoc.Name))
;
!-------------------------------------------------------------------------------
! 23 Наименования групп ДО/Договоров
#info 'Table: GroupSch. Fields: Name'
Update GroupSch
! GroupSch.Name - Требуется уникальность
  set GroupSch.Name := Trim('Группа ДО/Договоров ' + GroupSch.Kod +
                            ' #' + string(GroupSch.NRec))
;
!-------------------------------------------------------------------------------
! 24 Наименования и адреса пунктов маршрутов
#info 'Table: MarPunkt. Fields: Name, Code'
Update MarPunkt
  set  MarPunkt.Name := Trim('Пункт маршрута #' + string(MarPunkt.NRec))
      ,MarPunkt.Code := ''
;
!-------------------------------------------------------------------------------
! 25 Наименования групп пользователей
#info 'Table: GrDescr. Fields: Name'
Update GrDescr
  set GrDescr.Name := Trim('Группа пользователей ' + GrDescr.DesGr)
;
!-------------------------------------------------------------------------------
! 26 Наименования счетов учета платежных документов
#info 'Table: CashBank. Fields: Name'
Update CashBank
! CashBank.Name - Требуется уникальность по индексу Razdel + Name (U)
  set CashBank.Name := Trim(if (CashBank.Razdel = 1, 'Касса',
                              if (CashBank.Razdel = 2, 'Расчетный счет',
                              if (CashBank.Razdel = 3, 'Валютный счет',
                              if (CashBank.Razdel = 4, 'Бухсправка','??? Unknown')))) +
                            ' #' + string(CashBank.NRec))
;
!-------------------------------------------------------------------------------
! 27 Наименования групп ОС и видов ОС и НМА
#info 'Table: GrOs. Fields: NameGr'
Update GrOs
! GrOs.NameGr - Требуется уникальность
  set GrOs.NameGr := Trim('Группа ОС ' + GrOs.KodGr)
;
#info 'Table: VidNorma. Fields: Name'
Update VidNorma
  set VidNorma.Name := Trim('Вид (норма) ' +
                            if(VidNorma.TiDk = 15,'ОС ',
                            if(VidNorma.TiDk = 16,'НМА ','??? ')) + VidNorma.Kod)
;
!-------------------------------------------------------------------------------
! 28 Примечания из таблиц SoprHoz и BaseFin
#info 'Table: SoprHoz. Fields: NameSho'
Update SoprHoz
  set SoprHoz.NameSho := PadCh('','p',Length(SoprHoz.NameSho))
;
#info 'Table: BaseFin. Fields: NamePl'
Update BaseFin
  set BaseFin.NamePl := PadCh('','p',Length(BaseFin.NamePl))
;
!-------------------------------------------------------------------------------
! 29 Номера и примечания накладных и актов
#info 'Table: KatSopr. Fields: NSopr, Name'
Update KatSopr
  set  KatSopr.NSopr := PadCh('','n',Length(KatSopr.NSopr))
      ,KatSopr.Name  := PadCh('','p',Length(KatSopr.Name))
;
!-------------------------------------------------------------------------------
! 30 Номера бухсправок (кроме начинающихся с '#','&','*')
#info 'Table: PlPor. Fields: NoDok'
Update PlPor
  where (( 10 == PlPor.TiDkGal )) and
          (SubStr(PlPor.NoDok,1,1) <> '#') and
          (SubStr(PlPor.NoDok,1,1) <> '&') and
          (SubStr(PlPor.NoDok,1,1) <> '*')
  set PlPor.NoDok := PadCh('','n',Length(PlPor.NoDok))
;
!-------------------------------------------------------------------------------
! 31 Примечания и дополнения к счетам-фактурам и книгам покупок и продаж
#info 'Table: SchFact. Fields: Nazn'
Update SchFact
  set SchFact.Nazn := PadCh('','p',Length(SchFact.Nazn))
;
#info 'Table: BookPrZk. Fields: Nazn'
Update BookPrZk
  set BookPrZk.Nazn := PadCh('','p',Length(BookPrZk.Nazn))
;
!-------------------------------------------------------------------------------
