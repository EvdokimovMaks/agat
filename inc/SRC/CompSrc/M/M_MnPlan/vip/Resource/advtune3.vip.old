/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,99 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.71                                                      ║
 ║ Назначение    : Расширенная настройка уровня группировки по дате          ║
 ║ Ответственный : Медведский Алексей Леонидович (ALM)                       ║
 ║ Параметры     : Нет                                                       ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#include TreeTuneNastr.vih

Interface AdvTune3 'Расширенная настройка уровня' EscClose, Cyan;
Show at (7,10,73,20);

#include TreeTuneNastr.var

Create View
Var
 c1, c2, c3 : Comp;

As select
 RepGrOne.*,UserDeskRep.*
From
  RepGrOne,
  UserDeskRep,
  RepGroup                       (ReadOnly),
  ExClassName                    (ReadOnly),
  ExClassSeg                     (ReadOnly),
  TmpKatalog                     (ReadOnly)
Where
((
  c1                   == RepGrOne.cRepGrLst            and
  RepGrOne.cRec        == RepGroup.NRec                 and
  RepGrOne.ResComp[2]  == ExClassSeg.NRec               and
  RepGroup.wTable      == ExClassName.wTable            and
  ExClassSeg.ClassCode == ExClassName.ClassCode         and
  c3                   == UserDeskRep.NRec
))
;

Screen Scr01 (, hcLevelAdvEdit, sci1Esc);
Table RepGrOne;
Fields
 ExClassName.Name ('Внешняя классификация',, sci13Esc), Protect;
 RepGrOne.ResWord[3] ('Как печатать итоги по уровню',, sci13Esc) :
  [List 'печатать снизу','печатать сверху','не печатать'], Protect;
<<

 Внешняя классификация.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Итоги по уровню      .@@@@@@@@@@@@@@@@@@@@@
>>
End;  //'screen

Screen scr02(, hcLevelAdvEdit, sci1Esc);
 Table RepGrOne;
 Fields
  UserDeskRep.ResWord[30]('вид группировки по датам',,): NoProtect;
  RepGrOne.ResWord[3] ('Как печатать итоги по уровню',, sci13Esc) :
   [List 'печатать снизу','печатать сверху','не печатать'], Protect;
<<
             `Виды группировок по датам`
   [.] Год`
   [.] Полугодие`
   [.] Квартал`      Итоги по уровню      .@@@@@@@@@@@@@@@@@@@@@
   [.] Месяц`
   [.] Декада`
   [.] Неделя`
   [.] День`
>>
end; // screen scrGroupDates

Panel p1;
table UserDeskRep;
HandleEvent
cmUpdateRecord :
 {
  Update Current UserDeskRep;
 }
end; // handleevent panel p1
end; // panel p1

Panel p2;
Table RepGrOne;
HandleEvent
cmUpdateRecord :
 {
  Update Current RepGrOne;
 }

cmPick :
 Case CurField Of
  #ExClassName.Name :
   {
    if ( RunInterface('PickExClassifier', RepGroup.wTable, RepGrOne.ResComp[2], word(0)) <> cmCancel )
     {
      //'
     }
   }
 End;  //'case
end; // handleevent panel paRepGr
end; // panel paRepGr
//==========================================================================
HandleEvent  //'interface
//==========================================================================
cmInit :
 {
  c1 := oTreeTuneNastr.GetRepGruppa_c1;
  c2 := oTreeTuneNastr.GetRepGruppa_c2;
  c3 := oTreeTuneNastr.UserDeskRep_nrec;
  if ( c2 <> Comp(0) )
   {
    ResetBounds(tnRepGrOne);
    if ( GetFirst RepGrOne Where ((
                                   c1 == RepGrOne.cRepGrLst  and
                                   c2 == RepGrOne.nPos
                                 )) = tsOk )
      {
       PushPos(#RepGrOne);
       SetBounds(tnRepGrOne);
       PopPos(#RepGrOne);
      }
    else
      SetBounds(tnRepGrOne);
   }

  if ( RepGroup.wTable = coTmpKatalog )
     SetFormat(scr02);
  else
     SetFormat(scr01);
 }
End;  //'he interface
End.  //'interface