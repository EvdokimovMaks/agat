/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,98 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Модуль Управление ремонтами                               ║
 ║ Версия        : 5.80                                                      ║
 ║ Назначение    : Include для Потребности в трудовых ресурсах для ремонтов  ║
 ║ Ответственный : Бургардт Александр                                        ║
 ║ Изменения     :                                                           ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/
//     ======= ШАПКА ==========

    Fvedro3t.Write(name_ved);  // Заголовок

    Fvedro3t.Write(DateToStr(UserDeskRep.ResDate[1],'DD/MM/YYYY'));          // дата 1
    Fvedro3t.Write(DateToStr(UserDeskRep.ResDate[2],'DD/MM/YYYY'));          // дата 2

 if logika1(1,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('тип объектов      : ('+vo_RunSpr.KodSpr('TipObj' ,UserDeskRep.ResComp[1])+') '+vo_RunSpr.NameSpr('TipObj' ,UserDeskRep.ResComp[1]));
 if logika1(2,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('подразделение     : ('+vo_RunSpr.KodSpr('KatPodr',UsResPodr)+') '+vo_RunSpr.NameSpr('KatPodr',UsResPodr));
 if logika1(3,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('служба            : ('+vo_RunSpr.KodSpr('KurRem' ,UserDeskRep.ResComp[3] )+') '+vo_RunSpr.NameSpr('KurRem' ,UserDeskRep.ResComp[3] ));
 if logika1(4,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('виды ремонтов     : ('+vo_RunSpr.KodSpr('VidRem' ,UserDeskRep.ResComp[4])+') '+vo_RunSpr.NameSpr('VidRem' ,UserDeskRep.ResComp[4]));
 if logika1(5,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('исполнители       : ('+vo_RunSpr.KodSpr('IspRem' ,UserDeskRep.ResComp[5])+') '+vo_RunSpr.NameSpr('IspRem' ,UserDeskRep.ResComp[5]));
 if logika1(6,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('способ исполнения : ('+vo_RunSpr.KodSpr('SposRem',UserDeskRep.ResComp[6])+') '+vo_RunSpr.NameSpr('SposRem',UserDeskRep.ResComp[6]));
 if logika1(7,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('технолог. нитки   : ('+vo_RunSpr.KodSpr('KatTexN',UserDeskRep.ResComp[7])+') '+vo_RunSpr.NameSpr('KatTexN',UserDeskRep.ResComp[7]));
 if logika1(8,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('группы            : ('+vo_RunSpr.KodSpr('GrObj'  ,UserDeskRep.ResComp[8])+') '+vo_RunSpr.NameSpr('GrObj'  ,UserDeskRep.ResComp[8]));
 if logika1(9,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('марки,модели      : ('+vo_RunSpr.KodSpr('KatMark',UserDeskRep.ResComp[9])+') '+vo_RunSpr.NameSpr('KatMark',UserDeskRep.ResComp[9]));
if logika1(10,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('объект ремонта    : ('+vo_RunSpr.KodSpr('ObjRem',UserDeskRep.ResComp[10])  +') '+vo_RunSpr.NameSpr('ObjRem',UserDeskRep.ResComp[10]));
if logika1(11,UserDeskRep.ResWord[1])>0 FVedro3t.Write('заявка на ремонт № '+vo_RunSpr.KodSpr('BaseDoc',UserDeskRep.ResComp[11]));
case NReport of
 10003,10012: if logika1(12,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('работа (услуга)   : ('+vo_RunSpr.KodSpr('KatUsl' ,UserDeskRep.ResComp[12])+') '+vo_RunSpr.NameSpr('KatUsl' ,UserDeskRep.ResComp[12]));
 10004      : if logika1(12,UserDeskRep.ResWord[1])>0 FVedro3t.Write('должность         : ('+vo_RunSpr.KodSpr('Catalogs22' ,UserDeskRep.ResComp[12])+') '+vo_RunSpr.NameSpr('Catalogs22' ,UserDeskRep.ResComp[12]));
 10005,10013: if logika1(12,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('материал(запчасть): ('+vo_RunSpr.KodSpr('KatMc'  ,UserDeskRep.ResComp[12])+') '+vo_RunSpr.NameSpr('KatMc'  ,UserDeskRep.ResComp[12]));
 10006      : if logika1(12,UserDeskRep.ResWord[1])>0 Fvedro3t.Write('ремонтная техника : ('+vo_RunSpr.KodSpr('KatRTex',UserDeskRep.ResComp[12])+') '+vo_RunSpr.NameSpr('KatRTex',UserDeskRep.ResComp[12]));
 10033      : if logika1(12,UserDeskRep.ResWord[1])>0 FVedro3t.Write('инструмент        : ('+vo_RunSpr.KodSpr('KatMc',UserDeskRep.ResComp[12])+') '+vo_RunSpr.NameSpr('KatMc',UserDeskRep.ResComp[12]));
end
 if           UserDeskRep.ResWord[1] =0 Fvedro3t.Write('Без ограничений     ');
    Fvedro3t.PutEvent(feBreak);

    Fvedro3t.Write(DateToStr(Cur_Date,'DD/MM/YYYY'));       // текущaя дата
    Fvedro3t.Write(Cur_Time);       // текущее время
             si:=0;
DO BEGIN
             si    :=si+1 ;
             inv_w :=rBlank.p2+rBlank.p3+rBlank.p4;
             p_razr:=rBlank.p6;

             Fvedro3t.Write(si);
             Fvedro3t.Write(inv_w); // Название должности
             Fvedro3t.Write(p_razr);    // Тарифный разряд
             s1 := 0; s2 := 0; s3 := 0;
       do begin      // цикл по одной строке бланка..
             s1 :=s1 + double(rBlank.p8);   //   количество
             s2 :=s2 + double(rBlank.p7);   //   Время выполнения
             s3 :=s3 + double(rBlank.p5);   //   Сумма итого

             ts_ok := GetNext rBlank;          // взять Next запись
             State := NextVisual;              // следующий шаг визуализации
       end;
       while (ts_ok = tsOk and State and inv_w=rBlank.p2+rBlank.p3+rBlank.p4
                                     and p_razr=rBlank.p6);

             Fvedro3t.Write(s1);    // Кол.рабочих всего
             Fvedro3t.Write(s2);    // Время выполнения
             Fvedro3t.Write(s3);    // Сумма итого
 END;
    while ((ts_ok = tsOk) and State) ;

        Fvedro3t.PutEvent(feBreak);          // выйти из цикла в FRMе
        Fvedro3t.PutEvent(feBreak);          // выйти из цикла в FRMе
        StopVisual('',0);
        if (Not Fvedro3t.Error) Fvedro3t.Showfile(name_ved,false);  // просмотр
        else                    Fvedro3t.AbortForm;
        Fvedro3t.ReInit;                         // закрыть форму