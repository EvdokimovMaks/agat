//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - ДО
// Окно копирования
//******************************************************************************

#doc
Окно установки параметров копирования
#end
Window CopyDoPar 'Параметры копирования' DoAccept, EscClose, Gray;
  Show at (, , 56, 13);

//******************************************************************************

Create view loCopy
From
  StepDoc
, BaseDoc
, KatMC
, KatUsl
;

Panel pCopyDoPar

//******************************************************************************

Screen scCopyDoPar (, hcZakupACopySpecDial, sci1Esc)

Fields
  Par3          ('Перенести цену или взять из прайс-листа', , ), NoProtect;
  KlPrice1.Name ('Прайс-лист для МЦ', , sci13Esc), Protect;
  KlPrice2.Name ('Прайс-лист для услуг', , sci13Esc), Protect;
  Par5          ('Фильтр по номеру вагона', , ), NoProtect;
  Par6          ('Будут копироваться позиции с фильтром по заданным номерам вагонов', , sci13Esc), Protect;
  Par4          ('Перенести партию', , ), NoProtect;

Buttons
  cmYes, Default, , 'Продолжить'    , , sci1Esc;
  cmCancel, , ,    'Отмена', , sci1Esc;
<<
 `При копировании спецификации взять цену из`
  (.) копируемой позиции` Прайс-лист для
  (.) прайс-листа       `  `МЦ   `.@@@@@@@@@@@@@@@@@
 `Фильтр по № вагона`      `Услуг`.@@@@@@@@@@@@@@@@@
  (.) нет          `
  (.) одиночный    `              .@@@@@@@@@@@@@@@@@
  (.) множественный`

  [.] переносить партию МЦ`
  [.] уточнить спецификацию`

  <. Продолжить .>                  <.   Отмена   .>
>>
end;

//******************************************************************************

Screen scCopySpSoprPar (, hcm_zakup_LokKopir_Prich, sci1Esc)

Fields
  Par3          ('Перенести цену или взять из прайс-листа', , ), NoProtect;
  KlPrice1.Name ('Прайс-лист для МЦ', , sci13Esc), Protect;
  Par4          ('Перенести партию', , ), NoProtect;

Buttons
  cmYes, Default, , 'Продолжить', , sci1Esc;
  cmCancel, , ,     'Отмена'    , , sci1Esc;
<<

 `При копировании спецификации взять цену из`
  (.) накладной        `
  (.) прайс-листа      ` .@@@@@@@@@@@@@@@@@@@@@@@@@
  (.) складского ордера`

  [.] переносить партию МЦ`
  [.] уточнить спецификацию`


  <. Продолжить .>                <.   Отмена   .>
>>
end;

//******************************************************************************

HandleEvent

cmPick:
  case CurField of
  //--------------
    #KlPrice1.Name:
      RunInterface('GetKlPr1', cMCPrice, word(1), word(0));
  //--------------
    #KlPrice2.Name:
      RunInterface('GetKlPr1', cUSLPrice, word(2), word(0));
  //--------------
    #Par6:
      if RunInterface('GetVagon', word(4), comp(0), comp(0), comp(0), comp(0)) <> cmCancel
        {
          var VagonMarker: TPtr;
          VagonMarker := InitMarker('VagonMarker', 8, 10, 10);

          case GetMarkerCount(VagonMarker) of
          //---------------------------------
            0:
              set Par6 := '';
          //---------------------------------
            1:
              {
                PushPos(#VagonSp);
                var PickRec: comp;
                GetMarker(VagonMarker, 0, PickRec);

                if GetFirst VagonSp where (( PickRec == VagonSp.nRec )) = tsOk
                  set Par6 := VagonSp.Num
                else
                  set Par6 := '???';

                PopPos(#VagonSp);
              }
          //---------------------------------
            else
              set Par6 := 'Множ.фильтр ('+string(GetMarkerCount(VagonMarker))+')';
          //---------------------------------
          end;

          DoneMarker(VagonMarker, '');
        }
  //--------------
  end;

//------------------------------------------------------------------------------

cmDelOnProtect:
  case CurField of
  //--------------
    #KlPrice1.Name:
      set cMCPrice := 0;
  //--------------
    #KlPrice2.Name:
      set cUSLPrice := 0;
  //--------------
    #Par6:
      {
        var VagonMarker: TPtr;

        VagonMarker := InitMarker('VagonMarker', 8, 10, 10);
        ClearMarker(VagonMarker);
        DoneMarker(VagonMarker, 'VagonMarker');

        set Par6 := '';
      }
  //--------------
  end;

//------------------------------------------------------------------------------

cmCheckField:
  case CurField of
  //--------------
    #Par3:
      {
        ToggleFieldSelectable(#KlPrice1.Name, (Par3 = 1));

        if Par3 = 1
          {
            if (BaseDoc.VidDoc <> 102) AND (BaseDoc.VidDoc <> 202)
              ToggleFieldSelectable(#KlPrice2.Name, TRUE);
          }
        else
          ToggleFieldSelectable(#KlPrice2.Name, FALSE);
      }
  //--------------
    #Par5:
      {
        set Par6 := '';
        ToggleFieldVisible(#Par6, (Par5 <> 0));

        if Par5 <> 0
          {
            ToggleFieldProtected(#Par6, (Par5 = 2));
            SetHelpAndStatusContext(scCopyDoPar, #Par6, if(Par5 = 1, sci1Esc, sci13Esc), hcUseOwner);
          }
      }
  //--------------
  end;

end;

end;

//******************************************************************************

HandleEvent

cmInit:
{
  set cMCPrice  := 0;
  set cUSLPrice := 0;

  if (GetFirst KlPrice where ((coGetTune('Price.cPrice') == KlPrice.NRec)) = tsOk)
    if (KlPrice.Tip = 0)
      cMCPrice := KlPrice.NRec
    else
      cUSLPrice := KlPrice.NRec;

  ToggleFieldSelectable(#KlPrice1.Name, FALSE);
  ToggleFieldSelectable(#KlPrice2.Name, FALSE);

  set Par3 := 0;
  set Par4 := 1;
  set Par5 := 0;
  set Par6 := '';

  ToggleFieldVisible  (#Par6, FALSE);
  ToggleFieldProtected(#Par6, FALSE);

  SetHelpAndStatusContext(scCopyDoPar, #Par6, if(Par5 = 1, sci1Esc, sci13Esc), hcUseOwner);

  if (BaseDoc.VidDoc = 102)
    ToggleFieldSelectable(#Par4, FALSE);

  SetFormat(if (Par8 = 0, scCopyDoPar, scCopySpSoprPar));
}

cmYes:
  PutCommand(cmDefault);

end;
end;

//******************************************************************************
