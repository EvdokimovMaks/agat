#include System.vih

#component "System"

interface Collection;

var
  _list: IList;
  _count: longint;

//#Region IEnumerable

function GetEnumerator: IEnumerator;
{
  result := IEnumerator( new( System::ListEnumerator, ListEnumerator(IList(self)) ) );
}

//#EndRegion

//#Region ICollection

function Add(value: IObject): longint;
{
  InsertItem(_count, Value);
  result := _count;
}

procedure Remove(value: IObject);
{
  var i: longint;
  i := IndexOf(value);
  if (i <> -1) RemoveAt(i);
}

procedure Clear;
{
  ClearItems;
}

function Contains(value: IObject): boolean;
{
  result := IndexOf(value) <> -1;
}

property Count: longint read _count;

//#EndRegion

//#Region IList

function ItemGet(index: longint): IObject;
{
  result := GetItem(index);
}

procedure ItemSet(index: longint; value: IObject);
{
  SetItem(index, value);
}

function IndexOf(value: IObject): longint;
{
  result := -1;
  var i: longint;
  for (i:=0; i < _count; i++)
  {
    if GetItem(i) = value
    {
      result := i;
      break;
    }
  }
}

procedure Insert(index: longint; value: IObject);
{
  InsertItem(index, value);
}

procedure RemoveAt(index: longint);
{
  RemoveItem(index);
}

//#EndRegion

//#Region Collection private metods

constructor Init;
{
  _count := 0;
  result := loadvipref(_list, 'system::List');
}

function  GetItem(index: longint): IObject;
{
  result := _list.ItemGet(index);
}

procedure SetItem(index: longint; value: IObject);
{
  _list.ItemSet(index, value);
  _count := _list.Count;
}

procedure InsertItem(index: longint; value: IObject);
{
  _list.Insert(index, value);
  _count := _list.Count;
}

procedure RemoveItem(index: longint);
{
  _list.RemoveAt(index);
  _count := _list.Count;
}

procedure ClearItems;
{
  _list.Clear;
}

//#EndRegion

end.
