#include CardHelper.vih
#include AppFuncs.vih
#include ExtAttr.vih

#component "AGAT"

interface CardHelper;

var
  _appointmentsFunctions: AppointmentsFunctions;
  _extAttr: iExtAttr;

create view vGetCard
var
  _cardNrec: comp
as select *
from spkau
where ((
  _cardNrec == spkau.nrec
));

create view vGetLschet
var
  _appNrec: comp;
  _personNrec: comp;
as select *
from lschet
where ((
  _personNrec == lschet.tperson and
  _appNrec == lschet.cappoint
));

//#region Вспомогательные процедуры и функции

function GetLschetNrecOnDate(aPersonNrec: comp; aDate: date): comp;
{
  vGetLschet._appNrec := _appointmentsFunctions.GetAppNrec_OnDate(aPersonNrec, aDate);
  vGetLschet._personNrec := aPersonNrec;
  if vGetLschet.getfirst lschet = tsOk
    result := vGetLschet.lschet.nrec;
  else
    result := 0;
}

//#endregion

//#region ICardHelper

function  ICardHelper.GetCardNrecByLschet(aLschetNrec: comp): comp;
{
  result := _extAttr.coGetAttrID(coLschet, aLschetNrec, ATTRNAM_NREC_LSCHET_KORPORATIVNAYA_KARTA);
}

function  ICardHelper.GetCardNrecByPersonOnDate(aPersonNrec: comp; aDate: date = 0): comp;
{
  if aDate = 0
    aDate := cur_date;

  var lschetNrec: comp;
  lschetNrec := GetLschetNrecOnDate(aPersonNrec, aDate);

  if lschetNrec != 0
    result := GetCardNrecByLschet(lschetNrec);
  else
    result := 0;
}

function  ICardHelper.GetCardValueByLschet(aLschetNrec: comp): string;
{
  vGetCard._cardNrec := GetCardNrecByLschet(aLschetNrec);
  if vGetCard.getfirst spkau = tsOk
    result := vGetCard.spkau.name;
  else
    result := '';
}

function  ICardHelper.GetCardValueByPersonOnDate(aPersonNrec: comp; aDate: date = 0): string;
{
  vGetCard._cardNrec := GetCardNrecByPersonOnDate(aPersonNrec, aDate);
  if vGetCard.getfirst spkau = tsOk
    result := vGetCard.spkau.name;
  else
    result := '';
}

//#endregion
end.
